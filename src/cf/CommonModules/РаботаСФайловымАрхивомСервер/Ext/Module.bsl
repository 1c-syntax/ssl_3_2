///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает двоичные данные файла, извлеченные из Zip-архива.
//
// Параметры:
//  ДвоичныеДанныеZipАрхива	- ДвоичныеДанные - двоичные данные файла Zip-архива
//  ПрисоединенныйФайл		- ОпределяемыйТип.ПрисоединенныйФайл - ссылка на элемент справочника с файлом.
//
// Возвращаемое значение:
//  - ДвоичныеДанные
//  - Неопределено
//
Функция ДвоичныеДанныеФайлаИзвлеченногоИзZipАрхива(ДвоичныеДанныеZipАрхива, ПрисоединенныйФайл) Экспорт

	Результат = Неопределено;

	Если ТипЗнч(ДвоичныеДанныеZipАрхива) = Тип("ДвоичныеДанные") Тогда

		ОписаниеОшибки = НСтр("ru = 'Ошибка при извлечении файла из Zip-архива'");
		
		ВременныйКаталог = НовыйВременныйКаталогДляРаботыСZipАрхивом(ОписаниеОшибки);

		Попытка
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПрисоединенныйФайл, "Наименование,Расширение");

			ИмяИзвлекаемогоФайла = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(ЗначенияРеквизитов.Наименование, ЗначенияРеквизитов.Расширение);

			ДанныеИзвлеченногоФайла = ИзвлечьФайлИзZipАрхива(ДвоичныеДанныеZipАрхива, ИмяИзвлекаемогоФайла, ВременныйКаталог);

			Результат = Новый ДвоичныеДанные(ДанныеИзвлеченногоФайла.ИмяФайла);

			УдалитьВременныйКаталогДляРаботыСZipАрхивом(ВременныйКаталог);

		Исключение

			УдалитьВременныйКаталогДляРаботыСZipАрхивом(ВременныйКаталог);

		    ИнформацияОбОшибке = ИнформацияОбОшибке();
		    Уточнение = ОбщегоНазначенияКлиентСервер.УточнениеИсключения(ИнформацияОбОшибке, ОписаниеОшибки);

		    ВызватьИсключение(Уточнение.Текст, Уточнение.Категория,,, ИнформацияОбОшибке);			
		КонецПопытки;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Проверяет доступность хранилищ двоичных данных.
// Использование хранилищ двоичных данных поддерживается в БСП, начиная с версии платформы 8.3.26.1000.
// Хранилища двоичных данных недоступны для файловых информационных баз.
// Хранилища двоичных данных недоступны для распределенной информационной базы.
//
// Возвращаемое значение:
//  Булево
//
Функция ДоступныХранилищаДвоичныхДанных() Экспорт	

	Результат = Не ОбщегоНазначения.ИнформационнаяБазаФайловая();

	Если Результат Тогда
		Результат = Не ОбщегоНазначения.ЭтоРаспределеннаяИнформационнаяБаза()
	КонецЕсли;	

	Если Результат Тогда
		Результат = ДоступныИзмененияВСпособахХраненияФайлов();
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Проверяет версию платформы, начиная с которой доступны изменения в способах хранения файлов
//	Изменились названия способов хранения и стали доступны способы хранения в хранилищах двоичных данных.
// 
// Возвращаемое значение:
//  Булево
//
Функция ДоступныИзмененияВСпособахХраненияФайлов() Экспорт

	Результат = Истина;	

	Инфо = Новый СистемнаяИнформация;
	МинимальнаяВерсия = МинимальноНеобходимаяВерсияПлатформыДляРаботыСХранилищамиДвоичныхДанных();

	Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(
		Инфо.ВерсияПриложения, МинимальнаяВерсия) < 0 Тогда
		Результат = Ложь;
	КонецЕсли;	
	
	Возврат Результат;

КонецФункции

// Проверяет доступность функциональности файлового архива.
// Файловый архив недоступен для прикладных решений, отличных от версии КОРП.
// Файловый архив недоступен для файловых информационных баз.
// Файловый архив недоступен для распределенной информационной базы.
//
// Возвращаемое значение:
//  Булево
//
Функция ДоступенФайловыйАрхив() Экспорт

	Результат = РаботаСФайламиСлужебный.НастройкиФайлов().ДоступенФайловыйАрхив;

	Если Результат Тогда
		Результат = Не ОбщегоНазначения.ИнформационнаяБазаФайловая();
	КонецЕсли;

	Если Результат Тогда
		Результат = Не ОбщегоНазначения.ЭтоРаспределеннаяИнформационнаяБаза()
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Возвращает способ хранения файлов в томах хранения, соответствующий переданному способу хранения файлов.
//
// Параметры:
//  СпособХраненияФайла - Строка - см. значения константы "СпособХраненияФайлов"
//
// Возвращаемое значение:
//  - ПеречислениеСсылка.СпособыХраненияФайлов	- если переданный способ хранения файла не равен "ВИнформационнойБазеИТомахНаДиске".
//  - Строка									- если переданный способ хранения файла равен "ВИнформационнойБазеИТомахНаДиске".
//
Функция СпособХраненияДляПодбораТомовХранения(Знач СпособХраненияФайла = "") Экспорт

	Если Не ЗначениеЗаполнено(СпособХраненияФайла) Тогда
		СпособХраненияФайла = СпособХраненияФайлов();
	КонецЕсли;	

	Если СпособХраненияФайла = "ВТомахНаДиске" Тогда
		Результат = Перечисления.СпособыХраненияФайлов.ВСетевыхКаталогах;
	ИначеЕсли СпособХраненияФайла = "ВоВстроенномХранилищеДвоичныхДанных" Тогда
		Результат = Перечисления.СпособыХраненияФайлов.ВоВстроенномХранилище;
	ИначеЕсли СпособХраненияФайла = "ВоВнешнемХранилищеДвоичныхДанных" Тогда
		Результат = Перечисления.СпособыХраненияФайлов.ВоВнешнемХранилищеПоПротоколуS3;
	ИначеЕсли СпособХраненияФайла = "ВИнформационнойБазеИТомахНаДиске" Тогда
		Результат = "ВТомахСЛюбымСпособомХранения";
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Возвращает массив имен реквизитов справочника "ХранилищеДвоичныхДанных", соответствующий переданному способу хранения
// файлов.
//
// Параметры:
//  СпособХраненияФайлов - Строка - см. значения константы "СпособХраненияФайлов"
//
// Возвращаемое значение:
//  Массив
//
Функция ИменаРеквизитовХраненияФайловПоСпособуХранения(СпособХраненияФайлов) Экспорт

	Результат = Новый Массив;
	Результат.Добавить("ДвоичныеДанные");

	Если СпособХраненияФайлов = "ВоВстроенномХранилищеДвоичныхДанных" Тогда

		Результат.Добавить("ДвоичныеДанныеВОперационномВстроенномХранилище");

	ИначеЕсли СпособХраненияФайлов = "ВоВнешнемХранилищеДвоичныхДанных" Тогда

		Результат.Добавить("ДвоичныеДанныеВОперационномВнешнемХранилище");

	ИначеЕсли СпособХраненияФайлов = "ВИнформационнойБазеИТомахНаДиске" Тогда

		Результат.Добавить("ДвоичныеДанныеВОперационномВстроенномХранилище");
		Результат.Добавить("ДвоичныеДанныеВОперационномВнешнемХранилище");

	КонецЕсли;

	Возврат Результат;

КонецФункции

// Управляет видимостью поля формы списка НомерКартинкиЭтоАрхив в зависимости от использования функциональности
// файлового архива.
// 
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//
Процедура ВидимостьПоляСписокНомерКартинкиЭтоАрхив(ПолеФормы) Экспорт

	ПолеФормы.Видимость = ИспользоватьФайловыйАрхив();

КонецПроцедуры

#Область ОбработчикиСобытийПодсистемКонфигурации

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.НачальноеЗаполнение	= Истина;
	Обработчик.ОбщиеДанные			= Истина;
	Обработчик.Версия				= "3.2.1.258";
	Обработчик.Процедура			= "РаботаСФайловымАрхивомСервер.ПервоначальноеЗаполнениеКонстант";
	Обработчик.РежимВыполнения		= "Оперативно";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.НачальноеЗаполнение	= Истина;
	Обработчик.Версия				= "3.2.1.258";
	Обработчик.Процедура			= "РаботаСФайловымАрхивомСервер.ПервоначальноеЗаполнениеНастройкиРаботыСФайловымАрхивом";
	Обработчик.РежимВыполнения		= "Оперативно";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия				= "3.2.1.258";	
	Обработчик.Комментарий			= НСтр("ru = 'Заполнение новых реквизитов в справочнике ""Тома хранения файлов""'");
	Обработчик.ОбщиеДанные			= Истина;
	Обработчик.Процедура			= "Справочники.ТомаХраненияФайлов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.РежимВыполнения		= "Оперативно";

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия				= "3.2.1.258";
	Обработчик.Комментарий			= НСтр("ru = 'Заполнение недостающих сведений о хранении дедуплицированных файлов.'");
	Обработчик.Идентификатор		= Новый УникальныйИдентификатор("c36f86c4-77e8-4fe9-a07e-798d85929cf9");
	Обработчик.Процедура			= "РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.РежимВыполнения		= "Отложенно";
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки	= "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЧитаемыеОбъекты      = "РегистрСведений.ХранилищеФайлов";
	Обработчик.ИзменяемыеОбъекты    = "РегистрСведений.СведенияОХраненииДедуплицированныхФайлов";
	Обработчик.БлокируемыеОбъекты   = "РегистрСведений.ХранилищеФайлов";
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	Приоритет = Обработчик.ПриоритетыВыполнения.Добавить();
	Приоритет.Порядок	= "Любой";

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет список значений доступными видами способа хранения файлов в томах хранения файлов.
//
// Параметры:
//  СписокВыбора			- СписокЗначений - заполняемый список значений.
//  ВидТомаХраненияФайлов	- ПеречислениеСсылка.ВидыХраненияФайлов
//
Процедура СпособХраненияФайловЗаполнитьСписокВыбора(СписокВыбора, ВидТомаХраненияФайлов) Экспорт

	СписокВыбора.Добавить(Перечисления.СпособыХраненияФайлов.ВСетевыхКаталогах);

	Если ДоступныХранилищаДвоичныхДанных() Тогда
		СписокВыбора.Добавить(Перечисления.СпособыХраненияФайлов.ВоВнешнемХранилищеПоПротоколуS3);
		Если ВидТомаХраненияФайлов = Перечисления.ВидыХраненияФайлов.ОперационноеХранение Тогда	
			СписокВыбора.Добавить(Перечисления.СпособыХраненияФайлов.ВоВстроенномХранилище);	
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Возвращает значение константы "СпособХраненияФайлов".
//
// Возвращаемое значение:
//  Строка - см. Константа.СпособХраненияФайлов
//
Функция СпособХраненияФайлов() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	Результат = Константы.СпособХраненияФайлов.Получить();

	Возврат Результат;

КонецФункции

// Возвращает значение константы "ИспользоватьФайловыйАрхив".
//
// Возвращаемое значение:
//  Булево - см. Константа.ИспользоватьФайловыйАрхив
//
Функция ИспользоватьФайловыйАрхив() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	Результат = Константы.ИспользоватьФайловыйАрхив.Получить();

	Возврат Результат;

КонецФункции

// Возвращает значение константы "РазмерПорцииПеремещаемыхФайлов".
//
// Возвращаемое значение:
//  Число - см. Константа.РазмерПорцииПеремещаемыхФайлов
//
Функция РазмерПорцииПеремещаемыхФайлов()

	УстановитьПривилегированныйРежим(Истина);
	Результат = Константы.РазмерПорцииПеремещаемыхФайлов.Получить();

	Если Результат = 0 Тогда
		Результат = РазмерПорцииПеремещаемыхФайловЗначениеПоУмолчанию();
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Возвращает значение константы "ТекстИнформированияПользователяОНедоступностиФайлаВАрхиве".
//
// Возвращаемое значение:
//  Строка - см. Константа.ТекстИнформированияПользователяОНедоступностиФайлаВАрхиве
//
Функция ТекстИнформированияПользователяОНедоступностиФайлаВАрхиве() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	Результат = Константы.ТекстИнформированияПользователяОНедоступностиФайлаВАрхиве.Получить();
	УстановитьПривилегированныйРежим(Ложь);

	Если Не ЗначениеЗаполнено(Результат) Тогда
		Результат = ТекстИнформированияПользователяОНедоступностиФайлаВАрхивеЗначениеПоУмолчанию();
	КонецЕсли;

	Возврат Результат;	

КонецФункции

// Возвращает имя реквизита справочника "ХранилищеДвоичныхДанных", соответствующего переданному способу хранения тома.
//
// Параметры:
//  СпособХраненияФайлов - ПеречислениеСсылка.СпособыХраненияФайлов - см. реквизит "СпособХраненияФайлов" справочника "ТомаХраненияФайлов"
//
// Возвращаемое значение:
//  Строка
//
Функция ИмяРеквизитаХраненияДвоичныхДанныхПоСпособуХраненияТома(СпособХраненияТома) Экспорт

	Результат = "";

	Если СпособХраненияТома = Перечисления.СпособыХраненияФайлов.ВоВстроенномХранилище Тогда

		Результат = "ДвоичныеДанныеВОперационномВстроенномХранилище";

	ИначеЕсли СпособХраненияТома = Перечисления.СпособыХраненияФайлов.ВоВнешнемХранилищеПоПротоколуS3 Тогда

		Результат = "ДвоичныеДанныеВОперационномВнешнемХранилище";

	КонецЕсли;

	Возврат Результат;	

КонецФункции

// Возвращает тип хранения файла, соответствующий переданному способу хранения файлов.
//
// Параметры:
//  СпособХраненияФайла - Строка - см. значения константы "СпособХраненияФайлов"
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыХраненияФайлов
//
Функция ТипХраненияФайлаПоСпособуХранения(Знач СпособХраненияФайла = "") Экспорт

	Если ПустаяСтрока(СпособХраненияФайла) Тогда
		СпособХраненияФайла = СпособХраненияФайлов();
	КонецЕсли;

	Если СпособХраненияФайла = "ВТомахНаДиске" Тогда
		Результат = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске;
	ИначеЕсли СпособХраненияФайла = "ВоВстроенномХранилищеДвоичныхДанных" Тогда
		Результат = Перечисления.ТипыХраненияФайлов.ВоВстроенномХранилищеДвоичныхДанных;
	ИначеЕсли СпособХраненияФайла = "ВоВнешнемХранилищеДвоичныхДанных" Тогда
		Результат = Перечисления.ТипыХраненияФайлов.ВоВнешнемХранилищеДвоичныхДанных;
	ИначеЕсли СпособХраненияФайла = "ВИнформационнойБазеИТомахНаДиске" Тогда
		Результат = Перечисления.ТипыХраненияФайлов.ВТомахТомНеОпределен;
	Иначе
		Результат = Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Возвращает тип хранения файла, соответствующий переданному тому хранения или способу хранения файлов в томах.
//
// Параметры:
//  ТомИлиСпособХраненияФайлов - СправочникСсылка.ТомаХраненияФайлов, ПеречислениеСсылка.СпособыХраненияФайлов
//
// Возвращаемое значение:
//  - ПеречислениеСсылка.ТипыХраненияФайлов
//  - Неопределено
//
Функция ТипХраненияПоТомуХраненияФайлов(ТомИлиСпособХраненияФайлов = Неопределено) Экспорт

	Если ЗначениеЗаполнено(ТомИлиСпособХраненияФайлов) Тогда

		Если ТипЗнч(ТомИлиСпособХраненияФайлов) = Тип("СправочникСсылка.ТомаХраненияФайлов") Тогда
			СпособХранения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТомИлиСпособХраненияФайлов, "СпособХраненияФайлов");
		Иначе
			СпособХранения = ТомИлиСпособХраненияФайлов;
		КонецЕсли;

		Если СпособХранения = Перечисления.СпособыХраненияФайлов.ВСетевыхКаталогах Тогда
			Результат = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске;
		ИначеЕсли СпособХранения = Перечисления.СпособыХраненияФайлов.ВоВстроенномХранилище Тогда
			Результат = Перечисления.ТипыХраненияФайлов.ВоВстроенномХранилищеДвоичныхДанных;
		ИначеЕсли СпособХранения = Перечисления.СпособыХраненияФайлов.ВоВнешнемХранилищеПоПротоколуS3 Тогда
			Результат = Перечисления.ТипыХраненияФайлов.ВоВнешнемХранилищеДвоичныхДанных;
		Иначе
			Результат = Неопределено;
		КонецЕсли;
	Иначе
		Результат = Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Проверяет наличие в файловом архиве присоединенный файлов, связанных с переданным элементом справочника "ХранилищеДвоичныхДанных".
//
// Параметры:
//  ХранилищеДвоичныхДанных - СправочникСсылка.ХранилищеДвоичныхДанных
//
// Возвращаемое значение:
//  Булево
//
Функция ХранилищеДвоичныхДанныхПрисутствуетВФайловомАрхиве(ХранилищеДвоичныхДанных) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СправочникХранилищеДвоичныхДанных.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ХранилищеДвоичныхДанных КАК СправочникХранилищеДвоичныхДанных
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
		|		ПО (СправочникХранилищеДвоичныхДанных.Ссылка = &ХранилищеДвоичныхДанных)
		|			И СправочникХранилищеДвоичныхДанных.Ссылка = ХранилищеФайлов.ХранилищеДвоичныхДанных
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОФайлах КАК СведенияОФайлах
		|		ПО (ХранилищеФайлов.Файл = СведенияОФайлах.Файл)
		|			И (НЕ СведенияОФайлах.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВерсииФайлов КАК ВерсииФайлов
		|		ПО (ХранилищеФайлов.Файл = ВерсииФайлов.Ссылка)
		|			И (НЕ ВерсииФайлов.Владелец.ПометкаУдаления)
		|ГДЕ
		|	НЕ ЕСТЬNULL(СведенияОФайлах.ПометкаУдаления, ЕСТЬNULL(ВерсииФайлов.ПометкаУдаления, ЛОЖЬ))
		|	И ЕСТЬNULL(СведенияОФайлах.ДатаПереносаВАрхив, ЕСТЬNULL(ВерсииФайлов.ДатаПереносаВАрхив, ДАТАВРЕМЯ(1, 1, 1))) <> ДАТАВРЕМЯ(1, 1, 1)";

	Запрос.УстановитьПараметр("ХранилищеДвоичныхДанных", ХранилищеДвоичныхДанных);

	Возврат Не Запрос.Выполнить().Пустой();

КонецФункции

// Проверяет наличие присоединенного файла в файловом архиве.
//
// Параметры:
//  ПрисоединенныйФайл - ОпределяемыйТип.ПрисоединенныйФайл, СправочникСсылка.ВерсииФайлов - ссылка на элемент
//                                                                                           справочника с файлом.
//
// Возвращаемое значение:
//  Булево
//
Функция ФайлПеренесенВФайловыйАрхив(ПрисоединенныйФайл) Экспорт

	Результат = Ложь;

	Если ЗначениеЗаполнено(ПрисоединенныйФайл) Тогда 
		Если ТипЗнч(ПрисоединенныйФайл) = Тип("СправочникСсылка.ВерсииФайлов") Тогда

			ДатаПереносаВАрхив = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПрисоединенныйФайл, "ДатаПереносаВАрхив");

		ИначеЕсли ТипЗнч(ПрисоединенныйФайл) = Тип("СправочникСсылка.Файлы") Тогда

			ТекущаяВерсияФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПрисоединенныйФайл, "ТекущаяВерсия");

			ДатаПереносаВАрхив = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяВерсияФайла, "ДатаПереносаВАрхив");

		Иначе

			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	СведенияОФайлах.ДатаПереносаВАрхив КАК ДатаПереносаВАрхив
				|ИЗ
				|	РегистрСведений.СведенияОФайлах КАК СведенияОФайлах
				|ГДЕ
				|	СведенияОФайлах.Файл = &Файл";

			Запрос.УстановитьПараметр("Файл", ПрисоединенныйФайл);

			УстановитьПривилегированныйРежим(Истина);
			
			ВыборкаДанных = Запрос.Выполнить().Выбрать();
			Если ВыборкаДанных.Следующий() Тогда
				ДатаПереносаВАрхив = ВыборкаДанных.ДатаПереносаВАрхив;
			Иначе
				ДатаПереносаВАрхив = Дата(1,1,1);			
			КонецЕсли;
		КонецЕсли;

		Результат = ЗначениеЗаполнено(ДатаПереносаВАрхив);

	КонецЕсли;

	Возврат Результат;

КонецФункции

// Возвращает структуру с результатом хеширования переданных двоичных данных.
//
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные, Неопределено
//
// Возвращаемое значение:
//  Структура - см. РаботаСФайловымАрхивомСервер.ПараметрыРезультатаХешированияДвоичныхДанных.
//
Функция РезультатХешированияДвоичныхДанных(ДвоичныеДанные) Экспорт

	Хеширование = Новый ХешированиеДанных(ХешФункция.SHA256);

	ЭтоПустыеДвоичныеДанные = (ДвоичныеДанные = Неопределено);
	Если ЭтоПустыеДвоичныеДанные Тогда
		ПустыеДвоичныеДанные = ПолучитьДвоичныеДанныеИзСтроки("");
		Хеширование.Добавить(ПустыеДвоичныеДанные);
		Размер = ПустыеДвоичныеДанные.Размер();
	Иначе
		Хеширование.Добавить(ДвоичныеДанные);
		Размер = ДвоичныеДанные.Размер();
	КонецЕсли;
	Хеш = ПолучитьBase64СтрокуИзДвоичныхДанных(Хеширование.ХешСумма);	

	Результат = ПараметрыРезультатаХешированияДвоичныхДанных();
	
	Результат.ЭтоПустыеДвоичныеДанные = ЭтоПустыеДвоичныеДанные;
	Результат.Размер = Размер;
	Результат.Хеш = Хеш;

	Возврат Результат;

КонецФункции

// Возвращает сведения об элементе справочника "ХранилищеДвоичныхДанных", соответствующего переданным значениям хеша и размера.
//
// Параметры:
//  Хеш		- Строка - строка формата Base64, сформированная методом платформы "ПолучитьBase64СтрокуИзДвоичныхДанных"
//  Размер	- Число
//
// Возвращаемое значение:
//  Структура - сведения об элементе справочника ХранилищеДвоичныхДанных:
//   * ХранилищеДвоичныхДанныхСсылка - СправочникСсылка.ХранилищеДвоичныхДанных - если найден соответствующий элемент
//                                                                                справочника "ХранилищеДвоичныхДанных"
//									 - Неопределено - если не найден соответствующий элемент справочника "ХранилищеДвоичныхДанных"
//   * РазмерХранимыхДанных			 - Число - размер двоичных данных в байтах.
//
Функция СведенияОбЭлементеХранилищаДвоичныхДанныхПоХешуИРазмеру(Хеш, Размер) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ХранилищеДвоичныхДанныхСсылка"	, Неопределено);
	Результат.Вставить("РазмерХранимыхДанных"			, 0);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Хеш"		, Хеш);
	Запрос.УстановитьПараметр("Размер"	, Размер);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ХранилищеДвоичныхДанных.Ссылка КАК ХранилищеДвоичныхДанныхСсылка,
		|	РАЗМЕРХРАНИМЫХДАННЫХ(ХранилищеДвоичныхДанных.ДвоичныеДанные) КАК РазмерХранимыхДанных
		|ИЗ
		|	Справочник.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанных
		|ГДЕ
		|	ХранилищеДвоичныхДанных.Хеш = &Хеш
		|	И ХранилищеДвоичныхДанных.Размер = &Размер";

	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Заполняет реквизиты "ТипХраненияФайла" и "Том" присоединенного файла данными из регистра сведений "СведенияОХраненииДедуплицированныхФайлов".
//
// Параметры:
//  ПрисоединенныйФайл	- ОпределяемыйТип.ПрисоединенныйФайлОбъект, СправочникОбъект.ВерсииФайлов - объект элемента справочника с файлом.
//  ДвоичныеДанные		- ДвоичныеДанные - двоичные данные файла.
//
Процедура ЗаполнитьСвойстваФайлаПоСуществующемуЭлементуХранилищаДвоичныхДанных(ПрисоединенныйФайл, ДвоичныеДанные) Экспорт

	Если ТипЗнч(ДвоичныеДанные) <> Тип("ДвоичныеДанные") Или Не ДоступныХранилищаДвоичныхДанных() Тогда
		Возврат;
	КонецЕсли;

	СведенияОХраненииФайлов = ПараметрыХраненияДедуплицированногоФайла(ДвоичныеДанные);
	Если СведенияОХраненииФайлов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПрисоединенныйФайл, СведенияОХраненииФайлов);
	КонецЕсли;	

КонецПроцедуры

// Возвращает структуру с параметрами хранения дедуплицированных файлов, полученную из регистра сведений "СведенияОХраненииДедуплицированныхФайлов".
//
// Параметры:
//  ДвоичныеДанные			- ДвоичныеДанные - двоичные данные, для которых нужно получить элемент справочника "ХранилищеДвоичныхДанных".
//  ХранилищеДвоичныхДанных - СправочникСсылка.ХранилищеДвоичныхДанных - используется в целях оптимизации, если элемент
//                                                                       справочника "ХранилищеДвоичныхДанных" известен.
//  ВидХраненияФайлов		- ПеречислениеСсылка.ВидыХраненияФайлов
//
// Возвращаемое значение:
//  - Структура		- значения параметров "ТипХраненияФайла", "Том" и "ПутьКФайлу", полученные из регистра сведений "СведенияОХраненииДедуплицированныхФайлов".
//  - Неопределено	- в случае отсутствия параметров хранения дедуплицированных файлов в регистре сведений "СведенияОХраненииДедуплицированныхФайлов".
//
Функция ПараметрыХраненияДедуплицированногоФайла(ДвоичныеДанные, ХранилищеДвоичныхДанных = Неопределено, ВидХраненияФайлов = Неопределено)

	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(ХранилищеДвоичныхДанных) Тогда
		ХранилищеДвоичныхДанныхСсылка = ХранилищеДвоичныхДанных;
	Иначе
		ХранилищеДвоичныхДанныхСсылка = ЭлементХранилищаДвоичныхДанныхПоДвоичнымДанным(ДвоичныеДанные);
	КонецЕсли;

	Если ЗначениеЗаполнено(ХранилищеДвоичныхДанныхСсылка) Тогда
		Результат = РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов.СведенияОХраненииДедуплицированныхФайлов(ХранилищеДвоичныхДанныхСсылка, ВидХраненияФайлов);
	Иначе
		Результат = Неопределено;
	КонецЕсли;	

	Возврат Результат;

КонецФункции

// Возвращает элемент справочника "ХранилищеДвоичныхДанных", соответствующий переданным двоичным данным.
//
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные - двоичные данные присоединенного файла.
//
// Возвращаемое значение:
//  - СправочникСсылка.ХранилищеДвоичныхДанных - если соответствующий элемент справочника найден.
//  - Неопределено - если соответствующий элемент справочника не найден.
//
Функция ЭлементХранилищаДвоичныхДанныхПоДвоичнымДанным(ДвоичныеДанные)

	РезультатХеширования = РезультатХешированияДвоичныхДанных(ДвоичныеДанные);

	СведенияОХранилищеДвоичныхДанных = СведенияОбЭлементеХранилищаДвоичныхДанныхПоХешуИРазмеру(РезультатХеширования.Хеш, РезультатХеширования.Размер);
	Результат = СведенияОХранилищеДвоичныхДанных.ХранилищеДвоичныхДанныхСсылка;	

	Возврат Результат;

КонецФункции

// Возвращает свободный том хранения, подходящий для размещения присоединенного файла с указанным типом хранения.
//
// Параметры:
//  ПрисоединенныйФайл		- ОпределяемыйТип.ПрисоединенныйФайлОбъект
//                      		- см. РаботаСФайламиВТомахСлужебный.ПараметрыДобавленияФайла
//  ТипХраненияФайлов		- ПеречислениеСсылка.ТипыХраненияФайлов
//  ВидТомаХраненияФайлов	- ПеречислениеСсылка.ВидыХраненияФайлов
//
// Возвращаемое значение:
//   СправочникСсылка.ТомаХраненияФайлов
//
Функция СвободныйТомХраненияПоТипуХраненияФайлов(ПрисоединенныйФайл, ТипХраненияФайлов, ВидТомаХраненияФайлов) Экспорт

	СпособХраненияВТомах = СпособХраненияДляПоискаТомовХраненияПоТипуХраненияФайлов(ТипХраненияФайлов);

	Результат = РаботаСФайламиВТомахСлужебный.СвободныйТом(ПрисоединенныйФайл, ВидТомаХраненияФайлов, СпособХраненияВТомах);

	Возврат Результат;

КонецФункции

// Возвращает структуру параметров, используемых при записи двоичных данных в реквизиты хранения двоичных данных файлов.
//
// Возвращаемое значение:
//  Структура
//
Функция ПараметрыЗаписиВИнформационнуюБазу() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ТипХраненияФайла");
	Результат.Вставить("Том");
	Результат.Вставить("ПутьКФайлу"					, "");
	Результат.Вставить("ЭтоЗаписьВФайловыйАрхив"	, Ложь);
	Результат.Вставить("НеИзменятьДвоичныеДанные"	, Ложь);
	Результат.Вставить("ДвоичныеДанныеАрхива");

	Возврат Результат;

КонецФункции

// Возвращает заполненную структуру параметров, используемых при записи двоичных данных в реквизиты хранения двоичных
// данных файлов.
//
// Параметры:
//  ПрисоединенныйФайл - ОпределяемыйТип.ПрисоединенныйФайл, ОпределяемыйТип.ПрисоединенныйФайлОбъект, Структура -
//                       источник значений параметров.
//
// Возвращаемое значение:
//  Структура
//
Функция ЗаполненныеПараметрыЗаписиВИнформационнуюБазу(ПрисоединенныйФайл) Экспорт

	Результат = ПараметрыЗаписиВИнформационнуюБазу();

	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ПрисоединенныйФайл)) Тогда
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПрисоединенныйФайл, "ТипХраненияФайла,Том,ПутьКФайлу");
		ЗаполнитьЗначенияСвойств(Результат, ЗначенияРеквизитов);
	Иначе
		ЗаполнитьЗначенияСвойств(Результат, ПрисоединенныйФайл);
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Заполняет реквизиты "ТипХраненияФайла", "Том" и "ПутьКФайлу" присоединенного файла.
//
// Параметры:
//  ПрисоединенныйФайл	- ОпределяемыйТип.ПрисоединенныйФайлОбъект, СправочникОбъект.ВерсииФайлов - объект элемента справочника с файлом
//  ДвоичныеДанные		- ДвоичныеДанные - двоичные данные файла
//  Размер				- Число
//  Расширение			- Строка
//
Процедура ЗаполнитьПараметрыХраненияФайла(ПрисоединенныйФайл, ДвоичныеДанные, Размер, Расширение) Экспорт
	
	ПрисоединенныйФайл.ТипХраненияФайла = Неопределено;
	ПрисоединенныйФайл.Том = Неопределено;
	ПрисоединенныйФайл.ПутьКФайлу = Неопределено;

	ЗаполнитьСвойстваФайлаПоСуществующемуЭлементуХранилищаДвоичныхДанных(ПрисоединенныйФайл, ДвоичныеДанные);
	
	Если Не ЗначениеЗаполнено(ПрисоединенныйФайл.ТипХраненияФайла) Тогда
		ПрисоединенныйФайл.ТипХраненияФайла = РаботаСФайламиСлужебный.ТипХраненияФайла(Размер, Расширение);

		Если ПрисоединенныйФайл.ТипХраненияФайла <> Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе Тогда
			
			ПрисоединенныйФайл.Том = СвободныйТомХраненияПоТипуХраненияФайлов(ПрисоединенныйФайл, 
																												ПрисоединенныйФайл.ТипХраненияФайла,
																												Перечисления.ВидыХраненияФайлов.ОперационноеХранение);
			Если ПрисоединенныйФайл.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахТомНеОпределен Тогда
				ПрисоединенныйФайл.ТипХраненияФайла = ТипХраненияПоТомуХраненияФайлов(ПрисоединенныйФайл.Том);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Создает временный каталог и возвращает полный путь к нему.
//
// Параметры:
//  СтрокаОписанияОшибки - Строка - дополнение текста ошибки
//
// Возвращаемое значение:
//  Строка - полный путь к созданному временному каталогу
//
Функция НовыйВременныйКаталогДляРаботыСZipАрхивом(СтрокаОписанияОшибки = "")

	Попытка

		ВременныйКаталогФайлов = ФайловаяСистема.СоздатьВременныйКаталог();

	Исключение

		ШаблонОписанияОшибки = НСтр("ru = '""%1""
			|Не создан временный каталог для работы с zip-архивом
			|""%2""'");

		СтрокаИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОписанияОшибки,
			СтрокаОписанияОшибки,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));

		ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операций с файловым архивом'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			СтрокаИсключения);

		ВызватьИсключение СтрокаИсключения;

	КонецПопытки;

	Возврат ВременныйКаталогФайлов;

КонецФункции

// Возвращает элемент файла, извлекаемого из Zip-архива. Если файл с переданным именем не будет найден, то будет получен
// первый элемент в архиве.
//
// Параметры:
//  ЧтениеАрхива			- ЧтениеФайлаАрхива - прочитанный zip-файл.
//  ИмяИзвлекаемогоФайла	- Строка - имя извлекаемого файла с расширением.
//
// Возвращаемое значение:
//  ЭлементыФайлаАрхива	- элемент файла, извлекаемого из архива, в ситуации, когда элемент найден по имени файла или получен первый элемент в архиве.
//  Неопределено		- в ситуации, когда не удалось определить элемент zip-архива.
//
Функция ЭлементИзвлекаемогоФайла(ЧтениеАрхива, ИмяИзвлекаемогоФайла)

	Результат = ЧтениеАрхива.Элементы.Найти(ИмяИзвлекаемогоФайла);

	Если Результат = Неопределено Тогда
		Если ЧтениеАрхива.Элементы.Количество() > 0 Тогда
			Результат = ЧтениеАрхива.Элементы[0];
		Иначе
			Результат = Неопределено;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ПодготовитьДвоичныеДанныеДляЗаписи(ДанныеДляЗаписи)

	Если ТипЗнч(ДанныеДляЗаписи) = Тип("ДвоичныеДанные") Тогда
		Результат = Новый ХранилищеЗначения(ДанныеДляЗаписи, Новый СжатиеДанных(9));
	Иначе
		Результат = ДанныеДляЗаписи;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция МинимальноНеобходимаяВерсияПлатформыДляРаботыСХранилищамиДвоичныхДанных()
	
	Возврат "8.3.26.1000";
	
КонецФункции

Функция РазмерПорцииПеремещаемыхФайловЗначениеПоУмолчанию()
	Возврат 10;
КонецФункции

Функция ТекстИнформированияПользователяОНедоступностиФайлаВАрхивеЗначениеПоУмолчанию()

	Возврат НСтр("ru = 'Для получения файла обратитесь к администратору'");

КонецФункции

Функция СпособХраненияДляПоискаТомовХраненияПоТипуХраненияФайлов(ТипХраненияФайла)

	Если ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВоВнешнемХранилищеДвоичныхДанных Тогда
		Результат = Перечисления.СпособыХраненияФайлов.ВоВнешнемХранилищеПоПротоколуS3;
	ИначеЕсли ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВоВстроенномХранилищеДвоичныхДанных Тогда
		Результат = Перечисления.СпособыХраненияФайлов.ВоВстроенномХранилище;
	ИначеЕсли ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахТомНеОпределен Тогда
		Результат = "ВТомахСЛюбымСпособомХранения";
	Иначе
		Результат = Перечисления.СпособыХраненияФайлов.ВСетевыхКаталогах;
	КонецЕсли;		

	Возврат Результат;

КонецФункции

Функция ПараметрыРезультатаХешированияДвоичныхДанных()

	Результат = Новый Структура;
	Результат.Вставить("ЭтоПустыеДвоичныеДанные", Ложь);
	Результат.Вставить("Размер"					, 0);
	Результат.Вставить("Хеш"					, "");	

	Возврат Результат;

КонецФункции

Процедура УдалитьВременныйКаталогДляРаботыСZipАрхивом(ВременныйКаталог)

	Попытка
		УдалитьФайлы(ВременныйКаталог);
	Исключение

		ШаблонОписанияОшибки = НСтр("ru = 'Не получилось удалить временный каталог для работы с zip-архивом
			|%1
			|%2'");

		СтрокаИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОписанияОшибки,
			ВременныйКаталог,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));		
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операций с файловым архивом'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			СтрокаИсключения);		
	КонецПопытки;	
	
КонецПроцедуры

Функция ВременныйКаталогДляНеуникальногоФайла(ВременныйКаталог, ИмяФайла)

	ФайлНаДиске = Новый Файл(ВременныйКаталог + ИмяФайла);
	Если ФайлНаДиске.Существует() Тогда
		УникальноеИмя = РаботаСФайламиСлужебныйКлиентСервер.УникальноеИмяСПутем(ВременныйКаталог, ИмяФайла);

		Результат = ВременныйКаталог + СтрЗаменить(УникальноеИмя, ИмяФайла, "");
	Иначе
		Результат = ВременныйКаталог;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура ПервоначальноеЗаполнениеКонстант() Экспорт

	Константы.РазмерПорцииПеремещаемыхФайлов.Установить(
				РазмерПорцииПеремещаемыхФайловЗначениеПоУмолчанию());

	Константы.ТекстИнформированияПользователяОНедоступностиФайлаВАрхиве.Установить(
				ТекстИнформированияПользователяОНедоступностиФайлаВАрхивеЗначениеПоУмолчанию());
	
КонецПроцедуры

Процедура ПервоначальноеЗаполнениеНастройкиРаботыСФайловымАрхивом() Экспорт
	
	НаборЗаписейЗначениеПоУмолчанию = РегистрыСведений.НастройкиРаботыСФайловымАрхивом.СоздатьНаборЗаписей();

	НаборЗаписейЗначениеПоУмолчанию.Отбор.ВладелецФайла.Установить(Неопределено);
	НаборЗаписейЗначениеПоУмолчанию.Отбор.ТипВладельцаФайла.Установить(Неопределено);
	
	ЗаписьРС = НаборЗаписейЗначениеПоУмолчанию.Добавить();
	ЗаписьРС.ВладелецФайла						= Неопределено;
	ЗаписьРС.ТипВладельцаФайла					= Неопределено;
	ЗаписьРС.ПеренестиВФайловыйАрхивЧерезДней	= 365;	
	
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейЗначениеПоУмолчанию);	
	
КонецПроцедуры

// Создает новый файл ZIP-архива и помещает в него нужный файл.
//
// Параметры:
//  ПутьКФайлуАрхива		- Строка - путь к файлу создаваемого ZIP-архива.
//  ПутьКАрхивируемомуФайлу	- Строка - путь к упаковываемому в ZIP-архив файлу.
//
// Возвращаемое значение:
//  Структура
//
Функция УпаковатьФайлВZipАрхив(ПутьКФайлуАрхива, ПутьКАрхивируемомуФайлу)

	ФайлZipАрхива = Новый Файл(ПутьКФайлуАрхива);
	Если ФайлZipАрхива.Существует() Тогда
		УдалитьФайлы(ПутьКФайлуАрхива);
	КонецЕсли;

	ЗаписьАрхива = Новый ЗаписьФайлаАрхива(ПутьКФайлуАрхива);
	ЗаписьАрхива.Добавить(ПутьКАрхивируемомуФайлу, РежимСохраненияПутейФайлаАрхива.НеСохранятьПути);
	ЗаписьАрхива.Записать();

	ФайлZipАрхива = Новый Файл(ПутьКФайлуАрхива);

	Результат = Новый Структура;
	Результат.Вставить("ИмяФайла"	, ПутьКФайлуАрхива);
	Результат.Вставить("Размер"		, ФайлZipАрхива.Размер());	

	Возврат Результат;

КонецФункции

// Извлекает файл с определенным именем из ZIP-архива в указанный каталог.
//
// Параметры:
//  ДвоичныеДанныеZipАрхива - ДвоичныеДанные - двоичные данные ZIP-архива".
//  ИмяИзвлекаемогоФайла	- Строка - имя извлекаемого файла с расширением.
//  ВременныйКаталог		- Строка - путь к каталогу, в который нужно извлечь файл.
//
// Возвращаемое значение:
//  Структура
//
Функция ИзвлечьФайлИзZipАрхива(ДвоичныеДанныеZipАрхива, ИмяИзвлекаемогоФайла, ВременныйКаталог)

	ПотокДляЧтения = ДвоичныеДанныеZipАрхива.ОткрытьПотокДляЧтения();

	ЧтениеАрхива = Новый ЧтениеФайлаАрхива(ПотокДляЧтения);

	ЭлементИзвлекаемогоФайла = ЭлементИзвлекаемогоФайла(ЧтениеАрхива, ИмяИзвлекаемогоФайла);

	Если ЭлементИзвлекаемогоФайла = Неопределено Тогда 
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Файл %1 не найден в zip-архиве'"), ИмяИзвлекаемогоФайла);
	КонецЕсли;

	ВременныйКаталогДляИзвлеченияФайла = ВременныйКаталогДляНеуникальногоФайла(ВременныйКаталог, ИмяИзвлекаемогоФайла);

	ЧтениеАрхива.Извлечь(ЭлементИзвлекаемогоФайла, ВременныйКаталогДляИзвлеченияФайла);

	ПутьКИзвлеченномуФайлу = ВременныйКаталогДляИзвлеченияФайла + ЭлементИзвлекаемогоФайла.ИсходноеИмя;

	ЧтениеАрхива.Закрыть();
	ПотокДляЧтения.Закрыть();

	ИзвлеченныйФайл = Новый Файл(ПутьКИзвлеченномуФайлу);
	Если ИзвлеченныйФайл.Существует() Тогда
		Результат = Новый Структура;
		Результат.Вставить("ИмяФайла"	, ПутьКИзвлеченномуФайлу);
		Результат.Вставить("Размер"		, ИзвлеченныйФайл.Размер());
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось извлечь файл %1 из zip-архива'"), ИмяИзвлекаемогоФайла);
	КонецЕсли;

	Возврат Результат;

КонецФункции

#Область ПереносФайловМеждуОперационнымХранилищемИФайловымАрхивом

// Метод регламентного задания "ПереносФайловМеждуОперационнымХранилищемИФайловымАрхивом".
//
// Параметры:
//  Параметры		- Структура - произвольные параметры ПараметрыПроцедуры. 
//									см. ДлительныеОперации.ВыполнитьВФоне описание параметра "ИмяПроцедуры"
//  АдресРезультата - Строка    - адрес временного хранилища, в которое нужно поместить результат работы процедуры.
//									см. ДлительныеОперации.ВыполнитьВФоне описание параметра "ИмяПроцедуры"
//
Процедура ПеренестиФайлыМеждуОперационнымХранилищемИФайловымАрхивом(Параметры = Неопределено, АдресРезультата = Неопределено) Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ПереносФайловМеждуОперационнымХранилищемИФайловымАрхивом);
	
	Если Не ИспользоватьФайловыйАрхив() Тогда
		Возврат;
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);

	НастройкиПереносаФайлов = РегистрыСведений.НастройкиРаботыСФайловымАрхивом.ПолучитьНастройкиРаботыСФайловымАрхивомДляПереносаФайлов();
	НастройкиПереносаДляОбъектовМетаданных = НастройкиПереносаФайлов.НайтиСтроки(Новый Структура("ЭтоДетальнаяНастройкаПереносаФайлов", Ложь));

	Если НастройкиПереносаДляОбъектовМетаданных.Количество() > 0 Тогда

		РазмерПорции = РазмерПорцииПеремещаемыхФайлов();

		ТекстЗапроса = ТекстЗапросаПорцииФайлов(РазмерПорции);

		ДополнительныеПараметрыПереноса = ДополнительныеПараметрыПереносаФайлов();
		ДополнительныеПараметрыПереноса.ТекстЗапросаПолученияПорцииФайловДляПереносаВАрхив = ТекстЗапроса;

		ТекстЗапроса = ТекстЗапросаПорцииФайлов(РазмерПорции, Ложь);
		ДополнительныеПараметрыПереноса.ТекстЗапросаПолученияПорцииФайловДляВосстановленияИзАрхива = ТекстЗапроса;
		ДополнительныеПараметрыПереноса.СпособХраненияФайлов = СпособХраненияФайлов();
		
		КоличествоДнейХраненияПоУмолчанию = РегистрыСведений.НастройкиРаботыСФайловымАрхивом.ПолучитьКоличествоДнейХраненияИзНастройкиПоУмолчанию();

		Для Каждого Настройка Из НастройкиПереносаДляОбъектовМетаданных Цикл

			МассивИсключений = Новый Массив;

			ДетализированныеНастройки = НастройкиПереносаФайлов.НайтиСтроки(Новый Структура(
																"ОбщийИдентификаторВладельца, ЭтоДетальнаяНастройкаПереносаФайлов",
																Настройка.ОбщийИдентификаторВладельца,
																Истина));

			Если ДетализированныеНастройки.Количество() > 0 Тогда
				КоличествоДнейХраненияРодителя = КоличествоДнейХраненияДоПереносаВФайловыйАрхив(Настройка, КоличествоДнейХраненияПоУмолчанию);					
					
				Для Каждого ДетализированнаяНастройка Из ДетализированныеНастройки Цикл

					ВыполнитьОперацииСФайлами(ДетализированнаяНастройка, МассивИсключений, КоличествоДнейХраненияРодителя, ДополнительныеПараметрыПереноса);

				КонецЦикла;
			КонецЕсли;

			ВыполнитьОперацииСФайлами(Настройка, МассивИсключений, КоличествоДнейХраненияПоУмолчанию, ДополнительныеПараметрыПереноса);

		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ВыполнитьОперацииСФайлами(Настройка, МассивИсключений, КоличествоДнейХраненияРодителя, ДополнительныеПараметрыПереноса)

	КоличествоДнейОперативногоХранения = КоличествоДнейХраненияДоПереносаВФайловыйАрхив(Настройка, КоличествоДнейХраненияРодителя);
	
	ПеренестиФайлыВФайловыйАрхив(Настройка, МассивИсключений, КоличествоДнейОперативногоХранения, ДополнительныеПараметрыПереноса);

	ВосстановитьФайлыИзФайловогоАрхива(Настройка, МассивИсключений, КоличествоДнейОперативногоХранения, ДополнительныеПараметрыПереноса);

	МассивИсключений.Добавить(Настройка.ВладелецФайла);

КонецПроцедуры

Процедура ВосстановитьФайлыИзФайловогоАрхива(Настройка, МассивИсключений, КоличествоДнейОперативногоХранения, ДополнительныеПараметрыПереноса)

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	ЗаполнитьВременнуюТаблицуДляВосстановленияФайлов(Настройка, МассивИсключений, КоличествоДнейОперативногоХранения, МенеджерВременныхТаблиц);

	СведенияДляПодбораТома				= СведенияОФайлахДляПодбораТома();
	СведенияОПодобранномТомеХранения	= Неопределено;

	ПоследняяСсылка = ПустаяСсылкаНаПрисоединенныйФайл(Настройка);

	ТекстЗапроса = ДополнительныеПараметрыПереноса.ТекстЗапросаПолученияПорцииФайловДляВосстановленияИзАрхива;

	ФайлыДляПереноса = СледующаяПорцияФайлов(МенеджерВременныхТаблиц, ПоследняяСсылка, ТекстЗапроса);

	Пока ФайлыДляПереноса.Количество() > 0 Цикл

		СтрокаОписанияОшибки = СтрокаОписанияОшибкиПриОперацияхСВременнымКаталогом(Ложь);

		Для Каждого ОбрабатываемыйФайл Из ФайлыДляПереноса Цикл

			Если ОбрабатываемыйФайл.КоличествоДедуплицированныхФайлов > 0 Тогда
				//@skip-warning запрос выполняется не для всех итераций цикла.
				Если Не ФайлПеренесенВФайловыйАрхив(ОбрабатываемыйФайл.ПрисоединенныйФайл) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;

			ВременныйКаталог = НовыйВременныйКаталогДляРаботыСZipАрхивом(СтрокаОписанияОшибки);

			ПрисоединенныйФайлСсылка = ОбрабатываемыйФайл.ПрисоединенныйФайл;
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = Блокировка.Добавить(ПрисоединенныйФайлСсылка.Метаданные().ПолноеИмя());
			ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", ПрисоединенныйФайлСсылка);
			
			ЭлементБлокировкиДанных = Блокировка.Добавить(Метаданные.РегистрыСведений.СведенияОФайлах.ПолноеИмя());
			ЭлементБлокировкиДанных.УстановитьЗначение("Файл", ПрисоединенныйФайлСсылка);
			
			ЭлементБлокировкиДанных = Блокировка.Добавить(Метаданные.РегистрыСведений.ХранилищеФайлов.ПолноеИмя());
			ЭлементБлокировкиДанных.УстановитьЗначение("Файл", ПрисоединенныйФайлСсылка);			

			НачатьТранзакцию();
			Попытка

				Блокировка.Заблокировать();
				
				ПараметрыОперации = ПараметрыВыполненияОперацииВосстановления();
				ПараметрыОперации.ВременныйКаталог	= ВременныйКаталог;

				ИзвлеченныйФайл = ФайлИзвлеченныйИзZipАрхива(ОбрабатываемыйФайл, ПараметрыОперации);

				НовыйТипХраненияФайла = Неопределено;

				Если ЗначениеЗаполнено(ОбрабатываемыйФайл.ТипХраненияФайлаОперативный) Тогда
					НовыйТипХраненияФайла = ОбрабатываемыйФайл.ТипХраненияФайлаОперативный;

					СведенияОТомеХранения = СведенияОТомеХранения();
					СведенияОТомеХранения.Ссылка				= ОбрабатываемыйФайл.ТомОперативный;
					СведенияОТомеХранения.НовыйТипХраненияФайла = НовыйТипХраненияФайла;

					ПараметрыОперации.ПолученыПараметрыХранения	= Истина;

				Иначе

					ДвоичныеДанные = Новый ДвоичныеДанные(ИзвлеченныйФайл.ИмяФайла);

					ПараметрыХраненияФайла = ПараметрыХраненияДедуплицированногоФайла(
																			ДвоичныеДанные, 
																			ОбрабатываемыйФайл.ХранилищеДвоичныхДанных,
																			Перечисления.ВидыХраненияФайлов.ОперационноеХранение);

					Если ПараметрыХраненияФайла <> Неопределено Тогда
						НовыйТипХраненияФайла = ПараметрыХраненияФайла.ТипХраненияФайла;

						СведенияОТомеХранения = СведенияОТомеХранения();
						СведенияОТомеХранения.Ссылка				= ПараметрыХраненияФайла.Том;
						СведенияОТомеХранения.НовыйТипХраненияФайла = НовыйТипХраненияФайла;

						ПараметрыОперации.ПолученыПараметрыХранения = Истина;
					КонецЕсли;
				КонецЕсли;

				НовыйТипХраненияФайлаПредопределен = ЗначениеЗаполнено(НовыйТипХраненияФайла);

				Если Не НовыйТипХраненияФайлаПредопределен Тогда

					РазмерФайла = РазмерФайла(ИзвлеченныйФайл.ИмяФайла);
					// Дедуплицированный файл может переноситься в том, размещающий данный на диске. В этом случае для каждого
					// дедуплицированного файла на диск будет записана отдельная его копия. Поэтому при подборе тома хранения
					// учитывается весь необходимый объем свободного места
					СведенияДляПодбораТома.Размер = Макс(1, ОбрабатываемыйФайл.КоличествоДедуплицированныхФайлов) * РазмерФайла;

					НовыйТипХраненияФайла = РаботаСФайламиСлужебный.ТипХраненияФайла(СведенияДляПодбораТома.Размер, ОбрабатываемыйФайл.Расширение);

					Если НовыйТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе Тогда

						СведенияОТомеХранения = СведенияОТомеХранения();
						СведенияОТомеХранения.НовыйТипХраненияФайла = НовыйТипХраненияФайла;

					Иначе
						Если СведенияОПодобранномТомеХранения = Неопределено 
								Или СведенияОПодобранномТомеХранения.МаксимальныйРазмер <> 0 И СведенияОПодобранномТомеХранения.СвободныйОбъем - СведенияДляПодбораТома.Размер < 0 Тогда

							СведенияОПодобранномТомеХранения = ПодобратьТомХранения(СведенияДляПодбораТома, 
																					Перечисления.ВидыХраненияФайлов.ОперационноеХранение,
																					НовыйТипХраненияФайла);
						КонецЕсли;

						СведенияОТомеХранения = СведенияОПодобранномТомеХранения;

						// Если файл переносится в том, размещающий данные в ХДД, то объем тома должен быть уменьшен максимум на размер
						// одного файла, так как данные в таких томах дедуплицируются.
						Если СведенияОТомеХранения.НовыйТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
							РазмерФайла = СведенияДляПодбораТома.Размер;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;

				ПараметрыОперации.СведенияОТомеХранения = СведенияОТомеХранения;

				// @skip-check query-in-loop - Порционная обработка больших объемов данных.
				ВосстановитьФайлИзФайловогоАрхива(ОбрабатываемыйФайл, ИзвлеченныйФайл, ПараметрыОперации);

				Если Не НовыйТипХраненияФайлаПредопределен Тогда
					Если ЗначениеЗаполнено(НовыйТипХраненияФайла) И НовыйТипХраненияФайла <> Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе Тогда
						Если СведенияОТомеХранения.МаксимальныйРазмер <> 0 Тогда

							СведенияОТомеХранения.СвободныйОбъем = СведенияОТомеХранения.СвободныйОбъем - РазмерФайла;

						КонецЕсли;					
					КонецЕсли;
				КонецЕсли;

				ЗафиксироватьТранзакцию();

			Исключение

				ОтменитьТранзакцию();

				СтрокаИсключения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписатьВЖурналРегистрацииСобытиеПереносаФайлов(СтрокаИсключения, УровеньЖурналаРегистрации.Ошибка);

				УдалитьВременныйКаталог(ВременныйКаталог, СтрокаОписанияОшибки);
				Продолжить;
			КонецПопытки;

			УдалитьВременныйКаталог(ВременныйКаталог, СтрокаОписанияОшибки);

		КонецЦикла;

		ПоследняяСсылка = ОбрабатываемыйФайл.ПрисоединенныйФайл;

		// @skip-check query-in-loop - Порционная обработка больших объемов данных.
		ФайлыДляПереноса = СледующаяПорцияФайлов(МенеджерВременныхТаблиц, ПоследняяСсылка, ТекстЗапроса);

	КонецЦикла;	

КонецПроцедуры

Процедура ЗаполнитьВременнуюТаблицуДляВосстановленияФайлов(НастройкаПереносаФайлов, МассивИсключений, КоличествоДнейОперативногоХранения, МенеджерВременныхТаблиц)
	
	УстановитьПривилегированныйРежим(Истина);

	Если НастройкаПереносаФайлов.ЭтоДетальнаяНастройкаПереносаФайлов Тогда
		ВладелецФайла		= НастройкаПереносаФайлов.ИдентификаторВладельца;
		ЭлементИсключение	= НастройкаПереносаФайлов.ВладелецФайла;
	Иначе
		ВладелецФайла		= НастройкаПереносаФайлов.ВладелецФайла;
		ЭлементИсключение	= Неопределено;
	КонецЕсли;	

	ТекстЗапроса = ТекстЗапросаДляВосстановленияФайловИзФайловогоАрхива(ВладелецФайла, НастройкаПереносаФайлов, МассивИсключений, ЭлементИсключение);

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	Запрос.УстановитьПараметр("ТипВладельца"		, ТипЗнч(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецФайла, "ЗначениеПустойСсылки")));
	Запрос.УстановитьПараметр("КоличествоДней"		, КоличествоДнейОперативногоХранения);
	Запрос.УстановитьПараметр("ДатаОбработки"		, ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ВосстановитьВсеФайлы", НастройкаПереносаФайлов.Действие = Перечисления.ДействияВНастройкахРаботыСФайловымАрхивом.НеПереноситьВАрхив);

	Если МассивИсключений.Количество() > 0 Тогда

		Запрос.УстановитьПараметр("МассивИсключений", МассивИсключений);

	КонецЕсли;

	Если НастройкаПереносаФайлов.ЭтоДетальнаяНастройкаПереносаФайлов Тогда

		Запрос.УстановитьПараметр("ОтборПоДетализированнойНастройке", ЭлементИсключение);

	КонецЕсли;	

	Запрос.Выполнить();	

КонецПроцедуры

Функция ТекстЗапросаДляВосстановленияФайловИзФайловогоАрхива(ВладелецФайла, Настройка, МассивИсключений, ЭлементИсключение)

	РеквизитыСправочникаФайлов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Настройка.ТипВладельцаФайла, "ПолноеИмя, Имя");

	МетаданныеВладельцаФайлов = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ВладелецФайла);
	ПолноеИмяВладельцаФайлов = МетаданныеВладельцаФайлов.ПолноеИмя();

	МетаданныеОбъектаФайлов = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(Настройка.ТипВладельцаФайла);
	ЕстьВозможностьХранитьВерсии = ОбщегоНазначения.ЕстьРеквизитОбъекта("ТекущаяВерсия", МетаданныеОбъектаФайлов);

	ТекстыЗапросовПакета = Новый Массив;

	Если ЕстьВозможностьХранитьВерсии Тогда

		СправочникВерсийФайлов	= ОбщегоНазначения.ИдентификаторОбъектаМетаданных(МетаданныеОбъектаФайлов.Реквизиты.ТекущаяВерсия.Тип.Типы()[0]);
		МетаданныеВерсийФайлов	= ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(СправочникВерсийФайлов);

		ПолноеИмяСправочникаВерсийФайлов = МетаданныеВерсийФайлов.ПолноеИмя();

		ТекстЗапроса = 
           "ВЫБРАТЬ
           |	ВерсииФайлов.Ссылка КАК ПрисоединенныйФайл,
           |	ВерсииФайлов.ТипХраненияФайла КАК ТипХраненияФайла,
           |	ВерсииФайлов.Том КАК Том,
           |	ВерсииФайлов.ПутьКФайлу КАК ПутьКФайлу,
           |	ЕСТЬNULL(ХранилищеФайлов.ХранилищеДвоичныхДанных, ЗНАЧЕНИЕ(Справочник.ХранилищеДвоичныхДанных.ПустаяСсылка)) КАК ХранилищеДвоичныхДанных,
           |	ВерсииФайлов.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВТомахНаДиске) КАК ЭтоХранениеНаДиске,
           |	ВерсииФайлов.Наименование КАК Наименование,
           |	ВерсииФайлов.Расширение КАК Расширение,
           |	ВерсииФайлов.Владелец КАК ФайлДляЗаписиСведений,
           |	ВерсииФайлов.НомерВерсии КАК НомерВерсии,
           |	Файлы.ВладелецФайла КАК ВладелецФайла,
           |	ДОБАВИТЬКДАТЕ(ВерсииФайлов.ДатаСоздания, ДЕНЬ, &КоличествоДней) КАК ПлановаяДатаПереносаВАрхив,
           |	ВерсииФайлов.Представление КАК ФайлПредставление
           |ПОМЕСТИТЬ ВТ_ПодготовкаДанныхДляВосстановления
           |ИЗ
           |	#ПолноеИмяСправочникаФайлов КАК Файлы
           |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ПолноеИмяСправочникаВерсийФайлов КАК ВерсииФайлов
           |		ПО Файлы.Ссылка = ВерсииФайлов.Владелец
           |			И (НЕ Файлы.ПометкаУдаления)
           |			И (НЕ ВерсииФайлов.ПометкаУдаления)
           |			И (&ЭтоНеГруппа)
           |			И (ТИПЗНАЧЕНИЯ(Файлы.ВладелецФайла) = &ТипВладельца)
           |			И (&ОтборПоДетализированнойНастройке)
           |			И (&ОтборПоИсключениям)
           |			И (ВЫБОР
           |				КОГДА ВерсииФайлов.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВТомахНаДиске)
           |					ТОГДА (ВЫРАЗИТЬ(ВерсииФайлов.ПутьКФайлу КАК СТРОКА(100))) <> """"
           |							ИЛИ НЕ ВерсииФайлов.Том = ЗНАЧЕНИЕ(Справочник.ТомаХраненияФайлов.ПустаяСсылка)
           |				КОГДА ВерсииФайлов.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВоВнешнемХранилищеДвоичныхДанных)
           |					ТОГДА НЕ ВерсииФайлов.Том = ЗНАЧЕНИЕ(Справочник.ТомаХраненияФайлов.ПустаяСсылка)
           |				ИНАЧЕ ЛОЖЬ
           |			КОНЕЦ)
           |			И (ВерсииФайлов.ДатаПереносаВАрхив <> ДатаВремя(1,1,1))
           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
           |		ПО (ВерсииФайлов.Ссылка = ХранилищеФайлов.Файл)";

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ПолноеИмяСправочникаФайлов"		, РеквизитыСправочникаФайлов.ПолноеИмя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ПолноеИмяСправочникаВерсийФайлов", ПолноеИмяСправочникаВерсийФайлов);

		ТекстыЗапросовПакета.Добавить(ТекстЗапроса);

	Иначе // Не ЕстьВозможностьХранитьВерсии

		ТекстЗапроса = 
           "ВЫБРАТЬ
           |	Файлы.Ссылка КАК ПрисоединенныйФайл,
           |	Файлы.ТипХраненияФайла КАК ТипХраненияФайла,
           |	Файлы.Том КАК Том,
           |	Файлы.ПутьКФайлу КАК ПутьКФайлу,
           |	ЕСТЬNULL(ХранилищеФайлов.ХранилищеДвоичныхДанных, ЗНАЧЕНИЕ(Справочник.ХранилищеДвоичныхДанных.ПустаяСсылка)) КАК ХранилищеДвоичныхДанных,
           |	Файлы.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВТомахНаДиске) КАК ЭтоХранениеНаДиске,
           |	Файлы.Наименование КАК Наименование,
           |	Файлы.Расширение КАК Расширение,
           |	Файлы.Ссылка КАК ФайлДляЗаписиСведений,
           |	0 КАК НомерВерсии,
           |	Файлы.ВладелецФайла КАК ВладелецФайла,
           |	ДОБАВИТЬКДАТЕ(Файлы.ДатаСоздания, ДЕНЬ, &КоличествоДней) КАК ПлановаяДатаПереносаВАрхив,
           |	Файлы.Представление КАК ФайлПредставление
           |ПОМЕСТИТЬ ВТ_ПодготовкаДанныхДляВосстановления
           |ИЗ
           |	#ТипВладельцаФайла КАК Файлы
           |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #СправочникВладелецФайла КАК СправочникВладелецФайла
           |		ПО Файлы.ВладелецФайла = СправочникВладелецФайла.Ссылка
           |			И (НЕ СправочникВладелецФайла.ПометкаУдаления)
           |			И (НЕ Файлы.ПометкаУдаления)
           |			И (&ЭтоНеГруппа)
           |			И (ТИПЗНАЧЕНИЯ(Файлы.ВладелецФайла) = &ТипВладельца)
           |			И (&ОтборПоДетализированнойНастройке)
           |			И (&ОтборПоИсключениям)
           |			И (ВЫБОР
           |				КОГДА Файлы.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВТомахНаДиске)
           |					ТОГДА (ВЫРАЗИТЬ(Файлы.ПутьКФайлу КАК СТРОКА(100))) <> """"
           |							ИЛИ НЕ Файлы.Том = ЗНАЧЕНИЕ(Справочник.ТомаХраненияФайлов.ПустаяСсылка)
           |				КОГДА Файлы.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВоВнешнемХранилищеДвоичныхДанных)
           |					ТОГДА НЕ Файлы.Том = ЗНАЧЕНИЕ(Справочник.ТомаХраненияФайлов.ПустаяСсылка)
           |				ИНАЧЕ ЛОЖЬ
           |			КОНЕЦ)
           |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОФайлах КАК СведенияОФайлах
           |		ПО Файлы.Ссылка = СведенияОФайлах.Файл
           |			И (СведенияОФайлах.ДатаПереносаВАрхив <> ДатаВремя(1,1,1))
           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
           |		ПО Файлы.Ссылка = ХранилищеФайлов.Файл";

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТипВладельцаФайла", "Справочник." + РеквизитыСправочникаФайлов.Имя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#СправочникВладелецФайла", ПолноеИмяВладельцаФайлов);		   

		ТекстыЗапросовПакета.Добавить(ТекстЗапроса);

	КонецЕсли;	

	ТекстЗапроса = 
           "ВЫБРАТЬ РАЗЛИЧНЫЕ
           |	ВТ_ПодготовкаДанныхДляВосстановления.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанных
           |ПОМЕСТИТЬ ВТ_ДанныеДляПроверкиДедупликации
           |ИЗ
           |	ВТ_ПодготовкаДанныхДляВосстановления КАК ВТ_ПодготовкаДанныхДляВосстановления
           |ГДЕ
           |	НЕ ВТ_ПодготовкаДанныхДляВосстановления.ЭтоХранениеНаДиске
           |
           |ИНДЕКСИРОВАТЬ ПО
           |	ВТ_ПодготовкаДанныхДляВосстановления.ХранилищеДвоичныхДанных
           |;
           |
           |////////////////////////////////////////////////////////////////////////////////
           |ВЫБРАТЬ
           |	ХранилищеФайлов.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанных,
           |	КОЛИЧЕСТВО(*) КАК КоличествоДедуплицированныхФайлов,
           |	КОЛИЧЕСТВО(*) - СУММА(ВЫБОР
           |			КОГДА ЕСТЬNULL(СведенияОФайлах.ДатаПереносаВАрхив, ЕСТЬNULL(ВерсииФайлов.ДатаПереносаВАрхив, ДАТАВРЕМЯ(1, 1, 1))) <> ДАТАВРЕМЯ(1, 1, 1)
           |				ТОГДА ВЫРАЗИТЬ(1 КАК ЧИСЛО(17, 0))
           |			ИНАЧЕ 0
           |		КОНЕЦ) > 0 КАК ПодлежитВосстановлениюИзЗаНовыхФайлов
           |ПОМЕСТИТЬ ВТ_ДедуплицированныеФайлы
           |ИЗ
           |	ВТ_ДанныеДляПроверкиДедупликации КАК ВТ_ДанныеДляПроверкиДедупликации
           |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
           |		ПО ВТ_ДанныеДляПроверкиДедупликации.ХранилищеДвоичныхДанных = ХранилищеФайлов.ХранилищеДвоичныхДанных
           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОФайлах КАК СведенияОФайлах
           |		ПО (ХранилищеФайлов.Файл = СведенияОФайлах.Файл)
           |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВерсииФайлов КАК ВерсииФайлов
           |		ПО (ХранилищеФайлов.Файл = ВерсииФайлов.Ссылка)
           |			И (НЕ ВерсииФайлов.Владелец.ПометкаУдаления)
           |ГДЕ
           |	НЕ ЕСТЬNULL(СведенияОФайлах.ПометкаУдаления, ЕСТЬNULL(ВерсииФайлов.ПометкаУдаления, ЛОЖЬ))
           |
           |СГРУППИРОВАТЬ ПО
           |	ХранилищеФайлов.ХранилищеДвоичныхДанных
           |
           |ИМЕЮЩИЕ
           |	КОЛИЧЕСТВО(*) > 1
           |
           |ИНДЕКСИРОВАТЬ ПО
           |	ХранилищеФайлов.ХранилищеДвоичныхДанных
           |;
           |
           |////////////////////////////////////////////////////////////////////////////////
           |ВЫБРАТЬ
           |	ВТ_ПодготовкаДанныхДляВосстановления.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
           |	ВТ_ПодготовкаДанныхДляВосстановления.ТипХраненияФайла КАК ТипХраненияФайла,
           |	ВТ_ПодготовкаДанныхДляВосстановления.Том КАК Том,
           |	ВТ_ПодготовкаДанныхДляВосстановления.ПутьКФайлу КАК ПутьКФайлу,
           |	ВТ_ПодготовкаДанныхДляВосстановления.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанных,
           |	ВТ_ПодготовкаДанныхДляВосстановления.Наименование КАК Наименование,
           |	ВТ_ПодготовкаДанныхДляВосстановления.Расширение КАК Расширение,
           |	ВТ_ПодготовкаДанныхДляВосстановления.ФайлДляЗаписиСведений КАК ФайлДляЗаписиСведений,
           |	ВТ_ПодготовкаДанныхДляВосстановления.НомерВерсии КАК НомерВерсии,
           |	ВТ_ПодготовкаДанныхДляВосстановления.ВладелецФайла КАК ВладелецФайла,
           |	НЕ ВТ_ПодготовкаДанныхДляВосстановления.ЭтоХранениеНаДиске КАК ПолучатьДвоичныеДанные,
           |	ЕСТЬNULL(ВТ_ДедуплицированныеФайлы.КоличествоДедуплицированныхФайлов, 0) КАК КоличествоДедуплицированныхФайлов,
           |	ЕСТЬNULL(СведенияОХраненииДедуплицированныхФайлов.ТипХраненияФайла, ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ПустаяСсылка)) КАК ТипХраненияФайлаОперативный,
           |	ЕСТЬNULL(СведенияОХраненииДедуплицированныхФайлов.Том, ЗНАЧЕНИЕ(Справочник.ТомаХраненияФайлов.ПустаяСсылка)) КАК ТомОперативный,
           |	"""" КАК ПутьКФайлуОперативный,
           |	ВТ_ПодготовкаДанныхДляВосстановления.ФайлПредставление КАК ФайлПредставление
           |ПОМЕСТИТЬ ВТ_ДанныеДляПереноса
           |ИЗ
           |	ВТ_ПодготовкаДанныхДляВосстановления КАК ВТ_ПодготовкаДанныхДляВосстановления
           |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДедуплицированныеФайлы КАК ВТ_ДедуплицированныеФайлы
           |		ПО ВТ_ПодготовкаДанныхДляВосстановления.ХранилищеДвоичныхДанных = ВТ_ДедуплицированныеФайлы.ХранилищеДвоичныхДанных
           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОХраненииДедуплицированныхФайлов КАК СведенияОХраненииДедуплицированныхФайлов
           |		ПО ВТ_ПодготовкаДанныхДляВосстановления.ХранилищеДвоичныхДанных = СведенияОХраненииДедуплицированныхФайлов.ХранилищеДвоичныхДанных
           |			И (СведенияОХраненииДедуплицированныхФайлов.ВидХраненияФайлов = ЗНАЧЕНИЕ(Перечисление.ВидыХраненияФайлов.ОперационноеХранение))
           |ГДЕ
           |	(&ВосстановитьВсеФайлы
           |			ИЛИ ЕСТЬNULL(ВТ_ДедуплицированныеФайлы.ПодлежитВосстановлениюИзЗаНовыхФайлов, ЛОЖЬ)
           |			ИЛИ ВТ_ПодготовкаДанныхДляВосстановления.ПлановаяДатаПереносаВАрхив > &ДатаОбработки)
           |;
           |
           |////////////////////////////////////////////////////////////////////////////////
           |УНИЧТОЖИТЬ ВТ_ДанныеДляПроверкиДедупликации
           |;
           |
           |////////////////////////////////////////////////////////////////////////////////
           |УНИЧТОЖИТЬ ВТ_ДедуплицированныеФайлы
           |;
           |
           |////////////////////////////////////////////////////////////////////////////////
           |УНИЧТОЖИТЬ ВТ_ПодготовкаДанныхДляВосстановления";

	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);

	ТекстЗапроса = СтрСоединить(ТекстыЗапросовПакета, ОбщегоНазначения.РазделительПакетаЗапросов());

	Если МассивИсключений.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоИсключениям", "НЕ Файлы.ВладелецФайла В ИЕРАРХИИ (&МассивИсключений)");
	Иначе 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоИсключениям", "ИСТИНА");
	КонецЕсли;

	Если ЭлементИсключение <> Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоДетализированнойНастройке", "Файлы.ВладелецФайла В ИЕРАРХИИ (&ОтборПоДетализированнойНастройке)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоДетализированнойНастройке", "ИСТИНА");
	КонецЕсли;

	Если МетаданныеОбъектаФайлов.Иерархический Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЭтоНеГруппа", "НЕ Файлы.ЭтоГруппа");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЭтоНеГруппа", "ИСТИНА");
	КонецЕсли;

	Возврат ТекстЗапроса;

КонецФункции

Процедура ПеренестиФайлыВФайловыйАрхив(Настройка, МассивИсключений, КоличествоДнейОперативногоХранения, ДополнительныеПараметрыПереноса)

	Если Настройка.Действие = Перечисления.ДействияВНастройкахРаботыСФайловымАрхивом.НеПереноситьВАрхив Тогда
		Возврат;
	КонецЕсли;

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	ЗаполнениеВременнойТаблицыДляПереносаФайлов(Настройка, МассивИсключений, КоличествоДнейОперативногоХранения, МенеджерВременныхТаблиц);

	СведенияДляПодбораТома	= СведенияОФайлахДляПодбораТома();
	СведенияОТомеХранения	= Неопределено;

	ПоследняяСсылка = ПустаяСсылкаНаПрисоединенныйФайл(Настройка);

	ФайлыДляПереноса = СледующаяПорцияФайлов(МенеджерВременныхТаблиц, ПоследняяСсылка, ДополнительныеПараметрыПереноса.ТекстЗапросаПолученияПорцииФайловДляПереносаВАрхив);

	Пока ФайлыДляПереноса.Количество() > 0 Цикл

		СтрокаОписанияОшибки = СтрокаОписанияОшибкиПриОперацияхСВременнымКаталогом(Истина);

		Для Каждого ОбрабатываемыйФайл Из ФайлыДляПереноса Цикл

			Если ОбрабатываемыйФайл.КоличествоДедуплицированныхФайлов > 0 Тогда
				Если НЕ ОбрабатываемыйФайл.ПодлежитУсловномуПереносу Тогда
					Если ОбрабатываемыйФайл.ФайлРедактируется Тогда
						Продолжить;
					КонецЕсли;
					
					//@skip-warning запрос выполняется не для всех итераций цикла.
					Если ФайлПеренесенВФайловыйАрхив(ОбрабатываемыйФайл.ПрисоединенныйФайл) Тогда
						Продолжить;
					КонецЕсли;
				ИначеЕсли ОбрабатываемыйФайл.УсловноПеренесенВАрхив Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;

			ВременныйКаталог = НовыйВременныйКаталогДляРаботыСZipАрхивом(СтрокаОписанияОшибки);
			
			ПрисоединенныйФайлСсылка = ОбрабатываемыйФайл.ПрисоединенныйФайл;
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = Блокировка.Добавить(ПрисоединенныйФайлСсылка.Метаданные().ПолноеИмя());
			ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", ПрисоединенныйФайлСсылка);
			
			ЭлементБлокировкиДанных = Блокировка.Добавить(Метаданные.РегистрыСведений.СведенияОФайлах.ПолноеИмя());
			ЭлементБлокировкиДанных.УстановитьЗначение("Файл", ПрисоединенныйФайлСсылка);
			
			ЭлементБлокировкиДанных = Блокировка.Добавить(Метаданные.РегистрыСведений.ХранилищеФайлов.ПолноеИмя());
			ЭлементБлокировкиДанных.УстановитьЗначение("Файл", ПрисоединенныйФайлСсылка);

			НачатьТранзакцию();			
			Попытка   

				Блокировка.Заблокировать();

				Если ОбрабатываемыйФайл.ПодлежитУсловномуПереносу Тогда

					Если Не ОбрабатываемыйФайл.УсловноПеренесенВАрхив Тогда

						ВыполнитьУсловныйПереносФайла(ОбрабатываемыйФайл);

					КонецЕсли;

				Иначе			

					ПараметрыОперации = ПараметрыВыполненияОперацииПереноса();
					ПараметрыОперации.ВременныйКаталог	= ВременныйКаталог;

					НовыйZipАрхив = ZipАрхивФайлаПодлежащегоПереносу(ОбрабатываемыйФайл, ПараметрыОперации);

					РазмерФайла = НовыйZipАрхив.Размер;
					// Дедуплицированный файл может переноситься в том, размещающий данный на диске. В этом случае для каждого
					// дедуплицированного файла на диск будет записана отдельная его копия. Поэтому при подборе тома хранения
					// учитывается весь необходимый объем свободного места
					СведенияДляПодбораТома.Размер = Макс(1, ОбрабатываемыйФайл.КоличествоДедуплицированныхФайлов) * РазмерФайла;

					Если СведенияОТомеХранения = Неопределено 
							Или СведенияОТомеХранения.МаксимальныйРазмер <> 0 И СведенияОТомеХранения.СвободныйОбъем - СведенияДляПодбораТома.Размер < 0 Тогда
						// В файловом архиве могут быть только тома на дисках или том во внешнем хранилище S3, поэтому использовано значение "ВТомахТомНеОпределен"
						СведенияОТомеХранения = ПодобратьТомХранения(СведенияДляПодбораТома, 
																	Перечисления.ВидыХраненияФайлов.АрхивноеХранение,
																	Перечисления.ТипыХраненияФайлов.ВТомахТомНеОпределен);
					КонецЕсли;

					ПараметрыОперации.СведенияОТомеХранения = СведенияОТомеХранения;

					// @skip-check query-in-loop - Порционная обработка больших объемов данных.
					ПеренестиФайлВФайловыйАрхив(ОбрабатываемыйФайл, НовыйZipАрхив, ПараметрыОперации);

					Если СведенияОТомеХранения.МаксимальныйРазмер <> 0 Тогда

						// Если файл переносится в том, размещающий данные в ХДД, то объем тома должен быть уменьшен максимум на размер
						// одного файла, так как данные в таких томах дедуплицируются.
						Если СведенияОТомеХранения.НовыйТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
							РазмерФайла = СведенияДляПодбораТома.Размер;
						КонецЕсли;

						СведенияОТомеХранения.СвободныйОбъем = СведенияОТомеХранения.СвободныйОбъем - РазмерФайла;

					КонецЕсли;				
				КонецЕсли;
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();

				СтрокаИсключения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписатьВЖурналРегистрацииСобытиеПереносаФайлов(СтрокаИсключения, УровеньЖурналаРегистрации.Ошибка);
				
				УдалитьВременныйКаталог(ВременныйКаталог, СтрокаОписанияОшибки);
				Продолжить;				
			КонецПопытки;
		КонецЦикла;

		УдалитьВременныйКаталог(ВременныйКаталог, СтрокаОписанияОшибки);

		ПоследняяСсылка = ОбрабатываемыйФайл.ПрисоединенныйФайл;

		// @skip-check query-in-loop - Порционная обработка больших объемов данных.
		ФайлыДляПереноса = СледующаяПорцияФайлов(МенеджерВременныхТаблиц, ПоследняяСсылка, ДополнительныеПараметрыПереноса.ТекстЗапросаПолученияПорцииФайловДляПереносаВАрхив);

	КонецЦикла;

КонецПроцедуры

Функция СведенияОТомеХранения()

	Результат = Новый Структура;
	Результат.Вставить("Ссылка"					, Справочники.ТомаХраненияФайлов.ПустаяСсылка());
	Результат.Вставить("МаксимальныйРазмер"		, 0);
	Результат.Вставить("СвободныйОбъем"			, 0);
	Результат.Вставить("ПутьКТому"				, "");
	Результат.Вставить("НовыйТипХраненияФайла"	, Перечисления.ТипыХраненияФайлов.ПустаяСсылка());

	Возврат Результат;

КонецФункции

Функция ПодобратьТомХранения(ПрисоединенныйФайл, ВидТомаХраненияФайлов, ТипХраненияФайла)

	НовыйТипХраненияФайла = ТипХраненияФайла;

	Результат = СведенияОТомеХранения();

	СвободныйТом = СвободныйТомХраненияПоТипуХраненияФайлов(ПрисоединенныйФайл, НовыйТипХраненияФайла, ВидТомаХраненияФайлов);
	Если НовыйТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахТомНеОпределен Тогда
		НовыйТипХраненияФайла = ТипХраненияПоТомуХраненияФайлов(СвободныйТом);
	КонецЕсли;	

	ЗначенияРеквизитовТома = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СвободныйТом, "Ссылка, МаксимальныйРазмер");

	Результат.Ссылка				= ЗначенияРеквизитовТома.Ссылка;
	Результат.МаксимальныйРазмер	= ЗначенияРеквизитовТома.МаксимальныйРазмер;

	Если Результат.МаксимальныйРазмер <> 0 Тогда

		Результат.СвободныйОбъем = Результат.МаксимальныйРазмер * 1024 * 1024 - РаботаСФайламиВТомахСлужебный.ОбъемТома(СвободныйТом);

	КонецЕсли;

	Результат.НовыйТипХраненияФайла = НовыйТипХраненияФайла;
	Если НовыйТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
		Результат.ПутьКТому = РаботаСФайламиВТомахСлужебный.ПолныйПутьТома(СвободныйТом);
	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура ВосстановитьФайлИзФайловогоАрхива(ОбрабатываемыйФайл, ИзвлеченныйФайл, ПараметрыОперации)

	ИзменяемыеРеквизиты = Новый Структура;
	ИзменяемыеРеквизиты.Вставить("Том"						, ПараметрыОперации.СведенияОТомеХранения.Ссылка);
	ИзменяемыеРеквизиты.Вставить("ТипХраненияФайла"			, ПараметрыОперации.СведенияОТомеХранения.НовыйТипХраненияФайла);
	ИзменяемыеРеквизиты.Вставить("УсловноПеренесенВАрхив"	, Ложь); // Всегда сбрасывать признак условного переноса
	ИзменяемыеРеквизиты.Вставить("ДатаПереносаВАрхив"		, Дата(1,1,1));	

	Если ПараметрыОперации.СведенияОТомеХранения.НовыйТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда

		Если ОбрабатываемыйФайл.КоличествоДедуплицированныхФайлов = 0 Тогда

			ВосстановитьИзвлеченныйФайлВОперативныйТомНаДиске(ОбрабатываемыйФайл, ИзвлеченныйФайл, ПараметрыОперации);

			УдалятьИсходныйФайл = Ложь;

			Если ОбрабатываемыйФайл.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
				УдалятьИсходныйФайл = Истина;
			Иначе
				РегистрыСведений.ХранилищеФайлов.УдалитьДвоичныеДанные(ОбрабатываемыйФайл.ПрисоединенныйФайл);
			КонецЕсли;

			ИзменяемыеРеквизиты.Вставить("ПутьКФайлу", ПараметрыОперации.НовыйПутьКФайлуВнутриТома);
			ИзменитьРеквизитыПрисоединенногоФайла(ОбрабатываемыйФайл, ИзменяемыеРеквизиты);

			Если УдалятьИсходныйФайл Тогда
				РаботаСФайламиВТомахСлужебный.УдалитьФайл(ПараметрыОперации.ИсходныйПутьКФайлу);
			КонецЕсли;

		Иначе

			ДедуплицированныеФайлы = ДедуплицированныеФайлыПоХранилищуДвоичныхДанных(ОбрабатываемыйФайл.ХранилищеДвоичныхДанных, Истина);

			Для Каждого ДедуплицированныйФайл Из ДедуплицированныеФайлы Цикл

				ВосстановитьИзвлеченныйФайлВОперативныйТомНаДиске(ДедуплицированныйФайл, ИзвлеченныйФайл, ПараметрыОперации);
				РегистрыСведений.ХранилищеФайлов.УдалитьДвоичныеДанные(ДедуплицированныйФайл.ПрисоединенныйФайл);

				ИзменяемыеРеквизиты.Вставить("ПутьКФайлу", ПараметрыОперации.НовыйПутьКФайлуВнутриТома);
				ИзменитьРеквизитыПрисоединенногоФайла(ДедуплицированныйФайл, ИзменяемыеРеквизиты);

			КонецЦикла;
		КонецЕсли;

	Иначе

		ИзменяемыеРеквизиты.Вставить("ПутьКФайлу", "");
		ДвоичныеДанныеОбрабатываемогоФайла = Новый ДвоичныеДанные(ИзвлеченныйФайл.ИмяФайла);

		ПараметрыЗаписиВИБ = ПараметрыЗаписиВИнформационнуюБазу();
		ЗаполнитьЗначенияСвойств(ПараметрыЗаписиВИБ, ИзменяемыеРеквизиты);

		Если ОбрабатываемыйФайл.КоличествоДедуплицированныхФайлов = 0 Тогда

			УдалятьИсходныйФайл = Ложь;			

			Если ОбрабатываемыйФайл.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда

				Если ПараметрыОперации.ПолученыПараметрыХранения Тогда
					// Ранее уже были получены параметры оперативного хранения, поэтому достаточно изменить соответствующие реквизиты
					// присоединенного файла. И добавить запись в регистр сведений ХранилищеФайлов. Двоичные данные при этом не изменяются.
					ПараметрыЗаписиВИБ.НеИзменятьДвоичныеДанные = Истина;
				КонецЕсли;

				РегистрыСведений.ХранилищеФайлов.ЗаписатьДвоичныеДанные(ОбрабатываемыйФайл.ПрисоединенныйФайл, ДвоичныеДанныеОбрабатываемогоФайла, ПараметрыЗаписиВИБ);

				УдалятьИсходныйФайл = Истина;
			Иначе
				ВосстановитьФайлВОперативноеХранилище(ОбрабатываемыйФайл.ХранилищеДвоичныхДанных, ДвоичныеДанныеОбрабатываемогоФайла, ПараметрыОперации);
			КонецЕсли;

			ИзменитьРеквизитыПрисоединенногоФайла(ОбрабатываемыйФайл, ИзменяемыеРеквизиты);

			Если УдалятьИсходныйФайл Тогда
				РаботаСФайламиВТомахСлужебный.УдалитьФайл(ПараметрыОперации.ИсходныйПутьКФайлу);
			КонецЕсли;
			
		Иначе

			ВосстановитьФайлВОперативноеХранилище(ОбрабатываемыйФайл.ХранилищеДвоичныхДанных, ДвоичныеДанныеОбрабатываемогоФайла, ПараметрыОперации);

			ДедуплицированныеФайлы = ДедуплицированныеФайлыПоХранилищуДвоичныхДанных(ОбрабатываемыйФайл.ХранилищеДвоичныхДанных, Истина);

			Для Каждого ДедуплицированныйФайл Из ДедуплицированныеФайлы Цикл
				ИзменитьРеквизитыПрисоединенногоФайла(ДедуплицированныйФайл, ИзменяемыеРеквизиты);
			КонецЦикла;
		КонецЕсли
	КонецЕсли;

КонецПроцедуры

Процедура ВосстановитьФайлВОперативноеХранилище(ХранилищеДвоичныхДанных, ДвоичныеДанные, ПараметрыОперации)

	ТипХраненияФайла = ПараметрыОперации.СведенияОТомеХранения.НовыйТипХраненияФайла;

	ЗначенияРеквизитовХранения = Новый Структура;
	ЗначенияРеквизитовХранения.Вставить("ДвоичныеДанныеВАрхиве", Неопределено);	

	Если Не ПараметрыОперации.ПолученыПараметрыХранения Тогда

		ДобавитьИзменяемыйРеквизитХраненияДвоичныхДанных(ЗначенияРеквизитовХранения, ТипХраненияФайла, ДвоичныеДанные);

		ПараметрыЗаписиВИБ = ПараметрыЗаписиВИнформационнуюБазу();	
		ПараметрыЗаписиВИБ.ТипХраненияФайла			= ТипХраненияФайла;
		ПараметрыЗаписиВИБ.Том						= ПараметрыОперации.СведенияОТомеХранения.Ссылка;

		РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов.ДобавитьЗапись(ХранилищеДвоичныхДанных, ПараметрыЗаписиВИБ);
	КонецЕсли;

	ЗаписатьДвоичныеДанныеВИнформационнуюБазу(ХранилищеДвоичныхДанных, ЗначенияРеквизитовХранения);	

	РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов.УдалитьЗапись(ХранилищеДвоичныхДанных, Перечисления.ВидыХраненияФайлов.АрхивноеХранение);												

КонецПроцедуры

Процедура ПеренестиФайлВФайловыйАрхив(ОбрабатываемыйФайл, ZipАрхив, ПараметрыОперации)

	ИзменяемыеРеквизиты = Новый Структура;
	ИзменяемыеРеквизиты.Вставить("Том"						, ПараметрыОперации.СведенияОТомеХранения.Ссылка);
	ИзменяемыеРеквизиты.Вставить("ТипХраненияФайла"			, ПараметрыОперации.СведенияОТомеХранения.НовыйТипХраненияФайла);
	ИзменяемыеРеквизиты.Вставить("УсловноПеренесенВАрхив"	, Ложь);
	ИзменяемыеРеквизиты.Вставить("ДатаПереносаВАрхив"		, ТекущаяДатаСеанса());	

	Если ПараметрыОперации.СведенияОТомеХранения.НовыйТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда

		Если ОбрабатываемыйФайл.КоличествоДедуплицированныхФайлов = 0 Тогда

			ПеренестиZipАрхивВАрхивныйТомНаДиске(ОбрабатываемыйФайл, ZipАрхив, ПараметрыОперации);			

			УдалятьИсходныйФайл = Ложь;
			Если ОбрабатываемыйФайл.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
				УдалятьИсходныйФайл = Истина;
			Иначе
				РегистрыСведений.ХранилищеФайлов.УдалитьДвоичныеДанные(ОбрабатываемыйФайл.ПрисоединенныйФайл);
			КонецЕсли;

			ИзменяемыеРеквизиты.Вставить("ПутьКФайлу", ПараметрыОперации.НовыйПутьКФайлуВнутриТома);
			ИзменитьРеквизитыПрисоединенногоФайла(ОбрабатываемыйФайл, ИзменяемыеРеквизиты);

			Если УдалятьИсходныйФайл Тогда
				РаботаСФайламиВТомахСлужебный.УдалитьФайл(ПараметрыОперации.ИсходныйПутьКФайлу);
			КонецЕсли;
		Иначе

			ДедуплицированныеФайлы = ДедуплицированныеФайлыПоХранилищуДвоичныхДанных(ОбрабатываемыйФайл.ХранилищеДвоичныхДанных);

			Для Каждого ДедуплицированныйФайл Из ДедуплицированныеФайлы Цикл

				ПеренестиZipАрхивВАрхивныйТомНаДиске(ДедуплицированныйФайл, ZipАрхив, ПараметрыОперации);
				РегистрыСведений.ХранилищеФайлов.УдалитьДвоичныеДанные(ДедуплицированныйФайл.ПрисоединенныйФайл);

				ИзменяемыеРеквизиты.Вставить("ПутьКФайлу", ПараметрыОперации.НовыйПутьКФайлуВнутриТома);				
				ИзменитьРеквизитыПрисоединенногоФайла(ДедуплицированныйФайл, ИзменяемыеРеквизиты);

			КонецЦикла;
		КонецЕсли;
	Иначе
        // Перенос в архивное хранилище S3
		// Если файл хранился во внутренних хранилищах, то просто изменяется реквизит хранения
		// Если файл хранился на диске, то нужно найти или создать ХранилищеДвоичных данных и добавить запись в регистр
		// сведений ХранилищеФайлов

		ИзменяемыеРеквизиты.Вставить("ПутьКФайлу", "");

		Если ОбрабатываемыйФайл.КоличествоДедуплицированныхФайлов = 0 Тогда

			УдалятьИсходныйФайл = Ложь;

			Если ОбрабатываемыйФайл.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
				// Перенос с диска во внутреннее хранилище. Требуется проверка наличия дедуплицированных элементов
				ДвоичныеДанныеОбрабатываемогоФайла = Новый ДвоичныеДанные(ПараметрыОперации.ИсходныйПутьКФайлу);
				ХранилищеДвоичныхДанныхСсылка = ЭлементХранилищаДвоичныхДанныхПоДвоичнымДанным(ДвоичныеДанныеОбрабатываемогоФайла);

				Если ХранилищеДвоичныхДанныхСсылка <> Неопределено Тогда
					Если ХранилищеДвоичныхДанныхПрисутствуетВФайловомАрхиве(ХранилищеДвоичныхДанныхСсылка) Тогда
						// Общее хранилище двоичных данных перенесено в архив, поэтому выполнения переноса не требуется.
						// У присоединенного файла изменяются параметры хранения данными полученными из регистра сведений СведенияОХраненииДедуплицированныхФайлов
						// атрибут ДатаПереносаВАрхив заполняется текущей датой сеанса.
						ВидХраненияДедуплицированныхФайлов = Перечисления.ВидыХраненияФайлов.АрхивноеХранение;
					Иначе
						// Общее хранилище двоичных данных отсутствует в архиве, поэтому выполнения переноса не требуется.
						// У присоединенного файла изменяются параметры хранения данными полученными из регистра сведений СведенияОХраненииДедуплицированныхФайлов
						// атрибут ДатаПереносаВАрхив очищается, а в УсловноПеренесенВАрхив устанавливается Истина.
						ВидХраненияДедуплицированныхФайлов = Перечисления.ВидыХраненияФайлов.ОперационноеХранение;
						ИзменяемыеРеквизиты.Вставить("УсловноПеренесенВАрхив"	, Истина);
						ИзменяемыеРеквизиты.Вставить("ДатаПереносаВАрхив"		, Дата(1,1,1));
					КонецЕсли;

					СведенияОХраненииФайлов = РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов.СведенияОХраненииДедуплицированныхФайлов(
																								ХранилищеДвоичныхДанныхСсылка,
																								ВидХраненияДедуплицированныхФайлов);

					Если СведенияОХраненииФайлов = Неопределено Тогда

						ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
														"ru = 'В регистре сведений ""Сведения о хранении дедуплицированных файлов"" отсутствуют данные для 
														|элемента справочника ""Хранилище двоичных данных"": %1 по виду хранения файлов: %2'"),
														ХранилищеДвоичныхДанныхСсылка,
														ВидХраненияДедуплицированныхФайлов);
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(ИзменяемыеРеквизиты, СведенияОХраненииФайлов);
					
					РегистрыСведений.ХранилищеФайлов.ДобавитьЗапись(ОбрабатываемыйФайл.ПрисоединенныйФайл, ХранилищеДвоичныхДанныхСсылка);
					
				Иначе
					// Создать новый элемент справочника ХранилищеДвоичныхДанных, для которого двоичные данные записываются в файловый архив.
					ПараметрыЗаписиВИБ = ПараметрыЗаписиВИнформационнуюБазу();
					ЗаполнитьЗначенияСвойств(ПараметрыЗаписиВИБ,ИзменяемыеРеквизиты);
					ПараметрыЗаписиВИБ.ЭтоЗаписьВФайловыйАрхив = Истина;
					ПараметрыЗаписиВИБ.ДвоичныеДанныеАрхива = Новый ДвоичныеДанные(ZipАрхив.ИмяФайла);
					
					РегистрыСведений.ХранилищеФайлов.ЗаписатьДвоичныеДанные(ОбрабатываемыйФайл.ПрисоединенныйФайл, ДвоичныеДанныеОбрабатываемогоФайла, ПараметрыЗаписиВИБ);
				КонецЕсли;

				УдалятьИсходныйФайл = Истина;
			Иначе
				// Для хранения на дисках не поддерживается дедупликация, поэтому для файла с другим типом хранения уже создан
				// элемент справочника ХранилищеДвоичныхДанных Требуется перенести данные из реквизита оперативного хранения в
				// реквизит архивного хранения и изменить атрибуты присоединенного файла.
				ПеренестиZipАрхивВАрхивныйТомХранилищеS3(ОбрабатываемыйФайл, ZipАрхив, ПараметрыОперации);
			КонецЕсли;

			ИзменитьРеквизитыПрисоединенногоФайла(ОбрабатываемыйФайл, ИзменяемыеРеквизиты);

			Если УдалятьИсходныйФайл Тогда
				РаботаСФайламиВТомахСлужебный.УдалитьФайл(ПараметрыОперации.ИсходныйПутьКФайлу);
			КонецЕсли;

		Иначе
			// Дедупликация не поддерживается для хранения на дисках, поэтому наличие других дедуплицированных файлов говорит о
			// том, что уже создан элемент справочника ХранилищеДвоичныхДанных. Поэтому нужно перенести двоичные данные из
			// реквизита оперативного хранения в реквизит архивного хранения. Также нужно изменить атрибуты всех
			// дедуплицированных файлов - заполнить параметры хранения и атрибут ДатаПереносаВАрхив.
			ПеренестиZipАрхивВАрхивныйТомХранилищеS3(ОбрабатываемыйФайл, ZipАрхив, ПараметрыОперации);

			ДедуплицированныеФайлы = ДедуплицированныеФайлыПоХранилищуДвоичныхДанных(ОбрабатываемыйФайл.ХранилищеДвоичныхДанных);

			Для Каждого ДедуплицированныйФайл Из ДедуплицированныеФайлы Цикл

				ИзменитьРеквизитыПрисоединенногоФайла(ДедуплицированныйФайл, ИзменяемыеРеквизиты);

			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция ПараметрыВыполненияОперацииПереноса()

	Результат = Новый Структура;
	Результат.Вставить("СведенияОТомеХранения");
	Результат.Вставить("ИмяZipАрхиваБезРасширения"	, "");
	Результат.Вставить("РасширениеZipАрхива"		, "");
	Результат.Вставить("НовыйПутьКФайлуВнутриТома"	, "");
	Результат.Вставить("ИсходныйПутьКФайлу"			, "");
	Результат.Вставить("ВременныйКаталог"			, "");

	Возврат Результат;

КонецФункции

Функция ПараметрыВыполненияОперацииВосстановления()

	Результат = Новый Структура;
	Результат.Вставить("СведенияОТомеХранения");
	Результат.Вставить("НовыйПутьКФайлуВнутриТома"		, "");
	Результат.Вставить("ИсходныйПутьКФайлу"				, "");
	Результат.Вставить("ВременныйКаталог"				, "");
	Результат.Вставить("ПолученыПараметрыХранения"		, Ложь);
	Результат.Вставить("СпособХраненияФайлов"			, "");

	Возврат Результат;

КонецФункции

Процедура ДобавитьИзменяемыйРеквизитХраненияДвоичныхДанных(ИзменяемыеРеквизиты, ТипХраненияФайла, ЗначениеРеквизита = Неопределено)

	ИмяРеквизита = РаботаСФайламиВХранилищеДвоичныхДанныхСлужебный.ОпределитьРеквизитХраненияДвоичныхДанныхПоТипуХраненияФайлов(ТипХраненияФайла);
	Если ИмяРеквизита <> Неопределено Тогда
		ИзменяемыеРеквизиты.Вставить(ИмяРеквизита, ЗначениеРеквизита);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаписатьДвоичныеДанныеВИнформационнуюБазу(ХранилищеДвоичныхДанных, ЗначенияРеквизитовХранения)

	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);

	НачатьТранзакцию();
	Попытка

		Если ЗначенияРеквизитовХранения.Свойство("ДвоичныеДанные") Тогда

			Блокировка = Новый БлокировкаДанных;
			Блокировка.Добавить("Справочник.ХранилищеДвоичныхДанных").УстановитьЗначение("Ссылка", ХранилищеДвоичныхДанных);
			Блокировка.Заблокировать();

			ХранилищеДвоичныхДанныхОбъект = ХранилищеДвоичныхДанных.ПолучитьОбъект();
			ХранилищеДвоичныхДанныхОбъект.Заблокировать();

			ХранилищеДвоичныхДанныхОбъект.ДвоичныеДанные = ПодготовитьДвоичныеДанныеДляЗаписи(ЗначенияРеквизитовХранения.ДвоичныеДанные);
			ХранилищеДвоичныхДанныхОбъект.Записать();

		КонецЕсли;
		
		Если ИспользоватьМестаХраненияДвоичныхДанных(ЗначенияРеквизитовХранения) Тогда

			ЗначенияАтрибутовЗаписи = Новый Структура;
			Для Каждого ЗначениеРеквизита Из ЗначенияРеквизитовХранения Цикл
				
				Если ЗначениеРеквизита.Ключ <> "ДвоичныеДанные" Тогда
					ЗначенияАтрибутовЗаписи.Вставить(ЗначениеРеквизита.Ключ, ПодготовитьДвоичныеДанныеДляЗаписи(ЗначениеРеквизита.Значение));
				КонецЕсли;
				
			КонецЦикла;

			РегистрыСведений.МестаХраненияДвоичныхДанных.ДобавитьИзменитьЗапись(ХранилищеДвоичныхДанных, ЗначенияАтрибутовЗаписи);
		КонецЕсли;

		ЗафиксироватьТранзакцию();	
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

Функция ИспользоватьМестаХраненияДвоичныхДанных(ЗначенияРеквизитовХранения)

	Для Каждого ЗначениеРеквизитаХранения Из ЗначенияРеквизитовХранения Цикл
		Если ЗначениеРеквизитаХранения.Ключ = "ДвоичныеДанныеВАрхиве"
				Или ЗначениеРеквизитаХранения.Ключ = "ДвоичныеДанныеВОперационномВстроенномХранилище"
				Или ЗначениеРеквизитаХранения.Ключ = "ДвоичныеДанныеВОперационномВнешнемХранилище" Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;	

КонецФункции

Процедура ВосстановитьИзвлеченныйФайлВОперативныйТомНаДиске(ОбрабатываемыйФайл, ИзвлеченныйФайл, ПараметрыОперации)

	ПутьКИсходномуФайлу = ИзвлеченныйФайл.ИмяФайла;

	СвойстваФайла = РаботаСФайламиВТомахСлужебный.СвойстваФайлаВТоме();
	СвойстваФайла.Наименование	= ОбрабатываемыйФайл.Наименование;
	СвойстваФайла.Расширение	= ОбрабатываемыйФайл.Расширение;
	СвойстваФайла.Том			= ПараметрыОперации.СведенияОТомеХранения.Ссылка;
	СвойстваФайла.ПутьКФайлу	= "";
	СвойстваФайла.НомерВерсии	= ОбрабатываемыйФайл.НомерВерсии;
	СвойстваФайла.ВладелецФайла = ОбрабатываемыйФайл.ВладелецФайла;

	ПутьКФайлуКопии = РаботаСФайламиВТомахСлужебный.ПолноеИмяФайлаВТоме(СвойстваФайла);

	ПараметрыОперации.НовыйПутьКФайлуВнутриТома = Сред(ПутьКФайлуКопии, СтрДлина(ПараметрыОперации.СведенияОТомеХранения.ПутьКТому) + 1);

	КопироватьФайлСПроверкой(ПутьКИсходномуФайлу, ПутьКФайлуКопии);
	
КонецПроцедуры

Процедура КопироватьФайлСПроверкой(ПутьКИсходномуФайлу, ПутьКФайлуКопии)
		
	РазмерИсходногоФайла = РазмерФайла(ПутьКИсходномуФайлу);
	
	КопироватьФайл(ПутьКИсходномуФайлу, ПутьКФайлуКопии);
	
	РазмерФайлаКопии = РазмерФайла(ПутьКФайлуКопии);

	Если РазмерИсходногоФайла <> РазмерФайлаКопии Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не совпадает размер исходного и скопированного файла
					|		
					|Исходный файл
					|Путь к файлу: ""%1"" 
					|Размер файла: %2
					|
					|Файл копия
					|Путь к файлу: ""%3"" 
					|Размер файла: %4'"), ПутьКИсходномуФайлу, РазмерИсходногоФайла, ПутьКФайлуКопии, РазмерФайлаКопии);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеренестиZipАрхивВАрхивныйТомНаДиске(ОбрабатываемыйФайл, ZipАрхив, ПараметрыОперации, ГенерироватьНовоеИмяZipАрхива = Ложь)

	ПутьКИсходномуФайлу = ZipАрхив.ИмяФайла;
	
	СвойстваФайла = РаботаСФайламиВТомахСлужебный.СвойстваФайлаВТоме();
	СвойстваФайла.Наименование	= ?(ГенерироватьНовоеИмяZipАрхива, ИмяФайлаНовогоZipАрхива(), ПараметрыОперации.ИмяZipАрхиваБезРасширения);
	СвойстваФайла.Расширение	= ПараметрыОперации.РасширениеZipАрхива;
	СвойстваФайла.Том			= ПараметрыОперации.СведенияОТомеХранения.Ссылка;
	СвойстваФайла.ПутьКФайлу	= "";
	СвойстваФайла.НомерВерсии	= ОбрабатываемыйФайл.НомерВерсии;
	СвойстваФайла.ВладелецФайла = ОбрабатываемыйФайл.ВладелецФайла;

	ПутьКФайлуКопии = РаботаСФайламиВТомахСлужебный.ПолноеИмяФайлаВТоме(СвойстваФайла);

	ПараметрыОперации.НовыйПутьКФайлуВнутриТома = Сред(ПутьКФайлуКопии, СтрДлина(ПараметрыОперации.СведенияОТомеХранения.ПутьКТому) + 1);

	КопироватьФайлСПроверкой(ПутьКИсходномуФайлу, ПутьКФайлуКопии);

КонецПроцедуры

Процедура ПеренестиZipАрхивВАрхивныйТомХранилищеS3(ОбрабатываемыйФайл, ZipАрхив, ПараметрыОперации)

	ЗначенияРеквизитовХранения = Новый Структура;
	ЗначенияРеквизитовХранения.Вставить("ДвоичныеДанныеВАрхиве", Новый ДвоичныеДанные(ZipАрхив.ИмяФайла));
	ДобавитьИзменяемыйРеквизитХраненияДвоичныхДанных(ЗначенияРеквизитовХранения, ОбрабатываемыйФайл.ТипХраненияФайла);

	ЗаписатьДвоичныеДанныеВИнформационнуюБазу(ОбрабатываемыйФайл.ХранилищеДвоичныхДанных, ЗначенияРеквизитовХранения);

	ПараметрыЗаписиВИБ = ПараметрыЗаписиВИнформационнуюБазу();	
	ПараметрыЗаписиВИБ.ТипХраненияФайла			= ПараметрыОперации.СведенияОТомеХранения.НовыйТипХраненияФайла;
	ПараметрыЗаписиВИБ.Том						= ПараметрыОперации.СведенияОТомеХранения.Ссылка;
	ПараметрыЗаписиВИБ.ЭтоЗаписьВФайловыйАрхив	= Истина;

	РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов.ДобавитьЗапись(
										ОбрабатываемыйФайл.ХранилищеДвоичныхДанных, 
										ПараметрыЗаписиВИБ);

	РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов.УдалитьЗапись(ОбрабатываемыйФайл.ХранилищеДвоичныхДанных, Перечисления.ВидыХраненияФайлов.ОперационноеХранение);

КонецПроцедуры

Функция СведенияОФайлахДляПодбораТома()

	Результат = Новый Структура;
	Результат.Вставить("Размер"			, 0);
	Результат.Вставить("Наименование"	, "");
	Результат.Вставить("Расширение"		, "");

	Возврат Результат;

КонецФункции

Функция РазмерФайла(ПутьКФайлу, ВызыватьИсключение = Истина)

	Результат = 0;

	Файл = Новый Файл(ПутьКФайлу);
	Если Файл.Существует() Тогда
		Результат = Файл.Размер();
	ИначеЕсли ВызыватьИсключение Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Файл ""%1"" не найден.'"), ПутьКФайлу);		
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция СвойстваИзвлеченногоФайла()

	Результат = Новый Структура;
	Результат.Вставить("ИмяФайла"		, "");
	Результат.Вставить("ИмяСРасширением", "");
	Результат.Вставить("Размер"			, 0);

	Возврат Результат;

КонецФункции

Функция ФайлИзвлеченныйИзZipАрхива(ОбрабатываемыйФайл, ПараметрыОперации)

	Результат = СвойстваИзвлеченногоФайла();

	Попытка

		ВременныйКаталог = ПараметрыОперации.ВременныйКаталог;

		Если ОбрабатываемыйФайл.ТипХраненияФайла <> Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
			
			ДвоичныеДанныеZipАрхива = ДвоичныеДанныеФайла(ОбрабатываемыйФайл, Ложь);

		Иначе
			ПутьКФайлуАрхива = РаботаСФайламиВТомахСлужебный.ПолноеИмяФайлаВТоме(
											Новый Структура("Том, ПутьКФайлу", ОбрабатываемыйФайл.Том, ОбрабатываемыйФайл.ПутьКФайлу));

			ФайлНаДиске = Новый Файл(ПутьКФайлуАрхива);
			Если Не ФайлНаДиске.Существует() Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Файл ""%1"" не найден.'"), ПутьКФайлуАрхива);
			КонецЕсли;

			ПараметрыОперации.ИсходныйПутьКФайлу = ПутьКФайлуАрхива;

			ДвоичныеДанныеZipАрхива = Новый ДвоичныеДанные(ПутьКФайлуАрхива);
		КонецЕсли;

		ИмяИзвлекаемогоФайла = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(ОбрабатываемыйФайл.Наименование, ОбрабатываемыйФайл.Расширение);

		ДанныеИзвлеченногоФайла = ИзвлечьФайлИзZipАрхива(ДвоичныеДанныеZipАрхива, ИмяИзвлекаемогоФайла, ВременныйКаталог);

		Результат.Размер = ДанныеИзвлеченногоФайла.Размер;

		Результат.ИмяФайла			= ДанныеИзвлеченногоФайла.ИмяФайла;
		Результат.ИмяСРасширением	= ИмяИзвлекаемогоФайла;

	Исключение
		ШаблонОписанияОшибки = НСтр("ru = 'Ошибка при восстановлении файла ""%1""
			|Не удалось извлечь файл из zip-архива
			|""%2"".'");

		СтрокаИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОписанияОшибки,
			ИмяИзвлекаемогоФайла,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));

		ВызватьИсключение СтрокаИсключения;
	КонецПопытки;

	Возврат Результат;

КонецФункции

Функция ZipАрхивФайлаПодлежащегоПереносу(ОбрабатываемыйФайл, ПараметрыОперации)

	Попытка	

		ВременныйКаталог = ПараметрыОперации.ВременныйКаталог;

		Если ОбрабатываемыйФайл.ТипХраненияФайла <> Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
			ПутьКАрхивируемомуФайлу = ПутьКОбрабатываемомуФайлуВоВременномКаталоге(ОбрабатываемыйФайл, ВременныйКаталог);

			ДвоичныеДанныеФайла = ДвоичныеДанныеФайла(ОбрабатываемыйФайл);

			Если ТипЗнч(ДвоичныеДанныеФайла) <> Тип("ДвоичныеДанные") Тогда
				СтрокаИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
														НСтр("ru = 'Данных файла имеют неверный тип: ""%1""
															|Верный тип: ""ДвоичныеДанные""'"), ТипЗнч(ДвоичныеДанныеФайла));
				ВызватьИсключение СтрокаИсключения;
			КонецЕсли;
			
			ДвоичныеДанныеФайла.Записать(ПутьКАрхивируемомуФайлу);
		Иначе
			ПутьКАрхивируемомуФайлу = РаботаСФайламиВТомахСлужебный.ПолноеИмяФайлаВТоме(
											Новый Структура("Том, ПутьКФайлу", ОбрабатываемыйФайл.Том, ОбрабатываемыйФайл.ПутьКФайлу));

			ФайлНаДиске = Новый Файл(ПутьКАрхивируемомуФайлу);
			Если Не ФайлНаДиске.Существует() Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Архивируемый файл ""%1"" не найден.'"), ПутьКАрхивируемомуФайлу);
			КонецЕсли;											

			ПараметрыОперации.ИсходныйПутьКФайлу = ПутьКАрхивируемомуФайлу;
		КонецЕсли;

		ИмяАрхивногоФайлаБезРасширения = ИмяФайлаНовогоZipАрхива();
		ИмяАрхивногоФайла = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(ИмяАрхивногоФайлаБезРасширения, "zip");

		ПутьКФайлуАрхива = ВременныйКаталог + ИмяАрхивногоФайла;

		НовыйZipАрхив = УпаковатьФайлВZipАрхив(ПутьКФайлуАрхива, ПутьКАрхивируемомуФайлу);

		ПараметрыОперации.ИмяZipАрхиваБезРасширения = ИмяАрхивногоФайлаБезРасширения;
		ПараметрыОперации.РасширениеZipАрхива		= "zip";

	Исключение	

		ШаблонОписанияОшибки = НСтр("ru = 'Ошибка при переносе файла ""%1""
			|Не создан zip-архив
			|""%2"".'");

		СтрокаИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОписанияОшибки,
			ОбрабатываемыйФайл.Наименование + "." + ОбрабатываемыйФайл.Расширение,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));

		ВызватьИсключение СтрокаИсключения;

	КонецПопытки;

	Возврат НовыйZipАрхив;

КонецФункции

Функция ИмяФайлаНовогоZipАрхива()

	Возврат Строка(Новый УникальныйИдентификатор);

КонецФункции

Функция ДвоичныеДанныеФайла(ОбрабатываемыйФайл, ЭтоПереносВФайловыйАрхив = Истина)

	Результат = ОбрабатываемыйФайл.ДвоичныеДанныеФайла;

	Если ТипЗнч(Результат) = Тип("ХранилищеЗначения") Тогда
		Результат = Результат.Получить();
		Если ТипЗнч(Результат) = Тип("ДвоичныеДанные") Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;

	Если ЭтоПереносВФайловыйАрхив Тогда
		Если Не ЗначениеЗаполнено(Результат) Или ТипЗнч(Результат) <> Тип("ДвоичныеДанные") Тогда
			Результат = РаботаСФайлами.ДвоичныеДанныеФайла(ОбрабатываемыйФайл.ПрисоединенныйФайл);
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ПутьКОбрабатываемомуФайлуВоВременномКаталоге(ОбрабатываемыйФайл, ВременныйКаталог = "")

	УстановитьПривилегированныйРежим(Истина);

	Если ВременныйКаталог = "" Тогда
		СтрокаОписанияОшибки = СтрокаОписанияОшибкиПриОперацияхСВременнымКаталогом(Истина);
		ВременныйКаталог = НовыйВременныйКаталогДляРаботыСZipАрхивом(СтрокаОписанияОшибки);
	КонецЕсли;

	ИмяФайла = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(ОбрабатываемыйФайл.Наименование, ОбрабатываемыйФайл.Расширение);

	Возврат ВременныйКаталог + ИмяФайла;

КонецФункции

Процедура ВыполнитьУсловныйПереносФайла(ОбрабатываемыйФайл)

	ИзменяемыеРеквизиты = Новый Структура;
	ИзменяемыеРеквизиты.Вставить("УсловноПеренесенВАрхив", Истина);
	ИзменитьРеквизитыПрисоединенногоФайла(ОбрабатываемыйФайл, ИзменяемыеРеквизиты);	

КонецПроцедуры

Процедура ИзменитьРеквизитыПрисоединенногоФайла(ОбрабатываемыйФайл, ИзменяемыеРеквизиты)

	ФайлСсылка = ОбрабатываемыйФайл.ПрисоединенныйФайл;

	ЕстьАтрибутыОтсутствующиеВРеквизитах = ЕстьАтрибутыОтсутствующиеВРеквизитах(ИзменяемыеРеквизиты, ФайлСсылка);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировкиДанных = Блокировка.Добавить(ФайлСсылка.Метаданные().ПолноеИмя());
	ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", ФайлСсылка);

	Если ЕстьАтрибутыОтсутствующиеВРеквизитах Тогда	
		ЭлементБлокировкиДанных = Блокировка.Добавить(Метаданные.РегистрыСведений.СведенияОФайлах.ПолноеИмя());
		ЭлементБлокировкиДанных.УстановитьЗначение("Файл", ОбрабатываемыйФайл.ФайлДляЗаписиСведений);
	КонецЕсли;
	
	НачатьТранзакцию();	
	Попытка

		Блокировка.Заблокировать();

		ФайлОбъект = ФайлСсылка.ПолучитьОбъект();
		ФайлОбъект.Заблокировать();

		ЗаполнитьЗначенияСвойств(ФайлОбъект, ИзменяемыеРеквизиты);
		ФайлОбъект.Записать();

		Если ЕстьАтрибутыОтсутствующиеВРеквизитах Тогда

			НаборЗаписейРС = РегистрыСведений.СведенияОФайлах.СоздатьНаборЗаписей();
			НаборЗаписейРС.Отбор.Файл.Установить(ОбрабатываемыйФайл.ФайлДляЗаписиСведений);
			НаборЗаписейРС.Прочитать();
			
			Если НаборЗаписейРС.Количество() = 1 Тогда
				ЗаписьРС = НаборЗаписейРС[0];
			Иначе
				ЗаписьРС = НаборЗаписейРС.Добавить();
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ЗаписьРС.Файл) Тогда
				ЗаписьРС.Файл = ОбрабатываемыйФайл.ФайлДляЗаписиСведений;
				ЗаполнитьЗначенияСвойств(ЗаписьРС, ОбрабатываемыйФайл.ФайлДляЗаписиСведений);
			КонецЕсли;

			ЗаполнитьЗначенияСвойств(ЗаписьРС, ИзменяемыеРеквизиты);		

			НаборЗаписейРС.Записать();

		КонецЕсли;

		ЗафиксироватьТранзакцию();

	Исключение
		ОтменитьТранзакцию();

		ИмяДляЖурнала = ОбщегоНазначенияКлиентСервер.ПолучитьИмяСРасширением(ОбрабатываемыйФайл.Наименование,
			ОбрабатываемыйФайл.Расширение);

		Ошибка = Обработки.ПереносФайлов.ОшибкаПереноса(ФайлСсылка, ИнформацияОбОшибке());
		Ошибка.ИмяФайла = ИмяДляЖурнала;
		
		ПредставлениеСсылкиНаФайл = ПредставлениеПрисоединенногоФайла(ФайлСсылка, ОбрабатываемыйФайл.ФайлПредставление);
		
		СтрокаИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
															НСтр("ru = 'Не удалось перенести файл %1
															|%2
															|по причине:
															|%3"".'"),
															ПредставлениеСсылкиНаФайл, 
															ИмяДляЖурнала, Ошибка.ПодробноеПредставлениеОшибки);

		ВызватьИсключение СтрокаИсключения;

	КонецПопытки;		

КонецПроцедуры

Функция ПредставлениеПрисоединенногоФайла(ПрисоединенныйФайлСсылка, ПредставлениеФайла)
	
	ПрисоединенныйФайлМетаданные = ПрисоединенныйФайлСсылка.Метаданные();

	ПредставлениеОбъекта = ПрисоединенныйФайлМетаданные.ПредставлениеОбъекта;
	Если ПустаяСтрока(ПредставлениеОбъекта) Тогда
		ПредставлениеОбъекта = ПрисоединенныйФайлМетаданные.Представление();
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 (%2)", ПредставлениеФайла, ПредставлениеОбъекта);	

КонецФункции

Функция ЕстьАтрибутыОтсутствующиеВРеквизитах(ИзменяемыеРеквизиты, ФайлСсылка)

	Результат = Ложь;
	
	Реквизиты = ФайлСсылка.Метаданные().Реквизиты;
	Для Каждого Реквизит Из ИзменяемыеРеквизиты Цикл
		Если Реквизиты.Найти(Реквизит.Ключ) = Неопределено Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ПустаяСсылкаНаПрисоединенныйФайл(Настройка)

	Если Настройка.ЭтоФайл Тогда
		Результат = Справочники.ВерсииФайлов.ПустаяСсылка();
	Иначе
		Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Настройка.ТипВладельцаФайла, "ЗначениеПустойСсылки");
	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура ЗаполнениеВременнойТаблицыДляПереносаФайлов(НастройкаПереносаФайлов, МассивИсключений, КоличествоДнейОперативногоХранения, МенеджерВременныхТаблиц)

	УстановитьПривилегированныйРежим(Истина);

	Если НастройкаПереносаФайлов.ЭтоДетальнаяНастройкаПереносаФайлов Тогда
		ВладелецФайла		= НастройкаПереносаФайлов.ИдентификаторВладельца;
		ЭлементИсключение	= НастройкаПереносаФайлов.ВладелецФайла;
	Иначе
		ВладелецФайла		= НастройкаПереносаФайлов.ВладелецФайла;
		ЭлементИсключение	= Неопределено;
	КонецЕсли;	

	ТекстЗапроса = ТекстЗапросаДляПереносаФайловВФайловыйАрхив(ВладелецФайла, НастройкаПереносаФайлов, МассивИсключений, ЭлементИсключение);

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	Запрос.УстановитьПараметр("ТипВладельца"	, ТипЗнч(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецФайла, "ЗначениеПустойСсылки")));
	Запрос.УстановитьПараметр("КоличествоДней"	, КоличествоДнейОперативногоХранения);
	Запрос.УстановитьПараметр("ДатаОбработки"	, ТекущаяДатаСеанса());

	ПустыеПользователи = Новый Массив;
	ПустыеПользователи.Добавить(Неопределено);
	ПустыеПользователи.Добавить(Справочники.Пользователи.ПустаяСсылка());
	ПустыеПользователи.Добавить(Справочники.ВнешниеПользователи.ПустаяСсылка());
	ПустыеПользователи.Добавить(Справочники.УчетныеЗаписиСинхронизацииФайлов.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("ПустыеПользователи", ПустыеПользователи);

	Если МассивИсключений.Количество() > 0 Тогда

		Запрос.УстановитьПараметр("МассивИсключений", МассивИсключений);

	КонецЕсли;

	Если НастройкаПереносаФайлов.ЭтоДетальнаяНастройкаПереносаФайлов Тогда

		Запрос.УстановитьПараметр("ОтборПоДетализированнойНастройке", ЭлементИсключение);

	КонецЕсли;	

	Запрос.Выполнить();

КонецПроцедуры

Функция СледующаяПорцияФайлов(МенеджерВременныхТаблиц, ПоследняяСсылка, ТекстЗапроса)

	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ПоследняяСсылка", ПоследняяСсылка);

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ТекстЗапросаПорцииФайлов(РазмерПорции, ЭтоПереносВФайловыйАрхив = Истина)

	Результат = 
       "ВЫБРАТЬ ПЕРВЫЕ 10
       |	ВТ_ДанныеДляПереноса.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
       |	ВТ_ДанныеДляПереноса.ФайлПредставление КАК ФайлПредставление,	   
       |	ВТ_ДанныеДляПереноса.ТипХраненияФайла КАК ТипХраненияФайла,
       |	ВТ_ДанныеДляПереноса.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанных,
       |	ВТ_ДанныеДляПереноса.Том КАК Том,
       |	ВТ_ДанныеДляПереноса.ПутьКФайлу КАК ПутьКФайлу,
       |	ВТ_ДанныеДляПереноса.Наименование КАК Наименование,
       |	ВТ_ДанныеДляПереноса.Расширение КАК Расширение,
       |	ВТ_ДанныеДляПереноса.КоличествоДедуплицированныхФайлов КАК КоличествоДедуплицированныхФайлов,
       |	ВТ_ДанныеДляПереноса.ФайлДляЗаписиСведений КАК ФайлДляЗаписиСведений,
       |	ВТ_ДанныеДляПереноса.НомерВерсии КАК НомерВерсии,
       |	ВТ_ДанныеДляПереноса.ВладелецФайла КАК ВладелецФайла,
       |	&ДополнительныеПоля КАК ДополнительныеПоля,
       |	&ПолеДвоичныхДанных КАК ДвоичныеДанныеФайла
       |ИЗ
       |	ВТ_ДанныеДляПереноса КАК ВТ_ДанныеДляПереноса
       |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанныхСправочник
       |		ПО ВТ_ДанныеДляПереноса.ХранилищеДвоичныхДанных = ХранилищеДвоичныхДанныхСправочник.Ссылка
       |			И (ВТ_ДанныеДляПереноса.ПолучатьДвоичныеДанные)
       |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестаХраненияДвоичныхДанных КАК МестаХраненияДвоичныхДанных
       |		ПО ВТ_ДанныеДляПереноса.ХранилищеДвоичныхДанных = МестаХраненияДвоичныхДанных.ХранилищеДвоичныхДанных
       |ГДЕ
       |	ВТ_ДанныеДляПереноса.ПрисоединенныйФайл > &ПоследняяСсылка
       |
       |УПОРЯДОЧИТЬ ПО
       |	ПрисоединенныйФайл";
	
	Если ЭтоПереносВФайловыйАрхив Тогда

		Результат = СтрЗаменить(Результат, "&ПолеДвоичныхДанных", 
	       "ЕСТЬNULL(ВЫБОР
	       |			КОГДА ВТ_ДанныеДляПереноса.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВИнформационнойБазе)
	       |				ТОГДА ХранилищеДвоичныхДанныхСправочник.ДвоичныеДанные
	       |			КОГДА ВТ_ДанныеДляПереноса.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВоВстроенномХранилищеДвоичныхДанных)
		   |				ТОГДА ЕСТЬNULL(МестаХраненияДвоичныхДанных.ДвоичныеДанныеВОперационномВстроенномХранилище, НЕОПРЕДЕЛЕНО)
	       |			КОГДА ВТ_ДанныеДляПереноса.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВоВнешнемХранилищеДвоичныхДанных)
	       |		   		ТОГДА ЕСТЬNULL(МестаХраненияДвоичныхДанных.ДвоичныеДанныеВОперационномВнешнемХранилище, НЕОПРЕДЕЛЕНО)
	       |			ИНАЧЕ НЕОПРЕДЕЛЕНО
	       |		КОНЕЦ, НЕОПРЕДЕЛЕНО)");

		Результат = СтрЗаменить(Результат, "&ДополнительныеПоля КАК ДополнительныеПоля", 
			"ВТ_ДанныеДляПереноса.УсловноПеренесенВАрхив КАК УсловноПеренесенВАрхив,
	       	|	ВТ_ДанныеДляПереноса.ПодлежитУсловномуПереносу КАК ПодлежитУсловномуПереносу,
			|	ВТ_ДанныеДляПереноса.ФайлРедактируется КАК ФайлРедактируется");

	Иначе
		Результат = СтрЗаменить(Результат, "&ПолеДвоичныхДанных", "ЕСТЬNULL(МестаХраненияДвоичныхДанных.ДвоичныеДанныеВАрхиве, НЕОПРЕДЕЛЕНО)");

		Результат = СтрЗаменить(Результат, "&ДополнительныеПоля КАК ДополнительныеПоля",
			"ВТ_ДанныеДляПереноса.ТипХраненияФайлаОперативный КАК ТипХраненияФайлаОперативный,
			|	ВТ_ДанныеДляПереноса.ТомОперативный КАК ТомОперативный,
			|	ВТ_ДанныеДляПереноса.ПутьКФайлуОперативный КАК ПутьКФайлуОперативный");
	КонецЕсли;

	Если РазмерПорции <> 10 Тогда
		Результат = СтрЗаменить(Результат, "10", Формат(РазмерПорции, "ЧГ=0"));
	КонецЕсли;	

	Возврат Результат;

КонецФункции

Функция ТекстЗапросаДляПереносаФайловВФайловыйАрхив(ВладелецФайла, Настройка, МассивИсключений, ЭлементИсключение)

	РеквизитыСправочникаФайлов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Настройка.ТипВладельцаФайла, "ПолноеИмя, Имя");

	МетаданныеВладельцаФайлов = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ВладелецФайла);
	ПолноеИмяВладельцаФайлов = МетаданныеВладельцаФайлов.ПолноеИмя();

	МетаданныеОбъектаФайлов = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(Настройка.ТипВладельцаФайла);
	ЕстьВозможностьХранитьВерсии = ОбщегоНазначения.ЕстьРеквизитОбъекта("ТекущаяВерсия", МетаданныеОбъектаФайлов);

	ТекстыЗапросовПакета = Новый Массив;

	Если ЕстьВозможностьХранитьВерсии Тогда

		СправочникВерсийФайлов	= ОбщегоНазначения.ИдентификаторОбъектаМетаданных(МетаданныеОбъектаФайлов.Реквизиты.ТекущаяВерсия.Тип.Типы()[0]);
		МетаданныеВерсийФайлов	= ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(СправочникВерсийФайлов);

		ПолноеИмяСправочникаВерсийФайлов = МетаданныеВерсийФайлов.ПолноеИмя();

		ТекстЗапроса = 
           "ВЫБРАТЬ
           |	ВерсииФайлов.Ссылка КАК ПрисоединенныйФайл,
           |	ВерсииФайлов.ТипХраненияФайла КАК ТипХраненияФайла,
           |	ВерсииФайлов.Том КАК Том,
           |	ВерсииФайлов.ПутьКФайлу КАК ПутьКФайлу,
           |	ЕСТЬNULL(ХранилищеФайлов.ХранилищеДвоичныхДанных, ЗНАЧЕНИЕ(Справочник.ХранилищеДвоичныхДанных.ПустаяСсылка)) КАК ХранилищеДвоичныхДанных,
           |	ВерсииФайлов.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВТомахНаДиске) КАК ЭтоХранениеНаДиске,
           |	ВерсииФайлов.Наименование КАК Наименование,
           |	ВерсииФайлов.Расширение КАК Расширение,
           |	ВерсииФайлов.Владелец КАК ФайлДляЗаписиСведений,
           |	ВерсииФайлов.НомерВерсии КАК НомерВерсии,
           |	Файлы.ВладелецФайла КАК ВладелецФайла,
           |	ВерсииФайлов.УсловноПеренесенВАрхив КАК УсловноПеренесенВАрхив,
           |	ВерсииФайлов.Представление КАК ФайлПредставление
           |ПОМЕСТИТЬ ВТ_ПодготовкаДанныхДляПереноса
           |ИЗ
           |	#ПолноеИмяСправочникаФайлов КАК Файлы
           |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ПолноеИмяСправочникаВерсийФайлов КАК ВерсииФайлов
           |		ПО Файлы.Ссылка = ВерсииФайлов.Владелец
           |			И (НЕ Файлы.ПометкаУдаления)
           |			И (НЕ ВерсииФайлов.ПометкаУдаления)
           |			И (&ЭтоНеГруппа)
           |			И (ТИПЗНАЧЕНИЯ(Файлы.ВладелецФайла) = &ТипВладельца)
           |			И (&ОтборПоДетализированнойНастройке)
           |			И (&ОтборПоИсключениям)
           |			И (ВЫБОР
           |				КОГДА ВерсииФайлов.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВТомахНаДиске)
           |					ТОГДА (ВЫРАЗИТЬ(ВерсииФайлов.ПутьКФайлу КАК СТРОКА(100))) <> """"
           |							ИЛИ НЕ ВерсииФайлов.Том = ЗНАЧЕНИЕ(Справочник.ТомаХраненияФайлов.ПустаяСсылка)
           |				КОГДА ВерсииФайлов.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВоВстроенномХранилищеДвоичныхДанных)
           |						ИЛИ ВерсииФайлов.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВоВнешнемХранилищеДвоичныхДанных)
           |					ТОГДА НЕ ВерсииФайлов.Том = ЗНАЧЕНИЕ(Справочник.ТомаХраненияФайлов.ПустаяСсылка)
           |				ИНАЧЕ ИСТИНА
           |			КОНЕЦ)
           |			И (ДОБАВИТЬКДАТЕ(ВерсииФайлов.ДатаСоздания, ДЕНЬ, &КоличествоДней) <= &ДатаОбработки)
           |			И (ВерсииФайлов.ДатаПереносаВАрхив = ДатаВремя(1,1,1))
           |			И (Файлы.ТекущаяВерсия <> ВерсииФайлов.Ссылка
           |				ИЛИ Файлы.Редактирует В (&ПустыеПользователи))
           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
           |		ПО (ВерсииФайлов.Ссылка = ХранилищеФайлов.Файл)";

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ПолноеИмяСправочникаФайлов", РеквизитыСправочникаФайлов.ПолноеИмя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ПолноеИмяСправочникаВерсийФайлов", ПолноеИмяСправочникаВерсийФайлов);

		ТекстыЗапросовПакета.Добавить(ТекстЗапроса);

	Иначе // Не ЕстьВозможностьХранитьВерсии

		ТекстЗапроса = 
           "ВЫБРАТЬ
           |	Файлы.Ссылка КАК ПрисоединенныйФайл,
           |	Файлы.ТипХраненияФайла КАК ТипХраненияФайла,
           |	Файлы.Том КАК Том,
           |	Файлы.ПутьКФайлу КАК ПутьКФайлу,
           |	ЕСТЬNULL(ХранилищеФайлов.ХранилищеДвоичныхДанных, ЗНАЧЕНИЕ(Справочник.ХранилищеДвоичныхДанных.ПустаяСсылка)) КАК ХранилищеДвоичныхДанных,
           |	Файлы.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВТомахНаДиске) КАК ЭтоХранениеНаДиске,
           |	Файлы.Наименование КАК Наименование,
           |	Файлы.Расширение КАК Расширение,
           |	Файлы.Ссылка КАК ФайлДляЗаписиСведений,
           |	0 КАК НомерВерсии,
           |	Файлы.ВладелецФайла КАК ВладелецФайла,
           |	СведенияОФайлах.УсловноПеренесенВАрхив КАК УсловноПеренесенВАрхив,
           |	Файлы.Представление КАК ФайлПредставление
           |ПОМЕСТИТЬ ВТ_ПодготовкаДанныхДляПереноса
           |ИЗ
           |	#ТипВладельцаФайла КАК Файлы
           |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #СправочникВладелецФайла КАК СправочникВладелецФайла
           |		ПО Файлы.ВладелецФайла = СправочникВладелецФайла.Ссылка
           |			И (НЕ СправочникВладелецФайла.ПометкаУдаления)
           |			И (НЕ Файлы.ПометкаУдаления)
           |			И (&ЭтоНеГруппа)
           |			И (ТИПЗНАЧЕНИЯ(Файлы.ВладелецФайла) = &ТипВладельца)
           |			И (&ОтборПоДетализированнойНастройке)
           |			И (&ОтборПоИсключениям)
           |			И (ВЫБОР
           |				КОГДА Файлы.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВТомахНаДиске)
           |					ТОГДА (ВЫРАЗИТЬ(Файлы.ПутьКФайлу КАК СТРОКА(100))) <> """"
           |							ИЛИ НЕ Файлы.Том = ЗНАЧЕНИЕ(Справочник.ТомаХраненияФайлов.ПустаяСсылка)
           |				КОГДА Файлы.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВоВстроенномХранилищеДвоичныхДанных)
           |						ИЛИ Файлы.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВоВнешнемХранилищеДвоичныхДанных)
           |					ТОГДА НЕ Файлы.Том = ЗНАЧЕНИЕ(Справочник.ТомаХраненияФайлов.ПустаяСсылка)
           |				ИНАЧЕ ИСТИНА
           |			КОНЕЦ)
           |			И (ДОБАВИТЬКДАТЕ(Файлы.ДатаСоздания, ДЕНЬ, &КоличествоДней) <= &ДатаОбработки)
           |			И (Файлы.Редактирует В (&ПустыеПользователи))
           |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОФайлах КАК СведенияОФайлах
           |		ПО Файлы.Ссылка = СведенияОФайлах.Файл
           |			И (СведенияОФайлах.ДатаПереносаВАрхив = ДатаВремя(1,1,1))
           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
           |		ПО Файлы.Ссылка = ХранилищеФайлов.Файл";

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТипВладельцаФайла", "Справочник." + РеквизитыСправочникаФайлов.Имя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#СправочникВладелецФайла", ПолноеИмяВладельцаФайлов);		   

		ТекстыЗапросовПакета.Добавить(ТекстЗапроса);

	КонецЕсли;

	ТекстЗапроса = 
           "ВЫБРАТЬ РАЗЛИЧНЫЕ
           |	ВТ_ПодготовкаДанныхДляПереноса.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанных
           |ПОМЕСТИТЬ ВТ_ДанныеДляПроверкиДедупликации
           |ИЗ
           |	ВТ_ПодготовкаДанныхДляПереноса КАК ВТ_ПодготовкаДанныхДляПереноса
           |ГДЕ
           |	НЕ ВТ_ПодготовкаДанныхДляПереноса.ЭтоХранениеНаДиске
           |
           |ИНДЕКСИРОВАТЬ ПО
           |	ВТ_ПодготовкаДанныхДляПереноса.ХранилищеДвоичныхДанных
           |;
           |
           |////////////////////////////////////////////////////////////////////////////////
           |ВЫБРАТЬ
           |	ХранилищеФайлов.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанных,
           |	КОЛИЧЕСТВО(*) КАК КоличествоДедуплицированныхФайлов,
           |	КОЛИЧЕСТВО(*) - СУММА(ВЫБОР
           |			КОГДА ЕСТЬNULL(СведенияОФайлах.УсловноПеренесенВАрхив, ЕСТЬNULL(ВерсииФайлов.УсловноПеренесенВАрхив, ЛОЖЬ))
           |				ТОГДА ВЫРАЗИТЬ(1 КАК ЧИСЛО(17, 0))
           |			ИНАЧЕ 0
           |		КОНЕЦ) КАК КритерийУсловногоПереноса,
           |	МАКСИМУМ(ВЫБОР
           |			КОГДА НЕ ВерсииФайлов.Ссылка ЕСТЬ NULL
           |				ТОГДА ВерсииФайлов.Владелец.ТекущаяВерсия = ВерсииФайлов.Ссылка
           |						И НЕ ВерсииФайлов.Владелец.Редактирует В (&ПустыеПользователи)
           |			ИНАЧЕ НЕ СведенияОФайлах.Редактирует В (&ПустыеПользователи)
           |		КОНЕЦ) КАК ФайлРедактируется
           |ПОМЕСТИТЬ ВТ_ДедуплицированныеФайлы
           |ИЗ
           |	ВТ_ДанныеДляПроверкиДедупликации КАК ВТ_ДанныеДляПроверкиДедупликации
           |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
           |		ПО ВТ_ДанныеДляПроверкиДедупликации.ХранилищеДвоичныхДанных = ХранилищеФайлов.ХранилищеДвоичныхДанных
           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОФайлах КАК СведенияОФайлах
           |		ПО (ХранилищеФайлов.Файл = СведенияОФайлах.Файл)
           |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВерсииФайлов КАК ВерсииФайлов
           |		ПО (ХранилищеФайлов.Файл = ВерсииФайлов.Ссылка)
           |			И (НЕ ВерсииФайлов.Владелец.ПометкаУдаления)
           |ГДЕ
           |	НЕ ЕСТЬNULL(СведенияОФайлах.ПометкаУдаления, ЕСТЬNULL(ВерсииФайлов.ПометкаУдаления, ЛОЖЬ))
           |
           |СГРУППИРОВАТЬ ПО
           |	ХранилищеФайлов.ХранилищеДвоичныхДанных
           |
           |ИМЕЮЩИЕ
           |	КОЛИЧЕСТВО(*) > 1
           |
           |ИНДЕКСИРОВАТЬ ПО
           |	ХранилищеФайлов.ХранилищеДвоичныхДанных
           |;
           |
           |////////////////////////////////////////////////////////////////////////////////
           |ВЫБРАТЬ
           |	ВТ_ПодготовкаДанныхДляПереноса.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
           |	ВТ_ПодготовкаДанныхДляПереноса.ТипХраненияФайла КАК ТипХраненияФайла,
           |	ВТ_ПодготовкаДанныхДляПереноса.Том КАК Том,
           |	ВТ_ПодготовкаДанныхДляПереноса.ПутьКФайлу КАК ПутьКФайлу,
           |	ВТ_ПодготовкаДанныхДляПереноса.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанных,
           |	ВТ_ПодготовкаДанныхДляПереноса.Наименование КАК Наименование,
           |	ВТ_ПодготовкаДанныхДляПереноса.Расширение КАК Расширение,
           |	ВТ_ПодготовкаДанныхДляПереноса.ФайлДляЗаписиСведений КАК ФайлДляЗаписиСведений,
           |	ВТ_ПодготовкаДанныхДляПереноса.НомерВерсии КАК НомерВерсии,
           |	ВТ_ПодготовкаДанныхДляПереноса.ВладелецФайла КАК ВладелецФайла,
           |	ВЫБОР
           |		КОГДА ЕСТЬNULL(ВТ_ДедуплицированныеФайлы.КритерийУсловногоПереноса, 0) = 1
           |			ТОГДА ВТ_ПодготовкаДанныхДляПереноса.УсловноПеренесенВАрхив
           |		КОГДА ЕСТЬNULL(ВТ_ДедуплицированныеФайлы.КритерийУсловногоПереноса, 0) > 1
           |			ТОГДА ИСТИНА
           |		ИНАЧЕ ЛОЖЬ
           |	КОНЕЦ КАК ПодлежитУсловномуПереносу,
           |	НЕ ВТ_ПодготовкаДанныхДляПереноса.ЭтоХранениеНаДиске
           |		И НЕ ВЫБОР
           |				КОГДА ЕСТЬNULL(ВТ_ДедуплицированныеФайлы.КритерийУсловногоПереноса, 0) = 1
           |					ТОГДА ВТ_ПодготовкаДанныхДляПереноса.УсловноПеренесенВАрхив
           |				КОГДА ЕСТЬNULL(ВТ_ДедуплицированныеФайлы.КритерийУсловногоПереноса, 0) > 1
           |					ТОГДА ИСТИНА
           |				ИНАЧЕ ЛОЖЬ
           |			КОНЕЦ КАК ПолучатьДвоичныеДанные,
           |	ЕСТЬNULL(ВТ_ДедуплицированныеФайлы.КоличествоДедуплицированныхФайлов, 0) КАК КоличествоДедуплицированныхФайлов,
           |	ВТ_ПодготовкаДанныхДляПереноса.УсловноПеренесенВАрхив КАК УсловноПеренесенВАрхив,
           |	ЕСТЬNULL(ВТ_ДедуплицированныеФайлы.ФайлРедактируется, ЛОЖЬ) КАК ФайлРедактируется,
           |	ВТ_ПодготовкаДанныхДляПереноса.ФайлПредставление КАК ФайлПредставление
           |ПОМЕСТИТЬ ВТ_ДанныеДляПереноса
           |ИЗ
           |	ВТ_ПодготовкаДанныхДляПереноса КАК ВТ_ПодготовкаДанныхДляПереноса
           |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДедуплицированныеФайлы КАК ВТ_ДедуплицированныеФайлы
           |		ПО ВТ_ПодготовкаДанныхДляПереноса.ХранилищеДвоичныхДанных = ВТ_ДедуплицированныеФайлы.ХранилищеДвоичныхДанных
           |;
           |
           |////////////////////////////////////////////////////////////////////////////////
           |УНИЧТОЖИТЬ ВТ_ДанныеДляПроверкиДедупликации
           |;
           |
           |////////////////////////////////////////////////////////////////////////////////
           |УНИЧТОЖИТЬ ВТ_ДедуплицированныеФайлы
           |;
           |
           |////////////////////////////////////////////////////////////////////////////////
           |УНИЧТОЖИТЬ ВТ_ПодготовкаДанныхДляПереноса";

	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);

	Разделитель = 
	"
	|;
	|/////////////////////////////////////////////////////////////
	|";

	ТекстЗапроса = СтрСоединить(ТекстыЗапросовПакета, Разделитель);

	Если МассивИсключений.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоИсключениям", "НЕ Файлы.ВладелецФайла В ИЕРАРХИИ (&МассивИсключений)");
	Иначе 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоИсключениям", "ИСТИНА");
	КонецЕсли;

	Если ЭлементИсключение <> Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоДетализированнойНастройке", "Файлы.ВладелецФайла В ИЕРАРХИИ (&ОтборПоДетализированнойНастройке)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоДетализированнойНастройке", "ИСТИНА");
	КонецЕсли;

	Если МетаданныеОбъектаФайлов.Иерархический Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЭтоНеГруппа", "НЕ Файлы.ЭтоГруппа");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЭтоНеГруппа", "ИСТИНА");
	КонецЕсли;

	Возврат ТекстЗапроса;

КонецФункции

Функция КоличествоДнейХраненияДоПереносаВФайловыйАрхив(НастройкаПереносаФайлов, ЗначениеПоУмолчанию)

	Если НастройкаПереносаФайлов.ПеренестиВФайловыйАрхивЧерезДней = 0 Тогда
		Результат = ЗначениеПоУмолчанию;
	Иначе
		Результат = НастройкаПереносаФайлов.ПеренестиВФайловыйАрхивЧерезДней;
	КонецЕсли;	

	Возврат Результат;

КонецФункции

Процедура УдалитьВременныйКаталог(ПолноеИмяФайла, Знач ЗаголовокОписанияОшибки = Неопределено, Знач ВызыватьИсключение = Ложь)

	Если ПустаяСтрока(ПолноеИмяФайла) Тогда
		Возврат;
	КонецЕсли;

	Попытка
		УдалитьФайлы(ПолноеИмяФайла);
	Исключение
		
		Если ЗаголовокОписанияОшибки = Неопределено Тогда
			ЗаголовокОписанияОшибки = НСтр("ru = 'Ошибка переноса файлов'");
		КонецЕсли;

		ШаблонОписанияОшибки = НСтр("ru = '%1 
			|Не удален временный каталог
			|""%2"".'");

		СтрокаИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОписанияОшибки,
			ЗаголовокОписанияОшибки,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписатьВЖурналРегистрацииСобытиеПереносаФайлов(СтрокаИсключения, УровеньЖурналаРегистрации.Ошибка);			

		Если ВызыватьИсключение Тогда;
			ВызватьИсключение СтрокаИсключения;
		КонецЕсли;

	КонецПопытки;

КонецПроцедуры

Функция СтрокаОписанияОшибкиПриОперацияхСВременнымКаталогом(ЭтоПереносВФайловыйАрхив)

	Если ЭтоПереносВФайловыйАрхив Тогда
		Результат = НСтр("ru = 'Ошибка при переносе в файловый архив'");
	Иначе
		Результат = НСтр("ru = 'Ошибка при восстановлении из файлового архива'");
	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура ЗаписатьВЖурналРегистрацииСобытиеПереносаФайлов(Знач ТекстСообщения, Знач Уровень = Неопределено)

	Если Уровень = Неопределено Тогда
		Уровень = УровеньЖурналаРегистрации.Информация;
	КонецЕсли;

	ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрацииПереносаФайлов(), Уровень,,, ТекстСообщения);

КонецПроцедуры

Функция СобытиеЖурналаРегистрацииПереносаФайлов()

	Возврат НСтр("ru = 'Выполнение операций с файловым архивом'", ОбщегоНазначения.КодОсновногоЯзыка());

КонецФункции

Функция ДедуплицированныеФайлыПоХранилищуДвоичныхДанных(ХранилищеДвоичныхДанных, ТолькоФайлыВАрхиве = Ложь)

	Запрос = Новый Запрос;	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХранилищеФайлов.Файл КАК ПрисоединенныйФайл,
		|	ЕСТЬNULL(ВерсииФайлов.Наименование, ЕСТЬNULL(СведенияОФайлах.Наименование, """")) КАК Наименование,
		|	ЕСТЬNULL(ВерсииФайлов.Расширение, ЕСТЬNULL(СведенияОФайлах.Расширение, """")) КАК Расширение,
		|	ЕСТЬNULL(ВерсииФайлов.Владелец, ХранилищеФайлов.Файл) КАК ФайлДляЗаписиСведений,
		|	ЕСТЬNULL(ВерсииФайлов.НомерВерсии, 0) КАК НомерВерсии,
		|	ЕСТЬNULL(ВерсииФайлов.Владелец.ВладелецФайла, ЕСТЬNULL(СведенияОФайлах.ВладелецФайла, НЕОПРЕДЕЛЕНО)) КАК ВладелецФайла
		|ИЗ
		|	РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОФайлах КАК СведенияОФайлах
		|		ПО ХранилищеФайлов.Файл = СведенияОФайлах.Файл
		|			И (НЕ СведенияОФайлах.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВерсииФайлов КАК ВерсииФайлов
		|		ПО ХранилищеФайлов.Файл = ВерсииФайлов.Ссылка
		|			И (НЕ ВерсииФайлов.Владелец.ПометкаУдаления)
		|ГДЕ
		|	ХранилищеФайлов.ХранилищеДвоичныхДанных = &ХранилищеДвоичныхДанных
		|	И НЕ ЕСТЬNULL(СведенияОФайлах.ПометкаУдаления, ЕСТЬNULL(ВерсииФайлов.ПометкаУдаления, ЛОЖЬ))
		|	И (&ВсеФайлы
		|			ИЛИ ЕСТЬNULL(СведенияОФайлах.ДатаПереносаВАрхив, ЕСТЬNULL(ВерсииФайлов.ДатаПереносаВАрхив, ДАТАВРЕМЯ(1, 1, 1))) <> ДАТАВРЕМЯ(1, 1, 1))";

	Запрос.УстановитьПараметр("ХранилищеДвоичныхДанных"	, ХранилищеДвоичныхДанных);
	Запрос.УстановитьПараметр("ВсеФайлы"				, Не ТолькоФайлыВАрхиве);

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ДополнительныеПараметрыПереносаФайлов()

	Результат = Новый Структура;
	Результат.Вставить("ТекстЗапросаПолученияПорцииФайловДляПереносаВАрхив"			, "");
	Результат.Вставить("ТекстЗапросаПолученияПорцииФайловДляВосстановленияИзАрхива"	, "");
	Результат.Вставить("СпособХраненияФайлов"										, "");

	Возврат Результат;

КонецФункции

#КонецОбласти

// См. СтандартныеПодсистемыСервер.ПриОпределенииМетодовРазрешенныхДляВызоваКакПроизвольныйКод
Процедура ПриОпределенииМетодовРазрешенныхДляВызоваКакПроизвольныйКод(Методы) Экспорт
	
	Методы.Вставить("ПервоначальноеЗаполнениеКонстант");
	Методы.Вставить("ПервоначальноеЗаполнениеНастройкиРаботыСФайловымАрхивом");
	Методы.Вставить("ПеренестиФайлыМеждуОперационнымХранилищемИФайловымАрхивом", Истина);
	
КонецПроцедуры

#КонецОбласти