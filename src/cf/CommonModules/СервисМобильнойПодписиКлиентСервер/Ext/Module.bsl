///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Конструктор параметров подписанта для отправки на подписание и добавления.
// 
// Возвращаемое значение:
//  Структура:
//    * Ссылка - СправочникСсылка.ПодписантыСервисаМобильнойПодписи
//    * ИНН - Строка - не заполнен для ИП
//    * ИННФЛ - Строка - ИНН физического лица (индивидуального предпринимателя)
//    * НомерЗаписиОбАккредитации - Строка - номер записи об аккредитации филиала иностранной организации
//    * СНИЛС - Строка - обязателен для отправки в Госключ
//    * НомерТелефона - Строка - номер мобильного телефона, начинается с +7.
//    * Имя - Строка - (необязательный)
//    * Отчество - Строка - (необязательный)
//    * Фамилия - Строка - (необязательный)
//    * Должность - Строка - (необязательный)
//    * Организация - ОпределяемыйТип.Организация
//    * ОГРН - Строка - (необязательный)
//    * НаименованиеОрганизации - Строка - (необязательный) если не заполнена Организация
//    * ФизическоеЛицо - ОпределяемыйТип.ФизическоеЛицо - (необязательный)
//    * ВидПодписанта - ПеречислениеСсылка.ВидыПодписантовМобильногоСервиса
//    * МояПодпись - Булево
//    * ГосключУНЭП - Булево
//    * ГосключУКЭП - Булево
//
Функция ПараметрыПодписанта() Экспорт
	
	ПараметрыПодписанта = Новый Структура;
	ПараметрыПодписанта.Вставить("Ссылка");
	ПараметрыПодписанта.Вставить("ВидПодписанта");
	ПараметрыПодписанта.Вставить("ОГРН");
	ПараметрыПодписанта.Вставить("СНИЛС");
	ПараметрыПодписанта.Вставить("ИНН", "");
	ПараметрыПодписанта.Вставить("ИННФЛ");
	ПараметрыПодписанта.Вставить("Имя");
	ПараметрыПодписанта.Вставить("Отчество");
	ПараметрыПодписанта.Вставить("Фамилия");
	ПараметрыПодписанта.Вставить("НомерТелефона");
	ПараметрыПодписанта.Вставить("Должность");
	ПараметрыПодписанта.Вставить("Организация");
	ПараметрыПодписанта.Вставить("НаименованиеОрганизации");
	ПараметрыПодписанта.Вставить("ФизическоеЛицо");
	ПараметрыПодписанта.Вставить("Наименование");
	ПараметрыПодписанта.Вставить("ГосключУКЭП");
	ПараметрыПодписанта.Вставить("ГосключУНЭП");
	ПараметрыПодписанта.Вставить("МояПодпись");
	ПараметрыПодписанта.Вставить("НомерЗаписиОбАккредитации");
	
	Возврат ПараметрыПодписанта;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПараметрыПодписания() Экспорт
	
	ПараметрыПодписания = Новый Структура;
	ПараметрыПодписания.Вставить("ТипПодписи");
	ПараметрыПодписания.Вставить("ИдентификаторФормы");
	ПараметрыПодписания.Вставить("Доверенность");
	ПараметрыПодписания.Вставить("ПакетнаяОтправкаНаПодписание");
	ПараметрыПодписания.Вставить("ВидМобильнойПодписи");
	ПараметрыПодписания.Вставить("НастройкаОтправки");
	ПараметрыПодписания.Вставить("НаборДанныхПроверен");
	ПараметрыПодписания.Вставить("Атрибуты");
	ПараметрыПодписания.Вставить("ЗапросНаОтправкуДокумента");
	ПараметрыПодписания.Вставить("Комментарий");

	Возврат ПараметрыПодписания;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяСерверногоОповещения() Экспорт
	
	Возврат "СтандартныеПодсистемы.СервисМобильнойПодписи";
	
КонецФункции

Функция ТолькоЦифры(Строка) Экспорт
	
	ДлинаСтроки = СтрДлина(Строка);
	
	ОбработаннаяСтрока = "";
	Для НомерСимвола = 1 По ДлинаСтроки Цикл
		
		Символ = Сред(Строка, НомерСимвола, 1);
		Если Символ >= "0" И Символ <= "9" Тогда
			ОбработаннаяСтрока = ОбработаннаяСтрока + Символ;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОбработаннаяСтрока;
	
КонецФункции

Функция РеквизитыОрганизации(Организация = Неопределено) Экспорт
	РеквизитыОрганизации = Новый Структура;
	РеквизитыОрганизации.Вставить("Организация", Организация);
	РеквизитыОрганизации.Вставить("ИНН");
	РеквизитыОрганизации.Вставить("НаименованиеОрганизации");
	РеквизитыОрганизации.Вставить("ОГРН");
	РеквизитыОрганизации.Вставить("КодОКАТО");
	РеквизитыОрганизации.Вставить("ФизическиеЛица");
	Возврат РеквизитыОрганизации;
КонецФункции

Функция ПроверкаНабораДанных(НаборДанных, Параметры) Экспорт
	
	Дубли = Новый Соответствие;
	Госключ = Параметры.НастройкаОтправки.Госключ;
	
	Если Госключ Тогда
		РазрешенныеРасширения = Новый Соответствие;
		РазрешенныеРасширения.Вставить("pdf", Истина);
		РазрешенныеРасширения.Вставить("tif", Истина);
		РазрешенныеРасширения.Вставить("tiff", Истина);
		РазрешенныеРасширения.Вставить("jpg", Истина);
		РазрешенныеРасширения.Вставить("jpeg", Истина);
		РазрешенныеРасширения.Вставить("xml", Истина);
		РазрешенныеРасширения.Вставить("txt", Истина);
	КонецЕсли;
	
	КоличествоДляОтправки = 0;
	
	Для Каждого Элемент Из НаборДанных Цикл
		
		Если Элемент.Свойство("ИдентификаторДокумента") И ЗначениеЗаполнено(Элемент.ИдентификаторДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоДляОтправки = КоличествоДляОтправки + 1;
		
		Если Госключ Тогда
			
			Файл = Новый Файл(Элемент.Представление);
			
			Расширение = Прав(Файл.Расширение, СтрДлина(Файл.Расширение) -1);
			Если РазрешенныеРасширения.Получить(Расширение) = Неопределено Тогда
				Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Отправка файла с расширением ""%1"" в Госключ недоступна. Доступные расширения: ""pdf"", ""tif"", ""tiff"", ""jpg"", ""jpeg"", ""xml"", txt"".'"),
					Расширение);
			КонецЕсли;
			
		КонецЕсли;

		Если Дубли.Получить(Элемент.Представление) = Неопределено Тогда
			Дубли.Вставить(Элемент.Представление, Истина);
		Иначе
			Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='В пакете на подписание не должно быть одинаковых имен файлов: %1.'"),
				Элемент.Представление);
		КонецЕсли;
		
	КонецЦикла;

	Если Госключ И КоличествоДляОтправки > 20 Тогда
		Возврат НСтр("ru='В пакете на подписание для отправки в Госключ должно быть не более 20 файлов.'");
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция НовоеОписаниеОшибки(ЗаголовокОшибки, Знач ТекстОшибки) Экспорт
	
	Если Не СтрЗаканчиваетсяНа(ТекстОшибки, ".") Тогда
		ТекстОшибки = ТекстОшибки + ".";
	КонецЕсли;
	Результат = Новый Структура;
	Результат.Вставить("ЗаголовокОшибки", ЗаголовокОшибки);
	Результат.Вставить("ТекстОшибки", ТекстОшибки);
	Результат.Вставить("ИдентификаторЗапроса", "");
	Результат.Вставить("ДополнительныеФайлы", Новый Массив);
	
	Возврат Результат;

КонецФункции

#КонецОбласти
