///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет настройку состава хранимых данных для атрибутов справочника "ХранилищеДвоичныхДанных" и регистра сведений "МестаХраненияДвоичныхДанных", 
//  используемых для хранения двоичных данных файлов.
//
// Параметры:
//  РеквизитыДляХраненияФайлов - Массив - имена обрабатываемых реквизитов справочника "ХранилищеДвоичныхДанных".
//											см. РаботаСФайловымАрхивомСервер.ИменаРеквизитовХраненияФайловПоСпособуХранения.
//
Процедура ВыполнитьНастройкуСоставаХранимыхДанныхДляРеквизитов(Знач РеквизитыДляХраненияФайлов) Экспорт

	Если РеквизитыДляХраненияФайлов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если Не РаботаСФайловымАрхивомСервер.ДоступныХранилищаДвоичныхДанных() Тогда
		Возврат;
	КонецЕсли;

	СпособХраненияФайлов = РаботаСФайловымАрхивомСервер.СпособХраненияФайлов();
	Если СпособХраненияФайлов = "ВТомахНаДиске" Тогда
		Возврат;
	КонецЕсли;	

	НачальноеСостояниеМонопольногоРежима = МонопольныйРежим();

	УстановитьМонопольныйРежимДляИзмененияСоставаХранимыхДанных(НачальноеСостояниеМонопольногоРежима);

	Для Каждого ОбрабатываемыйРеквизит Из РеквизитыДляХраненияФайлов Цикл
		// @skip-check query-in-loop - Малый цикл
		НастроитьСоставХранимыхДанныхДляРеквизита(ОбрабатываемыйРеквизит);
	КонецЦикла;

	Если НачальноеСостояниеМонопольногоРежима <> МонопольныйРежим() Тогда
		УстановитьМонопольныйРежим(Ложь);
	КонецЕсли;

КонецПроцедуры

// Проверяет отсутствие подключенных хранилищ двоичных данных.
//
// Возвращаемое значение:
//  Булево
//
Функция ХранилищаДвоичныхДанныхОтсутствуют() Экспорт

	Возврат Не ХранилищеДвоичныхДанных.ПолучитьДоступностьХранилищаДвоичныхДанных() И ВнешниеХранилищаДвоичныхДанных.Количество() = 0;

КонецФункции

// Проверяет необходимость выполнения настройки состава хранимых данных подключенных хранилищ двоичных данных.
//
// Параметры:
//  ИмяРеквизита			- Строка - имя реквизита хранения двоичных данных файлов. 
//									   Под реквизитами подразумеваются следующие атрибуты объектов метаданных:
//                    * ДвоичныеДанные - реквизит справочника ХранилищеДвоичныхДанных.
//                    * ДвоичныеДанныеВАрхиве								- ресурс регистра сведений МестаХраненияДвоичныхДанных.
//                    * ДвоичныеДанныеВОперационномВстроенномХранилище	- ресурс регистра сведений МестаХраненияДвоичныхДанных.
//                    * ДвоичныеДанныеВОперационномВнешнемХранилище		- ресурс регистра сведений МестаХраненияДвоичныхДанных.
//  СпособХраненияФайлов	- строка см. значения для "Константа.СпособХраненияФайлов".
//
// Возвращаемое значение:
//  Булево
//
Функция НужноВыполнитьНастройкуСоставаХранимыхДанныхДляРеквизита(ИмяРеквизита, СпособХраненияФайлов = "") Экспорт

	Результат = Ложь;

	ОбъектМетаданных = ОбъектМетаданныхДляРеквизитаХраненияФайлов(ИмяРеквизита);	

	Если ИмяРеквизита = "ДвоичныеДанные" Тогда

		Результат = Не СоставХраненияНастроенДляРеквизита(ИмяРеквизита, , ОбъектМетаданных);

	ИначеЕсли ИмяРеквизита = "ДвоичныеДанныеВАрхиве" Тогда

		Если РаботаСФайловымАрхивомСервер.ИспользоватьФайловыйАрхив() Тогда

			ИмяВнешнегоХранилищаФайловогоАрхива = ИмяВнешнегоХранилищаТомаХраненияФайлов(Перечисления.ВидыХраненияФайлов.АрхивноеХранение);

			Если Не ПустаяСтрока(ИмяВнешнегоХранилищаФайловогоАрхива) 
				И Не ЕстьОшибкаВНастройкахХранилищаДвоичныхДанных(ИмяРеквизита, ИмяВнешнегоХранилищаФайловогоАрхива) Тогда

				Результат = Не СоставХраненияНастроенДляРеквизита(ИмяРеквизита, ИмяВнешнегоХранилищаФайловогоАрхива, ОбъектМетаданных);
            КонецЕсли;
		КонецЕсли;
	Иначе

		Если НЕ ЗначениеЗаполнено(СпособХраненияФайлов) Тогда
			СпособХраненияФайлов = РаботаСФайловымАрхивомСервер.СпособХраненияФайлов();
		КонецЕсли;

		Если ИмяРеквизита = "ДвоичныеДанныеВОперационномВстроенномХранилище" 
				И (СпособХраненияФайлов = "ВИнформационнойБазеИТомахНаДиске" Или СпособХраненияФайлов = "ВоВстроенномХранилищеДвоичныхДанных") Тогда

			ТомОперативногоХранения = ТомХраненияРазмещающийДанныеВоВстроенномХранилище();
			Если ТомОперативногоХранения <> Неопределено 			
					И Не ЕстьОшибкаВНастройкахХранилищаДвоичныхДанных(ИмяРеквизита) Тогда

				Результат = Не СоставХраненияНастроенДляРеквизита(ИмяРеквизита, , ОбъектМетаданных);

			КонецЕсли;

		ИначеЕсли ИмяРеквизита = "ДвоичныеДанныеВОперационномВнешнемХранилище"
				И (СпособХраненияФайлов = "ВИнформационнойБазеИТомахНаДиске" Или СпособХраненияФайлов = "ВоВнешнемХранилищеДвоичныхДанных") Тогда

			ИмяВнешнегоОперативногоХранилища = ИмяВнешнегоХранилищаТомаХраненияФайлов(Перечисления.ВидыХраненияФайлов.ОперационноеХранение);

			Если Не ПустаяСтрока(ИмяВнешнегоОперативногоХранилища)
				И Не ЕстьОшибкаВНастройкахХранилищаДвоичныхДанных(ИмяРеквизита, ИмяВнешнегоОперативногоХранилища) Тогда

				Результат = Не СоставХраненияНастроенДляРеквизита(ИмяРеквизита, ИмяВнешнегоОперативногоХранилища, ОбъектМетаданных);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РезервноеКопированиеВстроенногоХранилищаДвоичныхДанных

// Метод регламентного задания "РезервноеКопированиеВстроенногоХранилищаДвоичныхДанных".
//
Процедура РезервноеКопированиеВстроенногоХранилищаДвоичныхДанных() Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.РезервноеКопированиеВстроенногоХранилищаДвоичныхДанных);

	Если Не РаботаСФайловымАрхивомСервер.ДоступныХранилищаДвоичныхДанных() Тогда
		Возврат;
	КонецЕсли;

	Если ВыполняетсяФоновоеЗаданиеСозданияРезервнойКопииХДД() Тогда
		Возврат;
	КонецЕсли;
	
	НоваяРезервнаяКопияВстроенногоХДД();

КонецПроцедуры

Функция ВыполняетсяФоновоеЗаданиеСозданияРезервнойКопииХДД()

	РегламентноеЗадание = Метаданные.РегламентныеЗадания.РезервноеКопированиеВстроенногоХранилищаДвоичныхДанных;

	Отбор = Новый Структура;
	Отбор.Вставить("ИмяМетода", РегламентноеЗадание.ИмяМетода);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	ТекущиеФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если ТекущиеФоновыеЗадания.Количество() = 1 Тогда
		ФоновоеЗаданиеТекущегоСеанса = ПолучитьТекущийСеансИнформационнойБазы().ПолучитьФоновоеЗадание();
		Результат = ФоновоеЗаданиеТекущегоСеанса = Неопределено;
		Если Не Результат Тогда
			Результат = ФоновоеЗаданиеТекущегоСеанса.УникальныйИдентификатор <> ТекущиеФоновыеЗадания[0].УникальныйИдентификатор;
		КонецЕсли;
	Иначе
		Результат = ТекущиеФоновыеЗадания.Количество() > 0;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Процедура НоваяРезервнаяКопияВстроенногоХДД()

	УстановитьПривилегированныйРежим(Истина);

	ЗаписатьВЖурналРегистрацииСобытийРезервногоКопированияХДД(
												НСтр("ru = 'Начато регламентное создание резервной копии встроенного хранилища двоичных данных.'",
												ОбщегоНазначения.КодОсновногоЯзыка()));

	КомментарийЖурналаРегистрации = "";
	КаталогХраненияРезервныхКопий = ПроверенныйКаталогХраненияФайловРезервныхКопийВстроенногоХДД(КомментарийЖурналаРегистрации);

	Если Не ЗначениеЗаполнено(КаталогХраненияРезервныхКопий) Тогда

			ЗаписатьВЖурналРегистрацииСобытийРезервногоКопированияХДД(КомментарийЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка);

		Возврат;
	КонецЕсли;

	ИмяФайлаНовойРезервнойКопии = "";
	ВидРезервнойКопии = Перечисления.ВидыРезервныхКопийВстроенногоХранилищаДвоичныхДанных.ПустаяСсылка();

	ИмяФайлаПолнойРезервнойКопии = ИмяФайлаПоследнейПолнойРезервнойКопии();
	Если ЗначениеЗаполнено(ИмяФайлаПолнойРезервнойКопии) Тогда
		ПутьКФайлуПолнойРезервнойКопии = КаталогХраненияРезервныхКопий + ИмяФайлаПолнойРезервнойКопии;
		Если ФайлСуществует(ПутьКФайлуПолнойРезервнойКопии) Тогда
			ИмяФайлаНовойРезервнойКопии = НовоеИмяФайлаРезервнойКопии();
			ПутьКФайлуНовойДифференциальнойКопии = КаталогХраненияРезервныхКопий + ИмяФайлаНовойРезервнойКопии;

			Попытка
				ХранилищеДвоичныхДанных.СоздатьДифференциальнуюРезервнуюКопию(ПутьКФайлуНовойДифференциальнойКопии, ПутьКФайлуПолнойРезервнойКопии);
			Исключение
				
				ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				
				ЗаписатьВЖурналРегистрацииСобытийРезервногоКопированияХДД(ОписаниеОшибки, УровеньЖурналаРегистрации.Ошибка);
				Возврат;
															
			КонецПопытки;
			
			ВидРезервнойКопии = Перечисления.ВидыРезервныхКопийВстроенногоХранилищаДвоичныхДанных.Дифференциальная;
		КонецЕсли;
	КонецЕсли;

	Если ИмяФайлаНовойРезервнойКопии = "" Тогда
		ИмяФайлаНовойРезервнойКопии = НовоеИмяФайлаРезервнойКопии(Истина);
		ПутьКФайлуНовойПолнойРезервнойКопии = КаталогХраненияРезервныхКопий + ИмяФайлаНовойРезервнойКопии;

		Попытка
			ХранилищеДвоичныхДанных.СоздатьПолнуюРезервнуюКопию(ПутьКФайлуНовойПолнойРезервнойКопии);
		Исключение
			
			ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ЗаписатьВЖурналРегистрацииСобытийРезервногоКопированияХДД(ОписаниеОшибки, УровеньЖурналаРегистрации.Ошибка);
			Возврат;
														
		КонецПопытки;		

		ВидРезервнойКопии = Перечисления.ВидыРезервныхКопийВстроенногоХранилищаДвоичныхДанных.Полная;
	КонецЕсли;

	РегистрыСведений.ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных.ДобавитьЗапись(ВидРезервнойКопии, ТекущаяДатаСеанса(), ИмяФайлаНовойРезервнойКопии);
	
	ЗаписатьВЖурналРегистрацииСобытийРезервногоКопированияХДД(
												НСтр("ru = 'Завершено регламентное создание резервной копии встроенного хранилища двоичных данных.'",
												ОбщегоНазначения.КодОсновногоЯзыка()));

КонецПроцедуры
											
Функция ПроверенныйКаталогХраненияФайловРезервныхКопийВстроенногоХДД(ОписаниеОшибки = "")

	Результат = КаталогХраненияФайловРезервныхКопийВстроенногоХДД();
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ОписаниеОшибки = НСтр("ru = 'Не заполнена константа ""Каталог резервных копий встроенного хранилища двоичных данных""'");
	Иначе
		Результат = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Результат);

		Если Не ФайлСуществует(Результат) Тогда

			ШаблонСообщения = НСтр("ru = 'Не существует каталог, указанный для хранения резервных копий встроенного хранилища двоичных данных
									|%1'");
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Результат);

			Результат = "";
		КонецЕсли;		
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ИмяФайлаПоследнейПолнойРезервнойКопии()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных.ИмяФайлаРезервнойКопии КАК ИмяФайлаРезервнойКопии
		|ИЗ
		|	(ВЫБРАТЬ
		|		ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных.ВидРезервнойКопии КАК ВидРезервнойКопии,
		|		МАКСИМУМ(ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных.ДатаСоздания) КАК ДатаСоздания
		|	ИЗ
		|		РегистрСведений.ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных КАК ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных
		|	ГДЕ
		|		ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных.ВидРезервнойКопии = &ВидРезервнойКопии
		|		И ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных.ДатаСоздания <= &ДатаСоздания
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных.ВидРезервнойКопии) КАК ДатаПоследнейПолнойРезервнойКопии
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных КАК ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных
		|		ПО ДатаПоследнейПолнойРезервнойКопии.ВидРезервнойКопии = ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных.ВидРезервнойКопии
		|			И ДатаПоследнейПолнойРезервнойКопии.ДатаСоздания = ИсторияРезервногоКопированияВстроенногоХранилищаДвоичныхДанных.ДатаСоздания";

	Запрос.УстановитьПараметр("ВидРезервнойКопии", Перечисления.ВидыРезервныхКопийВстроенногоХранилищаДвоичныхДанных.Полная);
	Запрос.УстановитьПараметр("ДатаСоздания", ТекущаяДатаСеанса());

	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда
		Результат = СокрЛП(ВыборкаДанных.ИмяФайлаРезервнойКопии);
	Иначе
		Результат = "";
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ФайлСуществует(ПутьКФайлу)

	Результат = Не ПустаяСтрока(ПутьКФайлу);

	Если Результат Тогда
		Файл = Новый Файл(ПутьКФайлу);
		Результат = Файл.Существует();
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция НовоеИмяФайлаРезервнойКопии(ЭтоПолнаяКопия = Ложь)

	ТипРезервнойКопии = ?(ЭтоПолнаяКопия, "Full", "Differential");
	ДатаСозданияКопии = Формат(ТекущаяДатаСеанса(), "ДФ=ddMMyyyy_HHmmss");

	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1_%2.sbd", ТипРезервнойКопии, ДатаСозданияКопии);

КонецФункции

Процедура ЗаписатьВЖурналРегистрацииСобытийРезервногоКопированияХДД(Знач ТекстСообщения, Знач Уровень = Неопределено)

	Если Уровень = Неопределено Тогда
		Уровень = УровеньЖурналаРегистрации.Информация;
	КонецЕсли;

	ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрацииРезервногоКопированияВстроенногоХДД(), Уровень,,, ТекстСообщения);

КонецПроцедуры

Функция СобытиеЖурналаРегистрацииРезервногоКопированияВстроенногоХДД()
	
	Возврат НСтр("ru = 'Резервное копирование встроенного хранилища двоичных данных'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

Функция КаталогХраненияФайловРезервныхКопийВстроенногоХДД()

	УстановитьПривилегированныйРежим(Истина);

	Если ОбщегоНазначения.ЭтоWindowsСервер() Тогда

		Результат = Константы.КаталогРезервныхКопийВстроенногоХранилищаДвоичныхДанныхДляWindows.Получить();

	Иначе

		Результат = Константы.КаталогРезервныхКопийВстроенногоХранилищаДвоичныхДанныхДляLinux.Получить();

	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область НастройкаСоставаХранимыхДанных

// Вносит изменения в состав хранимых данных для хранилища двоичных данных, назначенного тому хранения файлов.
//
// Параметры:
//  ВидТомаХраненияФайлов		- ПеречислениеСсылка.ВидыХраненияФайлов
//  СпособХраненияФайлов		- ПеречислениеСсылка.СпособыХраненияФайлов
//  ИмяХранилищаДвоичныхДанных	- Строка - используется только для способа хранения файлов "ВоВнешнемХранилищеПоПротоколуS3"
//
Процедура ИзменитьСоставХранимыхДанных(ВидТомаХраненияФайлов, СпособХраненияФайлов, ИмяХранилищаДвоичныхДанных) Экспорт

	Если Не ЗначениеЗаполнено(СпособХраненияФайлов) Или СпособХраненияФайлов = Перечисления.СпособыХраненияФайлов.ВСетевыхКаталогах Тогда
		Возврат;		
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);

	ТекстОписаниеОшибки = "";
	
	Если ВидТомаХраненияФайлов = Перечисления.ВидыХраненияФайлов.АрхивноеХранение Тогда
		ИмяРеквизитаХранения = "ДвоичныеДанныеВАрхиве";
	Иначе
		ИмяРеквизитаХранения = РаботаСФайловымАрхивомСервер.ИмяРеквизитаХраненияДвоичныхДанныхПоСпособуХраненияТома(СпособХраненияФайлов);
	КонецЕсли;

	Если ЕстьОшибкаВНастройкахХранилищаДвоичныхДанных(ИмяРеквизитаХранения, ИмяХранилищаДвоичныхДанных, ТекстОписаниеОшибки) Тогда
		ВызватьИсключение ТекстОписаниеОшибки;
	КонецЕсли;

	ОбъектМетаданныхРеквизитаХранения = Неопределено;

	Если Не СоставХраненияНастроенДляРеквизита(ИмяРеквизитаХранения, ИмяХранилищаДвоичныхДанных, ОбъектМетаданныхРеквизитаХранения) Тогда

		НачальноеСостояниеМонопольногоРежима = МонопольныйРежим();

		УстановитьМонопольныйРежимДляИзмененияСоставаХранимыхДанных(НачальноеСостояниеМонопольногоРежима);

		НастроитьСоставХранимыхДанныхДляРеквизита(ИмяРеквизитаХранения, ОбъектМетаданныхРеквизитаХранения, ИмяХранилищаДвоичныхДанных);

		Если НачальноеСостояниеМонопольногоРежима <> МонопольныйРежим() Тогда
			УстановитьМонопольныйРежим(Ложь);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция ВидХраненияФайловПоИмениРеквизита(ИмяРеквизита)

	Если ИмяРеквизита = "ДвоичныеДанныеВАрхиве" Тогда
		Результат = Перечисления.ВидыХраненияФайлов.АрхивноеХранение;
	Иначе
		Результат = Перечисления.ВидыХраненияФайлов.ОперационноеХранение;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Функция СоставХраненияНастроенДляРеквизита(ИмяРеквизита, ИмяВнешнегоХранилищаДвоичныхДанных = "", ОбъектМетаданных = Неопределено)

	Результат = Ложь;

	ОбъектМетаданных = ОбъектМетаданныхДляРеквизитаХраненияФайлов(ИмяРеквизита, ОбъектМетаданных);

	Если ИмяРеквизита = "ДвоичныеДанныеВАрхиве" Или ИмяРеквизита = "ДвоичныеДанныеВОперационномВнешнемХранилище" Тогда
		
		Если Не ЗначениеЗаполнено(ИмяВнешнегоХранилищаДвоичныхДанных) Тогда
			ИмяВнешнегоХранилищаДвоичныхДанных = ИмяВнешнегоХранилищаТомаХраненияФайлов(ВидХраненияФайловПоИмениРеквизита(ИмяРеквизита));
		КонецЕсли;

		Если ЗначениеЗаполнено(ИмяВнешнегоХранилищаДвоичныхДанных) Тогда
			ВнешнееХранилищеДвоичныхДанных = ВнешниеХранилищаДвоичныхДанных.Найти(ИмяВнешнегоХранилищаДвоичныхДанных);

			Если ВнешнееХранилищеДвоичныхДанных <> Неопределено Тогда
				СоставХранимыхДанныхХранилища = ВнешнееХранилищеДвоичныхДанных.СоставХранимыхДанных;
				ЭлементСоставаХранимыхДанных = СоставХранимыхДанныхХранилища.Найти(ОбъектМетаданных);

				Если ЭлементСоставаХранимыхДанных <> Неопределено 
						И ЭлементСоставаХранимыхДанных.Хранение = ИспользованиеХраненияВХранилищеДвоичныхДанных.Использовать Тогда

					Результат = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли

	ИначеЕсли ИмяРеквизита = "ДвоичныеДанныеВОперационномВстроенномХранилище" Тогда

		Результат = РеквизитИспользуетсяВСоставеХранимыхДанныхВстроенногоХранилища(ИмяРеквизита, ОбъектМетаданных);

	ИначеЕсли ИмяРеквизита = "ДвоичныеДанные" Тогда

		Результат = ИспользованиеРеквизитаЯвноОтключеноВоВсехХранилищахДвоичныхДанных(ИмяРеквизита, ОбъектМетаданных);

	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура УстановитьМонопольныйРежимДляИзмененияСоставаХранимыхДанных(ТекущееСостояниеМонопольногоРежима)

	Если Не ТекущееСостояниеМонопольногоРежима Тогда

		Попытка
			УстановитьМонопольныйРежим(Истина);
		Исключение

			ШаблонОшибки = НСтр("ru = 'Не удалось установить монопольный режим для настройки состава реквизитов,
										|используемых в хранилищах двоичных данных:
										|%1'");

			ИнформацияОбОшибке	= ИнформацияОбОшибке();
			ТекстОписанияОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));			

			ВызватьИсключение ТекстОписанияОшибки;

		КонецПопытки;
	КонецЕсли;	
	
КонецПроцедуры

Процедура НастроитьСоставХранимыхДанныхДляРеквизита(ИмяРеквизита, ОбъектМетаданных = Неопределено, ИмяВнешнегоХранилища = "")

	ОбъектМетаданныхРеквизита = ОбъектМетаданныхДляРеквизитаХраненияФайлов(ИмяРеквизита, ОбъектМетаданных);

	Если ИмяРеквизита = "ДвоичныеДанные" Тогда

		Хранение = ИспользованиеХраненияВХранилищеДвоичныхДанных.НеИспользовать;

		СоставХранимыхДанных = ХранилищеДвоичныхДанных.ПолучитьСоставХранимыхДанных();

		Если УстановитьЭлементСоставаХранимыхДанныхДляРеквизита(СоставХранимыхДанных, ОбъектМетаданныхРеквизита, Хранение) Тогда

			ХранилищеДвоичныхДанных.УстановитьСоставХранимыхДанных(СоставХранимыхДанных);

		КонецЕсли;

		Для Каждого ВнешнееХранилище Из ВнешниеХранилищаДвоичныхДанных Цикл
			Если УстановитьЭлементСоставаХранимыхДанныхДляРеквизита(ВнешнееХранилище.СоставХранимыхДанных, ОбъектМетаданныхРеквизита, Хранение) Тогда
				ВнешнееХранилище.Записать();
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ИмяРеквизита = "ДвоичныеДанныеВАрхиве" Или ИмяРеквизита = "ДвоичныеДанныеВОперационномВнешнемХранилище" Тогда

		ИмяВнешнегоХранилища = ИмяВнешнегоХранилищаТомаХраненияФайлов(ВидХраненияФайловПоИмениРеквизита(ИмяРеквизита), ИмяВнешнегоХранилища);

		ИменаИсключаемыхХранилищ = ИменаИсключаемыхХранилищ(ИмяВнешнегоХранилища);

		УдалитьРеквизитИзСоставаХранимыхДанныхВсехХранилищ(ИмяРеквизита, ИменаИсключаемыхХранилищ, ОбъектМетаданныхРеквизита);

		Если Не ПустаяСтрока(ИмяВнешнегоХранилища) Тогда

			ВнешнееХранилище = ВнешниеХранилищаДвоичныхДанных.Найти(ИмяВнешнегоХранилища);
			Если ВнешнееХранилище <> Неопределено Тогда
			
				Хранение = ИспользованиеХраненияВХранилищеДвоичныхДанных.Использовать;

				Если УстановитьЭлементСоставаХранимыхДанныхДляРеквизита(ВнешнееХранилище.СоставХранимыхДанных, ОбъектМетаданныхРеквизита, Хранение) Тогда
					ВнешнееХранилище.Записать();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяРеквизита = "ДвоичныеДанныеВОперационномВстроенномХранилище" Тогда

		ИменаИсключаемыхХранилищ = ИменаИсключаемыхХранилищ("ВстроенноеХранилище");

		УдалитьРеквизитИзСоставаХранимыхДанныхВсехХранилищ(ИмяРеквизита, ИменаИсключаемыхХранилищ);

		Хранение = ИспользованиеХраненияВХранилищеДвоичныхДанных.Использовать;

		СоставХранимыхДанных = ХранилищеДвоичныхДанных.ПолучитьСоставХранимыхДанных();

		Если УстановитьЭлементСоставаХранимыхДанныхДляРеквизита(СоставХранимыхДанных, ОбъектМетаданныхРеквизита, Хранение) Тогда

			ХранилищеДвоичныхДанных.УстановитьСоставХранимыхДанных(СоставХранимыхДанных);

		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура УдалитьРеквизитИзСоставаХранимыхДанныхВсехХранилищ(ИмяРеквизита, ИменаИсключаемыхХранилищ = Неопределено, ОбъектМетаданных = Неопределено)

	ОбъектМетаданныхРеквизита = ОбъектМетаданныхДляРеквизитаХраненияФайлов(ИмяРеквизита, ОбъектМетаданных);

	Если Не(ИменаИсключаемыхХранилищ <> Неопределено И ИменаИсключаемыхХранилищ.Найти("ВстроенноеХранилище") <> Неопределено) Тогда

		СоставХранимыхДанных = ХранилищеДвоичныхДанных.ПолучитьСоставХранимыхДанных();

		Если УдалитьРеквизитИзСоставаХранимыхДанных(СоставХранимыхДанных, ОбъектМетаданныхРеквизита) Тогда

			ХранилищеДвоичныхДанных.УстановитьСоставХранимыхДанных(СоставХранимыхДанных);

		КонецЕсли;
	КонецЕсли;

	Для Каждого ВнешнееХранилище Из ВнешниеХранилищаДвоичныхДанных Цикл

		Если ИменаИсключаемыхХранилищ <> Неопределено И ИменаИсключаемыхХранилищ.Найти(ВнешнееХранилище.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Если УдалитьРеквизитИзСоставаХранимыхДанных(ВнешнееХранилище.СоставХранимыхДанных, ОбъектМетаданныхРеквизита) Тогда
			ВнешнееХранилище.Записать();
		КонецЕсли;
	КонецЦикла

КонецПроцедуры

Функция УдалитьРеквизитИзСоставаХранимыхДанных(СоставХранимыхДанных, ОбъектМетаданных)

	Результат = Ложь;

	ЭлементСоставаХранимыхДанных = СоставХранимыхДанных.Найти(ОбъектМетаданных);
	Если ЭлементСоставаХранимыхДанных <> Неопределено Тогда
		СоставХранимыхДанных.Удалить(ЭлементСоставаХранимыхДанных);
		Результат = Истина;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ИспользованиеРеквизитаЯвноОтключеноВоВсехХранилищахДвоичныхДанных(ИмяРеквизита, ОбъектМетаданных = Неопределено)

	Результат = ХранилищаДвоичныхДанныхОтсутствуют();

	Если Не Результат Тогда

		ОтключеноВоВстроенномХранилище = Не ХранилищеДвоичныхДанных.ПолучитьДоступностьХранилищаДвоичныхДанных();

		Если Не ОтключеноВоВстроенномХранилище Тогда
			
			СоставХранимыхДанныхХранилища = ХранилищеДвоичныхДанных.ПолучитьСоставХранимыхДанных();

			ЗначениеОбъектаМетаданных = ОбъектМетаданныхДляРеквизитаХраненияФайлов(ИмяРеквизита, ОбъектМетаданных);
			ЭлементСоставаХранимыхДанных = СоставХранимыхДанныхХранилища.Найти(ЗначениеОбъектаМетаданных);

			Если ЭлементСоставаХранимыхДанных <> Неопределено Тогда
				ОтключеноВоВстроенномХранилище = ЭлементСоставаХранимыхДанных.Хранение = ИспользованиеХраненияВХранилищеДвоичныхДанных.НеИспользовать;
			КонецЕсли;
		КонецЕсли;

		ОтключеноВоВнешнихХранилищах = ВнешниеХранилищаДвоичныхДанных.Количество() = 0;

		Если Не ОтключеноВоВнешнихХранилищах Тогда

			ОтключеноВоВнешнихХранилищах = Истина;

	        ЗначениеОбъектаМетаданных = ОбъектМетаданныхДляРеквизитаХраненияФайлов(ИмяРеквизита, ОбъектМетаданных);

			Для Каждого ВнешнееХранилище Из ВнешниеХранилищаДвоичныхДанных Цикл
				СоставХранимыхДанныхХранилища = ВнешнееХранилище.СоставХранимыхДанных;
				ЭлементСоставаХранимыхДанных = СоставХранимыхДанныхХранилища.Найти(ЗначениеОбъектаМетаданных);

				Если ЭлементСоставаХранимыхДанных = Неопределено 
						Или ЭлементСоставаХранимыхДанных.Хранение = ИспользованиеХраненияВХранилищеДвоичныхДанных.Использовать Тогда

					ОтключеноВоВнешнихХранилищах = Ложь;
					Прервать
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Результат = ОтключеноВоВстроенномХранилище И ОтключеноВоВнешнихХранилищах;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция РеквизитИспользуетсяВСоставеХранимыхДанныхВстроенногоХранилища(ИмяРеквизита, ОбъектМетаданных = Неопределено)

	Результат = Ложь;	

	Если ХранилищеДвоичныхДанных.ПолучитьДоступностьХранилищаДвоичныхДанных() Тогда

		СоставХранимыхДанныхХранилища = ХранилищеДвоичныхДанных.ПолучитьСоставХранимыхДанных();

		ЗначениеОбъектаМетаданных = ОбъектМетаданныхДляРеквизитаХраненияФайлов(ИмяРеквизита, ОбъектМетаданных);
		ЭлементСоставаХранимыхДанных = СоставХранимыхДанныхХранилища.Найти(ЗначениеОбъектаМетаданных);

		Если ЭлементСоставаХранимыхДанных <> Неопределено Тогда
			Результат = ЭлементСоставаХранимыхДанных.Хранение = ИспользованиеХраненияВХранилищеДвоичныхДанных.Использовать;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ИмяВнешнегоХранилищаТомаХраненияФайлов(ВидТомаХраненияФайлов, ИмяВнешнегоХранилища = "")

	Если ЗначениеЗаполнено(ИмяВнешнегоХранилища) Тогда

		Результат = ИмяВнешнегоХранилища;

	Иначе

		Результат = "";

		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТомаХраненияФайлов.ИмяХранилищаДвоичныхДанных КАК ИмяХранилищаДвоичныхДанных
		|ИЗ
		|	Справочник.ТомаХраненияФайлов КАК ТомаХраненияФайлов
		|ГДЕ
		|	НЕ ТомаХраненияФайлов.ПометкаУдаления
		|	И ТомаХраненияФайлов.ВидТомаХраненияФайлов = &ВидТомаХраненияФайлов
		|	И ТомаХраненияФайлов.СпособХраненияФайлов = ЗНАЧЕНИЕ(Перечисление.СпособыХраненияФайлов.ВоВнешнемХранилищеПоПротоколуS3)";

		Запрос.УстановитьПараметр("ВидТомаХраненияФайлов", ВидТомаХраненияФайлов);

		ВыборкаДанных = Запрос.Выполнить().Выбрать();
		Если ВыборкаДанных.Следующий() Тогда
			Результат = ВыборкаДанных.ИмяХранилищаДвоичныхДанных;
		КонецЕсли;		
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ЕстьОшибкаВНастройкахХранилищаДвоичныхДанных(ИмяРеквизита, Знач ИмяВнешнегоХранилища = "", ОписаниеОшибки = "")

	Результат = Ложь;

	Если ИмяРеквизита = "ДвоичныеДанныеВАрхиве" Или ИмяРеквизита = "ДвоичныеДанныеВОперационномВнешнемХранилище" Тогда

		Результат = ВнешниеХранилищаДвоичныхДанных.Количество() = 0;

		Если Результат Тогда
			ОписаниеОшибки = НСтр("ru = 'Отсутствуют подключенные внешние хранилища двоичных данных'");
		КонецЕсли;

		Если Не Результат Тогда

			ВидТомаХранения = ВидХраненияФайловПоИмениРеквизита(ИмяРеквизита);

			ИмяВнешнегоХранилища = ИмяВнешнегоХранилищаТомаХраненияФайлов(ВидТомаХранения, ИмяВнешнегоХранилища);

			Результат = ПустаяСтрока(ИмяВнешнегоХранилища);

			Если Результат Тогда
				ОписаниеОшибки = НСтр("ru = 'Не удалось определить имя внешнего хранилища двоичных данных'");
			КонецЕсли;

			Если Не Результат Тогда
				ВнешнееХранилищеДвоичныхДанных = ВнешниеХранилищаДвоичныхДанных.Найти(ИмяВнешнегоХранилища);

				Результат = ВнешнееХранилищеДвоичныхДанных = Неопределено;

				Если Результат Тогда
					ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'Отсутствует внешнее хранилище двоичных данных с именем %1'"),
												ИмяВнешнегоХранилища);
				КонецЕсли;

				Если Не Результат Тогда
					Результат = ВнешнееХранилищеДвоичныхДанных.РежимЧтенияЗаписи = РежимЧтенияЗаписиХранилищаДвоичныхДанных.ТолькоЧтение;

					Если Результат Тогда
						ОписаниеОшибки = ТекстОшибкиНедоступностиЗаписиВХранилищеДвоичныхДанных();
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИмяРеквизита = "ДвоичныеДанныеВОперационномВстроенномХранилище" Тогда

		РежимИспользованияХДД = ХранилищеДвоичныхДанных.ПолучитьРежимИспользованияХранилищаДвоичныхДанных();
		Результат = РежимИспользованияХДД <> РежимИспользованияХранилищаДвоичныхДанных.Разрешить;

		Если Результат Тогда
			ОписаниеОшибки = НСтр("ru = 'Встроенное хранилище двоичных данных недоступно'");
		КонецЕсли;

		Если Не Результат Тогда
			Результат = ХранилищеДвоичныхДанных.ПолучитьРежимЧтенияЗаписиХранилищаДвоичныхДанных() = РежимЧтенияЗаписиХранилищаДвоичныхДанных.ТолькоЧтение;
			Если Результат Тогда
				ОписаниеОшибки = ТекстОшибкиНедоступностиЗаписиВХранилищеДвоичныхДанных(Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ТекстОшибкиНедоступностиЗаписиВХранилищеДвоичныхДанных(ВнешнееХранилище = Истина)
	
	Если ВнешнееХранилище Тогда
		Результат = НСтр("ru = 'Внешнее хранилище двоичных данных доступно только для чтения
							|В настройках хранилища отключено разрешение на запись'");

	Иначе
		Результат = НСтр("ru = 'Встроенное хранилище двоичных данных доступно только для чтения
							|В настройках хранилища отключено разрешение на запись'");		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция УстановитьЭлементСоставаХранимыхДанныхДляРеквизита(СоставХранимыхДанных, ОбъектМетаданных, Хранение)

	ЭлементСоставаХранимыхДанных = СоставХранимыхДанных.Найти(ОбъектМетаданных);

	Если ЭлементСоставаХранимыхДанных <> Неопределено Тогда
		Если ЭлементСоставаХранимыхДанных.Хранение = Хранение Тогда
			Возврат Ложь;
		Иначе
			СоставХранимыхДанных.Удалить(ЭлементСоставаХранимыхДанных);
		КонецЕсли;
	КонецЕсли;

	СоставХранимыхДанных.Добавить(ОбъектМетаданных, Хранение);

	Возврат Истина;

КонецФункции

Функция ИменаИсключаемыхХранилищ(ИменаХранилищ = "")

	Результат = Неопределено;

	Если Не ПустаяСтрока(ИменаХранилищ) Тогда
		Результат = СтрРазделить(ИменаХранилищ, ",", Ложь);
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ТомХраненияРазмещающийДанныеВоВстроенномХранилище()

	Результат = Неопределено;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТомаХраненияФайлов.Ссылка КАК ТомОперативногоХранения
	|ИЗ
	|	Справочник.ТомаХраненияФайлов КАК ТомаХраненияФайлов
	|ГДЕ
	|	НЕ ТомаХраненияФайлов.ПометкаУдаления
	|	И ТомаХраненияФайлов.ВидТомаХраненияФайлов = ЗНАЧЕНИЕ(Перечисление.ВидыХраненияФайлов.ОперационноеХранение)
	|	И ТомаХраненияФайлов.СпособХраненияФайлов = ЗНАЧЕНИЕ(Перечисление.СпособыХраненияФайлов.ВоВстроенномХранилище)";

	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда
		Результат = ВыборкаДанных.ТомОперативногоХранения;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ОбъектМетаданныхДляРеквизитаХраненияФайлов(ИмяРеквизита, ОбъектМетаданных = Неопределено)

	Результат = ОбъектМетаданных;
	Если ОбъектМетаданных = Неопределено Тогда
		Если ИмяРеквизита = "ДвоичныеДанные" Тогда
			Результат = Метаданные.Справочники.ХранилищеДвоичныхДанных.Реквизиты[ИмяРеквизита];
		Иначе
			Результат = Метаданные.РегистрыСведений.МестаХраненияДвоичныхДанных.Ресурсы[ИмяРеквизита];
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#КонецОбласти