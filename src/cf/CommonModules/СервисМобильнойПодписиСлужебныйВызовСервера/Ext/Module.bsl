///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Функция ПараметрыПодписанта(Знач Подписант) Экспорт
	
	Возврат СервисМобильнойПодписи.ПараметрыПодписанта(Подписант);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция АвторизоватьсяВГосключе(Знач Настройка, Знач Цель, Знач Подпись) Экспорт
	
	Результат = СервисМобильнойПодписиСлужебный.АвторизоватьсяВГосключе(Настройка, Цель, Подпись);
	Если ЗначениеЗаполнено(Результат.Ошибка) Тогда
		Возврат Результат.Ошибка;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции 

Функция НастройкаОтправки(Знач ПараметрыПодписания, ДляАвторизации = Ложь) Экспорт
	Возврат СервисМобильнойПодписиСлужебный.НастройкаОтправки(ПараметрыПодписания, ДляАвторизации);
КонецФункции

Функция ЗапросНаОтправкуДокумента(Знач ПараметрыПодписанта, Знач ВидМобильнойПодписи, Знач НастройкаОтправки, Знач УникальныйИдентификатор) Экспорт
	
	ПараметрыПодписанта.МояПодпись = Ложь;
	ПараметрыПодписанта.ГосключУКЭП = ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.ГосключУКЭП;
	ПараметрыПодписанта.ГосключУНЭП = ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.ГосключУНЭП;
	
	Запрос = СервисМобильнойПодписиСлужебный.ЗапросНаОтправкуДокумента(ПараметрыПодписанта, НастройкаОтправки);
	Возврат ПоместитьВоВременноеХранилище(ПолучитьДвоичныеДанныеИзСтроки(Запрос), УникальныйИдентификатор);
	
КонецФункции

Функция ПодписатьДанныеВСервисе(Знач УникальныйИдентификатор, Знач НаборДанных, Знач Подписант, Знач Параметры) Экспорт
	
	ПараметрыПодписания = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписания();
	ЗаполнитьЗначенияСвойств(ПараметрыПодписания, Параметры);
	
	Если Не ЗначениеЗаполнено(ПараметрыПодписания.ИдентификаторФормы) Тогда
		ПараметрыПодписания.ИдентификаторФормы = УникальныйИдентификатор;
	КонецЕсли; 
	
	Если ТипЗнч(ПараметрыПодписания.НастройкаОтправки) <> Тип("Структура") Тогда
		НастройкаОтправки = СервисМобильнойПодписиСлужебный.НастройкаОтправки(ПараметрыПодписания);
		Если ТипЗнч(НастройкаОтправки) = Тип("Строка") Тогда
			Возврат НастройкаОтправки;
		КонецЕсли;
		ПараметрыПодписания.НастройкаОтправки = НастройкаОтправки;
	КонецЕсли;
	
	Если ПараметрыПодписания.НаборДанныхПроверен <> Истина Тогда
		НаборДанныхПроверен = СервисМобильнойПодписиКлиентСервер.ПроверкаНабораДанных(НаборДанных, ПараметрыПодписания);
		Если ТипЗнч(НаборДанныхПроверен) = Тип("Строка") Тогда
			Возврат НаборДанныхПроверен;
		КонецЕсли;
	КонецЕсли;
		
	ПараметрыПодписания.НаборДанныхПроверен = Истина;
	
	Для Каждого Элемент Из НаборДанных Цикл
		Если ЭтоАдресВременногоХранилища(Элемент.Данные) Тогда
			Элемент.Данные = ПолучитьИзВременногоХранилища(Элемент.Данные);
		КонецЕсли;
		Если Элемент.Свойство("СвойстваПодписи") И ЭтоАдресВременногоХранилища(Элемент.СвойстваПодписи) Тогда
			Элемент.СвойстваПодписи = ПолучитьИзВременногоХранилища(Элемент.СвойстваПодписи);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПараметрыПодписания.ЗапросНаОтправкуДокумента) Тогда
		ПараметрыПодписания.ЗапросНаОтправкуДокумента.ДанныеЗапроса = ПолучитьИзВременногоХранилища(
			ПараметрыПодписания.ЗапросНаОтправкуДокумента.ДанныеЗапроса);
		Если ЭтоАдресВременногоХранилища(ПараметрыПодписания.ЗапросНаОтправкуДокумента.СвойстваПодписи) Тогда
			ПараметрыПодписания.ЗапросНаОтправкуДокумента.СвойстваПодписи = ПолучитьИзВременногоХранилища(
				ПараметрыПодписания.ЗапросНаОтправкуДокумента.СвойстваПодписи);
		КонецЕсли;
	КонецЕсли;

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "СервисМобильнойПодписиСлужебный.ОтправитьНаПодписание",
		НаборДанных, Подписант, ПараметрыПодписания);
	
КонецФункции


Функция РезультатПроверкиОжидаемойПодписи(Знач Данные, Знач ИдентификаторПодписи, Знач ПараметрыПроверкиПодписи) Экспорт
	Результат = СервисМобильнойПодписиСлужебный.РезультатПроверкиОжидаемыхПодписей(ИдентификаторПодписи, Истина);

	Если ЗначениеЗаполнено(Результат.Ошибка) Тогда
		РезультатПроверкиПодписи = ЭлектроннаяПодписьКлиентСервер.РезультатПроверкиПодписи();
		РезультатПроверкиПодписи.Вставить("ДатаПодписи");
		РезультатПроверкиПодписи.Вставить("ИдентификаторПодписи", ИдентификаторПодписи);
		РезультатПроверкиПодписи.Вставить("ОшибкаПроверкиДополнительныхАтрибутов", СервисМобильнойПодписиСлужебный.ТекстОшибкиОжидаемойПодписи(Результат.Ошибка));
		Возврат РезультатПроверкиПодписи;
	КонецЕсли;
	
	РезультатПроверкиПодписи = Результат.РезультатыПроверки.Получить(ИдентификаторПодписи);
	Если ЗначениеЗаполнено(РезультатПроверкиПодписи) Тогда
		Возврат РезультатПроверкиПодписи;
	КонецЕсли;
	
	Возврат ЭлектроннаяПодписьКлиентСервер.РезультатПроверкиПодписи();
КонецФункции

#КонецОбласти
