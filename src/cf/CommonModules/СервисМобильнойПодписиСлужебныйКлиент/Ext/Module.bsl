///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьНастройкуОтправкиВГосключ(ВладелецФормы = Неопределено, ОповещениеОЗавершении = Неопределено) Экспорт
	
	РежимОткрытияОкна = ?(ВладелецФормы = Неопределено, Неопределено,
		РежимБлокировкиПриОткрытииОкнаФормы.БлокироватьВладельца);
	
	ОткрытьФорму("Справочник.НастройкиОтправкиВГосключ.Форма.ФормаЭлемента", , ВладелецФормы,,,,
		ОповещениеОЗавершении, РежимОткрытияОкна);

КонецПроцедуры 

Функция ИмяФормыОтправкиНаПодписание() Экспорт
	
	Возврат "Справочник.ПодписантыСервисаМобильнойПодписи.Форма.ОтправитьНаПодписание";
	
КонецФункции 

Процедура ПроверитьОжидаемуюПодпись(Оповещение, Данные, ИдентификаторПодписи, ПараметрыПроверкиПодписи) Экспорт
	
	Результат = СервисМобильнойПодписиСлужебныйВызовСервера.РезультатПроверкиОжидаемойПодписи(Данные, ИдентификаторПодписи, ПараметрыПроверкиПодписи);
	СвойстваЗаписаннойПодписи = Неопределено;
	Если Результат.Свойство("ЗаписаннаяПодпись", СвойстваЗаписаннойПодписи) Тогда
		ЗаполнитьЗначенияСвойств(Оповещение.ДополнительныеПараметры.СтрокаПодписи, Результат);
		Оповещение.ДополнительныеПараметры.СтрокаПодписи.АдресПодписи = ПоместитьВоВременноеХранилище(
			Результат.Подпись, Оповещение.ДополнительныеПараметры.Форма.УникальныйИдентификатор);
		Оповещение.ДополнительныеПараметры.СтрокаПодписи.АдресСертификата = ПоместитьВоВременноеХранилище(
			Результат.Сертификат, Оповещение.ДополнительныеПараметры.Форма.УникальныйИдентификатор);
		Оповещение.ДополнительныеПараметры.СтрокаПодписи.ПорядковыйНомер = СвойстваЗаписаннойПодписи.ПорядковыйНомер;
		ЭлектроннаяПодписьКлиент.ПроверитьПодпись(Оповещение, Данные, Результат.Подпись,,,ПараметрыПроверкиПодписи);
		Возврат;
	КонецЕсли;
	ВыполнитьОбработкуОповещения(Оповещение, Результат);
	
КонецПроцедуры

// Подписание с получением результата из формы ПодписаниеДанных
Процедура ПодписатьДанныеВСервисе(Оповещение, КонтекстПодписания, ПараметрыПодписания) Экспорт
	
	ОписаниеДанных = КонтекстПодписания.ПараметрыВыполнения.ОписаниеДанных;
	Подписант = ОписаниеДанных.ВыбранныйПодписант;
	
	НаборДанных = Новый Массив;
	СписокПредставлений = Неопределено;
	ОписаниеДанных.Свойство("СписокПредставлений", СписокПредставлений);
		
	Если ОписаниеДанных.Свойство("Данные")
		Или ОписаниеДанных.Свойство("Объект") Тогда
		ЗаполнитьНаборДанныхДляОтправкиНаПодписание(НаборДанных, ОписаниеДанных, СписокПредставлений, 0);
	Иначе
		Индекс = 0;
		Для каждого ЭлементДанных Из ОписаниеДанных.НаборДанных Цикл
			ЗаполнитьНаборДанныхДляОтправкиНаПодписание(НаборДанных, ЭлементДанных, СписокПредставлений, Индекс);
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("КонтекстПодписания", КонтекстПодписания);
	ПараметрыВыполнения.Вставить("ОповещениеОПодписанииВСервисе", Оповещение);
	ПараметрыВыполнения.Вставить("ЭтоПодписание", Истина);

	НастройкаОтправки = СервисМобильнойПодписиСлужебныйВызовСервера.НастройкаОтправки(ПараметрыПодписания);
	Если ТипЗнч(НастройкаОтправки) = Тип("Строка") Тогда
		ОбработатьОшибку(ПараметрыВыполнения, НастройкаОтправки);
		Возврат;
	КонецЕсли;
	
	ПараметрыПодписания.НастройкаОтправки = НастройкаОтправки;
	
	НаборДанныхПроверен = СервисМобильнойПодписиКлиентСервер.ПроверкаНабораДанных(НаборДанных, ПараметрыПодписания);
	Если ТипЗнч(НаборДанныхПроверен) = Тип("Строка") Тогда
		ОбработатьОшибку(ПараметрыВыполнения, НаборДанныхПроверен);
		Возврат;
	КонецЕсли;
	
	ПараметрыПодписания.НаборДанныхПроверен = Истина;
	ПараметрыВыполнения.Вставить("ПараметрыПодписания", ПараметрыПодписания);
	
	Если НастройкаОтправки.Госключ И (Не НастройкаОтправки.ПарольСертификатаУстановлен
		Или Не НастройкаОтправки.РазрешеноПодписаниеНаСервере) Тогда
		
		Контекст = Новый Структура;
		Контекст.Вставить("ПараметрыВыполнения", ПараметрыВыполнения);
		Контекст.Вставить("НаборДанных", НаборДанных);
		ПодписатьДанныеПередОтправкойВГосключ(Контекст);
		Возврат;
		
	КонецЕсли;
	
	ПодписатьДанныеВСервисеДлительнаяОперация(ПараметрыВыполнения, НаборДанных);
	
КонецПроцедуры

Процедура ПодписантПриОткрытии(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(Форма.Подписант) Тогда
		СтандартнаяОбработка = Ложь;
		ДобавитьПодписантаВФорме(Форма, Элемент);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодписантПриСоздании(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ДобавитьПодписантаВФорме(Форма, Элемент);

КонецПроцедуры

Процедура ПодписантПриОчистке(Форма, Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Форма.Подписант = ПредопределенноеЗначение("Справочник.ПодписантыСервисаМобильнойПодписи.ПустаяСсылка");
	
КонецПроцедуры

// См. СтандартныеПодсистемыКлиент.ПриПолученииСерверногоОповещения.
Процедура ПриПолученииСерверногоОповещения(ИмяОповещения, Результат) Экспорт
	
	Если ИмяОповещения <> СервисМобильнойПодписиКлиентСервер.ИмяСерверногоОповещения() Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.НастройкиОтправкиВГосключ") Тогда
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Требуется авторизация в Госключе'"), 
			Новый ОписаниеОповещения("ОткрытьНастройкуОтправкиВГосключ", ЭтотОбъект, Результат), Результат,
			БиблиотекаКартинок.ДиалогВосклицание, СтатусОповещенияПользователя.Важное, Результат);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОткрытьНастройкуОтправкиВГосключ(Настройка) Экспорт
	ПоказатьЗначение(, Настройка);
КонецПроцедуры

// Отправка без получения результата из формы ОтправкаНаПодписание
Процедура ОтправитьНаПодписаниеВСервис(Оповещение, КонтекстПодписания, ПараметрыПодписания) Экспорт
	
	ОписаниеДанных = КонтекстПодписания.ПараметрыВыполнения.ОписаниеДанных;
	Подписант = ОписаниеДанных.ВыбранныйПодписант;
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("КонтекстПодписания", КонтекстПодписания);
	ПараметрыВыполнения.Вставить("ОповещениеОПодписанииВСервисе", Оповещение);
	ПараметрыВыполнения.Вставить("ЭтоПодписание", Ложь);
	
	НаборДанных = Новый Массив;
	Успех = Истина;
	
	Индекс = -1;
	Для Каждого Элемент Из ОписаниеДанных.НаборДанных Цикл
		Индекс = Индекс +1;
		
		Если ТипЗнч(Элемент.Данные) <> Тип("Строка") И ТипЗнч(Элемент.Данные) <> Тип("ДвоичныеДанные") Тогда
			ОписаниеДанных.Вставить("ТекущийЭлементНабораДанных", Элемент);
			ОбработатьОшибку(ПараметрыВыполнения, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Не поддерживается отправка в сервис данных с типом %1'"), ТипЗнч(Элемент.Данные)));
			Возврат;
		КонецЕсли;
		
		Структура = Новый Структура;
		Структура.Вставить("Данные", Элемент.Данные);
		
		Если Элемент.Свойство("Объект") Тогда
			Структура.Вставить("Объект", Элемент.Объект);
		КонецЕсли;
		Если Элемент.Свойство("ВерсияОбъекта") Тогда
			Структура.Вставить("ВерсияОбъекта", Элемент.ВерсияОбъекта);
		КонецЕсли;
		
		Структура.Вставить("Представление", Элемент.Представление.Представление);
		НаборДанных.Добавить(Структура);
	КонецЦикла;
	
	СписокПредставлений = Неопределено;
	ОписаниеДанных.Свойство("СписокПредставлений", СписокПредставлений);
		
	НастройкаОтправки = СервисМобильнойПодписиСлужебныйВызовСервера.НастройкаОтправки(ПараметрыПодписания);
	Если ТипЗнч(НастройкаОтправки) = Тип("Строка") Тогда
		ОбработатьОшибку(ПараметрыВыполнения, НастройкаОтправки);
		Возврат;
	КонецЕсли;
	
	ПараметрыПодписания.НастройкаОтправки = НастройкаОтправки;
	
	ПроверкаНабораДанных = СервисМобильнойПодписиКлиентСервер.ПроверкаНабораДанных(НаборДанных, ПараметрыПодписания);
	
	Если ТипЗнч(ПроверкаНабораДанных) = Тип("Строка") Тогда
		ОбработатьОшибку(ПараметрыВыполнения, ПроверкаНабораДанных);
		Возврат;
	КонецЕсли;
	
	ПараметрыПодписания.НаборДанныхПроверен = Истина;
	ПараметрыВыполнения.Вставить("ПараметрыПодписания", ПараметрыПодписания);
	
	Если НастройкаОтправки.Госключ И (Не НастройкаОтправки.ПарольСертификатаУстановлен
		Или Не НастройкаОтправки.РазрешеноПодписаниеНаСервере) Тогда
		
		Контекст = Новый Структура;
		Контекст.Вставить("ПараметрыВыполнения", ПараметрыВыполнения);
		Контекст.Вставить("НаборДанных", НаборДанных);
		ПодписатьДанныеПередОтправкойВГосключ(Контекст);
		Возврат;
	
	КонецЕсли;
	
	ПодписатьДанныеВСервисеДлительнаяОперация(ПараметрыВыполнения, НаборДанных);

КонецПроцедуры

Процедура ПодписатьДанныеВСервисеДлительнаяОперация(ПараметрыВыполнения, НаборДанных)
	
	ДлительнаяОперация = СервисМобильнойПодписиСлужебныйВызовСервера.ПодписатьДанныеВСервисе(
		ПараметрыВыполнения.КонтекстПодписания.ИдентификаторФормы,
		НаборДанных, ПараметрыВыполнения.КонтекстПодписания.ПараметрыВыполнения.ОписаниеДанных.ВыбранныйПодписант,
		ПараметрыВыполнения.ПараметрыПодписания);
		
	Если ТипЗнч(ДлительнаяОперация) = Тип("Строка") Тогда
		ОбработатьОшибку(ПараметрыВыполнения, ДлительнаяОперация);
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеОтправкиНаПодписаниеДанныхВСервис", ЭтотОбъект, ПараметрыВыполнения);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ПараметрыВыполнения.КонтекстПодписания.ПараметрыВыполнения.Форма);
	Если ПараметрыВыполнения.ПараметрыПодписания.НастройкаОтправки.Госключ Тогда
		ПараметрыОжидания.Заголовок = НСтр("ru='Отправка документов на подписание в приложение ""Госключ""'");
	Иначе
		ПараметрыОжидания.Заголовок = НСтр("ru='Отправка документов на подписание в приложение ""Моя подпись"" от ФНС'");
	КонецЕсли;
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

Процедура ПодписатьДанныеПередОтправкойВГосключ(Контекст)
	
	НастройкаОтправки = Контекст.ПараметрыВыполнения.ПараметрыПодписания.НастройкаОтправки;
	Если Не НастройкаОтправки.ПараметрыАвторизацииВГосключеУстановлены Тогда
		Если Не ЗначениеЗаполнено(НастройкаОтправки.Ключ) Тогда
			ОбработатьОшибку(Контекст.ПараметрыВыполнения,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Не указан ключ доступа в настройке %1'"), НастройкаОтправки.Ссылка));
			Возврат;
		КонецЕсли;
		АвторизоватьсяВГосключе(Контекст);
		Возврат;
	КонецЕсли;
	
	ПодписатьЗаявлениеИНаборДанных(Контекст);
	
КонецПроцедуры

Процедура АвторизоватьсяВГосключе(Контекст)
	
	НастройкаОтправки = Контекст.ПараметрыВыполнения.ПараметрыПодписания.НастройкаОтправки;
	ПодписатьТокенДоступа(Новый ОписаниеОповещения("ПослеПодписанияКлючаАвторизации", ЭтотОбъект, Контекст), НастройкаОтправки);
	
КонецПроцедуры
	
Процедура ПодписатьТокенДоступа(Оповещение, НастройкаОтправки) Экспорт
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Операция", НСтр("ru='Подписание запроса для получения токена доступа'"));
	ОписаниеДанных.Вставить("ЗаголовокДанных", НСтр("ru='Код доступа'"));
	Если ЗначениеЗаполнено(НастройкаОтправки.Сертификат) Тогда
		ОписаниеДанных.Вставить("ОтборСертификатов", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			НастройкаОтправки.Сертификат));
	КонецЕсли;
	ПараметрыПодписи = ЭлектроннаяПодписьКлиент.НовыйТипПодписи();
	ПараметрыПодписи.ТипыПодписи = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПредопределенноеЗначение("Перечисление.ТипыПодписиКриптографии.БазоваяCAdESBES"));
	
	Данные = Новый Структура("ПараметрыCMS, Данные", ЭлектроннаяПодписьКлиент.ПараметрыCMS(), НастройкаОтправки.Ключ);
	ОписаниеДанных.Вставить("Данные", Данные);
	
	ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, ЭтотОбъект, 
		Оповещение, ПараметрыПодписи);
	
КонецПроцедуры

Процедура ПослеПодписанияКлючаАвторизации(Результат, Контекст) Экспорт
	
	Если Результат.Отказ = Истина Или Не Результат.Успех Тогда
		ОбработатьОшибку(Контекст.ПараметрыВыполнения, 
			НСтр("ru='Не удалось подписать данные для авторизации в Госключе'"));
		Возврат;
	КонецЕсли;
	
	СвойстваПодписи = Результат.СвойстваПодписи;
	Если ЭтоАдресВременногоХранилища(СвойстваПодписи) Тогда
		СвойстваПодписи = ПолучитьИзВременногоХранилища(СвойстваПодписи);
	КонецЕсли;
	
	НастройкаОтправки = Контекст.ПараметрыВыполнения.ПараметрыПодписания.НастройкаОтправки;
	
	Результат = СервисМобильнойПодписиСлужебныйВызовСервера.АвторизоватьсяВГосключе(
		НастройкаОтправки, "", СвойстваПодписи.Подпись);
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ОбработатьОшибку(Контекст.ПараметрыВыполнения, Результат);
		Возврат;
	КонецЕсли;
	НастройкаОтправки.Ключ = "";
	ПодписатьЗаявлениеИНаборДанных(Контекст);
	
КонецПроцедуры

Процедура ПодписатьЗаявлениеИНаборДанных(Контекст)
	
	НастройкаОтправки = Контекст.ПараметрыВыполнения.ПараметрыПодписания.НастройкаОтправки;
	
	ДанныеЗапроса = СервисМобильнойПодписиСлужебныйВызовСервера.ЗапросНаОтправкуДокумента(
		Контекст.ПараметрыВыполнения.КонтекстПодписания.ПараметрыВыполнения.ОписаниеДанных.ВыбранныйПодписант,
		Контекст.ПараметрыВыполнения.ПараметрыПодписания.ВидМобильнойПодписи,
		НастройкаОтправки,
		Контекст.ПараметрыВыполнения.КонтекстПодписания.ИдентификаторФормы);
		
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Операция", НСтр("ru='Подписание запроса для отправки в Госключ'"));
	ОписаниеДанных.Вставить("ЗаголовокДанных", "");
	ОписаниеДанных.Вставить("БезПодтверждения", Истина);
	Если ЗначениеЗаполнено(НастройкаОтправки.Сертификат) Тогда
		ОписаниеДанных.Вставить("ОтборСертификатов", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			НастройкаОтправки.Сертификат));
	КонецЕсли;
	
	НаборДанных = Новый Массив;
	Для Каждого Элемент Из Контекст.НаборДанных Цикл
		Если Элемент.Свойство("Объект") И ЗначениеЗаполнено(Элемент.Объект) Тогда
			Представление = Новый Структура("Значение, Представление", 
				Элемент.Объект, Элемент.Представление)
		Иначе
			Представление = Элемент.Представление;
		КонецЕсли;
		
		НаборДанных.Добавить(Новый Структура("Данные, Представление", 
			Элемент.Данные, Представление));
	КонецЦикла;
	
	НаборДанных.Добавить(Новый Структура("Данные, Представление", 
		ДанныеЗапроса, Новый Структура("Значение, Представление", 
		Новый ОписаниеОповещения("ОткрытьФайлЗапроса", ЭтотОбъект, ДанныеЗапроса), "req.xml")));
	
	ОписаниеДанных.Вставить("ПредставлениеНабора", НСтр("ru='Файлы запроса (%1)'"));
	ОписаниеДанных.Вставить("НаборДанных", НаборДанных);
	
	ПараметрыПодписи = ЭлектроннаяПодписьКлиент.НовыйТипПодписи();
	ПараметрыПодписи.ТипыПодписи = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		ПредопределенноеЗначение("Перечисление.ТипыПодписиКриптографии.БазоваяCAdESBES"));
	ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, ЭтотОбъект, 
		Новый ОписаниеОповещения("ПослеПодписанияЗаявленияИНабораДанных", ЭтотОбъект, Контекст), ПараметрыПодписи);
	
КонецПроцедуры
	
Процедура ОткрытьФайлЗапроса(Результат, ДанныеЗапроса) Экспорт
	ДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеЗапроса);
	Строка = ПолучитьСтрокуИзДвоичныхДанных(ДанныеФайла);
	ПоказатьЗначение(, Строка);
КонецПроцедуры

Процедура ПослеПодписанияЗаявленияИНабораДанных(Результат, Контекст) Экспорт
	
	Если Результат.Отказ = Истина Или Не Результат.Успех Тогда
		
		Если Результат.Отказ Тогда
			ТекстОшибки = НСтр("ru='Подписание запроса для отправки в Госключ отменено.'")
		Иначе
			Если Результат.Свойство("Ошибка") Тогда
				ОбработатьОшибку(Контекст.ПараметрыВыполнения, Результат.Ошибка);
				Возврат;
			Иначе
				ТекстОшибки = НСтр("ru='Не удалось подписать запрос отправки в Госключ.'");
				ОбработатьОшибку(Контекст.ПараметрыВыполнения, ТекстОшибки);
			КонецЕсли;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	НаборДанных = Контекст.НаборДанных;
	ИдентификаторФормы = Контекст.ПараметрыВыполнения.КонтекстПодписания.ИдентификаторФормы;
	Индекс = 0;
	Для Каждого Элемент Из Результат.НаборДанных Цикл
		Если НаборДанных.ВГраница() < Индекс Тогда
			ЗапросНаОтправкуДокумента = Результат.НаборДанных[Индекс];
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(Элемент.СвойстваПодписи) = Тип("Структура") Тогда
			НаборДанных[Индекс].Вставить("СвойстваПодписи", ПоместитьВоВременноеХранилище(Элемент.СвойстваПодписи, ИдентификаторФормы));
		Иначе
			НаборДанных[Индекс].Вставить("СвойстваПодписи", Элемент.СвойстваПодписи);
		КонецЕсли;
		
		Если ТипЗнч(НаборДанных[Индекс].Данные) = Тип("ДвоичныеДанные") Тогда
			НаборДанных[Индекс].Данные = ПоместитьВоВременноеХранилище(НаборДанных[Индекс].Данные, ИдентификаторФормы);
		КонецЕсли;
		
		Индекс = Индекс + 1;
	КонецЦикла;

	Контекст.ПараметрыВыполнения.ПараметрыПодписания.ЗапросНаОтправкуДокумента = Новый Структура(
		"ДанныеЗапроса, СвойстваПодписи",
		ЗапросНаОтправкуДокумента.Данные,
		ЗапросНаОтправкуДокумента.СвойстваПодписи);
	ПодписатьДанныеВСервисеДлительнаяОперация(Контекст.ПараметрыВыполнения,
		НаборДанных);
	
КонецПроцедуры

Процедура ДобавитьПодписантаВФорме(Форма, Элемент)
	ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	Если ЗначениеЗаполнено(Форма.ПараметрыПодписанта) Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Форма.ПараметрыПодписанта);
	КонецЕсли;
	СервисМобильнойПодписиКлиент.ДобавитьПодписанта(ПараметрыПодписанта, Элемент,
		Новый ОписаниеОповещения("ПослеСозданияПодписанта", ЭтотОбъект, Форма));
КонецПроцедуры

Процедура ПослеСозданияПодписанта(Результат, Форма) Экспорт
	Форма.Подписант = Результат;
КонецПроцедуры

Процедура ЗаполнитьНаборДанныхДляОтправкиНаПодписание(НаборДанных, ЭлементДанных,
	СписокПредставлений = Неопределено, Индекс = Неопределено)
	
	ЭлементСписка = Новый Структура("Значение, Представление, ИмяФайла", Неопределено, "", "");
		
	Если ЭлементДанных.Свойство("Представление")
	   И ТипЗнч(ЭлементДанных.Представление) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭлементСписка, ЭлементДанных.Представление);
		
	ИначеЕсли ЭлементДанных.Свойство("Представление")
	   И ТипЗнч(ЭлементДанных.Представление) <> Тип("Строка") Тогда
	
		ЭлементСписка.Значение = ЭлементДанных.Представление;
		
	ИначеЕсли ЭлементДанных.Свойство("Объект") И ТипЗнч(ЭлементДанных.Объект) <> Тип("ОписаниеОповещения") Тогда
		
		ЭлементСписка.Значение = ЭлементДанных.Объект;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭлементСписка.Представление) И ЭлементДанных.Свойство("Представление")
	   И ТипЗнч(ЭлементДанных.Представление) = Тип("Строка") Тогда
		ЭлементСписка.Представление = ЭлементДанных.Представление;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭлементСписка.ИмяФайла) Тогда
		Если Не ЗначениеЗаполнено(ЭлементСписка.Представление) Тогда
			Если СписокПредставлений <> Неопределено Тогда
				ИмяФайла = СписокПредставлений[Индекс].Представление;
			Иначе
				ИмяФайла = Строка(ЭлементСписка.Значение);
			КонецЕсли;
		Иначе
			ИмяФайла = ЭлементСписка.Представление;
		КонецЕсли;
		ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла);
		Файл = Новый Файл(ИмяФайла);
		Расширение = Файл.Расширение;
		Если Не ЗначениеЗаполнено(Расширение) Тогда
			Расширение = ".bin";
		КонецЕсли;
		ДлинаИмени = 250 - СтрДлина(Расширение);
		ИмяБезРасширения = Лев(Файл.ИмяБезРасширения, ДлинаИмени);
		ИмяФайла = ИмяБезРасширения + Расширение;
	Иначе
		ИмяФайла = ЭлементСписка.ИмяФайла;
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("Данные", ЭлементДанных.Данные);
	Структура.Вставить("Представление", ИмяФайла);
	
	НаборДанных.Добавить(Структура);

КонецПроцедуры

// Обработать ошибку взаимодействия с сервисом мобильной подписи.
// 
// Параметры:
//  Ошибка - см. ЭлектроннаяПодписьСлужебныйПовтИсп.КлассификаторОшибокКриптографии
//  Контекст - см. МашиночитаемыеДоверенностиФНССлужебныйКлиент.КонтекстДляОбработкиОшибкиРР
//
Процедура ОбработатьОшибкуВзаимодействияРР(Ошибка, Контекст) Экспорт
	
	Если Контекст.ОповещениеОЗавершении <> Неопределено Тогда
		
		Результат = РезультатОбработкиОшибкиВзаимодействияРР();
		Результат.Ошибка = Ошибка;
		ВыполнитьОбработкуОповещения(Контекст.ОповещениеОЗавершении, Результат);
		
	Иначе
		
		ОткрытьФормуРасширенногоПредставленияОшибки(Контекст.ЗаголовокПредупреждения, Ошибка, Контекст.Форма);
		
	КонецЕсли;

КонецПроцедуры

Процедура ОткрытьФормуРасширенногоПредставленияОшибки(Заголовок, Знач Ошибка, Форма, Оповещение = Неопределено) Экспорт
	
	ПараметрыФормы = ПараметрыРасширенногоПредставленияОшибки();
	ПараметрыФормы.ЗаголовокПредупреждения = Заголовок;
	ПараметрыФормы.ТекстОшибки = Ошибка.ТекстОшибки;
	ПараметрыФормы.ОписаниеОшибки = СтрШаблон("%1: %2", Заголовок, Ошибка.ТекстОшибки);
	ПараметрыФормы.ПоказатьТребуетсяПомощь = Истина;
	ПараметрыФормы.ПоказатьИнструкцию = Истина;
	
	ОшибкаПоКлассификатору = ЭлектроннаяПодписьСлужебныйКлиент.ОшибкаПоКлассификатору(Ошибка.ТекстОшибки);
	
	Если ОшибкаПоКлассификатору = Неопределено Тогда
		Ошибка.Вставить("Причина", "");
		Ошибка.Вставить("Решение", "");
	Иначе
		Ошибка.Вставить("Причина", ОшибкаПоКлассификатору.Причина);
		Ошибка.Вставить("Решение", ОшибкаПоКлассификатору.Решение);
	КонецЕсли;
	
	ПараметрыФормы.ДополнительныеДанные.Вставить("ДополнительныеДанныеПроверок", Ошибка);
	Если ЗначениеЗаполнено(Ошибка.ДополнительныеФайлы) Тогда
		ПараметрыФормы.ДополнительныеДанные.Вставить("ДополнительныеФайлы", Ошибка.ДополнительныеФайлы);
		Ошибка.Удалить("ДополнительныеФайлы");
	КонецЕсли;
	
	ЭлектроннаяПодписьСлужебныйКлиент.ОткрытьФормуРасширенногоПредставленияОшибки(ПараметрыФормы, Форма, Оповещение);
	
КонецПроцедуры

Функция РезультатОбработкиОшибкиВзаимодействияРР()
	Возврат Новый Структура("Ошибка");
КонецФункции

Функция ПараметрыРасширенногоПредставленияОшибки()
	Параметры = Новый Структура;
	Параметры.Вставить("ЗаголовокПредупреждения", "");
	Параметры.Вставить("ТекстОшибкиКлиент", "");
	Параметры.Вставить("ДополнительныеДанные", Новый Структура);
	Параметры.Вставить("ОписаниеОшибки", "");
	Параметры.Вставить("ПоказатьИнструкцию", Ложь);
	Параметры.Вставить("ПоказатьПереходКНастройкеПрограмм", Ложь);
	Параметры.Вставить("ПоказатьТребуетсяПомощь", Ложь);
	Параметры.Вставить("ПоказатьУстановкуРасширения", Ложь);
	Параметры.Вставить("ТекстОшибкиСервер", "");
	Параметры.Вставить("ТекстОшибки", "");
	Параметры.Вставить("ИнформацияДляПоддержки", "");
	Возврат Параметры;
КонецФункции

Процедура ПослеОтправкиНаПодписаниеДанныхВСервис(Результат, Контекст) Экспорт
	
	РезультатВыполнения = Новый Структура;
	
	Если Результат = Неопределено Тогда
		РезультатВыполнения.Вставить("ОтменаПодписания");
		ВыполнитьОбработкуОповещения(Контекст.ОповещениеОПодписанииВСервисе, РезультатВыполнения);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ИнформацияОбОшибке = Результат.ИнформацияОбОшибке;
		ОбработатьОшибку(Контекст, ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
		Возврат;
	КонецЕсли;
	
	НаборДанных = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если ТипЗнч(НаборДанных) = Тип("Структура") Тогда
		ОбработатьОшибку(Контекст, НаборДанных);
		Возврат;
	КонецЕсли;
	
	Если Контекст.ЭтоПодписание Тогда
		
		ИдентификаторыДокументов = Новый Массив;
		Для Каждого Данные Из НаборДанных Цикл
			ИдентификаторыДокументов.Добавить(Данные.ИдентификаторДокумента);
		КонецЦикла;

		Контекст.Вставить("НаборДанных", НаборДанных);

		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПолученияРезультатовПодписания", ЭтотОбъект, Контекст);

		ОткрытьФорму("Справочник.ПодписантыСервисаМобильнойПодписи.Форма.ПолучениеПодписей",
			Новый Структура("ИдентификаторыДокументов, Подписант, НастройкаОтправки",
				ИдентификаторыДокументов, Контекст.КонтекстПодписания.ПараметрыВыполнения.ОписаниеДанных.ВыбранныйПодписант, 
				Контекст.ПараметрыПодписания.НастройкаОтправки),
			Контекст.КонтекстПодписания.ПараметрыВыполнения.Форма,,,,
			ОписаниеОповещения);

	Иначе
		ВыполнитьОбработкуОповещения(Контекст.ОповещениеОПодписанииВСервисе, НаборДанных);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолученияРезультатовПодписания(Результат, Контекст) Экспорт
	
	РезультатВыполнения = Новый Структура;
	
	Если Результат = Неопределено Тогда
		РезультатВыполнения.Вставить("ОтменаПодписания");
		ВыполнитьОбработкуОповещения(Контекст.ОповещениеОПодписанииВСервисе, РезультатВыполнения);
		Возврат;
	КонецЕсли;
	
	РезультатВыполнения.Вставить("ОперацияНачалась", Истина);
		
	Контекст.КонтекстПодписания.ПараметрыВыполнения.ОписаниеДанных.Вставить("ОперацияНачалась");
	Ошибка = Новый Массив;
	
	Если Контекст.КонтекстПодписания.ПараметрыВыполнения.ОписаниеДанных.Свойство("Данные") Тогда
		
		СвойстваПодписи = Результат.Получить(Контекст.НаборДанных[0].ИдентификаторДокумента);
		Если СвойстваПодписи <> Неопределено Тогда
			Если Не ЗначениеЗаполнено(СвойстваПодписи.ОшибкаПроверкиДополнительныхАтрибутов) Тогда
				ДобавитьСвойстваПодписиВОписаниеДанных(
					Контекст.КонтекстПодписания.ПараметрыВыполнения.ОписаниеДанных, СвойстваПодписи, Контекст,
						РезультатВыполнения);
				РезультатВыполнения.Вставить("ЕстьОбработанныеЭлементыДанных");
			ИначеЕсли ЗначениеЗаполнено(СвойстваПодписи.ОшибкаПроверкиДополнительныхАтрибутов) Тогда
				Ошибка.Добавить(СвойстваПодписи.ОшибкаПроверкиДополнительныхАтрибутов);
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		Индекс = 0;
		Для Каждого Элемент Из Контекст.НаборДанных Цикл
			СвойстваПодписи = Результат.Получить(Элемент.ИдентификаторДокумента);
			Если СвойстваПодписи <> Неопределено Тогда
				Если Не ЗначениеЗаполнено(СвойстваПодписи.ОшибкаПроверкиДополнительныхАтрибутов) Тогда
					ДобавитьСвойстваПодписиВОписаниеДанных(
					Контекст.КонтекстПодписания.ПараметрыВыполнения.ОписаниеДанных.НаборДанных[Индекс],
						СвойстваПодписи, Контекст, РезультатВыполнения);
					РезультатВыполнения.Вставить("ЕстьОбработанныеЭлементыДанных");
					Индекс = Индекс + 1;
				ИначеЕсли ЗначениеЗаполнено(СвойстваПодписи.ОшибкаПроверкиДополнительныхАтрибутов) Тогда
					Если Ошибка.Найти(СвойстваПодписи.ОшибкаПроверкиДополнительныхАтрибутов) = Неопределено Тогда
						Ошибка.Добавить(СвойстваПодписи.ОшибкаПроверкиДополнительныхАтрибутов);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Ошибка.Количество() > 0 Тогда
		РезультатВыполнения.Вставить("Ошибка", СтрСоединить(Ошибка, "Символы.ПС"));
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.ОповещениеОПодписанииВСервисе, РезультатВыполнения);
	
КонецПроцедуры

Процедура ДобавитьСвойстваПодписиВОписаниеДанных(ЭлементДанных, СвойстваПодписи, Контекст, РезультатВыполнения)
	
	ЭлементДанных.Вставить("СвойстваПодписи", СвойстваПодписи);

	Если ЭлементДанных.Свойство("Объект") И ТипЗнч(ЭлементДанных.Объект) <> Тип("ОписаниеОповещения") Тогда
		
		ВерсияОбъекта = Неопределено;
		ЭлементДанных.Свойство("ВерсияОбъекта", ВерсияОбъекта);
		
		// СтандартныеПодсистемы.МашиночитаемыеДоверенности
		
		Если ЗначениеЗаполнено(Контекст.КонтекстПодписания.ПараметрыВыполнения.ОписаниеДанных.ВыбраннаяДоверенность)
			И ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.МашиночитаемыеДоверенности") Тогда
			
			МодульМашиночитаемыеДоверенностиФНССлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
				"МашиночитаемыеДоверенностиФНССлужебныйКлиент");
			РезультатПроверкиПодписиПоМЧД = МодульМашиночитаемыеДоверенностиФНССлужебныйКлиент.РезультатПроверкиПодписиПоМЧД(
				Контекст.КонтекстПодписания.ПараметрыВыполнения.ОписаниеДанных.ВыбраннаяДоверенность, ЭлементДанных.Объект,
			СвойстваПодписи.Сертификат, Неопределено, Неопределено);
			Если ЗначениеЗаполнено(РезультатПроверкиПодписиПоМЧД) Тогда
				СвойстваПодписи.Вставить("РезультатПроверкиПодписиПоМЧД", РезультатПроверкиПодписиПоМЧД);
			КонецЕсли;
			
		КонецЕсли;
		
		// Конец СтандартныеПодсистемы.МашиночитаемыеДоверенности
		
		ПредставлениеОшибки = ЭлектроннаяПодписьСлужебныйВызовСервера.ДобавитьПодпись(
			ЭлементДанных.Объект, СвойстваПодписи, Контекст.КонтекстПодписания.ИдентификаторФормы, ВерсияОбъекта);
		Если ЗначениеЗаполнено(ПредставлениеОшибки) Тогда
			ЭлементДанных.Удалить("СвойстваПодписи");
			ОбработатьОшибку(Контекст, ПредставлениеОшибки, РезультатВыполнения);
			Возврат;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибку(Контекст, Ошибка, РезультатВыполнения = Неопределено)
	
	Если РезультатВыполнения = Неопределено Тогда
		РезультатВыполнения = Новый Структура;
	КонецЕсли;
	
	Если Контекст.ЭтоПодписание Тогда
		Если ТипЗнч(Ошибка) = Тип("Структура") Тогда
			Ошибка = Ошибка.ЗаголовокОшибки + ": " + Ошибка.ТекстОшибки;
		КонецЕсли;
	Иначе
		Если ТипЗнч(Ошибка) = Тип("Строка") Тогда
			Ошибка = СервисМобильнойПодписиКлиентСервер.НовоеОписаниеОшибки(
				НСтр("ru='Не удалось отправить документы на подписание в Госключ.'"), Ошибка);
		КонецЕсли;
	КонецЕсли;
	
	РезультатВыполнения.Вставить("Ошибка", Ошибка);
	ВыполнитьОбработкуОповещения(Контекст.ОповещениеОПодписанииВСервисе, РезультатВыполнения);
	
КонецПроцедуры

#КонецОбласти
