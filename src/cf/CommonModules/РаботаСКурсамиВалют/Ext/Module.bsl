///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Добавляет в справочник указанные валюты из классификатора.
// Для вызова из обработчиков начального заполнения.
//
// При отсутствии классификатора валюты добавляются с наименованием "Валюта",
// символьный код соответствует цифровому.
//
// Параметры:
//   КодыВалют - Массив из Строка - цифровые коды добавляемых валют.
//
// Возвращаемое значение:
//   Массив из СправочникСсылка.Валюты
//
Функция ДобавитьВалютыПоКоду(Знач КодыВалют) Экспорт
	
	Результат = Новый Массив;
	СтандартнаяОбработка = Истина;
	РаботаСКурсамиВалютЛокализация.ПриДобавленииВалютПоКоду(КодыВалют, Результат, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого КодВалюты Из КодыВалют Цикл
		ВалютаСсылка = Справочники.Валюты.НайтиПоКоду(КодВалюты);
		Если ВалютаСсылка.Пустая() Тогда
			ВалютаОбъект = Справочники.Валюты.СоздатьЭлемент();
			ВалютаОбъект.Код = КодВалюты;
			ВалютаОбъект.Наименование = КодВалюты;
			ВалютаОбъект.НаименованиеПолное = НСтр("ru = 'Валюта'");
			ВалютаОбъект.СпособУстановкиКурса = Перечисления.СпособыУстановкиКурсаВалюты.РучнойВвод;
			ВалютаОбъект.Записать();
			ВалютаСсылка = ВалютаОбъект.Ссылка;
		КонецЕсли;
		Результат.Добавить(ВалютаСсылка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает курс валюты на дату.
//
// Параметры:
//   Валюта    - СправочникСсылка.Валюты - валюта, для которой получается курс.
//   ДатаКурса - Дата - дата, на которую получается курс.
//
// Возвращаемое значение: 
//   Структура:
//    * Курс      - Число - курс валюты на указанную дату.
//    * Кратность - Число - кратность валюты на указанную дату.
//    * Валюта    - СправочникСсылка.Валюты - ссылка валюты.
//    * ДатаКурса - Дата - дата получения курса.
//
Функция ПолучитьКурсВалюты(Валюта, ДатаКурса) Экспорт
	
	Результат = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаКурса, Новый Структура("Валюта", Валюта));
	
	Результат.Вставить("Валюта",    Валюта);
	Результат.Вставить("ДатаКурса", ДатаКурса);
	
	Возврат Результат;
	
КонецФункции

// Формирует представление суммы прописью в указанной валюте.
//
// Параметры:
//   СуммаЧислом - Число - сумма, которую надо представить прописью.
//   Валюта - СправочникСсылка.Валюты - валюта, в которой нужно представить сумму.
//   БезДробнойЧасти - Булево - указать Истина, если требуется получить сумму без дробной части (без копеек).
//   КодЯзыка - Строка - язык, на котором требуется получить сумму прописью.
//                       Состоит из кода языка по ISO 639-1 и, опционально, кода страны по ISO 3166-1, разделенных
//                       символом подчеркивания. Примеры: "en", "en_US", "en_GB", "ru", "ru_RU".
//                       Значение по умолчанию - язык конфигурации.
//   ДробнаяЧастьПрописью - Булево
//
// Возвращаемое значение:
//   Строка - сумма прописью.
//
Функция СформироватьСуммуПрописью(СуммаЧислом, Валюта, БезДробнойЧасти = Ложь, Знач КодЯзыка = Неопределено, ДробнаяЧастьПрописью = Ложь) Экспорт
	
	ПараметрыПрописи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Валюта, "ПараметрыПрописи", , КодЯзыка);
	
	Если Не ЗначениеЗаполнено(КодЯзыка) Тогда
		КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	КонецЕсли;
	
	Сумма = ?(СуммаЧислом < 0, -СуммаЧислом, СуммаЧислом);
	Формат = СтрШаблон("L=%1;ДП=%2", КодЯзыка, ?(ДробнаяЧастьПрописью, "Истина", "Ложь"));
	Результат = ЧислоПрописью(Сумма, Формат, ПараметрыПрописи); // АПК:1297 АПК:1357
	Если БезДробнойЧасти И Цел(Сумма) = Сумма Тогда
		Результат = Лев(Результат, СтрНайти(Результат, "0") - 1);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Пересчитывает сумму из одной валюты в другую.
//
// Параметры:
//  Сумма          - Число - сумма, которую необходимо пересчитать;
//  ИсходнаяВалюта - СправочникСсылка.Валюты - пересчитываемая валюта;
//  НоваяВалюта    - СправочникСсылка.Валюты - валюта, в которую необходимо пересчитать;
//  Дата           - Дата - дата курсов валют.
//
// Возвращаемое значение:
//  Число - пересчитанная сумма.
//
Функция ПересчитатьВВалюту(Сумма, ИсходнаяВалюта, НоваяВалюта, Дата) Экспорт
	
	Возврат РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(Сумма,
		ПолучитьКурсВалюты(ИсходнаяВалюта, Дата),
		ПолучитьКурсВалюты(НоваяВалюта, Дата));
		
КонецФункции

// Предназначена для использования в конструкторе типа Число для денежных полей.
//
// Параметры:
//  ДопустимыйЗнакПоля - ДопустимыйЗнак - определяет допустимый знак числа. Значение по умолчанию - ДопустимыйЗнак.Любой.
// 
// Возвращаемое значение:
//  ОписаниеТипов - тип значения для денежного поля.
//
Функция ОписаниеТипаДенежногоПоля(Знач ДопустимыйЗнакПоля = Неопределено) Экспорт
	
	Если ДопустимыйЗнакПоля = Неопределено Тогда
		ДопустимыйЗнакПоля = ДопустимыйЗнак.Любой;
	КонецЕсли;
	
	Если ДопустимыйЗнакПоля = ДопустимыйЗнак.Любой Тогда
		Возврат Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип;
	КонецЕсли;
	
	Возврат Метаданные.ОпределяемыеТипы.ДенежнаяСуммаНеотрицательная.Тип;
	
КонецФункции

// Добавляет возможность вывода числового реквизита прописью при печати.
// Для вызова из УправлениеПечатьюПереопределяемый.ПриОпределенииИсточниковДанныхПечати.
// 
// Параметры:
//  ИсточникиДанныхПечати - см. УправлениеПечатьюПереопределяемый.ПриОпределенииИсточниковДанныхПечати.ИсточникиДанныхПечати
//  ИмяИсточникаПолей - Строка - полное имя объекта метаданных, по которому будут вычисляться поля валют для
//                               использования в прописях. Если не указано, то добавляется поле по умолчанию и первый в
//                               списке метаданных объекта печати реквизит валюты.
//
Процедура ПодключитьИсточникДанныхПечатиЧислоПрописью(ИсточникиДанныхПечати, ИмяИсточникаПолей = "") Экспорт
	
	СхемаКомпоновки = СхемаДанныеПечатиСуммаПрописью(ИмяИсточникаПолей);
	Если СхемаКомпоновки <> Неопределено Тогда
		ИсточникиДанныхПечати.Добавить(СхемаКомпоновки, "ДанныеПечатиСуммаПрописью");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий подсистем конфигурации.

// Параметры:
//   ТекущиеДела - см. ТекущиеДелаСервер.ТекущиеДела.
//
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	Если Не ДоступнаЗагрузкаКурсовВалют() Тогда
		Возврат;
	КонецЕсли;

	РаботаСКурсамиВалютЛокализация.ПриЗаполненииСпискаТекущихДел(ТекущиеДела);
	
КонецПроцедуры

// См. ЗагрузкаДанныхИзФайлаПереопределяемый.ПриОпределенииСправочниковДляЗагрузкиДанных.
Процедура ПриОпределенииСправочниковДляЗагрузкиДанных(ЗагружаемыеСправочники) Экспорт
	
	// Загрузка в классификатор валют запрещена.
	СтрокаТаблицы = ЗагружаемыеСправочники.Найти(Метаданные.Справочники.Валюты.ПолноеИмя(), "ПолноеИмя");
	Если СтрокаТаблицы <> Неопределено Тогда 
		ЗагружаемыеСправочники.Удалить(СтрокаТаблицы);
	КонецЕсли;
	
КонецПроцедуры

// См. ГрупповоеИзменениеОбъектовПереопределяемый.ПриОпределенииОбъектовСРедактируемымиРеквизитами.
Процедура ПриОпределенииОбъектовСРедактируемымиРеквизитами(Объекты) Экспорт
	Объекты.Вставить(Метаданные.Справочники.Валюты.ПолноеИмя(), "РеквизитыРедактируемыеВГрупповойОбработке");
КонецПроцедуры

// См. РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий.
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	РаботаСКурсамиВалютЛокализация.ПриОпределенииНастроекРегламентныхЗаданий(Настройки);
КонецПроцедуры

// См. ПользователиПереопределяемый.ПриОпределенииНазначенияРолей.
Процедура ПриОпределенииНазначенияРолей(НазначениеРолей) Экспорт
	
	// СовместноДляПользователейИВнешнихПользователей.
	НазначениеРолей.СовместноДляПользователейИВнешнихПользователей.Добавить(
		Метаданные.Роли.ЧтениеКурсовВалют.Имя);
	
КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииПараметровРаботыКлиентаПриЗапуске.
Процедура ПриДобавленииПараметровРаботыКлиентаПриЗапуске(Параметры) Экспорт
	
	ОповещатьОНеобходимостиОбновленияКурсовВалют = ОповещатьОНеобходимостиОбновленияКурсовВалют();
	
	Параметры.Вставить("Валюты", Новый ФиксированнаяСтруктура("ТребуетсяОбновитьКурсыВалют",
		ОповещатьОНеобходимостиОбновленияКурсовВалют И Не КурсыАктуальны()));
	
КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииИсключенийПоискаСсылок.
Процедура ПриДобавленииИсключенийПоискаСсылок(ИсключенияПоискаСсылок) Экспорт
	
	ИсключенияПоискаСсылок.Добавить(Метаданные.РегистрыСведений.КурсыВалют.ПолноеИмя());
	
КонецПроцедуры

// См. РаботаВБезопасномРежимеПереопределяемый.ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам.
Процедура ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений) Экспорт
	
	РаботаСКурсамиВалютЛокализация.ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений);
	
КонецПроцедуры

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	РаботаСКурсамиВалютЛокализация.ПриДобавленииОбработчиковОбновления(Обработчики);
	
КонецПроцедуры

// См. ИнтернетПоддержкаПользователейПереопределяемый.ПриИзмененииДанныхАутентификацииИнтернетПоддержки.
Процедура ПриИзмененииДанныхАутентификацииИнтернетПоддержки(ДанныеПользователя) Экспорт
	
	РаботаСКурсамиВалютЛокализация.ПриИзмененииДанныхАутентификацииИнтернетПоддержки(ДанныеПользователя);
	
КонецПроцедуры

// Проверяет наличие установленного курса и кратности валюты на 1 января 1980 года.
// В случае отсутствия устанавливает курс и кратность равными единице.
//
// Параметры:
//  Валюта - ссылка на элемент справочника Валют.
//
Процедура ПроверитьКорректностьКурсаНа01_01_1980(Валюта) Экспорт
	
	ДатаКурса = Дата("19800101");
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.КурсыВалют");
	ЭлементБлокировки.УстановитьЗначение("Валюта", Валюта);
	ЭлементБлокировки.УстановитьЗначение("Период", ДатаКурса);
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		СтруктураКурса = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаКурса, Новый Структура("Валюта", Валюта));
		
		Если (СтруктураКурса.Курс = 0) Или (СтруктураКурса.Кратность = 0) Тогда
			НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Валюта.Установить(Валюта);
			НаборЗаписей.Отбор.Период.Установить(ДатаКурса);
			Запись = НаборЗаписей.Добавить();
			Запись.Валюта = Валюта;
			Запись.Период = ДатаКурса;
			Запись.Курс = 1;
			Запись.Кратность = 1;
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения");
			НаборЗаписей.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// См. УправлениеПечатьюПереопределяемый.ПриПодготовкеДанныхПечати
Процедура ПриПодготовкеДанныхПечати(ИсточникиДанных, ВнешниеНаборыДанных, ИдентификаторСхемыКомпоновкиДанных, КодЯзыка,
	ДополнительныеПараметры) Экспорт
	
	Если ИдентификаторСхемыКомпоновкиДанных = "ДанныеПечатиСуммаПрописью" Тогда
		ВнешниеНаборыДанных.Вставить("Данные", 
			ДанныеПечатиСуммаПрописью(ДополнительныеПараметры.ОписанияИсточниковДанных, КодЯзыка));
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  Булево
//
Функция ДоступнаЗагрузкаКурсовВалют() Экспорт

	Результат = Ложь;
	РаботаСКурсамиВалютЛокализация.ПриОпределенииДоступностиЗагрузкиКурсовВалют(Результат);
	Возврат Результат;

КонецФункции

#Область ЭкспортныеСлужебныеПроцедурыИФункции

// Возвращает массив валют, курсы которых загружаются из внешних ресурсов.
//
Функция ЗагружаемыеВалюты() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Валюты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Валюты КАК Валюты
	|ГДЕ
	|	Валюты.СпособУстановкиКурса = ЗНАЧЕНИЕ(Перечисление.СпособыУстановкиКурсаВалюты.ЗагрузкаИзИнтернета)
	|	И НЕ Валюты.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Валюты.НаименованиеПолное";

	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ЗаполнитьДанныеКурсаДляВалюты(ВыбраннаяВалюта) Экспорт
	
	ДанныеКурса = Новый Структура("ДатаКурса, Курс, Кратность");
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ РегКурсы.Период, РегКурсы.Курс, РегКурсы.Кратность
	              | ИЗ РегистрСведений.КурсыВалют.СрезПоследних(&ОкончаниеПериодаЗагрузки, Валюта = &ВыбраннаяВалюта) КАК РегКурсы";
	Запрос.УстановитьПараметр("ВыбраннаяВалюта", ВыбраннаяВалюта);
	Запрос.УстановитьПараметр("ОкончаниеПериодаЗагрузки", ТекущаяДатаСеанса());
	
	ВыборкаКурс = Запрос.Выполнить().Выбрать();
	ВыборкаКурс.Следующий();
	
	ДанныеКурса.ДатаКурса = ВыборкаКурс.Период;
	ДанныеКурса.Курс      = ВыборкаКурс.Курс;
	ДанныеКурса.Кратность = ВыборкаКурс.Кратность;
	
	Возврат ДанныеКурса;
	
КонецФункции

Функция СписокЗависимыхВалют(ВалютаБазовая, ДополнительныеСвойства = Неопределено) Экспорт
	
	Кэшировать = (ТипЗнч(ДополнительныеСвойства) = Тип("Структура"));
	
	Если Кэшировать Тогда
		
		ЗависимыеВалюты = ДополнительныеСвойства.ЗависимыеВалюты.Получить(ВалютаБазовая);
		
		Если ТипЗнч(ЗависимыеВалюты) = Тип("ТаблицаЗначений") Тогда
			Возврат ЗависимыеВалюты;
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпрВалюты.Ссылка,
	|	СпрВалюты.Наценка,
	|	СпрВалюты.СпособУстановкиКурса,
	|	СпрВалюты.ФормулаРасчетаКурса
	|ИЗ
	|	Справочник.Валюты КАК СпрВалюты
	|ГДЕ
	|	СпрВалюты.ОсновнаяВалюта = &ВалютаБазовая
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СпрВалюты.Ссылка,
	|	СпрВалюты.Наценка,
	|	СпрВалюты.СпособУстановкиКурса,
	|	СпрВалюты.ФормулаРасчетаКурса
	|ИЗ
	|	Справочник.Валюты КАК СпрВалюты
	|ГДЕ
	|	СпрВалюты.ФормулаРасчетаКурса ПОДОБНО &СимвольныйКод СПЕЦСИМВОЛ ""~""";
	
	Запрос.УстановитьПараметр("ВалютаБазовая", ВалютаБазовая);
	Запрос.УстановитьПараметр("СимвольныйКод", "%" +  ОбщегоНазначения.СформироватьСтрокуДляПоискаВЗапросе(ВалютаБазовая) + "%");
	ЗависимыеВалюты = Запрос.Выполнить().Выгрузить();	
	Если Кэшировать Тогда		
		ДополнительныеСвойства.ЗависимыеВалюты.Вставить(ВалютаБазовая, ЗависимыеВалюты);		
	КонецЕсли;
	
	Возврат ЗависимыеВалюты;
	
КонецФункции

Процедура ОбновитьКурсВалюты(Параметры, АдресРезультата) Экспорт
	
	ЗависимаяВалюта = Параметры.Валюта;
	СписокВалют = Параметры.Валюта.ИспользуемыеВалютыПриРасчетеКурса;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КурсыВалют.Период КАК Период,
	|	КурсыВалют.Валюта КАК Валюта
	|ИЗ
	|	РегистрСведений.КурсыВалют КАК КурсыВалют
	|ГДЕ
	|	КурсыВалют.Валюта В(&Валюта)
	|
	|СГРУППИРОВАТЬ ПО
	|	КурсыВалют.Период,
	|	КурсыВалют.Валюта
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Валюта", СписокВалют);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбновленныеПериоды = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		Если ОбновленныеПериоды[Выборка.Период] <> Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			Для Каждого Валюта Из СписокВалют Цикл
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.КурсыВалют");
				ЭлементБлокировки.УстановитьЗначение("Валюта", Валюта);
				ЭлементБлокировки.УстановитьЗначение("Период", Выборка.Период);
			КонецЦикла;
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Валюта.Установить(Выборка.Валюта);
			НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
			НаборЗаписей.Прочитать();
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОбновитьКурсЗависимойВалюты", ЗависимаяВалюта);
			НаборЗаписей.ДополнительныеСвойства.Вставить("КодыВалют", Параметры.КодыВалют);
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОбновленныеПериоды", ОбновленныеПериоды);
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения");
			НаборЗаписей.Записать();
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
		
		ОбновленныеПериоды.Вставить(Выборка.Период, Истина);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеКурсовВалют

// Проверяет актуальность курсов всех валют.
//
Функция КурсыАктуальны() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Валюты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втВалюты
	|ИЗ
	|	Справочник.Валюты КАК Валюты
	|ГДЕ
	|	Валюты.СпособУстановкиКурса = ЗНАЧЕНИЕ(Перечисление.СпособыУстановкиКурсаВалюты.ЗагрузкаИзИнтернета)
	|	И Валюты.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле1
	|ИЗ
	|	втВалюты КАК Валюты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО Валюты.Ссылка = КурсыВалют.Валюта
	|			И (КурсыВалют.Период = &ТекущаяДата)
	|ГДЕ
	|	КурсыВалют.Валюта ЕСТЬ NULL ";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Пустой();
КонецФункции

// Для процедуры ПриДобавленииПараметровРаботыКлиентаПриЗапуске
//
Функция ОповещатьОНеобходимостиОбновленияКурсовВалют()
	
	// В модели сервиса обновляются автоматически.
	Если ОбщегоНазначения.РазделениеВключено() Или ОбщегоНазначения.ЭтоАвтономноеРабочееМесто() 
		Или Не РаботаСКурсамиВалютСлужебный.ЕстьПравоИзмененияКурсовВалют() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВключитьОповещение = Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ТекущиеДела");
	РаботаСКурсамиВалютПереопределяемый.ПриОпределенииНеобходимостиПоказаПредупрежденияОбУстаревшихКурсахВалют(ВключитьОповещение);
	
	Возврат ВключитьОповещение;
	
КонецФункции

#КонецОбласти

#Область Печать

Функция СхемаДанныеПечатиСуммаПрописью(ИмяИсточникаПолей)
	
	Если НЕ ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Печать") Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	МодульУправлениеПечатью = ОбщегоНазначения.ОбщийМодуль("УправлениеПечатью");
	
	СписокПолей = МодульУправлениеПечатью.ТаблицаПолейДанныхПечати();
	
	Поле = СписокПолей.Добавить();
	Поле.Идентификатор = "Ссылка";
	Поле.Представление = НСтр("ru = 'Ссылка'");
	Поле.ТипЗначения = Новый ОписаниеТипов();
	
	Поле = СписокПолей.Добавить();
	Поле.Идентификатор = "Валюта";
	Поле.Представление = НСтр("ru = 'Валюта по умолчанию'");
	Поле.ТипЗначения = Новый ОписаниеТипов();
	
	Поле = СписокПолей.Добавить();
	Поле.Идентификатор = "ЧислоПрописью";
	Поле.Представление = НСтр("ru = 'Прописью (с валютой по умолчанию)'");
	Поле.ТипЗначения = Новый ОписаниеТипов("Строка");
	
	Если НЕ ПустаяСтрока(ИмяИсточникаПолей) Тогда
		
		МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяИсточникаПолей, ".");
		МассивИзДвухСтрок = 2;
		
		Если МассивПодстрок.Количество() >= МассивИзДвухСтрок Тогда
			
			МассивПодстрокИмениОбъекта = Новый Массив;
			МассивПодстрокИмениОбъекта.Добавить(МассивПодстрок[0]);
			МассивПодстрокИмениОбъекта.Добавить(МассивПодстрок[1]);
			ИмяОбъекта = СтрСоединить(МассивПодстрокИмениОбъекта, ".");
			ОбъектМетаданных = ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(ИмяОбъекта);
			
			Если ОбъектМетаданных <> Неопределено Тогда
				
				ТаблицаВалютныхРеквизитов = ТаблицаВалютныхРеквизитовОбъекта(ОбъектМетаданных);
				ТипОбъекта = СтандартныеПодсистемыСервер.ТипСсылкиИлиКлючаЗаписиОбъектаМетаданных(ОбъектМетаданных);
				ДанныеВалютыПоУмолчанию = ДанныеВалюты();
				ПолучитьПолеВалютаПоУмолчаниюДляОбъекта(ТипОбъекта, ТаблицаВалютныхРеквизитов, ДанныеВалютыПоУмолчанию);
				
				Если НЕ ПустаяСтрока(ДанныеВалютыПоУмолчанию.ПредставлениеПоля)
				   И ДанныеВалютыПоУмолчанию.Переопределена Тогда
					
					ПредставлениеПоля = ДанныеВалютыПоУмолчанию.ПредставлениеПоля;
					СтрокаТаблицы = СписокПолей.Найти("Валюта", "Идентификатор");
					СтрокаТаблицы.Представление = СтрокаТаблицы.Представление + " (" + ПредставлениеПоля + ")";
					
				КонецЕсли;
				
				Для Каждого Реквизит Из ТаблицаВалютныхРеквизитов Цикл
					
					Идентификатор = ИдентификаторПоляЧислоПрописью(Реквизит.Идентификатор);
					Если СписокПолей.Найти(Идентификатор, "Идентификатор") = Неопределено Тогда
						
						Поле = СписокПолей.Добавить();
						Поле.Идентификатор = Идентификатор;
						Поле.Представление = ПредставлениеПоляЧислоПрописью(Реквизит.Представление);
						Поле.ТипЗначения = Новый ОписаниеТипов("Строка");
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МодульУправлениеПечатью.СхемаКомпоновкиДанныхПечати(СписокПолей);
	
КонецФункции

Функция ДанныеПечатиСуммаПрописью(ОписанияИсточниковДанных, КодЯзыка)
	
	ДанныеПечати = Новый ТаблицаЗначений();
	ДанныеПечати.Колонки.Добавить("Ссылка");
	ДанныеПечати.Колонки.Добавить("Валюта");
	ДанныеПечати.Колонки.Добавить("ЧислоПрописью");
	
	Результат = ОписанияИсточниковДанныхПоМетаданным(ОписанияИсточниковДанных);
	ОбъектыМетаданных = Результат.ОбъектыМетаданных;
	СсылкиМетаданных = Результат.СсылкиМетаданных;
	
	ОбъектыМетаданных = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОбъектыМетаданных);
	
	Для Каждого ОбъектМетаданных Из ОбъектыМетаданных Цикл
		
		ТипОбъекта = СтандартныеПодсистемыСервер.ТипСсылкиИлиКлючаЗаписиОбъектаМетаданных(ОбъектМетаданных);
		ТаблицаВалютныхРеквизитов = ТаблицаВалютныхРеквизитовОбъекта(ОбъектМетаданных);
		
		Для Каждого Реквизит Из ТаблицаВалютныхРеквизитов Цикл
			
			ИмяКолонки = ИдентификаторПоляЧислоПрописью(Реквизит.Идентификатор);
			Если ДанныеПечати.Колонки.Найти(ИмяКолонки) = Неопределено Тогда
				ДанныеПечати.Колонки.Добавить(ИмяКолонки);
			КонецЕсли;
			
		КонецЦикла;
		
		МассивСсылок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СсылкиМетаданных[ОбъектМетаданных]);
		МассивПолей = ТаблицаВалютныхРеквизитов.ВыгрузитьКолонку("Идентификатор");
		ЗначенияВалютныхРеквизитов = Новый Соответствие;
		Если МассивПолей.Количество() > 0 Тогда
			ЗначенияВалютныхРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСсылок, МассивПолей);
		КонецЕсли;
		
		ДанныеВалютыПоУмолчанию = ДанныеВалюты();
		ПолучитьПолеВалютаПоУмолчаниюДляОбъекта(ТипОбъекта, ТаблицаВалютныхРеквизитов, ДанныеВалютыПоУмолчанию, Истина);
		
		Для Каждого ТекущаяСсылка Из МассивСсылок Цикл
			
			ЗначенияВалютныхРеквизитовСсылки = ЗначенияВалютныхРеквизитов[ТекущаяСсылка];
			Если НЕ ПустаяСтрока(ДанныеВалютыПоУмолчанию.ИмяПоля)
			   И НЕ ЗначениеЗаполнено(ДанныеВалютыПоУмолчанию.ЗначениеВалюты) Тогда
				
				ИмяПоля = ДанныеВалютыПоУмолчанию.ИмяПоля;
				Если ЗначенияВалютныхРеквизитовСсылки.Свойство(ИмяПоля) Тогда
					ДанныеВалютыПоУмолчанию.ЗначениеВалюты = ЗначенияВалютныхРеквизитовСсылки[ИмяПоля];
				КонецЕсли;
				
			КонецЕсли;
			
			Отбор = Новый Структура;
			Отбор.Вставить("Владелец", ТекущаяСсылка);
			ОписанияИсточниковДанныхПоСсылке = ОписанияИсточниковДанных.НайтиСтроки(Отбор);
			
			ПараметрыДляЗаполненияВалюты = Новый Структура;
			ПараметрыДляЗаполненияВалюты.Вставить("ТаблицаВалютныхРеквизитов", ТаблицаВалютныхРеквизитов);
			ПараметрыДляЗаполненияВалюты.Вставить("ЗначенияВалютныхРеквизитовСсылки", ЗначенияВалютныхРеквизитовСсылки);
			ПараметрыДляЗаполненияВалюты.Вставить("ВалютаПоУмолчанию", ДанныеВалютыПоУмолчанию);
			
			ЗаполнитьПоляСуммойПрописью(
				ОписанияИсточниковДанныхПоСсылке,
				ДанныеПечати,
				ПараметрыДляЗаполненияВалюты,
				КодЯзыка);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ДанныеПечати;
	
КонецФункции

// Параметры:
//  ОписанияИсточниковДанных - ТаблицаЗначений:
//  * Владелец - любая Ссылка
//  * Имя - Строка
//  * Значение - Произвольный 
// 
// Возвращаемое значение:
//  Структура:
//  * ОбъектыМетаданных - Массив из ОбъектМетаданных
//  * СсылкиМетаданных - Соответствие из КлючИЗначение:
//   ** Ключ - ОбъектМетаданных
//   ** Значение - ЛюбаяСсылка  
//
Функция ОписанияИсточниковДанныхПоМетаданным(Знач ОписанияИсточниковДанных)
	
	ОбъектыМетаданных = Новый Массив; // Массив из ОбъектМетаданных
	СсылкиМетаданных = Новый Соответствие;
	
	Для Каждого ОписаниеИсточника Из ОписанияИсточниковДанных Цикл
		
		Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ОписаниеИсточника.Владелец)) Тогда
			
			ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(ОписаниеИсточника.Владелец));
			
			Если ОбъектыМетаданных.Найти(ОбъектМетаданных) = Неопределено Тогда
				
				МассивСсылок = Новый Массив; // Массив из ЛюбаяСсылка
				СсылкиМетаданных.Вставить(ОбъектМетаданных, МассивСсылок);
				
			КонецЕсли;
			
			СсылкиМетаданных[ОбъектМетаданных].Добавить(ОписаниеИсточника.Владелец);
			ОбъектыМетаданных.Добавить(ОбъектМетаданных);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ОбъектыМетаданных", ОбъектыМетаданных);
	Результат.Вставить("СсылкиМетаданных", СсылкиМетаданных);
	
	Возврат Результат;
	
КонецФункции

Процедура ПолучитьПолеВалютаПоУмолчаниюДляОбъекта(ТипОбъекта, ТаблицаВалютныхРеквизитов, ДанныеВалютыПоУмолчанию, ДанныеПечати = Ложь)
	
	ПолныйПутьКПолюВалюты = "";
	ИмяПоляВалюты = "";
	ПолеВалютыОпределено = Ложь;
	
	Если ТаблицаВалютныхРеквизитов.Количество() > 0 Тогда
		
		ПолныйПутьКПолюВалюты = ТаблицаВалютныхРеквизитов[0].Идентификатор;
		
	КонецЕсли;
	
	ПолныйПутьКПолюВалютыСтарый = ПолныйПутьКПолюВалюты;
	РаботаСКурсамиВалютПереопределяемый.ПриОпределенииВалютыОбъектаПоУмолчанию(ТипОбъекта, ПолныйПутьКПолюВалюты);
	МассивПодстрок = Новый Массив; // Массив из Строка
	Если НЕ ПустаяСтрока(ПолныйПутьКПолюВалюты) Тогда
		
		МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПолныйПутьКПолюВалюты, ".");
		ИмяПоляВалюты = МассивПодстрок[МассивПодстрок.ВГраница()];
		
	КонецЕсли;
	
	// Если МассивПодстрок содержит больше одной строки, то в качестве имени поля передали имя поля общего списка
	МаксимальноеКоличествоПодстрок = 2;
	ПутьКОбщемуПолю = "ОбщееПоле";
	Если МассивПодстрок.Количество() = 1 Тогда
		
		СтрокаТаблицы = ТаблицаВалютныхРеквизитов.Найти(ИмяПоляВалюты, "Идентификатор");
		Если СтрокаТаблицы <> Неопределено Тогда
			
			ДанныеВалютыПоУмолчанию.ИмяПоля = ИмяПоляВалюты;
			ДанныеВалютыПоУмолчанию.ПредставлениеПоля = СтрокаТаблицы.Представление;
			ДанныеВалютыПоУмолчанию.Переопределена = (ПолныйПутьКПолюВалютыСтарый <> ИмяПоляВалюты);
			ПолеВалютыОпределено = Истина;
			
		КонецЕсли;
		
	ИначеЕсли МассивПодстрок.Количество() = МаксимальноеКоличествоПодстрок
	   И МассивПодстрок[0] = ПутьКОбщемуПолю 
	   И ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Печать") Тогда
		
		ТаблицаОбщихПолей = ТаблицаОбщихПолейДляПечати();
		МодульУправлениеПечатьюПереопределяемый = ОбщегоНазначения.ОбщийМодуль("УправлениеПечатьюПереопределяемый");
		МодульУправлениеПечатьюПереопределяемый.ПриЗаполненииСпискаОбщихПолей(ТипОбъекта, ТаблицаОбщихПолей);
		СтрокаТаблицы = ТаблицаОбщихПолей.Найти(ИмяПоляВалюты, "Идентификатор");
		
		Если СтрокаТаблицы <> Неопределено Тогда
			
			ДанныеВалютыПоУмолчанию.ИмяПоля = ИмяПоляВалюты;
			ДанныеВалютыПоУмолчанию.ПредставлениеПоля = СтрокаТаблицы.Представление;
			ДанныеВалютыПоУмолчанию.ЗначениеВалюты = СтрокаТаблицы.Значение;
			ДанныеВалютыПоУмолчанию.Переопределена = Истина;
			ПолеВалютыОпределено = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ПолеВалютыОпределено
	   И НЕ ПустаяСтрока(ПолныйПутьКПолюВалюты)
	   И ДанныеПечати Тогда
		
		ШаблонСообщения = НСтр("ru = 'Для типа объекта ""%1"" неверно указано имя поля ""%2"" для определения валюты по умолчанию'");
		ТекстСообщений = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения,
			Строка(ТипОбъекта),
		ПолныйПутьКПолюВалюты);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщений);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоляСуммойПрописью(ОписанияИсточниковДанных, ДанныеПечати, ПараметрыДляЗаполненияВалюты, КодЯзыка)
	
	ТаблицаВалютныхРеквизитов = ПараметрыДляЗаполненияВалюты.ТаблицаВалютныхРеквизитов;
	ЗначенияВалютныхРеквизитовСсылки = ПараметрыДляЗаполненияВалюты.ЗначенияВалютныхРеквизитовСсылки;
	ВалютаПоУмолчанию = ПараметрыДляЗаполненияВалюты.ВалютаПоУмолчанию;
	
	Если ПустаяСтрока(ВалютаПоУмолчанию.ПредставлениеПоля)
	   И ЗначенияВалютныхРеквизитовСсылки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ОписаниеИсточника Из ОписанияИсточниковДанных Цикл
		
		СтрокаТаблицы = ДанныеПечати.Добавить();
		СтрокаТаблицы.Ссылка = ОписаниеИсточника.Значение;
		
		ВалютаПоУмолчаниюДобавлена =
			ДобавитьВалютуПоУмолчанию(СтрокаТаблицы, ВалютаПоУмолчанию, ОписаниеИсточника, КодЯзыка);
		
		Если ЗначенияВалютныхРеквизитовСсылки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СчетчикРеквизитов = 0;
		Для Каждого Реквизит Из ТаблицаВалютныхРеквизитов Цикл
			
			СчетчикРеквизитов = СчетчикРеквизитов + 1;
			Валюта = ЗначенияВалютныхРеквизитовСсылки[Реквизит.Идентификатор];
			
			Если НЕ ЗначениеЗаполнено(Валюта) Тогда
				Продолжить;
			КонецЕсли;
			
			Если СчетчикРеквизитов = 1
			   И НЕ ВалютаПоУмолчаниюДобавлена  Тогда
				
				СтрокаТаблицы.Валюта = Валюта;
				СтрокаТаблицы.ЧислоПрописью =
					СформироватьСуммуПрописью(ОписаниеИсточника.Значение, Валюта,, КодЯзыка);
				
			КонецЕсли;
			
			ИмяКолонки = ИдентификаторПоляЧислоПрописью(Реквизит.Идентификатор);
			СтрокаТаблицы[ИмяКолонки] = СформироватьСуммуПрописью(ОписаниеИсточника.Значение, Валюта,, КодЯзыка);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДобавитьВалютуПоУмолчанию(СтрокаТаблицы, ВалютаПоУмолчанию, ОписаниеИсточника, КодЯзыка)
	
	ВалютаПоУмолчаниюДобавлена = Ложь;
	
	Если НЕ ПустаяСтрока(ВалютаПоУмолчанию.ПредставлениеПоля) Тогда
		
		ВалютаПоУмолчаниюДобавлена = Истина;
		
		Если ЗначениеЗаполнено(ВалютаПоУмолчанию.ЗначениеВалюты) Тогда
			
			СтрокаТаблицы.Валюта = ВалютаПоУмолчанию.ЗначениеВалюты;
			СтрокаТаблицы.ЧислоПрописью = СформироватьСуммуПрописью(
				ОписаниеИсточника.Значение,
				ВалютаПоУмолчанию.ЗначениеВалюты,
				,
				КодЯзыка);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВалютаПоУмолчаниюДобавлена;
	
КонецФункции

Функция ТаблицаВалютныхРеквизитовОбъекта(ОбъектМетаданных)
	
	ТаблицаПолейВалют = ТаблицаВалютныхПолейДляПечати();
	
	Для Каждого Реквизит Из ОбъектМетаданных.Реквизиты Цикл
		
		Если Реквизит.Тип.СодержитТип(Тип("СправочникСсылка.Валюты")) Тогда
			
			НоваяСтрока = ТаблицаПолейВалют.Добавить();
			НоваяСтрока.Идентификатор = Реквизит.Имя;
			НоваяСтрока.Представление = Реквизит.Синоним;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаПолейВалют;
	
КонецФункции

// Возвращаемое значение:
//  Структура - данные валюты по умолчанию:
// * ЗначениеВалюты - СправочникСсылка.Валюты 
// * ИмяПоля - Строка 
// * ПредставлениеПоля - Строка
// * Переопределена - Булево
//
Функция ДанныеВалюты()
	
	ДанныеВалюты = Новый Структура;
	ДанныеВалюты.Вставить("ЗначениеВалюты", Справочники.Валюты.ПустаяСсылка());
	ДанныеВалюты.Вставить("ИмяПоля", "");
	ДанныеВалюты.Вставить("ПредставлениеПоля", "");
	ДанныеВалюты.Вставить("Переопределена", Ложь);
	
	Возврат ДанныеВалюты;
	
КонецФункции

// Возвращаемое значение:
//  ТаблицаЗначений - таблица валют для печати:
// * Идентификатор - Строка
// * Представление - Строка
//
Функция ТаблицаВалютныхПолейДляПечати()
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТаблицаЗначений.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначений.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	
	Возврат ТаблицаЗначений;
	
КонецФункции

// Возвращаемое значение:
//  ТаблицаЗначений - новая таблица общих полей:
// * Идентификатор - Строка
// * Представление - Строка
// * Значение -ЛюбаяСсылка
// 
Функция ТаблицаОбщихПолейДляПечати()
	
	ТаблицаОбщихПолей = Новый ТаблицаЗначений;
	ТаблицаОбщихПолей.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаОбщихПолей.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	ТаблицаОбщихПолей.Колонки.Добавить("Значение");
	
	Возврат ТаблицаОбщихПолей;
	
КонецФункции

Функция ИдентификаторПоляЧислоПрописью(Идентификатор)
	
	Возврат "ЧислоПрописью" + "_" + СтрЗаменить(Идентификатор, ".", "_");
	
КонецФункции

Функция ПредставлениеПоляЧислоПрописью(Представление)
	
	Возврат НСтр("ru = 'Прописью'" )  + " (" + Представление + ")";
	
КонецФункции

#КонецОбласти

// См. СтандартныеПодсистемыСервер.ПриОпределенииМетодовРазрешенныхДляВызоваКакПроизвольныйКод
Процедура ПриОпределенииМетодовРазрешенныхДляВызоваКакПроизвольныйКод(Методы) Экспорт
	
	Методы.Вставить("ОбновитьКурсВалюты", Истина);
	
КонецПроцедуры

#КонецОбласти