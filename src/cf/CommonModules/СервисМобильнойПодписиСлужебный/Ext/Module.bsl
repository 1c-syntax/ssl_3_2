///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает заголовок команды отправки на подписание.
// 
// Возвращаемое значение:
//  Строка
// 
Функция ЗаголовокКомандыОтправитьНаПодписание() Экспорт
	
	ИспользоватьГосключ = ПолучитьФункциональнуюОпцию("ИспользоватьГосключ");
	ИспользоватьСервисМобильнойПодписи = ПолучитьФункциональнуюОпцию("ИспользоватьСервисМобильнойПодписи");
	
	Если ИспользоватьГосключ И Не ИспользоватьСервисМобильнойПодписи Тогда
		Возврат НСтр("ru='Отправить в Госключ'") + "...";
	КонецЕсли;
	
	Если Не ИспользоватьГосключ И ИспользоватьСервисМобильнойПодписи Тогда
		Возврат НСтр("ru='Отправить в Моя подпись(ФНС)'") + "...";
	КонецЕсли;
	
	Возврат НСтр("ru='Отправить в мобильное приложение'") + "...";
	
КонецФункции

// Добавляет в список способов подписания отправку в сервис мобильной подписи.
// 
// Параметры:
//  СписокВыбора - СписокЗначений
//
Процедура ПриПолученииТиповПодписания(СписокВыбора) Экспорт
	
	ИспользоватьСервисМобильнойПодписи = СервисМобильнойПодписи.ИспользоватьСервисМобильнойПодписи();
	ИспользоватьГосключ = СервисМобильнойПодписи.ИспользоватьГосключ();
	
	Если ИспользоватьСервисМобильнойПодписи И Не ИспользоватьГосключ Тогда
		Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
			СписокВыбора.Добавить("ОтправитьНаПодписание", 
				НСтр("ru='В ""Моя подпись"" от ФНС'"));
		Иначе
			СписокВыбора.Добавить("ОтправитьНаПодписание", 
				НСтр("ru='В мобильном приложении ""Моя подпись"" от ФНС'"));
			КонецЕсли;
	ИначеЕсли Не ИспользоватьСервисМобильнойПодписи И ИспользоватьГосключ Тогда
		Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
			СписокВыбора.Добавить("ОтправитьНаПодписание", 
				НСтр("ru='В ""Госключе""'"));
		Иначе
			СписокВыбора.Добавить("ОтправитьНаПодписание", 
				НСтр("ru='В мобильном приложении ""Госключ""'"));
		КонецЕсли;
	ИначеЕсли ИспользоватьСервисМобильнойПодписи И ИспользоватьГосключ Тогда
		СписокВыбора.Добавить("ОтправитьНаПодписание", 
				НСтр("ru='В мобильном приложении'"));
	КонецЕсли;
	
КонецПроцедуры

// Добавить ожидаемые подписи объекта к списку подписей объекта.
// 
// Параметры:
//  Объект - ЛюбаяСсылка
//  Результат - Массив
//
Процедура ДобавитьОжидаемыеПодписиОбъекта(Объект, Результат) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ПРЕДСТАВЛЕНИЕ(СервисМобильнойПодписиСтатусы.Подписант) КАК ПодписантПредставление,
	|	ПРЕДСТАВЛЕНИЕ(СервисМобильнойПодписиСтатусы.Отправитель) КАК ОтправительПредставление,
	|	СервисМобильнойПодписиСтатусы.Отправитель КАК Отправитель,
	|	СервисМобильнойПодписиСтатусы.ДатаОтправки КАК ДатаОтправки,
	|	СервисМобильнойПодписиСтатусы.ПодписанныйОбъект КАК ПодписанныйОбъект,
	|	СервисМобильнойПодписиСтатусы.Комментарий КАК Комментарий,
	|	СервисМобильнойПодписиСтатусы.Доверенность КАК Доверенность,
	|	СервисМобильнойПодписиСтатусы.Статус КАК Статус,
	|	СервисМобильнойПодписиСтатусы.Результат КАК Результат
	|ИЗ
	|	РегистрСведений.СервисМобильнойПодписиСтатусы КАК СервисМобильнойПодписиСтатусы
	|ГДЕ
	|	СервисМобильнойПодписиСтатусы.ПодписанныйОбъект = &Объект
	|	И СервисМобильнойПодписиСтатусы.Операция = ЗНАЧЕНИЕ(Перечисление.СервисМобильнойПодписиОперации.Подписание)";
	
	Запрос.УстановитьПараметр("Объект", Объект);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СвойстваПодписи = ЭлектроннаяПодписьКлиентСервер.НовыеСвойстваПодписи();
		Если Выборка.Статус = Перечисления.СервисМобильнойПодписиСтатусы.Успешно Тогда
			Подпись = Выборка.Результат.Получить();
			СвойстваПрочитанные = ЭлектроннаяПодпись.СвойстваПодписи(Подпись);
			ЗаполнитьЗначенияСвойств(СвойстваПодписи, СвойстваПрочитанные);
		Иначе
			СвойстваПодписи.ИдентификаторПодписи = Выборка.ИдентификаторДокумента;
			СвойстваПодписи.ПодписанныйОбъект = Выборка.ПодписанныйОбъект;
			СвойстваПодписи.КомуВыданСертификат = СтрШаблон("%1 (%2)", Выборка.ПодписантПредставление, Выборка.ОтправительПредставление);
			СвойстваПодписи.УстановившийПодпись = Выборка.Отправитель;
			СвойстваПодписи.ДатаПодписи = Выборка.ДатаОтправки;
			СвойстваПодписи.Комментарий = Выборка.Комментарий;
			Ошибка = Выборка.Результат.Получить();
			Если ТипЗнч(Ошибка) = Тип("Структура") Тогда
				СвойстваПодписи.ОшибкаПроверкиДополнительныхАтрибутов = ТекстОшибкиОжидаемойПодписи(Ошибка);
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Доверенность) И ОбщегоНазначения.ПодсистемаСуществует(
									"СтандартныеПодсистемы.МашиночитаемыеДоверенности") Тогда
			МодульМашиночитаемыеДоверенностиФНССлужебный = ОбщегоНазначения.ОбщийМодуль(
										"МашиночитаемыеДоверенностиФНССлужебный");
			СвойстваПодписи.РезультатПроверкиПодписиПоМЧД = 
				МодульМашиночитаемыеДоверенностиФНССлужебный.НовыйРезультатПроверкиПодписиПоМЧД(Выборка.Доверенность);
		КонецЕсли;
		Результат.Добавить(СвойстваПодписи);
	КонецЦикла;
	
КонецПроцедуры

// Документ отправлен на подпись в сервис мобильной подписи.
// 
// Параметры:
//  ПодписанныйОбъект - ЛюбаяСсылка
// 
// Возвращаемое значение:
//  Булево
//
Функция ЕстьОжидаемыеПодписи(ПодписанныйОбъект) Экспорт
	
	Если Не ЗначениеЗаполнено(ПодписанныйОбъект) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента КАК ИдентификаторДокумента
	|ИЗ
	|	РегистрСведений.СервисМобильнойПодписиСтатусы КАК СервисМобильнойПодписиСтатусы
	|ГДЕ
	|	СервисМобильнойПодписиСтатусы.ПодписанныйОбъект = &ПодписанныйОбъект
	|	И СервисМобильнойПодписиСтатусы.Операция = ЗНАЧЕНИЕ(Перечисление.СервисМобильнойПодписиОперации.Подписание)";
	
	Запрос.УстановитьПараметр("ПодписанныйОбъект", ПодписанныйОбъект);
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Удаляет запросы, отправленные на подпись в сервис мобильно подписи и устанавливает признак ПодписанЭП = Ложь, 
// если у объекта нет других подписей.
// 
// Параметры:
//  ПодписанныйОбъект - ЛюбаяСсылка
//  ИдентификаторыДокументов - Массив из УникальныйИдентификатор
//  ЕстьПраваНаУдалениеЧужихПодписей - Булево
//  СообщениеЖурналаРегистрации - Строка - параметры подписи для записи в журнал регистрации.
//
Процедура УдалитьОжидаемыеПодписиОбъекта(ПодписанныйОбъект,
	ИдентификаторыДокументов, ЕстьПраваНаУдалениеЧужихПодписей, СообщениеЖурналаРегистрации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента КАК ИдентификаторДокумента,
	               |	СервисМобильнойПодписиСтатусы.Подписант КАК Подписант,
	               |	СервисМобильнойПодписиСтатусы.Отправитель КАК Отправитель,
	               |	СервисМобильнойПодписиСтатусы.ДатаОтправки КАК ДатаОтправки,
	               |	СервисМобильнойПодписиСтатусы.НаименованиеДокумента КАК НаименованиеДокумента,
	               |	СервисМобильнойПодписиСтатусы.ПодписанныйОбъект КАК ПодписанныйОбъект
	               |ИЗ
	               |	РегистрСведений.СервисМобильнойПодписиСтатусы КАК СервисМобильнойПодписиСтатусы
	               |ГДЕ
	               |	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента В(&ИдентификаторыДокументов)
	               |	И СервисМобильнойПодписиСтатусы.Операция = ЗНАЧЕНИЕ(Перечисление.СервисМобильнойПодписиОперации.Подписание)
	               |	И СервисМобильнойПодписиСтатусы.ПодписанныйОбъект = &ПодписанныйОбъект";
	
	Запрос.УстановитьПараметр("ИдентификаторыДокументов", ИдентификаторыДокументов);
	Запрос.УстановитьПараметр("ПодписанныйОбъект", ПодписанныйОбъект);
	
	Если ЕстьПраваНаУдалениеЧужихПодписей Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ"); // @query-part-1, @query-part-2
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.РегистрыСведений.СервисМобильнойПодписиСтатусы.ПолноеИмя());
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса.Выгрузить();
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторДокумента", "ИдентификаторДокумента");
		Блокировка.Заблокировать();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			СообщениеЖурналаРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Файл %1, Отправитель %2, ДатаОтправки %3, Подписант %4'"),
				ВыборкаДетальныеЗаписи.НаименованиеДокумента,
				ВыборкаДетальныеЗаписи.Отправитель,
				ВыборкаДетальныеЗаписи.ДатаОтправки,
				ВыборкаДетальныеЗаписи.Подписант);
				
			ЕстьПрава = ЕстьПраваНаУдалениеЧужихПодписей 
				Или ВыборкаДетальныеЗаписи.Отправитель = Пользователи.АвторизованныйПользователь();
				
			Если Не ЕстьПрава Тогда
				ВызватьИсключение(НСтр("ru = 'Недостаточно прав на удаление ожидаемой подписи.'"), КатегорияОшибки.НарушениеПравДоступа);
			КонецЕсли;

			УдалитьОжидаемуюПодпись(ВыборкаДетальныеЗаписи.ИдентификаторДокумента, СообщениеЖурналаРегистрации, ВыборкаДетальныеЗаписи.ПодписанныйОбъект);
			
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Удаляет записи о документах, отправленных на подпись в сервис мобильной подписи из регистра СервисМобильнойПодписиСтатусы.
// Также пытается удалить запрос на подписание в сервисе, он будет удален, если еще не доставлен получателю.
// Если отправка была пакетная, то запрос на подпись в сервисе будет удален, если указаны все документы из пакета.
// 
// Параметры:
//  ИдентификаторыДокументов - Массив из УникальныйИдентификатор
//  ПриПодписании - Булево - удаление служебной записи при подписании в ЭлектроннаяПодписьКлиент.Подписать, 
//         Ложь - при отмене подписания, Истина - при успешном подписании, Неопределено - при других сценариях удаления.
//
Процедура УдалитьОжидаемыеПодписи(ИдентификаторыДокументов, ПриПодписании = Неопределено) Экспорт

	ЕстьПраваНаУдалениеЧужихПодписей = Пользователи.ЭтоПолноправныйПользователь() 
		Или Пользователи.РолиДоступны("УдалениеЭлектронныхПодписей");

	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	СервисМобильнойПодписиСтатусы.ИдентификаторВСервисе КАК ИдентификаторВСервисе,
	|	СервисМобильнойПодписиСтатусы.Подписант КАК Подписант,
	|	СервисМобильнойПодписиСтатусы.Отправитель КАК Отправитель,
	|	СервисМобильнойПодписиСтатусы.ДатаОтправки КАК ДатаОтправки,
	|	СервисМобильнойПодписиСтатусы.НаименованиеДокумента КАК НаименованиеДокумента,
	|	СервисМобильнойПодписиСтатусы.ПодписанныйОбъект КАК ПодписанныйОбъект,
	|	СервисМобильнойПодписиСтатусы.Статус КАК Статус,
	|	СервисМобильнойПодписиСтатусы.НастройкаОтправкиВГосключ
	|ПОМЕСТИТЬ ДокументыНаПодписании
	|ИЗ
	|	РегистрСведений.СервисМобильнойПодписиСтатусы КАК СервисМобильнойПодписиСтатусы
	|ГДЕ
	|	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента В (&ИдентификаторыДокументов)
	|	И СервисМобильнойПодписиСтатусы.Операция = ЗНАЧЕНИЕ(Перечисление.СервисМобильнойПодписиОперации.Подписание)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДокументыНаПодписании.ИдентификаторВСервисе КАК ИдентификаторВСервисе
	|ПОМЕСТИТЬ ДругиеПодписиВПакете
	|ИЗ
	|	ДокументыНаПодписании КАК ДокументыНаПодписании
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисМобильнойПодписиСтатусы КАК СервисМобильнойПодписиСтатусы
	|		ПО ДокументыНаПодписании.ИдентификаторВСервисе = СервисМобильнойПодписиСтатусы.ИдентификаторВСервисе
	|ГДЕ
	|	НЕ СервисМобильнойПодписиСтатусы.ИдентификаторДокумента В (&ИдентификаторыДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	СервисМобильнойПодписиСтатусы.ИдентификаторВСервисе КАК ИдентификаторВСервисе,
	|	СервисМобильнойПодписиСтатусы.Подписант КАК Подписант,
	|	СервисМобильнойПодписиСтатусы.Отправитель КАК Отправитель,
	|	СервисМобильнойПодписиСтатусы.ДатаОтправки КАК ДатаОтправки,
	|	СервисМобильнойПодписиСтатусы.НаименованиеДокумента КАК НаименованиеДокумента,
	|	СервисМобильнойПодписиСтатусы.ПодписанныйОбъект КАК ПодписанныйОбъект,
	|	ВЫБОР
	|		КОГДА ДругиеПодписиВПакете.ИдентификаторВСервисе ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьДругиеПодписиВПакете,
	|	ИСТИНА КАК ДетальнаяЗапись,
	|	СервисМобильнойПодписиСтатусы.Статус КАК Статус,
	|	СервисМобильнойПодписиСтатусы.НастройкаОтправкиВГосключ
	|ИЗ
	|	ДокументыНаПодписании КАК СервисМобильнойПодписиСтатусы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДругиеПодписиВПакете КАК ДругиеПодписиВПакете
	|		ПО СервисМобильнойПодписиСтатусы.ИдентификаторВСервисе = ДругиеПодписиВПакете.ИдентификаторВСервисе
	|ИТОГИ
	|ПО
	|	ИдентификаторВСервисе";
	
	Запрос.УстановитьПараметр("ИдентификаторыДокументов", ИдентификаторыДокументов);
	УдалитьВСервисе = Новый Массив;
	ОшибкиУдаленияВСервисе = Новый Массив;
	
	Если ЕстьПраваНаУдалениеЧужихПодписей Или ПриПодписании <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ"); // @query-part-1, @query-part-2
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.РегистрыСведений.СервисМобильнойПодписиСтатусы.ПолноеИмя());
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса.Выгрузить().Скопировать(Новый Структура("ДетальнаяЗапись", Истина));
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторДокумента", "ИдентификаторДокумента");
		Блокировка.Заблокировать();
		Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока Выборка.Следующий() Цикл
			
			ЕстьДругиеПодписиВПакете = Новый Массив;
			УдалятьВСервисе = Истина;

			ВыборкаДетальныеЗаписи = Выборка.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				Если ВыборкаДетальныеЗаписи.ЕстьДругиеПодписиВПакете Тогда
					ЕстьДругиеПодписиВПакете.Добавить(ВыборкаДетальныеЗаписи.НаименованиеДокумента);
				КонецЕсли;
				
				ПодписанныйОбъект = ВыборкаДетальныеЗаписи.ПодписанныйОбъект;
				
				Если ПриПодписании = Ложь Тогда
					СообщениеЖурналаРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Файл %1, Отправитель %2, ДатаОтправки %3, Подписант %4, Идентификатор в сервисе %5 - отмена при подписании'"),
						ВыборкаДетальныеЗаписи.НаименованиеДокумента,
						ВыборкаДетальныеЗаписи.Отправитель,
						ВыборкаДетальныеЗаписи.ДатаОтправки,
						ВыборкаДетальныеЗаписи.Подписант,
						ВыборкаДетальныеЗаписи.ИдентификаторВСервисе);
				Иначе
					СообщениеЖурналаРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Файл %1, Отправитель %2, ДатаОтправки %3, Подписант %4, Идентификатор в сервисе %5'"),
						ВыборкаДетальныеЗаписи.НаименованиеДокумента,
						ВыборкаДетальныеЗаписи.Отправитель,
						ВыборкаДетальныеЗаписи.ДатаОтправки,
						ВыборкаДетальныеЗаписи.Подписант,
						ВыборкаДетальныеЗаписи.ИдентификаторВСервисе);
				КонецЕсли;
				
				СтандартнаяОбработка = Истина;
				УдалитьПодпись = Истина;
				
				ПараметрыПодписи = Новый Структура;
				ПараметрыПодписи.Вставить("ИдентификаторДокумента", ВыборкаДетальныеЗаписи.ИдентификаторДокумента);
				ПараметрыПодписи.Вставить("ПодписанныйОбъект", ВыборкаДетальныеЗаписи.ПодписанныйОбъект);
				ПараметрыПодписи.Вставить("Отправитель", ВыборкаДетальныеЗаписи.Отправитель);
				ПараметрыПодписи.Вставить("Подписант", ВыборкаДетальныеЗаписи.Подписант);
				
				Если ПриПодписании = Неопределено Тогда
					СервисМобильнойПодписиПереопределяемый.ПриУдаленииОжидаемойПодписиОбъекта(
						ПараметрыПодписи, УдалитьПодпись, СтандартнаяОбработка);
				КонецЕсли;
				
				Если СтандартнаяОбработка Тогда
					
					ЕстьПрава = ЕстьПраваНаУдалениеЧужихПодписей 
						Или ВыборкаДетальныеЗаписи.Отправитель = Пользователи.АвторизованныйПользователь();
					
					Если Не ЕстьПрава Тогда
						ЗаписьЖурналаРегистрации(НСтр("ru = 'Сервис мобильной подписи.Удаление ожидаемой подписи'", ОбщегоНазначения.КодОсновногоЯзыка()),
							УровеньЖурналаРегистрации.Информация,
							?(ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ПодписанныйОбъект), ВыборкаДетальныеЗаписи.ПодписанныйОбъект.Метаданные(), Неопределено),
							ВыборкаДетальныеЗаписи.ПодписанныйОбъект,
							СообщениеЖурналаРегистрации, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
						ВызватьИсключение(НСтр("ru = 'Недостаточно прав на удаление ожидаемой подписи.'"), КатегорияОшибки.НарушениеПравДоступа);
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ПодписанныйОбъект) И ЭлектроннаяПодписьСлужебный.ДоступнаЭлектроннаяПодпись(ТипЗнч(ПодписанныйОбъект)) Тогда
						ЭлектроннаяПодпись.УдалитьПодпись(ПодписанныйОбъект, ВыборкаДетальныеЗаписи.ИдентификаторДокумента);
						Продолжить;
					ИначеЕсли ЗначениеЗаполнено(ПодписанныйОбъект) Тогда
						Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
							МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
							МодульУправлениеДоступом.ПроверитьИзменениеРазрешено(ПодписанныйОбъект);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				Если УдалитьПодпись Тогда
					Если ВыборкаДетальныеЗаписи.Статус = Перечисления.СервисМобильнойПодписиСтатусы.Успешно
						Или ВыборкаДетальныеЗаписи.Статус = Перечисления.СервисМобильнойПодписиСтатусы.Отклонено
						Или ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.НастройкаОтправкиВГосключ) Тогда
						УдалятьВСервисе = Ложь;
					КонецЕсли;
					УдалитьОжидаемуюПодпись(ВыборкаДетальныеЗаписи.ИдентификаторДокумента, СообщениеЖурналаРегистрации, ПодписанныйОбъект, ПриПодписании);
				Иначе
					УдалятьВСервисе = Ложь;
				КонецЕсли;
			КонецЦикла;
			
			Если УдалятьВСервисе Тогда
				Если ЕстьДругиеПодписиВПакете.Количество() > 0 Тогда
					ОшибкиУдаленияВСервисе.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Запрос на удаление документов (%1) не отправлен: в пакете на подписание %2 есть другие документы.'"),
							СтрСоединить(ЕстьДругиеПодписиВПакете, ", "),
						Выборка.ИдентификаторВСервисе));
				Иначе
					УдалитьВСервисе.Добавить(Выборка.ИдентификаторВСервисе);
				КонецЕсли;
			КонецЕсли;

		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Если ЗначениеЗаполнено(СообщениеЖурналаРегистрации) Тогда
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Сервис мобильной подписи.Ошибка удаления ожидаемой подписи'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Информация,
				?(ЗначениеЗаполнено(ПодписанныйОбъект), ПодписанныйОбъект.Метаданные(), Неопределено),
				ПодписанныйОбъект,
				СообщениеЖурналаРегистрации + "
				|
				|" + ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
		КонецЕсли;
		
		ВызватьИсключение;
	КонецПопытки;
	
	Для Каждого ИдентификаторЗапроса Из УдалитьВСервисе Цикл
		Попытка
			УдалитьЗапросНаПодписание(ИдентификаторЗапроса);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Сервис мобильной подписи.Ошибка удаления запроса на подписание'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,,, ИдентификаторЗапроса + ": " + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		КонецПопытки;
	КонецЦикла;
	
	Если ОшибкиУдаленияВСервисе.Количество() > 0 Тогда
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Сервис мобильной подписи.Ошибка удаления запроса на подписание'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,, СтрСоединить(ОшибкиУдаленияВСервисе, Символы.ПС));
	КонецЕсли;
	
КонецПроцедуры

// Оформляет поле Подписант в форме подписания или отправки на подпись в зависимости от переданного параметра.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Подписант - Структура, СправочникСсылка.ПодписантыСервисаМобильнойПодписи
//
Процедура ПодписантПриСозданииНаСервере(Форма, Подписант) Экспорт
	
	Если ЗначениеЗаполнено(Подписант) Тогда
		Если ТипЗнч(Подписант) = Тип("Структура") Тогда
			ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
			ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Подписант);
			Форма.Подписант = НайденныйПодписант(ПараметрыПодписанта);
			Если Не ЗначениеЗаполнено(Форма.Подписант) Тогда
				Наименование = Справочники.ПодписантыСервисаМобильнойПодписи.АвтоНаименование(ПараметрыПодписанта);
				Форма.ПараметрыПодписанта = ПараметрыПодписанта;
				Форма.Элементы.Подписант.ТолькоПросмотр = Истина;
				Форма.Элементы.Подписант.ПодсказкаВвода = Наименование;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Подписант) = Тип("СправочникСсылка.ПодписантыСервисаМобильнойПодписи") Тогда
			Форма.Подписант = Подписант;
			Форма.Элементы.Подписант.ТолькоПросмотр = ЗначениеЗаполнено(Подписант);
		КонецЕсли;
	Иначе
		ПоследнийПодписант = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
				Пользователи.ТекущийПользователь(), "ПодписаниеДанных_ПодписантПоУмолчанию", Неопределено);
		Если ТипЗнч(ПоследнийПодписант) = Тип("СправочникСсылка.ПодписантыСервисаМобильнойПодписи") Тогда
			Форма.Подписант = ПоследнийПодписант;
		Иначе
			Форма.Подписант = Справочники.ПодписантыСервисаМобильнойПодписи.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
	ПодписантПриИзмененииНаСервере(Форма);

КонецПроцедуры

Процедура ПодписантПриИзмененииНаСервере(Форма) Экспорт
	
	Если Не ЗначениеЗаполнено(Форма.Подписант) Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбора = Форма.Элементы.ВидМобильнойПодписи.СписокВыбора;
	СписокВыбора.Очистить();
	
	РеквизитыПодписанта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Форма.Подписант, "МояПодпись, ГосключУНЭП, ГосключУКЭП");
	Если РеквизитыПодписанта.МояПодпись Тогда
		СписокВыбора.Добавить(Перечисления.ВидыМобильныхПодписей.МояПодпись);
	КонецЕсли;
	Если РеквизитыПодписанта.ГосключУНЭП 
		И Форма.ПроверятьСертификат <> ЭлектроннаяПодписьСлужебныйКлиентСервер.ТолькоКвалифицированные() Тогда
		СписокВыбора.Добавить(Перечисления.ВидыМобильныхПодписей.ГосключУНЭП);
	КонецЕсли;
	Если РеквизитыПодписанта.ГосключУКЭП Тогда
		СписокВыбора.Добавить(Перечисления.ВидыМобильныхПодписей.ГосключУКЭП);
	КонецЕсли;
	Форма.ВидМобильнойПодписи = Неопределено;
	
	Количество = СписокВыбора.Количество();
	Если Количество > 0 Тогда
		НастройкаПодписанта = НастройкаПодписанта(Форма);
		Если НастройкаПодписанта <> Неопределено 
			И СписокВыбора.НайтиПоЗначению(НастройкаПодписанта.ВидМобильнойПодписи) <> Неопределено Тогда
			Форма.ВидМобильнойПодписи = НастройкаПодписанта.ВидМобильнойПодписи;
		Иначе
			Форма.ВидМобильнойПодписи = СписокВыбора[0].Значение;
		КонецЕсли;
	Иначе 
		НастройкаПодписанта = Неопределено;
	КонецЕсли;
	
	Форма.Элементы.ВидМобильнойПодписи.Видимость = Количество > 1;
	
	ЗаполнитьНастройкуОтправки(Форма);
	
	Если НастройкаПодписанта <> Неопределено И Форма.ВидМобильнойПодписи = НастройкаПодписанта.ВидМобильнойПодписи
		И Форма.Элементы.НастройкаОтправки.СписокВыбора.НайтиПоЗначению(НастройкаПодписанта.НастройкаОтправки) <> Неопределено Тогда
		Форма.НастройкаОтправки = НастройкаПодписанта.НастройкаОтправки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьНастройкуПодписанта(Форма, НастройкиДляСохранения, Подписант) Экспорт
	
	Если Не ЗначениеЗаполнено(Подписант) Тогда
		Возврат;
	КонецЕсли;
	НастройкаПодписанта = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		Подписант, "ПодписаниеДанных_НастройкаПодписанта", Неопределено);
	Если НастройкаПодписанта = Неопределено Тогда
		НастройкаПодписанта = Новый Соответствие;
	КонецЕсли;
	НастройкаПодписанта.Вставить(КлючНастроекПодписанта(Форма.ПараметрыОтправителя),
		Новый Структура("ВидМобильнойПодписи, НастройкаОтправки", Форма.ВидМобильнойПодписи, Форма.НастройкаОтправки));
	НастройкиДляСохранения.Добавить(Новый Структура("Объект, Настройка, Значение", Подписант,
		"ПодписаниеДанных_НастройкаПодписанта", НастройкаПодписанта));
	
КонецПроцедуры

Процедура ЗаполнитьНастройкуОтправки(Форма) Экспорт
	
	Если Форма.ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.ГосключУНЭП 
		Или Форма.ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.ГосключУКЭП Тогда
			
		СписокВыбора = Форма.Элементы.НастройкаОтправки.СписокВыбора;
		
		Если Не ЗначениеЗаполнено(Форма.НастройкаОтправки) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	НастройкиОтправкиВГосключ.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.НастройкиОтправкиВГосключ КАК НастройкиОтправкиВГосключ
			|ГДЕ
			|	НЕ НастройкиОтправкиВГосключ.ПометкаУдаления
			|	И НастройкиОтправкиВГосключ.СрокДействия > &ТекущаяДата
			|	И НастройкиОтправкиВГосключ.Организация = &Организация";
			
			Если ТипЗнч(Форма.ПараметрыОтправителя) = Тип("Структура") И ЗначениеЗаполнено(Форма.ПараметрыОтправителя.Организация) Тогда
				Запрос.УстановитьПараметр("Организация", Форма.ПараметрыОтправителя.Организация);
			Иначе
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "И НастройкиОтправкиВГосключ.Организация = &Организация", "");
			КонецЕсли;
			
			Запрос.УстановитьПараметр("ТекущаяДата", КонецДня(ТекущаяДатаСеанса()));
			Выборка = Запрос.Выполнить().Выбрать();
			
			СписокВыбора.Очистить();
			
			Пока Выборка.Следующий() Цикл
				СписокВыбора.Добавить(Выборка.Ссылка);
			КонецЦикла;
			
			Если СписокВыбора.Количество() = 0 Тогда
				Форма.НастройкаОтправки = Неопределено;
			Иначе
				Форма.НастройкаОтправки = СписокВыбора[0].Значение;
			КонецЕсли;
		
		КонецЕсли;
		
		Форма.Элементы.НастройкаОтправки.Видимость = СписокВыбора.Количество() > 1;
		Если ЗначениеЗаполнено(Форма.НастройкаОтправки) Тогда
			Если Форма.ИмяФормы <> "Справочник.ПодписантыСервисаМобильнойПодписи.Форма.ОтправитьНаПодписание" Тогда
				Форма.Элементы.ПодписантРасширеннаяПодсказка.Заголовок = НСтр("ru = 'Документы будут отправлены подписанту в приложение ""Госключ"", необходимо будет дождаться получения результатов подписания'");
			Иначе
				Форма.Элементы.ПодписантРасширеннаяПодсказка.Заголовок = НСтр("ru = 'Документы будут отправлены подписанту в приложение ""Госключ""'");
			КонецЕсли;
		Иначе
			Форма.Элементы.ПодписантРасширеннаяПодсказка.Заголовок = НСтр("ru = 'Недоступны настройки для отправки подписанту в приложение ""Госключ"", обратитесь к администратору'");
		КонецЕсли;
		
	Иначе
		
		Форма.НастройкаОтправки = Неопределено;
		Форма.Элементы.НастройкаОтправки.Видимость = Ложь;
		Если Форма.ИмяФормы <> "Справочник.ПодписантыСервисаМобильнойПодписи.Форма.ОтправитьНаПодписание" Тогда
			Форма.Элементы.ПодписантРасширеннаяПодсказка.Заголовок = НСтр("ru = 'Документы будут отправлены подписанту в приложение ""Моя подпись"" от ФНС, необходимо будет дождаться получения результатов подписания'");
		Иначе
			Форма.Элементы.ПодписантРасширеннаяПодсказка.Заголовок = НСтр("ru = 'Документы будут отправлены подписанту в приложение ""Моя подпись"" от ФНС'");
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция ОтборДляДоверенностейПоПодписанту(Подписант) Экспорт
	
	Префикс = "Представитель";
	
	Отбор = Новый Структура;
	ПараметрыПодписанта = СервисМобильнойПодписи.ПараметрыПодписанта(Подписант);
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ИНН) Тогда
		Отбор.Вставить(Префикс + "ИНН", ПараметрыПодписанта.ИНН);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ОГРН) Тогда
		Отбор.Вставить(Префикс + "ОГРН", ПараметрыПодписанта.ОГРН);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ИННФЛ) Тогда
		Отбор.Вставить(Префикс + "ИННФЛ", ПараметрыПодписанта.ИННФЛ);
	КонецЕсли;
	
	Возврат Отбор;
	
КонецФункции

Процедура ДополнитьВыборкуПриложений(ТаблицаВыборки) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьГосключ") Тогда
		Возврат;
	КонецЕсли;
	
	КолонкаСсылки = ТаблицаВыборки.Колонки.Найти("Ссылка");
	Если КолонкаСсылки <> Неопределено Тогда
		КолонкаСсылки.Имя = "НоваяСсылка";
		ТаблицаВыборки.Колонки.Добавить("Ссылка");
		Для каждого СтрокаТаблицы Из ТаблицаВыборки Цикл
			СтрокаТаблицы.Ссылка = СтрокаТаблицы.НоваяСсылка;
		КонецЦикла;
		ТаблицаВыборки.Колонки.Удалить(КолонкаСсылки);
	КонецЕсли;
	
	НастройкиОтправки = ПолучитьНастройкиОтправки();
	
	Для каждого СтрокаТаблицы Из НастройкиОтправки Цикл
		НоваяСтрока = Новый Структура(
				"ЭтоВстроенныйКриптопровайдер, 
				|ТипРазмещения,
				|ИмяПрограммы,
				|Ссылка,
				|Наименование, 
				|ПометкаУдаления", 
				Ложь, 2, СтрокаТаблицы.Сервер);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1 (Госключ)'"), НоваяСтрока.Наименование);
		СтрокаВыборки = ТаблицаВыборки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаВыборки, НоваяСтрока);
	КонецЦикла;
	
	ТаблицаВыборки.Сортировать("Наименование");
	
КонецПроцедуры

Процедура ИзменитьПометкуУдаленияНастройки(Настройка, УникальныйИдентификатор, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(Настройка) <> Тип("СправочникСсылка.НастройкиОтправкиВГосключ") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПроверитьИзменениеРазрешено(Настройка);
	КонецЕсли;
	
	ЗаблокироватьДанныеДляРедактирования(Настройка, , УникальныйИдентификатор);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.НастройкиОтправкиВГосключ");
	ЭлементБлокировки.УстановитьЗначение("Ссылка", Настройка);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка.Заблокировать();
		
		Объект = Настройка.ПолучитьОбъект();
		Объект.ПометкаУдаления = Не Объект.ПометкаУдаления;
		Объект.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		РазблокироватьДанныеДляРедактирования(Настройка, УникальныйИдентификатор);
		ВызватьИсключение;
	КонецПопытки;
	
	РазблокироватьДанныеДляРедактирования(Настройка, УникальныйИдентификатор);

КонецПроцедуры

#Область ОбработчикиСобытийПодсистемКонфигурации

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.2.1.335";
	Обработчик.Комментарий = НСтр("ru = 'Заполнение реквизита ВидПодписанта и МояПодпись в справочнике ПодписантыСервисаМобильнойПодписи'");
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("ac1cf438-b59b-41dd-9013-179c58cfa8d4");
	Обработчик.Процедура = "Справочники.ПодписантыСервисаМобильнойПодписи.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ПодписантыСервисаМобильнойПодписи.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ЧитаемыеОбъекты      = "Справочник.ПодписантыСервисаМобильнойПодписи";
	Обработчик.ИзменяемыеОбъекты    = "Справочник.ПодписантыСервисаМобильнойПодписи";
	Обработчик.ПроцедураПроверки    = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	
КонецПроцедуры

// Используется для инициализации параметра сеанса в рамках механизмов БСП,
// см. ИнтеграцияПодсистемБСП.ПриДобавленииОбработчиковУстановкиПараметровСеанса.
//
Процедура ПриДобавленииОбработчиковУстановкиПараметровСеанса(Обработчики) Экспорт
	
	Обработчики.Вставить("ПараметрыАвторизацииВСервисеМобильнойПодписи", 
		"СервисМобильнойПодписиСлужебный.УстановитьПараметрСеансаПараметрыАвторизацииВСервисеМобильнойПодписи");
	
КонецПроцедуры

// Используется для инициализации параметра сеанса в рамках механизмов БСП
// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииОбработчиковУстановкиПараметровСеанса.
//
Процедура УстановитьПараметрСеансаПараметрыАвторизацииВСервисеМобильнойПодписи(
		ИмяПараметра = Неопределено,
		УстановленныеПараметры = Неопределено) Экспорт
	
	Если ИмяПараметра = "ПараметрыАвторизацииВСервисеМобильнойПодписи" Тогда
		ПараметрыРезультатаАвторизации = ПараметрыРезультатаАвторизацииВСервисеМобильнойПодписи();
		ПараметрыСеанса.ПараметрыАвторизацииВСервисеМобильнойПодписи = Новый ФиксированнаяСтруктура(
			ПараметрыРезультатаАвторизации);
		УстановленныеПараметры.Добавить("ПараметрыАвторизацииВСервисеМобильнойПодписи");
	КонецЕсли;
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.Справочники.ПодписантыСервисаМобильнойПодписи, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.СервисМобильнойПодписиСтатусы, Истина);
	Списки.Вставить(Метаданные.Справочники.НастройкиОтправкиВГосключ, Истина);
	
КонецПроцедуры

// См. РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	Зависимость = Настройки.Добавить();
	Зависимость.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ПолучениеРезультатовИзСервисаМобильнойПодписи;
	Зависимость.ДоступноВМоделиСервиса = Истина;
	Зависимость.ДоступноВАвтономномРабочемМесте = Истина;
	Зависимость.ОбращаетсяКВнешнимРесурсам = Истина;
	Зависимость.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИспользоватьСервисМобильнойПодписи;
	
	Зависимость = Настройки.Добавить();
	Зависимость.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ПолучениеРезультатовИзСервисаМобильнойПодписи;
	Зависимость.ДоступноВМоделиСервиса = Истина;
	Зависимость.ДоступноВАвтономномРабочемМесте = Истина;
	Зависимость.ОбращаетсяКВнешнимРесурсам = Истина;
	Зависимость.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИспользоватьГосключ;
	
КонецПроцедуры

// См. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков.
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт

	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ПолучениеРезультатовИзСервисаМобильнойПодписи.ИмяМетода);

КонецПроцедуры

// См. ЗащитаПерсональныхДанныхПереопределяемый.ПриЗаполненииСведенийОбУничтожаемыхПерсональныхДанных.
Процедура ПриЗаполненииСведенийОбУничтожаемыхПерсональныхДанных(ТаблицаСведений) Экспорт
	
	МетаданныеПодписанты = Метаданные.Справочники.ПодписантыСервисаМобильнойПодписи;
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект = МетаданныеПодписанты.ПолноеИмя();
	НовыеСведения.ПолеСубъект = МетаданныеПодписанты.Реквизиты.ФизическоеЛицо.Имя;
	
	Поля = Новый Массив; // Массив из Строка
	Поля.Добавить(МетаданныеПодписанты.Реквизиты.Фамилия.ПолноеИмя());
	Поля.Добавить(МетаданныеПодписанты.Реквизиты.Имя.ПолноеИмя());
	Поля.Добавить(МетаданныеПодписанты.Реквизиты.Отчество.ПолноеИмя());
	Поля.Добавить(МетаданныеПодписанты.СтандартныеРеквизиты.Наименование.Имя);
	
	НовыеСведения.Поля = Поля;
	НовыеСведения.КатегорияДанных = "ФИО";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект = МетаданныеПодписанты.ПолноеИмя();
	НовыеСведения.ПолеСубъект = МетаданныеПодписанты.Реквизиты.ФизическоеЛицо.Имя;
	НовыеСведения.Поля = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		МетаданныеПодписанты.Реквизиты.ИННФЛ.ПолноеИмя());
	НовыеСведения.КатегорияДанных = "ИНН";
	
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект = МетаданныеПодписанты.ПолноеИмя();
	НовыеСведения.ПолеСубъект = МетаданныеПодписанты.Реквизиты.ФизическоеЛицо.Имя;
	НовыеСведения.Поля = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		МетаданныеПодписанты.Реквизиты.СНИЛС.ПолноеИмя());
	НовыеСведения.КатегорияДанных = "СНИЛС";

	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект = МетаданныеПодписанты.ПолноеИмя();
	НовыеСведения.ПолеСубъект = МетаданныеПодписанты.Реквизиты.ФизическоеЛицо.Имя;
	НовыеСведения.Поля = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		МетаданныеПодписанты.Реквизиты.НомерТелефона.ПолноеИмя());
	НовыеСведения.КатегорияДанных = "КонтактнаяИнформация";

КонецПроцедуры

// См. ЗащитаПерсональныхДанныхПереопределяемый.ПриЗаполненииСведенийОбУничтожаемыхПерсональныхДанных.
Процедура ЗаполнитьОбластиПерсональныхДанных(КатегорииПерсональныхДанных) Экспорт
	
	Если КатегорииПерсональныхДанных.Найти("ЛичныеДанные", "Имя") = Неопределено Тогда
		НоваяОбласть = КатегорииПерсональныхДанных.Добавить();
		НоваяОбласть.Имя = "ЛичныеДанные";
		НоваяОбласть.Представление = НСтр("ru = 'Личные данные'");
	КонецЕсли;
	
	Если КатегорииПерсональныхДанных.Найти("КонтактнаяИнформация", "Имя") = Неопределено Тогда
		НоваяОбласть = КатегорииПерсональныхДанных.Добавить();
		НоваяОбласть.Имя = "КонтактнаяИнформация";
		НоваяОбласть.Представление = НСтр("ru = 'Адреса и телефоны'");
		НоваяОбласть.Родитель = "ЛичныеДанные";
	КонецЕсли;
		
	Если КатегорииПерсональныхДанных.Найти("ФИО", "Имя") = Неопределено Тогда
		НоваяОбласть = КатегорииПерсональныхДанных.Добавить();
		НоваяОбласть.Имя = "ФИО";
		НоваяОбласть.Представление = НСтр("ru = 'Фамилия, имя, отчество'");
		НоваяОбласть.Родитель = "ЛичныеДанные";
	КонецЕсли;
	
	Если КатегорииПерсональныхДанных.Найти("ИНН", "Имя") = Неопределено Тогда
		НоваяОбласть = КатегорииПерсональныхДанных.Добавить();
		НоваяОбласть.Имя = "ИНН";
		НоваяОбласть.Представление = НСтр("ru = 'Сведения о постановке на налоговый учет (ИНН)'");
		НоваяОбласть.Родитель = "ЛичныеДанные";
	КонецЕсли;
		
	Если КатегорииПерсональныхДанных.Найти("СНИЛС", "Имя") = Неопределено Тогда
		НоваяОбласть = КатегорииПерсональныхДанных.Добавить();
		НоваяОбласть.Имя = "СНИЛС";
		НоваяОбласть.Представление = НСтр("ru = 'Сведения о регистрации в Пенсионном фонде (номер страхового свидетельства)'");
		НоваяОбласть.Родитель = "ЛичныеДанные";
	КонецЕсли;
	
КонецПроцедуры

// См. ЗащитаПерсональныхДанныхПереопределяемый.ЗаполнитьСведенияОПерсональныхДанных.
Процедура ЗаполнитьСведенияОПерсональныхДанных(ТаблицаСведений) Экспорт
	
	Сведения = ТаблицаСведений.Добавить();
	Сведения.Объект				= "Справочник.ПодписантыСервисаМобильнойПодписи";
	Сведения.ПоляРегистрации	= "ФизическоеЛицо";
	Сведения.ПоляДоступа		= "Наименование,Фамилия,Имя,Отчество";
	Сведения.ОбластьДанных		= "ФИО";
	
	Сведения = ТаблицаСведений.Добавить();
	Сведения.Объект				= "Справочник.ПодписантыСервисаМобильнойПодписи";
	Сведения.ПоляРегистрации	= "ФизическоеЛицо";
	Сведения.ПоляДоступа		= "ИННФЛ";
	Сведения.ОбластьДанных		= "ИНН";
	
	Сведения = ТаблицаСведений.Добавить();
	Сведения.Объект				= "Справочник.ПодписантыСервисаМобильнойПодписи";
	Сведения.ПоляРегистрации	= "ФизическоеЛицо";
	Сведения.ПоляДоступа		= "СНИЛС";
	Сведения.ОбластьДанных		= "СНИЛС";
	
	Сведения = ТаблицаСведений.Добавить();
	Сведения.Объект				= "Справочник.ПодписантыСервисаМобильнойПодписи";
	Сведения.ПоляРегистрации	= "ФизическоеЛицо";
	Сведения.ПоляДоступа		= "НомерТелефона";
	Сведения.ОбластьДанных		= "КонтактнаяИнформация";
	
КонецПроцедуры

// См. РаботаВБезопасномРежимеПереопределяемый.ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам.
Процедура ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений) Экспорт

	НовыеРазрешения = Новый Массив;
	МодульРаботаВБезопасномРежиме = ОбщегоНазначения.ОбщийМодуль("РаботаВБезопасномРежиме");

	Если ПолучитьФункциональнуюОпцию("ИспользоватьСервисМобильнойПодписи") Тогда
		Сервис = СвойстваСервисаМобильнойПодписи();

		Элемент = МодульРаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(ВРег(Сервис.Схема), Сервис.Хост,
			Сервис.Порт, НСтр("ru = 'Отправка документов на подписание в Моя подпись (ФНС).'"));

		НовыеРазрешения.Добавить(Элемент);
	КонецЕсли;

	Если ПолучитьФункциональнуюОпцию("ИспользоватьГосключ") Тогда

		Параметры = Новый Структура("АдресЕСИА, АдресСервиса", "", "");

		Сервис = СвойстваСервисаАвторизацииГосключа(Параметры);
		Элемент = МодульРаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(ВРег(Сервис.Схема), Сервис.Хост,
			Сервис.Порт, НСтр("ru = 'Отправка документов на подписание в Госключ.'"));
		НовыеРазрешения.Добавить(Элемент);

		Сервис = СвойстваСервисаГосключа(Параметры);
		Элемент = МодульРаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(ВРег(Сервис.Схема), Сервис.Хост,
			Сервис.Порт, НСтр("ru = 'Отправка документов на подписание в Госключ.'"));
		НовыеРазрешения.Добавить(Элемент);

		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкиОтправкиВГосключ.АдресСервиса КАК АдресСервиса
		|ИЗ
		|	Справочник.НастройкиОтправкиВГосключ КАК НастройкиОтправкиВГосключ
		|ГДЕ
		|	НЕ НастройкиОтправкиВГосключ.ПометкаУдаления И НастройкиОтправкиВГосключ.АдресСервиса <> """"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкиОтправкиВГосключ.АдресЕСИА
		|ИЗ
		|	Справочник.НастройкиОтправкиВГосключ КАК НастройкиОтправкиВГосключ
		|ГДЕ
		|	НЕ НастройкиОтправкиВГосключ.ПометкаУдаления И НастройкиОтправкиВГосключ.АдресСервиса <> """"";

		Запрос = Новый Запрос(ТекстЗапроса);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Сервис = ОбщегоНазначенияКлиентСервер.СтруктураURI(Выборка.АдресСервиса);
			Элемент = МодульРаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(ВРег(Сервис.Схема),
				Сервис.Хост, Сервис.Порт, НСтр("ru = 'Отправка документов на подписание в Госключ (из настроек).'"));
			НовыеРазрешения.Добавить(Элемент);
		КонецЦикла;

	КонецЕсли;

	ЗапросыРазрешений.Добавить(МодульРаботаВБезопасномРежиме.ЗапросНаИспользованиеВнешнихРесурсов(НовыеРазрешения));

КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииСерверныхОповещений
Процедура ПриДобавленииСерверныхОповещений(Оповещения) Экспорт
	
	Оповещение = СерверныеОповещения.НовоеСерверноеОповещение(СервисМобильнойПодписиКлиентСервер.ИмяСерверногоОповещения());
	
	Оповещение.ИмяМодуляОтправки  = "";
	Оповещение.ИмяМодуляПолучения = "СервисМобильнойПодписиСлужебныйКлиент";
	
	Оповещения.Вставить(Оповещение.Имя, Оповещение);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НастройкаПодписанта(Форма)
	
	НастройкаПодписанта = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		Форма.Подписант, "ПодписаниеДанных_НастройкаПодписанта", Неопределено);
		
	Если НастройкаПодписанта = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НастройкаПодписанта = НастройкаПодписанта.Получить(КлючНастроекПодписанта(Форма.ПараметрыОтправителя));
	Возврат НастройкаПодписанта;
	
КонецФункции

Функция КлючНастроекПодписанта(ПараметрыОтправителя)
	
	Если ТипЗнч(ПараметрыОтправителя) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Организация");
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, ПараметрыОтправителя);
	
	КлючОтбора = Новый Структура;
	Если ЗначениеЗаполнено(СтруктураОтбора.Организация) Тогда
		КлючОтбора.Вставить("Организация", Строка(СтруктураОтбора.Организация.УникальныйИдентификатор()));
	КонецЕсли;
	
	Возврат ?(КлючОтбора.Количество() = 0, Неопределено,
		ЗаписатьЗначениеJSON(КлючОтбора));
	
КонецФункции

// Формирует таблицу значений с данными настроек отправки в Госключ.
//
// Возвращаемое значение:
//  ТаблицаЗначений
//
Функция ПолучитьНастройкиОтправки(СправочникСсылка = Неопределено)
	
	НастройкиОтправки = Новый ТаблицаЗначений;

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиОтправкиВГосключ.Ссылка КАК Ссылка,
	|	НастройкиОтправкиВГосключ.Наименование КАК Наименование,
	|	""Госключ"" КАК Сервер,
	|	НастройкиОтправкиВГосключ.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Справочник.НастройкиОтправкиВГосключ КАК НастройкиОтправкиВГосключ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	НастройкиОтправки = Запрос.Выполнить().Выгрузить();
	
	Возврат НастройкиОтправки;
	
КонецФункции

Процедура ПроверкаПараметровПодписанта(ПараметрыПодписанта, ДляДобавления = Ложь) Экспорт
	
	Если Не ЗначениеЗаполнено(ПараметрыПодписанта.ВидПодписанта) И ЗначениеЗаполнено(ПараметрыПодписанта.ИНН) Тогда
		ПараметрыПодписанта.ВидПодписанта = Перечисления.ВидыПодписантовМобильногоСервиса.РуководительЮЛ;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыПодписанта.ВидПодписанта) Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен вид подписанта'");
	КонецЕсли;
	
	ЭтоЮрЛицо = ПараметрыПодписанта.ВидПодписанта = Перечисления.ВидыПодписантовМобильногоСервиса.РуководительЮЛ;
	ЭтоФизЛицо = ПараметрыПодписанта.ВидПодписанта = Перечисления.ВидыПодписантовМобильногоСервиса.ФизическоеЛицо;
	
	Если Не ЭтоЮрЛицо Тогда
		ПараметрыПодписанта.ИНН = "";
		ПараметрыПодписанта.Должность = "";
		ПараметрыПодписанта.НаименованиеОрганизации = "";
	КонецЕсли;
	
	Если ЭтоФизЛицо Тогда
		ПараметрыПодписанта.ОГРН = "";
		ПараметрыПодписанта.МояПодпись = Ложь;
	Иначе
		ПараметрыПодписанта.ГосключУНЭП = Ложь;
	КонецЕсли;
	
	НомерТелефона = СервисМобильнойПодписиКлиентСервер.ТолькоЦифры(ПараметрыПодписанта.НомерТелефона);
	
	Если (Не ЗначениеЗаполнено(НомерТелефона) Или СтрДлина(НомерТелефона) = 1) И ПараметрыПодписанта.МояПодпись = Истина Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен номер телефона'");
	КонецЕсли;
	
	Если ЭтоЮрЛицо И Не ЗначениеЗаполнено(ПараметрыПодписанта.ИНН) Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен ИНН'");
	КонецЕсли;
	
	Если ПараметрыПодписанта.МояПодпись = Истина И Не ЭтоЮрЛицо И Не ЗначениеЗаполнено(ПараметрыПодписанта.ИННФЛ) Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен ИНН физического лица'");
	КонецЕсли;
	
	Если ЭтоЮрЛицо И Не (ЗначениеЗаполнено(ПараметрыПодписанта.ОГРН) 
		Или ЗначениеЗаполнено(ПараметрыПодписанта.НомерЗаписиОбАккредитации)) Тогда
		
		Если СтрНачинаетсяС(ПараметрыПодписанта.ИНН, "9909") Тогда
			ВызватьИсключение НСтр("ru = 'Не заполнен номер записи об аккредитации'");
		Иначе
			ВызватьИсключение НСтр("ru = 'Не заполнен ОГРН'");
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтрНачинаетсяС(ПараметрыПодписанта.ИНН, "9909") 
		И ЗначениеЗаполнено(ПараметрыПодписанта.НомерЗаписиОбАккредитации)
		И Не ЗначениеЗаполнено(ПараметрыПодписанта.ИННФЛ) Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен ИНН ФЛ'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НомерТелефона) Тогда
		Если СтрДлина(НомерТелефона) = 11 И СтрНачинаетсяС(НомерТелефона, "8") Тогда
			НомерТелефона = "7" + Прав(НомерТелефона, 10);
		КонецЕсли;
		
		Если СтрДлина(НомерТелефона) > 1 И (СтрДлина(НомерТелефона) <> 11 Или Не СтрНачинаетсяС(НомерТелефона, "7")) Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректный номер телефона подписанта %1'"), ПараметрыПодписанта.НомерТелефона);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыПодписанта.НомерТелефона = ?(СтрДлина(НомерТелефона) > 1, НомерТелефона, "");
	ПараметрыПодписанта.ИННФЛ = СервисМобильнойПодписиКлиентСервер.ТолькоЦифры(ПараметрыПодписанта.ИННФЛ);
	ПараметрыПодписанта.ИНН = СервисМобильнойПодписиКлиентСервер.ТолькоЦифры(ПараметрыПодписанта.ИНН);
	ПараметрыПодписанта.СНИЛС = СервисМобильнойПодписиКлиентСервер.ТолькоЦифры(ПараметрыПодписанта.СНИЛС);
	
	Если Не ЗначениеЗаполнено(ПараметрыПодписанта.СНИЛС) И (ПараметрыПодписанта.ГосключУНЭП = Истина 
		Или ПараметрыПодписанта.ГосключУКЭП = Истина) Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен СНИЛС для отправки в Госключ'");
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ПараметрыПодписанта.СНИЛС) И СтрДлина(ПараметрыПодписанта.СНИЛС) <> 11 Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'СНИЛС физического лица должен содержать 11 цифр: %1'"), ПараметрыПодписанта.ИННФЛ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ИНН) Тогда
		ПараметрыПодписанта.ИННФЛ = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ИННФЛ) И СтрДлина(ПараметрыПодписанта.ИННФЛ) <> 12 Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'ИНН физического лица должен быть длиной 12 символов: %1'"), ПараметрыПодписанта.ИННФЛ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ИНН) И СтрДлина(ПараметрыПодписанта.ИНН) <> 10 Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'ИНН юридического лица должен быть длиной 10 символов: %1'"), ПараметрыПодписанта.ИНН);
	КонецЕсли;

КонецПроцедуры

// Удалить ожидаемую подпись.
// 
// Параметры:
//  ИдентификаторДокумента - УникальныйИдентификатор
//  СообщениеЖурналаРегистрации - Строка - сообщение журнала регистрации
//  ПодписанныйОбъект - ЛюбаяСсылка
//  ПриПодписании - см. УдалитьОжидаемыеПодписи.ПриПодписании
//
Процедура УдалитьОжидаемуюПодпись(ИдентификаторДокумента, СообщениеЖурналаРегистрации, ПодписанныйОбъект = Неопределено, ПриПодписании = Неопределено)
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СервисМобильнойПодписиСтатусы");
	ЭлементБлокировки.УстановитьЗначение("ИдентификаторДокумента", ИдентификаторДокумента);

	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		НаборЗаписей = РегистрыСведений.СервисМобильнойПодписиСтатусы.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторДокумента.Установить(ИдентификаторДокумента);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Выбран() Тогда
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
			Если ПриПодписании <> Истина Тогда
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Сервис мобильной подписи.Удаление ожидаемой подписи'",
					ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Информация, ?(ЗначениеЗаполнено(
				ПодписанныйОбъект), ПодписанныйОбъект.Метаданные(), Неопределено), ПодписанныйОбъект,
					СообщениеЖурналаРегистрации, РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецЕсли;
		КонецЕсли;
		ЗафиксироватьТранзакцию();

	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Функция ОтправитьНаПодписание(Знач НаборДанных, Знач Подписант, Знач Параметры) Экспорт
	
	Если ТипЗнч(Параметры.НастройкаОтправки) <> Тип("Структура") Тогда
		НастройкаОтправки = НастройкаОтправки(Параметры);
		Если ТипЗнч(НастройкаОтправки) = Тип("Строка") Тогда
			РезультатПроверки = ОшибкаМобильногоСервиса(НСтр("ru = 'Не удалось отправить документы'"), НастройкаОтправки, "");
			РезультатПроверки.Вставить("НаборДанных", НаборДанных);
			Возврат РезультатПроверки;
		КонецЕсли;
		Параметры.Вставить("НастройкаОтправки", НастройкаОтправки);
	КонецЕсли;
	
	Если Параметры.НаборДанныхПроверен <> Истина Тогда
		ПроверкаНабораДанных = СервисМобильнойПодписиКлиентСервер.ПроверкаНабораДанных(НаборДанных, Параметры);
		Если ТипЗнч(ПроверкаНабораДанных) = Тип("Строка") Тогда
			РезультатПроверки = ОшибкаМобильногоСервиса(НСтр("ru = 'Не удалось отправить документы'"), ПроверкаНабораДанных, "");
			РезультатПроверки.Вставить("НаборДанных", НаборДанных);
			Возврат РезультатПроверки;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.ПакетнаяОтправкаНаПодписание Тогда
		Если Параметры.ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.МояПодпись Тогда
			РезультатОтправкиНаПодписание = ОтправитьНаПодписаниеВСервис(НаборДанных, Подписант, Параметры);
		Иначе
			РезультатОтправкиНаПодписание = ОтправитьНаПодписаниеВГосключ(НаборДанных, Подписант, Параметры);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РезультатОтправкиНаПодписание.Ошибка) Тогда
			РезультатОтправкиНаПодписание.Ошибка.Вставить("НаборДанных", НаборДанных);
			Возврат РезультатОтправкиНаПодписание.Ошибка;
		КонецЕсли;
	Иначе
		РеквизитыПодписанта = Неопределено;
		Для Каждого Элемент Из НаборДанных Цикл
			
			НовыйНаборДанных = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элемент);
			
			Если Параметры.ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.МояПодпись Тогда
			//@skip-check query-in-loop - запрос выполнится один раз, если Подписант передан ссылкой.
				РезультатОтправкиНаПодписание = ОтправитьНаПодписаниеВСервис(
					НовыйНаборДанных, Подписант, Параметры, РеквизитыПодписанта);
			Иначе
			//@skip-check query-in-loop - запрос выполнится один раз, если Подписант передан параметрами.
				РезультатОтправкиНаПодписание = ОтправитьНаПодписаниеВГосключ(
					НаборДанных, Подписант, Параметры, РеквизитыПодписанта);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(РезультатОтправкиНаПодписание.Ошибка) Тогда
				РезультатОтправкиНаПодписание.Ошибка.Вставить("НаборДанных", НаборДанных);
				Возврат РезультатОтправкиНаПодписание.Ошибка;
			Иначе
				Элемент.Вставить("ИдентификаторДокумента", НовыйНаборДанных[0].ИдентификаторДокумента);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат НаборДанных;
	
КонецФункции

Процедура ПолучениеРезультатовИзСервисаМобильнойПодписи() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
			Метаданные.РегламентныеЗадания.ПолучениеРезультатовИзСервисаМобильнойПодписи);
	Если Не СервисМобильнойПодписи.ИспользоватьСервисМобильнойПодписи()
		И Не СервисМобильнойПодписи.ИспользоватьГосключ() Тогда
		Возврат;
	КонецЕсли;
	
	ПолучитьРезультатыИзСервисаМобильнойПодписи();
	
КонецПроцедуры

Функция СогласиеПодписанта(Подписант, ОтправитьСогласие = Ложь, ТокенДоступа = "")
	
	ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	
	Если ТипЗнч(Подписант) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Подписант);
	Иначе
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Подписант, "НомерТелефона, ИННФЛ");
		ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Реквизиты);
	КонецЕсли;
	
	Результат = Новый Структура("Статус, Ошибка, Код");
	
	ЗаголовокОшибки = НСтр("ru='Не удалось получить информацию о согласии подписанта'");

	Если Не ЗначениеЗаполнено(ТокенДоступа) Тогда
		РезультатАвторизации = АвторизоватьсяВСервисеМобильнойПодписи(ЗаголовокОшибки);
		Если РезультатАвторизации.Ошибка <> Неопределено Тогда
			Результат.Ошибка = РезультатАвторизации.Ошибка;
			Возврат Результат;
		КонецЕсли;
		ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	КонецЕсли;
	
	СвойстваСервисаМобильнойПодписи = СвойстваСервисаМобильнойПодписи();
	РесурсНаСервере = СвойстваСервисаМобильнойПодписи.РесурсКорняAPI + "fns-ms/public-api/v1/signings/approvements/check";
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	ЗаголовкиHTTP.Вставить("Authorization", "Bearer " + ТокенДоступа);
	ИдентификаторЗапроса = ИдентификаторЗапроса();
	ЗаголовкиHTTP.Вставить("X-Request-Id", ИдентификаторЗапроса);
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	СтруктураЗапроса = Новый Структура;
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ИНН) Тогда
		СтруктураЗапроса.Вставить("inn", ПараметрыПодписанта.ИНН);
		СтруктураЗапроса.Вставить("legalEntity", Новый Структура);
		СтруктураЗапроса.legalEntity.Вставить("inn", ПараметрыПодписанта.ИНН);
		СтруктураЗапроса.legalEntity.Вставить("ogrn", ПараметрыПодписанта.ОГРН);
	Иначе
		СтруктураЗапроса.Вставить("inn", ПараметрыПодписанта.ИННФЛ);
	КонецЕсли;
	СтруктураЗапроса.Вставить("phone", ПараметрыПодписанта.НомерТелефона);
	
	ТелоЗапроса = ОбщегоНазначения.ЗначениеВJSON(СтруктураЗапроса);
	ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса);
	
	ОтветHTTP = Неопределено;
	ВидОперации = НСтр("ru='Сервис мобильной подписи.Согласие на подписание'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Попытка
		Соединение = СоединениеССерверомИнтернета(ВидОперации, СвойстваСервисаМобильнойПодписи.АдресСервера);
		СоединениеHTTP = Соединение.HTTPСоединение;
		ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("POST", ЗапросHTTP);
	
	Исключение
		ПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецПопытки;

	Если ОтветHTTP.КодСостояния = 200 Тогда
		
		Попытка
			СтруктураОтвета = СтруктураОтвета(ОтветHTTP);
			Результат.Статус = СтруктураОтвета.Status;
		Исключение
			ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			Возврат Результат;
		КонецПопытки;
		
	ИначеЕсли ОтветHTTP.КодСостояния = 204 Тогда
		Результат.Статус = СтатусСогласиеНеОтправлено();
	Иначе
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки(ОтветHTTP), ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецЕсли;

	Если Результат.Статус = СтатусСогласиеНеОтправлено() И ОтправитьСогласие Тогда
		РезультатОтправки = ОтправитьСогласие(Подписант, ТокенДоступа);
		Если РезультатОтправки.ИдентификаторЗапроса <> Неопределено Тогда
			Результат.Статус = СтатусОжидаетПодписания();
			Результат.Код = РезультатОтправки.Код;
		Иначе
			Результат.Ошибка = РезультатОтправки.Ошибка;
		КонецЕсли;
	ИначеЕсли Результат.Статус = СтатусОтклонено() Тогда
		ЗаголовокОшибки = НСтр("ru='Отсутствует согласие на получение документов'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Подписант отклонил согласие на получение документов из ""1С:Мобильная подпись"".
			|Статус согласия: %1.'"), СтруктураОтвета.message);
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ТекстОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
	ИначеЕсли Результат.Статус = СтатусОшибка() Тогда
		Если СтруктураОтвета.ОшибкаИзСервиса = ОшибкаКлиентНеНайден() И ОтправитьСогласие Тогда
			РезультатОтправки = ОтправитьСогласие(Подписант, ТокенДоступа);
			Если РезультатОтправки.ИдентификаторЗапроса <> Неопределено Тогда
				Результат.Статус = ОшибкаКлиентНеНайден();
				Результат.Код = РезультатОтправки.Код;
			Иначе
				Результат.Ошибка = РезультатОтправки.Ошибка;
			КонецЕсли;
		Иначе
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, СтруктураОтвета.ОшибкаИзСервиса, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОтправитьСогласие(Подписант, ТокенДоступа = "")
	
	Результат = Новый Структура("ИдентификаторЗапроса, Ошибка, Код");
	
	ЗаголовокОшибки = НСтр("ru='Не удалось отправить на подпись согласие на получение документов на подписание в 1С: Мобильная подпись'");

	Если Не ЗначениеЗаполнено(ТокенДоступа) Тогда
		РезультатАвторизации = АвторизоватьсяВСервисеМобильнойПодписи(ЗаголовокОшибки);
		Если РезультатАвторизации.Ошибка <> Неопределено Тогда
			Результат.Ошибка = РезультатАвторизации.Ошибка;
			Возврат Результат;
		КонецЕсли;
		ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	КонецЕсли;
	
	СвойстваСервисаМобильнойПодписи = СвойстваСервисаМобильнойПодписи();
	РесурсНаСервере = СвойстваСервисаМобильнойПодписи.РесурсКорняAPI + "fns-ms/public-api/v1/signings/approvements/jobs";
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	ЗаголовкиHTTP.Вставить("Authorization", "Bearer " + ТокенДоступа);
	ИдентификаторЗапроса = ИдентификаторЗапроса();
	ЗаголовкиHTTP.Вставить("X-Request-Id", ИдентификаторЗапроса);
	
	ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	Если ТипЗнч(Подписант) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Подписант);
	Иначе
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Подписант, "ИНН, НомерТелефона, ИННФЛ, ОГРН");
		ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Реквизиты);
	КонецЕсли;
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	СтруктураЗапроса = Новый Структура;
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ИНН) Тогда
		СтруктураЗапроса.Вставить("inn", ПараметрыПодписанта.ИНН);
		СтруктураЗапроса.Вставить("legalEntity", Новый Структура);
		СтруктураЗапроса.legalEntity.Вставить("inn", ПараметрыПодписанта.ИНН);
		СтруктураЗапроса.legalEntity.Вставить("ogrn", ПараметрыПодписанта.ОГРН);
	Иначе
		СтруктураЗапроса.Вставить("inn", ПараметрыПодписанта.ИННФЛ);
	КонецЕсли;
	СтруктураЗапроса.Вставить("phone", ПараметрыПодписанта.НомерТелефона);
	
	Генератор = Новый ГенераторСлучайныхЧисел(ТекущаяДатаСеанса() - Дата(1, 1, 1));
	Код = Формат(Генератор.СлучайноеЧисло(1000, 9999), "ЧГ=0");
	
	СтруктураЗапроса.Вставить("syncCode", Код);
	
	ТелоЗапроса = ОбщегоНазначения.ЗначениеВJSON(СтруктураЗапроса);
	ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса);
	
	ОтветHTTP = Неопределено;
	ВидОперации = НСтр("ru='Сервис мобильной подписи.Согласие на подписание'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Попытка
		Соединение = СоединениеССерверомИнтернета(ВидОперации, СвойстваСервисаМобильнойПодписи.АдресСервера);
		СоединениеHTTP = Соединение.HTTPСоединение;
		ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("POST", ЗапросHTTP);
	
	Исключение
		ПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецПопытки;
	
	Если ОтветHTTP.КодСостояния = 202 Тогда
		Попытка
			СтруктураОтвета = СтруктураОтвета(ОтветHTTP);
			Результат.ИдентификаторЗапроса = СтруктураОтвета.jobID;
			Результат.Код = Код;
			ЗаписьЖурналаРегистрации(ВидОперации, УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Отправлено на подпись согласие %1 для %2. Код %3'"), Результат.ИдентификаторЗапроса, ТелоЗапроса, Код));
		Исключение
			ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(ВидОперации, УровеньЖурналаРегистрации.Ошибка,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Не удалось отправить на подпись согласие %1 для %2. %3'"), 
					Результат.ИдентификаторЗапроса, ТелоЗапроса, ПредставлениеОшибки));
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			Возврат Результат;
		КонецПопытки;
	Иначе
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки(ОтветHTTP), ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.ИдентификаторЗапроса) Тогда
		Если ТипЗнч(Подписант) = Тип("Структура") Тогда
			Подписант = ДобавитьПодписантаВСправочник(ПараметрыПодписанта).Ссылка;
		КонецЕсли;
		ЗаписатьСтатусОтправленВСервис(ПараметрыПодписанта);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Процедура ЗаписатьСтатусОтправленВСервис(ПараметрыПодписанта)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПодписантыСервисаМобильнойПодписи.Ссылка
	|ИЗ
	|	Справочник.ПодписантыСервисаМобильнойПодписи КАК ПодписантыСервисаМобильнойПодписи
	|ГДЕ 
	|	НЕ ПодписантыСервисаМобильнойПодписи.ПометкаУдаления
	|	И НЕ ПодписантыСервисаМобильнойПодписи.ОтправленВСервис
	|	И ПодписантыСервисаМобильнойПодписи.ИННФЛ = &ИННФЛ
	|	И ПодписантыСервисаМобильнойПодписи.ИНН = &ИНН
	|	И ПодписантыСервисаМобильнойПодписи.НомерТелефона = &НомерТелефона";
	
	Запрос.УстановитьПараметр("НомерТелефона", ПараметрыПодписанта.НомерТелефона);
	Запрос.УстановитьПараметр("ИННФЛ", ПараметрыПодписанта.ИННФЛ);
	Запрос.УстановитьПараметр("ИНН", ПараметрыПодписанта.ИНН);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.ПодписантыСервисаМобильнойПодписи");
	
	НачатьТранзакцию();
	Попытка
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Таблица = РезультатЗапроса.Выгрузить();
			ЭлементБлокировки.ИсточникДанных = Таблица;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
			Блокировка.Заблокировать();
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
				СправочникОбъект.ОтправленВСервис = Истина;
				СправочникОбъект.ОбменДанными.Загрузка = Истина;
				СправочникОбъект.Записать();
			КонецЦикла;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Функция ДобавитьПодписантаВСправочник(Знач ПараметрыПодписанта) Экспорт
	
	Результат = Новый Структура("ЭтоНовый, ОбновленыДанные, Ссылка", Ложь, Ложь);
	
	ПроверкаПараметровПодписанта(ПараметрыПодписанта);
	ПравоДоступаДобавление = ПравоДоступа("Добавление", Метаданные.Справочники.ПодписантыСервисаМобильнойПодписи);
	ЗаписатьОбъект = Ложь;
	УстановитьПривилегированныйРежим(Истина);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.ПодписантыСервисаМобильнойПодписи");
	
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		
		НайденныйПодписант = НайденныйПодписант(ПараметрыПодписанта, Истина, Истина);
		
		Если Не ЗначениеЗаполнено(НайденныйПодписант) Тогда
			
			Если Не ПравоДоступаДобавление Тогда
				ВызватьИсключение(НСтр("ru = 'Недостаточно прав для добавления подписанта.'"), КатегорияОшибки.НарушениеПравДоступа);
			КонецЕсли;
			
			Результат.ЭтоНовый = Истина;
			
			СправочникОбъект = Справочники.ПодписантыСервисаМобильнойПодписи.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(СправочникОбъект, ПараметрыПодписанта);
			СправочникОбъект.Добавил = ТекущийПользователь;
			Если Не ЗначениеЗаполнено(СправочникОбъект.ИНН) Тогда
				СправочникОбъект.Должность = "";
			КонецЕсли;
			
		Иначе
			
			СправочникОбъект = НайденныйПодписант.ПолучитьОбъект(); // СправочникОбъект.ПодписантыСервисаМобильнойПодписи
			Если СправочникОбъект.ПометкаУдаления Тогда
				Если Не ПравоДоступаДобавление Тогда
					ВызватьИсключение(НСтр("ru = 'Недостаточно прав для добавления подписанта.'"), КатегорияОшибки.НарушениеПравДоступа);
				КонецЕсли;
				Результат.ОбновленыДанные = Истина;
				ЗаполнитьЗначенияСвойств(СправочникОбъект, ПараметрыПодписанта);
				СправочникОбъект.Добавил = ТекущийПользователь;
				СправочникОбъект.ПометкаУдаления = Ложь;
				ЗаписатьОбъект = Истина;
			Иначе
				
				ОбъектМетаданных = СправочникОбъект.Метаданные();
				Для Каждого КлючИЗначение Из ПараметрыПодписанта Цикл
					Если КлючИЗначение.Ключ = "ОтправленВСервис" Тогда
						Продолжить;
					КонецЕсли;
					
					Если (КлючИЗначение.Ключ = "МояПодпись"
						Или КлючИЗначение.Ключ = "ГосключУНЭП"
						Или КлючИЗначение.Ключ = "ГосключУКЭП") 
						И КлючИЗначение.Значение = Истина И СправочникОбъект[КлючИЗначение.Ключ] = Ложь Тогда
						СправочникОбъект[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
						Результат.ОбновленыДанные = Истина;
						ЗаписатьОбъект = Истина;
						Продолжить;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(КлючИЗначение.Значение)
						И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СправочникОбъект, КлючИЗначение.Ключ)
						И Не ЗначениеЗаполнено(СправочникОбъект[КлючИЗначение.Ключ]) Тогда
						СправочникОбъект[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
						Результат.ОбновленыДанные = Истина;
						ЗаписатьОбъект = Истина;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если СправочникОбъект.Пользователи.Найти(ТекущийПользователь, "Пользователь") = Неопределено Тогда
			НоваяСтрока = СправочникОбъект.Пользователи.Добавить();
			НоваяСтрока.Пользователь = ТекущийПользователь;
			ЗаписатьОбъект = Истина;
		КонецЕсли;
		
		Если ЗаписатьОбъект Тогда
			СправочникОбъект.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Результат.Ссылка = СправочникОбъект.Ссылка;
	
	Возврат Результат;
	
КонецФункции

Функция НайденныйПодписант(ПараметрыПодписанта, Привилегированный = Ложь, ПомеченныеНаУдаление = Ложь)
	
	Если ЗначениеЗаполнено(ПараметрыПодписанта.Ссылка) Тогда
		Возврат ПараметрыПодписанта.Ссылка;
	КонецЕсли;
	
	ПроверкаПараметровПодписанта(ПараметрыПодписанта);
	
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПодписантыСервисаМобильнойПодписи.Ссылка
	|ИЗ
	|	Справочник.ПодписантыСервисаМобильнойПодписи КАК ПодписантыСервисаМобильнойПодписи
	|ГДЕ
	 |	&УсловияЗапроса";
	
	УсловияЗапроса = Новый Массив;
	
	Если Не ПомеченныеНаУдаление Тогда
		УсловияЗапроса.Добавить("Не ПодписантыСервисаМобильнойПодписи.ПометкаУдаления");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыПодписанта.НомерТелефона) Тогда
		Запрос.УстановитьПараметр("НомерТелефона", ПараметрыПодписанта.НомерТелефона);
		УсловияЗапроса.Добавить("(ПодписантыСервисаМобильнойПодписи.НомерТелефона = &НомерТелефона ИЛИ ПодписантыСервисаМобильнойПодписи.НомерТелефона = """")");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ВидПодписанта) Тогда
		Запрос.УстановитьПараметр("ВидПодписанта", ПараметрыПодписанта.ВидПодписанта);
		УсловияЗапроса.Добавить("ПодписантыСервисаМобильнойПодписи.ВидПодписанта = &ВидПодписанта");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ИНН", "");
	УсловияЗапроса.Добавить("ПодписантыСервисаМобильнойПодписи.ИНН = &ИНН");
	
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ИНН) Тогда
		Запрос.УстановитьПараметр("ИНН", ПараметрыПодписанта.ИНН);
		Если ЗначениеЗаполнено(ПараметрыПодписанта.ОГРН) Тогда
			Запрос.УстановитьПараметр("ОГРН", ПараметрыПодписанта.ОГРН);
			УсловияЗапроса.Добавить("ПодписантыСервисаМобильнойПодписи.ОГРН = &ОГРН");
		КонецЕсли;
		Если ЗначениеЗаполнено(ПараметрыПодписанта.СНИЛС) Тогда
			УсловияЗапроса.Добавить("ПодписантыСервисаМобильнойПодписи.СНИЛС = &СНИЛС");
			Запрос.УстановитьПараметр("СНИЛС", ПараметрыПодписанта.СНИЛС);
		ИначеЕсли ЗначениеЗаполнено(ПараметрыПодписанта.ИННФЛ) Тогда
			Запрос.УстановитьПараметр("ИННФЛ", ПараметрыПодписанта.ИННФЛ);
			УсловияЗапроса.Добавить("ПодписантыСервисаМобильнойПодписи.ИННФЛ = &ИННФЛ");
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(ПараметрыПодписанта.ИННФЛ) Тогда
		Запрос.УстановитьПараметр("ИННФЛ", ПараметрыПодписанта.ИННФЛ);
		УсловияЗапроса.Добавить("ПодписантыСервисаМобильнойПодписи.ИННФЛ = &ИННФЛ");
	ИначеЕсли ЗначениеЗаполнено(ПараметрыПодписанта.СНИЛС) Тогда
		УсловияЗапроса.Добавить("ПодписантыСервисаМобильнойПодписи.СНИЛС = &СНИЛС");
		Запрос.УстановитьПараметр("СНИЛС", ПараметрыПодписанта.СНИЛС);
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловияЗапроса", СтрСоединить(УсловияЗапроса, Символы.ПС + " " + "И "));
	
	Если Привилегированный Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ"); // @Query-part-1, @Query-part-2
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		РезультатЗапроса = Запрос.Выполнить();
	КонецЕсли;

	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.ПодписантыСервисаМобильнойПодписи.ПустаяСсылка();
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

// Возвращает описание HTTP соединения.
// 
// Параметры:
//   URL - Строка
//   Таймаут - Число
//   ПроверятьДоставкуПакетовПриОшибке - см. ПолучениеФайловИзИнтернета.ДиагностикаСоединения.ПроверятьДоставкуПакетов.
// Возвращаемое значение:
//   Структура:
//    * HTTPСоединение - HTTPСоединение
//    * СтруктураURI - см. ОбщегоНазначенияКлиентСервер.СтруктураURI
//
Функция СоединениеССерверомИнтернета(ВидОперации, URL, Таймаут = Неопределено, 
	ВыполнятьДиагностику = Истина, ПроверятьДоставкуПакетовПриОшибке = Ложь)
	Возврат ЭлектроннаяПодписьСлужебныйРФ.СоединениеССерверомИнтернета(ВидОперации, URL, Таймаут, ВыполнятьДиагностику, ПроверятьДоставкуПакетовПриОшибке);
КонецФункции

Функция ОтправитьНаПодписаниеВСервис(НаборДанных, Подписант, ПараметрыПодписания, РеквизитыПодписанта = Неопределено)
	
	ЗаголовокОшибки = НСтр("ru='Не удалось отправить документы на подписание'");
	Результат = Новый Структура("Ошибка, НаборДанных");
	Результат.НаборДанных = НаборДанных;
	
	Если Не ПараметрыПодписания.Свойство("ТокенДоступа") Тогда
		РезультатАвторизации = АвторизоватьсяВСервисеМобильнойПодписи(ЗаголовокОшибки);
		Если РезультатАвторизации.Ошибка <> Неопределено Тогда
			Результат.Ошибка = РезультатАвторизации.Ошибка;
			Возврат Результат;
		КонецЕсли;
		ПараметрыПодписания.Вставить("ТокенДоступа", РезультатАвторизации.ТокенДоступа);
	КонецЕсли;
	
	ТокенДоступа = ПараметрыПодписания.ТокенДоступа;
	
	СвойстваСервисаМобильнойПодписи = СвойстваСервисаМобильнойПодписи();
	РесурсНаСервере = СвойстваСервисаМобильнойПодписи.РесурсКорняAPI + "fns-ms/public-api/v1/signings/simplifieds/jobs";
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	ЗаголовкиHTTP.Вставить("Authorization", "Bearer " + ТокенДоступа);
	ИдентификаторЗапроса = ИдентификаторЗапроса();
	ЗаголовкиHTTP.Вставить("X-Request-Id", ИдентификаторЗапроса);
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	
	Если РеквизитыПодписанта = Неопределено Тогда
		
		ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
		
		Если ТипЗнч(Подписант) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Подписант);
			ОтправленВСервис = Неопределено;
		Иначе
			РеквизитыПодписантаИзСправочника = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Подписант,
				"Ссылка, ИНН, НомерТелефона, ИННФЛ, ОГРН, ОтправленВСервис, ВидПодписанта");
			ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, РеквизитыПодписантаИзСправочника);
			ОтправленВСервис = РеквизитыПодписантаИзСправочника.ОтправленВСервис;
		КонецЕсли;
		
		ПараметрыПодписанта.МояПодпись = Истина;
		ПараметрыПодписанта.ГосключУКЭП = Ложь;
		ПараметрыПодписанта.ГосключУНЭП = Ложь;
		
		Попытка
			ПроверкаПараметровПодписанта(ПараметрыПодписанта);
			РеквизитыПодписанта = ПараметрыПодписанта;
			РеквизитыПодписанта.Вставить("ОтправленВСервис", ОтправленВСервис);
		Исключение
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ОбработкаОшибок.КраткоеПредставлениеОшибки(
				ИнформацияОбОшибке()), "");
			Возврат Результат;
		КонецПопытки;
		
	Иначе
		ПараметрыПодписанта = РеквизитыПодписанта;
	КонецЕсли;
	
	СтруктураЗапроса = Новый Структура;
	СтруктураЗапроса.Вставить("phone", ПараметрыПодписанта.НомерТелефона);
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ИНН) Тогда
		СтруктураЗапроса.Вставить("inn", ПараметрыПодписанта.ИНН);
		СтруктураЗапроса.Вставить("legalEntity", Новый Структура);
		СтруктураЗапроса.legalEntity.Вставить("inn", ПараметрыПодписанта.ИНН);
		СтруктураЗапроса.legalEntity.Вставить("ogrn", ПараметрыПодписанта.ОГРН);
	Иначе
		СтруктураЗапроса.Вставить("inn", ПараметрыПодписанта.ИННФЛ);
	КонецЕсли;
	СтруктураЗапроса.Вставить("documents", Новый Массив);
	Если ПараметрыПодписания.ТипПодписи = Перечисления.ТипыПодписиКриптографии.СМеткойДоверенногоВремениCAdEST Тогда
		СтруктураЗапроса.Вставить("signatureType", "T");
	ИначеЕсли ПараметрыПодписания.ТипПодписи = Перечисления.ТипыПодписиКриптографии.CAdESXLongType1 Тогда
		СтруктураЗапроса.Вставить("signatureType", "XLT1");
	ИначеЕсли ЗначениеЗаполнено(ПараметрыПодписания.ТипПодписи)
		И ПараметрыПодписания.ТипПодписи <> Перечисления.ТипыПодписиКриптографии.БазоваяCAdESBES
		И ПараметрыПодписания.ТипПодписи <> Перечисления.ТипыПодписиКриптографии.ОбычнаяCMS
		И ПараметрыПодписания.ТипПодписи <> Перечисления.ТипыПодписиКриптографии.Неопределен Тогда
		
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='В сервисе мобильной подписи не поддерживается тип подписи: %1'"),
				ПараметрыПодписания.ТипПодписи), "");
		Возврат Результат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыПодписания.Атрибуты) Тогда
		Атрибуты = Новый Массив;
		Для Каждого Элемент Из ПараметрыПодписания.Атрибуты Цикл
			Атрибуты.Добавить(Новый Структура("key, value", Элемент.Ключ, Элемент.Значение));
		КонецЦикла;
		СтруктураЗапроса.Вставить("data", Новый Структура("attributes", Атрибуты));
	КонецЕсли;

	ОшибкаНабораДанных = Неопределено;
	
	РазмерЗапроса = 0;
	
	Для Каждого Элемент Из НаборДанных Цикл
		Если Элемент.Свойство("ИдентификаторДокумента") И ЗначениеЗаполнено(Элемент.ИдентификаторДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭтоАдресВременногоХранилища(Элемент.Данные) Тогда
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(Элемент.Данные);
			Если ТипЗнч(ДвоичныеДанные) <> Тип("ДвоичныеДанные") Тогда
				Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не поддерживается тип данных для отправки в сервис: %1 - %2'"),
				ТипЗнч(ДвоичныеДанные), Элемент.Представление), "");
				Прервать;
			КонецЕсли;

		ИначеЕсли ТипЗнч(Элемент.Данные) = Тип("ДвоичныеДанные") Тогда
			ДвоичныеДанные = Элемент.Данные;
		Иначе
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Не поддерживается тип данных для отправки в сервис: %1 - %2'"),
					ТипЗнч(Элемент.Данные), Элемент.Представление), "");
			Прервать;
		КонецЕсли;
		
		Размер = ДвоичныеДанные.Размер();
	
		Если Размер = 0 Тогда
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Пустые данные для подписи: %1'"),
				Элемент.Представление), "");
			Прервать;
		КонецЕсли;
		
		ПреобразоватьИмяФайла(Элемент.Представление);
		
		РазмерЗапроса = РазмерЗапроса + Размер;
		
		ЭлементДанных = Новый Структура("title, path", Элемент.Представление, Base64Строка(ДвоичныеДанные));
		СтруктураЗапроса.documents.Добавить(ЭлементДанных);
	КонецЦикла;
	
	Если Ошибка <> Неопределено Тогда
		Результат.Ошибка = Ошибка;
		Возврат Результат;
	КонецЕсли;
	
	Если СтруктураЗапроса.documents.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если РазмерЗапроса > 1024 * 1024 * 70 Тогда
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Размер отправляемых файлов в одном пакете не должен превышать 70 Мб: %1 байт'"),
				РазмерЗапроса),
				"");
		Возврат Результат;
	КонецЕсли;
	
	ТелоЗапроса = ОбщегоНазначения.ЗначениеВJSON(СтруктураЗапроса);
	ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса);
	
	ОтветHTTP = Неопределено;
	ВидОперации = НСтр("ru='Сервис мобильной подписи.Отправка на подписание'", ОбщегоНазначения.КодОсновногоЯзыка());

	Попытка
		Соединение = СоединениеССерверомИнтернета(ВидОперации, СвойстваСервисаМобильнойПодписи.АдресСервера);
		СоединениеHTTP = Соединение.HTTPСоединение;
		ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("POST", ЗапросHTTP);
	
	Исключение
		ПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецПопытки;
	
	Если ОтветHTTP.КодСостояния = 200 Или ОтветHTTP.КодСостояния = 202 Тогда
		Попытка
			СтруктураОтвета = СтруктураОтвета(ОтветHTTP);
			НомерЗадачи = СтруктураОтвета.jobID;
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ЗаписьЖурналаРегистрации(ВидОперации, УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ОбработкаОшибок.КраткоеПредставлениеОшибки(
				ИнформацияОбОшибке), ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			Возврат Результат;
		КонецПопытки;
		
		ДатаОтправки = ТекущаяДатаСеанса();
		ТекущийПользователь = Пользователи.ТекущийПользователь();
		ОтправленныеДокументы = Новый Массив;
		
		Если ПараметрыПодписанта.Ссылка = Неопределено Тогда
			ПараметрыПодписанта.Ссылка = ДобавитьПодписантаВСправочник(ПараметрыПодписанта).Ссылка;
			РеквизитыПодписанта.ОтправленВСервис = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				ПараметрыПодписанта.Ссылка, "ОтправленВСервис");
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		Для Каждого Элемент Из НаборДанных Цикл
			ПредставлениеОшибки = "";
			
			НачатьТранзакцию();
			Попытка
				ИдентификаторДокумента = Новый УникальныйИдентификатор;
				
				НаборЗаписей = РегистрыСведений.СервисМобильнойПодписиСтатусы.СоздатьНаборЗаписей();
				НаборЗаписей.Заполнить(Неопределено);
				НаборЗаписей.Отбор.ИдентификаторДокумента.Установить(ИдентификаторДокумента);
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.ИдентификаторДокумента = ИдентификаторДокумента;
				НоваяЗапись.ИдентификаторВСервисе = НомерЗадачи;
				НоваяЗапись.НаименованиеДокумента = Элемент.Представление;
				Если Элемент.Свойство("Объект") Тогда
					НоваяЗапись.ПодписанныйОбъект = Элемент.Объект;
				КонецЕсли;
				НоваяЗапись.Отправитель = ТекущийПользователь;
				НоваяЗапись.ДатаОтправки = ДатаОтправки;
				НоваяЗапись.Подписант = ПараметрыПодписанта.Ссылка;
				НоваяЗапись.Доверенность = ПараметрыПодписания.Доверенность;
				НоваяЗапись.Комментарий = ПараметрыПодписания.Комментарий;
				НоваяЗапись.Статус = Перечисления.СервисМобильнойПодписиСтатусы.ОжидаетОбработки;
				НоваяЗапись.Операция = Перечисления.СервисМобильнойПодписиОперации.Подписание;
				НаборЗаписей.Записать();
				
				Если Элемент.Свойство("Объект") И ЭлектроннаяПодписьСлужебный.ДоступнаЭлектроннаяПодпись(ТипЗнч(Элемент.Объект)) Тогда
					
					ВерсияОбъекта = Неопределено;
					Элемент.Свойство("ВерсияОбъекта", ВерсияОбъекта);
					ЗаблокироватьДанныеДляРедактирования(Элемент.Объект, ВерсияОбъекта, ПараметрыПодписания.ИдентификаторФормы);
					ОбъектДанных = Элемент.Объект.ПолучитьОбъект();
					
					Блокировка = Новый БлокировкаДанных;
					ЭлементБлокировки = Блокировка.Добавить(ОбъектДанных.Метаданные().ПолноеИмя());
					ЭлементБлокировки.УстановитьЗначение("Ссылка", ОбъектДанных.Ссылка);
					
					Блокировка.Заблокировать();
					
					Если Не ОбъектДанных.ПодписанЭП Тогда
						ОбъектДанных.ПодписанЭП = Истина;
						// Чтобы определить, что это запись с целью добавления/удаления подписи.
						ОбъектДанных.ДополнительныеСвойства.Вставить("ЗаписьПодписанногоОбъекта", Истина);
						Если ОбъектДанных.Модифицированность() Тогда
							ОбъектДанных.Записать();
						КонецЕсли;
					КонецЕсли;
					
					РазблокироватьДанныеДляРедактирования(Элемент.Объект, ПараметрыПодписания.ИдентификаторФормы);
				КонецЕсли;
				
				Элемент.Вставить("ИдентификаторДокумента", ИдентификаторДокумента);
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				ПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
				ЗаписьЖурналаРегистрации(НСтр("ru='Сервис мобильной подписи.Запись результата отправки на подписание'", ОбщегоНазначения.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Ошибка,,, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			КонецПопытки;
			
			Если ЗначениеЗаполнено(ПредставлениеОшибки) Тогда
				Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса);
				Возврат Результат;
			КонецЕсли;

		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		Если РеквизитыПодписанта.ОтправленВСервис <> Истина Тогда
			ЗаписатьСтатусОтправленВСервис(ПараметрыПодписанта);
			РеквизитыПодписанта.ОтправленВСервис = Истина;
		КонецЕсли;
		
		Результат.НаборДанных = НаборДанных;
		
	ИначеЕсли ОтветHTTP.КодСостояния = 403 Тогда
		
		СогласиеПодписанта = СогласиеПодписанта(ПараметрыПодписанта, Истина);
		
		Если Не ЗначениеЗаполнено(СогласиеПодписанта.Ошибка) Тогда
			
			Если СогласиеПодписанта.Статус = СтатусОжидаетПодписания() Тогда
				
				Если Не ЗначениеЗаполнено(СогласиеПодписанта.Код) Тогда
					ТекстОшибки = НСтр(
						"ru='Не подписано согласие подписанта. Подписант должен подписать согласие на получение документов из ""1С:Мобильная подпись"" в приложении ""Моя подпись"" от ФНС.'");
				Иначе
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
						"ru='Не подписано согласие подписанта. Сообщите подписанту секретный код %1 для подписания согласия на получение документов из ""1С:Мобильная подпись"" в приложении ""Моя подпись"" от ФНС.'"),
						СогласиеПодписанта.Код);
				КонецЕсли;
				Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ТекстОшибки, ИдентификаторЗапроса);
				
			ИначеЕсли СогласиеПодписанта.Статус = ОшибкаКлиентНеНайден() Тогда
				
				Телефон = Лев(ПараметрыПодписанта.НомерТелефона, 3) + "****" + Прав(ПараметрыПодписанта.НомерТелефона, 4);
		
				Если ЗначениеЗаполнено(ПараметрыПодписанта.ИНН) Тогда
					
						ДанныеПодписанта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='ИНН ЮЛ: %1, ОГРН: %2, Телефон: +%3'"), ПараметрыПодписанта.ИНН,
							ПараметрыПодписанта.ОГРН, Телефон);
		
				Иначе
					ДанныеПодписанта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='ИНН ФЛ: %1, Телефон: +%2'"), ПараметрыПодписанта.ИННФЛ,
						Телефон);
				КонецЕсли;
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
						"ru='Для подписанта с данными %1 сервис ФНС ответил ""%2"". 
						|Проверьте данные, убедитесь, что у подписанта установлено приложение ""Моя подпись"" от ФНС и выпущен сертификат.
						|Сообщите подписанту секретный код %3 для подписания согласия на получение документов из ""1С:Мобильная подпись"" в приложении ""Моя подпись"" от ФНС.'"),
						ДанныеПодписанта, ОшибкаКлиентНеНайден(), СогласиеПодписанта.Код);
						
				Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ТекстОшибки, ИдентификаторЗапроса);

			ИначеЕсли СогласиеПодписанта.Статус = СтатусОтклонено() Тогда
				ТекстОшибки = НСтр("ru='Подписант отклонил согласие на получение документов из ""1С:Мобильная подпись"".'");
				Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ТекстОшибки, ИдентификаторЗапроса);
			ИначеЕсли СогласиеПодписанта.Статус = СтатусОшибка() Тогда
				Результат.Ошибка = СогласиеПодписанта.Ошибка;
			Иначе
				Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, СогласиеПодписанта.Статус, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			КонецЕсли;
			
		Иначе
			Результат.Ошибка = СогласиеПодписанта.Ошибка;
		КонецЕсли;
		
		Возврат Результат;
	Иначе
		
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки(ОтветHTTP), ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		ЗаписьЖурналаРегистрации(ВидОперации,
			УровеньЖурналаРегистрации.Ошибка,,, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1
				|Идентификатор запроса: %2'"), Результат.Ошибка.ТекстОшибки, ИдентификаторЗапроса));
	
		Возврат Результат;
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ПолучитьРезультатыИзСервисаМобильнойПодписи(ИдентификаторыДокументов = Неопределено, Ошибка = Null) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СервисМобильнойПодписиСтатусы.ИдентификаторВСервисе КАК ИдентификаторВСервисе,
	|	СервисМобильнойПодписиСтатусы.НастройкаОтправкиВГосключ КАК НастройкаОтправкиВГосключ
	|ИЗ
	|	РегистрСведений.СервисМобильнойПодписиСтатусы КАК СервисМобильнойПодписиСтатусы
	|ГДЕ
	|	СервисМобильнойПодписиСтатусы.Операция = ЗНАЧЕНИЕ(Перечисление.СервисМобильнойПодписиОперации.Подписание)
	|	И СервисМобильнойПодписиСтатусы.Статус = ЗНАЧЕНИЕ(Перечисление.СервисМобильнойПодписиСтатусы.ОжидаетОбработки)
	|	И СервисМобильнойПодписиСтатусы.ИдентификаторДокумента В (&ИдентификаторыДокументов)
	|ИТОГИ
	|ПО
	|	НастройкаОтправкиВГосключ";

	Если ИдентификаторыДокументов = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И СервисМобильнойПодписиСтатусы.ИдентификаторДокумента В (&ИдентификаторыДокументов)", "");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И СервисМобильнойПодписиСтатусы.Статус = ЗНАЧЕНИЕ(Перечисление.СервисМобильнойПодписиСтатусы.ОжидаетОбработки)", "");
		Если ТипЗнч("ИдентификаторыДокументов") = Тип("УникальныйИдентификатор") Тогда
			Запрос.УстановитьПараметр("ИдентификаторыДокументов", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
				ИдентификаторыДокументов));
		Иначе
			Запрос.УстановитьПараметр("ИдентификаторыДокументов", ИдентификаторыДокументов);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	
	ЕстьОшибкиВЖурналеРегистрации = Ложь;
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаПоСервисам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоСервисам.Следующий() Цикл

			Если ЗначениеЗаполнено(ВыборкаПоСервисам.НастройкаОтправкиВГосключ) И СервисМобильнойПодписи.ИспользоватьГосключ() Тогда
				ЗаголовокОшибки = НСтр("ru='Не удалось получить подпись документа в Госключе'");
				РезультатАвторизации = АвторизоватьсяВГосключе(ВыборкаПоСервисам.НастройкаОтправкиВГосключ, ЗаголовокОшибки); // @skip-check query-in-loop - авторизация перед получением результатов
			ИначеЕсли Не ЗначениеЗаполнено(ВыборкаПоСервисам.НастройкаОтправкиВГосключ) И СервисМобильнойПодписи.ИспользоватьСервисМобильнойПодписи() Тогда
				ЗаголовокОшибки = НСтр("ru='Не удалось получить подпись документа в 1С: Мобильная подпись'");
				РезультатАвторизации = АвторизоватьсяВСервисеМобильнойПодписи(ЗаголовокОшибки);
			Иначе
				Продолжить;
			КонецЕсли;

			Если РезультатАвторизации.Ошибка <> Неопределено Тогда
				Если Ошибка = Null Тогда
					ЕстьОшибкиВЖурналеРегистрации = Истина;
					ЗаписьЖурналаРегистрации(
						НСтр("ru='Сервис мобильной подписи.ПолучениеРезультатовПодписания'",
						ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,
						ВыборкаПоСервисам.НастройкаОтправкиВГосключ, РезультатАвторизации.Ошибка.ТекстОшибки);
					Продолжить;
				КонецЕсли;
				Ошибка = РезультатАвторизации.Ошибка;
			КонецЕсли;

			Если ЗначениеЗаполнено(Ошибка) Тогда
				Продолжить;
			КонецЕсли;

			ТокенДоступа = РезультатАвторизации.ТокенДоступа;
			
			Выборка = ВыборкаПоСервисам.Выбрать();

			Пока Выборка.Следующий() Цикл
				Если ЗначениеЗаполнено(Выборка.НастройкаОтправкиВГосключ) Тогда
					ОбновитьСтатусПодписанияВГосключе(Выборка.ИдентификаторВСервисе, Выборка.НастройкаОтправкиВГосключ, ТокенДоступа, Ошибка); // @skip-check query-in-loop - проверка результатов подписания в мобильном сервисе по задачам
				Иначе
					ОбновитьСтатусПодписания(Выборка.ИдентификаторВСервисе, ТокенДоступа, Ошибка); // @skip-check query-in-loop - проверка результатов подписания в мобильном сервисе по задачам
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если ЕстьОшибкиВЖурналеРегистрации Тогда
		ВызватьИсключение НСтр("ru='Не удалось получить результаты подписания из сервиса мобильной подписи, подробнее см. в журнале регистрации.'");
	КонецЕсли;
	
КонецПроцедуры

Функция РезультатПроверкиОжидаемыхПодписей(ИдентификаторыДокументов, ДляПроверкиПодписиВФорме = Истина) Экспорт
	
	Результат = Новый Структура("РезультатыПроверки, Ошибка", Новый Соответствие);
	ЗаголовокОшибки = НСтр("ru='Не удалось получить подпись документа в 1С: Мобильная подпись'");
	
	ПолучитьРезультатыИзСервисаМобильнойПодписи(ИдентификаторыДокументов, Результат.Ошибка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СервисМобильнойПодписиСтатусы.Результат КАК Результат,
	|	СервисМобильнойПодписиСтатусы.Статус КАК Статус,
	|	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	NULL КАК ПодписанныйОбъект,
	|	NULL КАК ПорядковыйНомер,
	|	NULL КАК ТребуетсяПроверка,
	|	NULL КАК ТипПодписи,
	|	NULL КАК СрокДействияПоследнейМеткиВремени,
	|	NULL КАК ДатаПроверкиПодписи,
	|	NULL КАК Комментарий,
	|	NULL КАК КомуВыданСертификат,
	|	NULL КАК Отпечаток,
	|	NULL КАК Подпись,
	|	NULL КАК ПодписьВерна,
	|	NULL КАК ДатаПодписи,
	|	NULL КАК УстановившийПодпись,
	|	NULL КАК ПодписьМатематическиВерна,
	|	NULL КАК ОшибкаМатематическойПроверкиПодписи,
	|	NULL КАК ОшибкаПроверкиДополнительныхАтрибутов,
	|	СервисМобильнойПодписиСтатусы.НастройкаОтправкиВГосключ
	|ИЗ
	|	РегистрСведений.СервисМобильнойПодписиСтатусы КАК СервисМобильнойПодписиСтатусы
	|ГДЕ
	|	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента В (&ИдентификаторДокумента)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	NULL,
	|	ЭлектронныеПодписи.ИдентификаторПодписи,
	|	ЭлектронныеПодписи.ПодписанныйОбъект,
	|	ЭлектронныеПодписи.ПорядковыйНомер,
	|	ЭлектронныеПодписи.ТребуетсяПроверка,
	|	ЭлектронныеПодписи.ТипПодписи,
	|	ЭлектронныеПодписи.СрокДействияПоследнейМеткиВремени,
	|	ЭлектронныеПодписи.ДатаПроверкиПодписи,
	|	ЭлектронныеПодписи.Комментарий,
	|	ЭлектронныеПодписи.КомуВыданСертификат,
	|	ЭлектронныеПодписи.Отпечаток,
	|	ЭлектронныеПодписи.Подпись,
	|	ЭлектронныеПодписи.ПодписьВерна,
	|	ЭлектронныеПодписи.ДатаПодписи,
	|	ЭлектронныеПодписи.УстановившийПодпись,
	|	ЭлектронныеПодписи.ПодписьМатематическиВерна,
	|	ЭлектронныеПодписи.ОшибкаМатематическойПроверкиПодписи,
	|	ЭлектронныеПодписи.ОшибкаПроверкиДополнительныхАтрибутов,
	|	NULL
	|ИЗ
	|	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
	|ГДЕ
	|	ЭлектронныеПодписи.ИдентификаторПодписи В (&ИдентификаторДокумента)";
	
	Запрос.УстановитьПараметр("ИдентификаторДокумента", ИдентификаторыДокументов);
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ДляПроверкиПодписиВФорме Тогда
			РезультатПроверкиПодписи = ЭлектроннаяПодписьКлиентСервер.РезультатПроверкиПодписи();
			РезультатПроверкиПодписи.Вставить("ДатаПодписи");
			РезультатПроверкиПодписи.Вставить("ИдентификаторПодписи", Выборка.ИдентификаторДокумента);
		Иначе
			РезультатПроверкиПодписи = ЭлектроннаяПодписьСлужебныйКлиентСервер.СвойстваПодписи(
				Неопределено, Неопределено, Неопределено, Неопределено);
			РезультатПроверкиПодписи.ИдентификаторПодписи = Выборка.ИдентификаторДокумента;
		КонецЕсли;
		
		РезультатПроверкиПодписи.Вставить("Подпись");
		
		Если Выборка.Статус = Null Тогда
			
			ЗаполнитьЗначенияСвойств(РезультатПроверкиПодписи, Выборка);
			РезультатПроверкиПодписи.НеподтвержденнаяДатаПодписи = РезультатПроверкиПодписи.ДатаПодписи;
			РезультатПроверкиПодписи.Вставить("Подпись", Выборка.Подпись.Получить());
			РезультатПроверкиПодписи.Сертификат = ЭлектроннаяПодпись.СертификатИзДвоичныхДанныхПодписи(
				РезультатПроверкиПодписи.Подпись);
			Если ДляПроверкиПодписиВФорме Тогда
				РезультатПроверкиПодписи.Вставить("ЗаписаннаяПодпись", Новый Структура("ПодписанныйОбъект, ПорядковыйНомер", 
					Выборка.ПодписанныйОбъект, Выборка.ПорядковыйНомер));
			КонецЕсли;
		Иначе
			РезультатПодписания = Выборка.Результат.Получить();
			
			Если Выборка.Статус = Перечисления.СервисМобильнойПодписиСтатусы.Успешно Тогда
				СвойстваПодписи = ЭлектроннаяПодпись.СвойстваПодписи(РезультатПодписания);
				ЗаполнитьЗначенияСвойств(РезультатПроверкиПодписи, СвойстваПодписи);
				РезультатПроверкиПодписи.ДатаПодписи = СвойстваПодписи.НеподтвержденнаяДатаПодписи;
				РезультатПроверкиПодписи.Вставить("Подпись", РезультатПодписания);
			ИначеЕсли Выборка.Статус = Перечисления.СервисМобильнойПодписиСтатусы.Отклонено Тогда
				Если ТипЗнч(РезультатПодписания) = Тип("Структура") И ЗначениеЗаполнено(РезультатПодписания.ТекстОшибки) Тогда
					РезультатПроверкиПодписи.ОшибкаПроверкиДополнительныхАтрибутов = ТекстОшибкиОжидаемойПодписи(РезультатПодписания);
				ИначеЕсли ЗначениеЗаполнено(Выборка.НастройкаОтправкиВГосключ) Тогда
					РезультатПроверкиПодписи.ОшибкаПроверкиДополнительныхАтрибутов = НСтр("ru='Отклонено подписание в Госключе'");
				Иначе
					РезультатПроверкиПодписи.ОшибкаПроверкиДополнительныхАтрибутов = НСтр("ru='Отклонено подписание в Моя подпись (ФНС)'");
				КонецЕсли;
			ИначеЕсли Выборка.Статус = Перечисления.СервисМобильнойПодписиСтатусы.Ошибка Тогда
				Если ТипЗнч(РезультатПодписания) = Тип("Структура") И ЗначениеЗаполнено(РезультатПодписания.ТекстОшибки) Тогда
					РезультатПроверкиПодписи.ОшибкаПроверкиДополнительныхАтрибутов = ТекстОшибкиОжидаемойПодписи(РезультатПодписания);
				ИначеЕсли ЗначениеЗаполнено(Выборка.НастройкаОтправкиВГосключ) Тогда
					РезультатПроверкиПодписи.ОшибкаПроверкиДополнительныхАтрибутов = НСтр("ru='Не удалось отправить документ в Госключ'");
				Иначе
					РезультатПроверкиПодписи.ОшибкаПроверкиДополнительныхАтрибутов = НСтр("ru='Не удалось отправить документ в 1С: Мобильная подпись'");
				КонецЕсли;
			Иначе
				Если ТипЗнч(РезультатПодписания) = Тип("Структура") И ЗначениеЗаполнено(РезультатПодписания.ТекстОшибки) Тогда
					РезультатПроверкиПодписи.ОшибкаПроверкиДополнительныхАтрибутов = ТекстОшибкиОжидаемойПодписи(РезультатПодписания);
				ИначеЕсли ЗначениеЗаполнено(Выборка.НастройкаОтправкиВГосключ) Тогда
					РезультатПроверкиПодписи.ОшибкаПроверкиДополнительныхАтрибутов = НСтр("ru='Ожидается подписание документа в Госключе'");
				Иначе
					РезультатПроверкиПодписи.ОшибкаПроверкиДополнительныхАтрибутов = НСтр("ru='Ожидается подписание документа в 1С: Мобильная подпись'");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		РезультатПроверкиПодписи.Вставить("СтатусВМобильномСервисе", ?(Выборка.Статус = Null, Перечисления.СервисМобильнойПодписиСтатусы.Успешно, Выборка.Статус));
		Результат.РезультатыПроверки.Вставить(Выборка.ИдентификаторДокумента, РезультатПроверкиПодписи);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ТекстОшибкиОжидаемойПодписи(РезультатПодписания) Экспорт
	
	Если ЗначениеЗаполнено(РезультатПодписания.ЗаголовокОшибки) Тогда
		ТекстОшибки = РезультатПодписания.ЗаголовокОшибки + ": " + РезультатПодписания.ТекстОшибки;
		Возврат ТекстОшибки;
	КонецЕсли;
	
	Возврат РезультатПодписания.ТекстОшибки;
	
КонецФункции

Процедура УдалитьЗапросНаПодписание(ИдентификаторЗадачи, ТокенДоступа = "")
	
	ЗаголовокОшибки = НСтр("ru='Не удалось удалить запрос на подписание в 1С: Мобильная подпись'");

	Если Не ЗначениеЗаполнено(ТокенДоступа) Тогда
		РезультатАвторизации = АвторизоватьсяВСервисеМобильнойПодписи(ЗаголовокОшибки);
		Если РезультатАвторизации.Ошибка <> Неопределено Тогда
			ВызватьИсключение РезультатАвторизации.Ошибка.ТекстОшибки;
		КонецЕсли;
		ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	КонецЕсли;
	
	СвойстваСервисаМобильнойПодписи = СвойстваСервисаМобильнойПодписи();
	РесурсНаСервере = СвойстваСервисаМобильнойПодписи.РесурсКорняAPI + "fns-ms/public-api/v1/signings/simplifieds/jobs/" + ИдентификаторЗадачи + "/cancel";
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	ЗаголовкиHTTP.Вставить("Authorization", "Bearer " + ТокенДоступа);
	ИдентификаторЗапроса = ИдентификаторЗапроса();
	ЗаголовкиHTTP.Вставить("X-Request-Id", ИдентификаторЗапроса);
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	ОтветHTTP = Неопределено;
	
	ВидОперации = НСтр("ru='Сервис мобильной подписи.Удаление запроса на подписание'", ОбщегоНазначения.КодОсновногоЯзыка());
	Соединение = СоединениеССерверомИнтернета(ВидОперации, СвойстваСервисаМобильнойПодписи.АдресСервера);
	СоединениеHTTP = Соединение.HTTPСоединение;
	ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("POST", ЗапросHTTP);
	
	СтруктураОтвета = СтруктураОтвета(ОтветHTTP);
	Если СтруктураОтвета.Свойство("ОшибкаИзСервиса") И ЗначениеЗаполнено(СтруктураОтвета.ОшибкаИзСервиса) Тогда
		ВызватьИсключение СтруктураОтвета.ОшибкаИзСервиса;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьСтатусПодписания(ИдентификаторЗадачи, ТокенДоступа = "", Ошибка = Null)
	
	Статус = Неопределено;
	
	ЗаголовокОшибки = НСтр("ru='Не удалось получить информацию о подписании документов в 1С: Мобильная подпись'");

	Если Не ЗначениеЗаполнено(ТокенДоступа) Тогда
		РезультатАвторизации = АвторизоватьсяВСервисеМобильнойПодписи(ЗаголовокОшибки);
		Если РезультатАвторизации.Ошибка <> Неопределено Тогда
			Если Ошибка = Null Тогда
				ВызватьИсключение РезультатАвторизации.Ошибка.ТекстОшибки;
			КонецЕсли;
			Ошибка = РезультатАвторизации.Ошибка;
		КонецЕсли;
		
		Если Ошибка <> Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	КонецЕсли;
	
	СвойстваСервисаМобильнойПодписи = СвойстваСервисаМобильнойПодписи();
	РесурсНаСервере = СвойстваСервисаМобильнойПодписи.РесурсКорняAPI + "fns-ms/public-api/v1/signings/simplifieds/jobs/" + ИдентификаторЗадачи;
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
	ЗаголовкиHTTP.Вставить("Authorization", "Bearer " + ТокенДоступа);
	ИдентификаторЗапроса = ИдентификаторЗапроса();
	ЗаголовкиHTTP.Вставить("X-Request-Id", ИдентификаторЗапроса);
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	ОтветHTTP = Неопределено;
	ВидОперации = НСтр("ru='Сервис мобильной подписи.Проверка статуса подписания'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Попытка
		Соединение = СоединениеССерверомИнтернета(ВидОперации, СвойстваСервисаМобильнойПодписи.АдресСервера);
		СоединениеHTTP = Соединение.HTTPСоединение;
		ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("GET", ЗапросHTTP);
	Исключение
		Если Ошибка <> Null Тогда
			ПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			Возврат;
		КонецЕсли;
		ВызватьИсключение;
	КонецПопытки;

	Если ОтветHTTP.КодСостояния = 200 Тогда
		
		Попытка
			СтруктураОтвета = СтруктураОтвета(ОтветHTTP);
			Статус = СтруктураОтвета.Status;
		Исключение
			Если Ошибка <> Null Тогда
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				ЗаписьЖурналаРегистрации(ВидОперации, УровеньЖурналаРегистрации.Ошибка,,,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
				Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ОбработкаОшибок.КраткоеПредставлениеОшибки(
					ИнформацияОбОшибке), ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
				Возврат;
			КонецЕсли;
			ВызватьИсключение;
		КонецПопытки;
		
		Если Статус = СтатусУспешно() Тогда
			РезультатПодписания = СтруктураОтвета.result;
			Если РезультатПодписания.Status = "SIGNED" Тогда
				ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, РезультатПодписания.Signatures, Перечисления.СервисМобильнойПодписиСтатусы.Успешно);
			ИначеЕсли РезультатПодписания.Status = "REJECTED" Тогда
				ЗаголовокОшибки = НСтр("ru='Подписание отклонено'");
				ПредставлениеОшибки = РезультатПодписания.message;
				Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
				ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, Ошибка, Перечисления.СервисМобильнойПодписиСтатусы.Отклонено);
			КонецЕсли;
		ИначеЕсли Статус = СтатусОшибка() Тогда
			ЗаголовокОшибки = НСтр("ru='Не удалось подписать документы в приложении ""Моя подпись""'");
			ПредставлениеОшибки = ?(СтруктураОтвета.Свойство("ОшибкаИзСервиса"), СтруктураОтвета.ОшибкаИзСервиса, ОтветHTTP.ПолучитьТелоКакСтроку());
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, Ошибка, Перечисления.СервисМобильнойПодписиСтатусы.Ошибка);
		ИначеЕсли Статус = СтатусОтмена() Тогда
			ЗаголовокОшибки = НСтр("ru='Отправка документов на подпись была отменена'");
			ПредставлениеОшибки = ?(СтруктураОтвета.Свойство("ОшибкаИзСервиса"), СтруктураОтвета.ОшибкаИзСервиса, ОтветHTTP.ПолучитьТелоКакСтроку());
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, Ошибка, Перечисления.СервисМобильнойПодписиСтатусы.Ошибка);
		Иначе
			ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, Неопределено, Перечисления.СервисМобильнойПодписиСтатусы.ОжидаетОбработки);
		КонецЕсли;
	Иначе
		Статус = Неопределено;
		Если ОтветHTTP.КодСостояния = 204 Тогда
			ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='В 1С:Мобильная подпись не найдена задача на подписание с кодом %1'"), ИдентификаторЗадачи);
			Статус = Перечисления.СервисМобильнойПодписиСтатусы.Ошибка;
		Иначе
			ПредставлениеОшибки = ПредставлениеОшибки(ОтветHTTP);
		КонецЕсли;
		
		Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, Ошибка, Статус);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьСтатусПодписанияВГосключе(ИдентификаторЗадачи, Настройка, ТокенДоступа = "", Ошибка = Null)
	
	Статус = Неопределено;
	
	ЗаголовокОшибки = НСтр("ru='Не удалось получить информацию о подписании документов в 1С: Мобильная подпись'");

	Если Не ЗначениеЗаполнено(ТокенДоступа) Тогда
		РезультатАвторизации = АвторизоватьсяВГосключе(Настройка, ЗаголовокОшибки);
		Если РезультатАвторизации.Ошибка <> Неопределено Тогда
			Если Ошибка = Null Тогда
				ВызватьИсключение РезультатАвторизации.Ошибка.ТекстОшибки;
			КонецЕсли;
			Ошибка = РезультатАвторизации.Ошибка;
			Возврат;
		КонецЕсли;
		ТокенДоступа = РезультатАвторизации.ТокенДоступа;
	КонецЕсли;
	
	СвойстваСервисаМобильнойПодписи = СвойстваСервисаГосключа(Настройка);
	РесурсНаСервере = "/api/gusmev/order/" + ИдентификаторЗадачи;

	ЗаголовкиHTTP = Новый Соответствие(); 
	ЗаголовкиHTTP.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTP.Вставить("Authorization", "Bearer " + ТокенДоступа);
	ИдентификаторЗапроса = ИдентификаторЗадачи;
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	ОтветHTTP = Неопределено;
	ВидОперации = НСтр("ru='Сервис мобильной подписи.Проверка статуса подписания в Госключе'", ОбщегоНазначения.КодОсновногоЯзыка());
	ОтветHTTP = Неопределено;
	
	Попытка
		Соединение = СоединениеССерверомИнтернета(ВидОперации, СвойстваСервисаМобильнойПодписи.АдресСервера);
		СоединениеHTTP = Соединение.HTTPСоединение;
		ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("POST", ЗапросHTTP);
		Структура = СтруктураОтвета(ОтветHTTP);
	Исключение
		Если Ошибка <> Null Тогда
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ЗаписьЖурналаРегистрации(ВидОперации, УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ОбработкаОшибок.КраткоеПредставлениеОшибки(
				ИнформацияОбОшибке), ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			Возврат;
		КонецЕсли;
		ВызватьИсключение;
	КонецПопытки;

	Если ОтветHTTP.КодСостояния = 200 Тогда
		
		ОшибкаИзСервиса = "";
		Попытка
			СтруктураОтвета = СтруктураОтвета(ОтветHTTP);
			Если СтруктураОтвета.Свойство("ОшибкаИзСервиса") Тогда
				ОшибкаИзСервиса = СтруктураОтвета.ОшибкаИзСервиса;
			Иначе
				СтруктураЗаявки = ОбщегоНазначения.JSONВЗначение(СтруктураОтвета.order,,Ложь);
				Статус = ?(СтруктураЗаявки.Свойство("orderStatusId"), СтруктураЗаявки.orderStatusId, "");
				СтатусСтрокой = ?(СтруктураЗаявки.Свойство("orderStatusName"), СтруктураЗаявки.orderStatusName, "");
			КонецЕсли;
		Исключение
			Если Ошибка <> Null Тогда
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				ЗаписьЖурналаРегистрации(ВидОперации, УровеньЖурналаРегистрации.Ошибка,,,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
				Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ОбработкаОшибок.КраткоеПредставлениеОшибки(
					ИнформацияОбОшибке), ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
				Возврат;
			КонецЕсли;
			ВызватьИсключение;
		КонецПопытки;
		
		Если Не ПустаяСтрока(ОшибкаИзСервиса) Тогда
			ЗаголовокОшибки = НСтр("ru='Отправка документов на подпись была отменена'");
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ОшибкаИзСервиса, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, Ошибка, Перечисления.СервисМобильнойПодписиСтатусы.Отклонено);
			Возврат;
		КонецЕсли;
		
		Если СтатусДокументыПодписаныГосключ(Статус) Тогда
			
			МассивПодписей = Новый Массив;
			Файлы = ?(СтруктураЗаявки.Свойство("orderResponseFiles"), СтруктураЗаявки.orderResponseFiles, Новый Массив);
			
			Для Каждого Элемент Из Файлы Цикл
				
				ИмяФайла = Элемент.fileName;
				Если СтрЗаканчиваетсяНа(ИмяФайла, "sig") Тогда
					Продолжить;
				КонецЕсли;
				
				КоличествоПеренаправлений = 0;
				
				Попытка
					РесурсНаСервере = "/api/gusmev/files/download/" + Формат(СтруктураЗаявки.currentStatusHistoryId,
						"ЧГ=0") + "/3?mnemonic=" + КодироватьСтроку(ИмяФайла, СпособКодированияСтроки.КодировкаURL) + "&eserviceCode=" + СтруктураЗаявки.eserviceId;
					
					ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
					ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("GET", ЗапросHTTP);

					Пока ОтветHTTP.КодСостояния = 302 Цикл
						КоличествоПеренаправлений = КоличествоПеренаправлений + 1;
						Если КоличествоПеренаправлений > 7 Тогда
							ВызватьИсключение НСтр("ru = 'Превышено количество перенаправлений.'");
						КонецЕсли;
						РесурсНаСервере = ОтветHTTP.Заголовки.Получить("Location");
						ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
						ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("GET", ЗапросHTTP);
					КонецЦикла;
				Исключение
					Если Ошибка <> Null Тогда
						ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
						Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса,
							СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
						Возврат;
					КонецЕсли;
					ВызватьИсключение;
				КонецПопытки;
				
				Если ОтветHTTP.КодСостояния = 200 Тогда
					МассивПодписей.Добавить(Новый Структура("title, signature", ИмяФайла,
						ОтветHTTP.ПолучитьТелоКакДвоичныеДанные()));
				Иначе
					
					Попытка
						СтруктураОтвета = СтруктураОтвета(ОтветHTTP);
					Исключение
						СтруктураОтвета = Неопределено;
					КонецПопытки;
					
					Если СтруктураОтвета <> Неопределено И СтруктураОтвета.Свойство("ОшибкаИзСервиса") Тогда
						Если Ошибка <> Null Тогда
							Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, СтруктураОтвета.ОшибкаИзСервиса, ИдентификаторЗапроса,
								СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
							Возврат;
						КонецЕсли;
						ВызватьИсключение СтруктураОтвета.ОшибкаИзСервиса;
					Иначе
						ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( НСтр("ru = 'Сервис Госключа вернул код запроса %1.'"), ОтветHTTP.КодСостояния);
						Если Ошибка <> Null Тогда
							Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса,
								СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
							Возврат;
						КонецЕсли;
						ВызватьИсключение ПредставлениеОшибки;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если МассивПодписей.Количество() > 0 Тогда
				ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, МассивПодписей, Перечисления.СервисМобильнойПодписиСтатусы.Успешно);
			КонецЕсли;
			
		ИначеЕсли Статус = СтатусОшибка() Тогда
			ЗаголовокОшибки = НСтр("ru='Не удалось подписать документы в Госключе'");
			ПредставлениеОшибки = ?(СтруктураОтвета.Свойство("ОшибкаИзСервиса"), СтруктураОтвета.ОшибкаИзСервиса,
				ОтветHTTP.ПолучитьТелоКакСтроку());
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса,
				СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, Ошибка,
				Перечисления.СервисМобильнойПодписиСтатусы.Ошибка);
		ИначеЕсли СтатусОтмененГосключ(Статус) Тогда
			ЗаголовокОшибки = НСтр("ru='Подписание отменено'");
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, СтатусСтрокой, ИдентификаторЗапроса, СоединениеHTTP,
				ЗапросHTTP, ОтветHTTP);
			ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, Ошибка,
				Перечисления.СервисМобильнойПодписиСтатусы.Ошибка);
		Иначе
			ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, Неопределено,
				Перечисления.СервисМобильнойПодписиСтатусы.ОжидаетОбработки);
		КонецЕсли;
	Иначе
		Статус = Неопределено;
		ПредставлениеОшибки = ПредставлениеОшибки(ОтветHTTP);
		Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, ИдентификаторЗапроса, СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, Ошибка, Статус);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьРезультатПодписанияВРегистрСтатусов(ИдентификаторЗадачи, Результат, Статус = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	СервисМобильнойПодписиСтатусы.Подписант КАК Подписант,
	|	ПРЕДСТАВЛЕНИЕ(СервисМобильнойПодписиСтатусы.Подписант) КАК ПодписантПредставление,
	|	СервисМобильнойПодписиСтатусы.Отправитель КАК Отправитель,
	|	СервисМобильнойПодписиСтатусы.ДатаОтправки КАК ДатаОтправки,
	|	СервисМобильнойПодписиСтатусы.ИдентификаторВСервисе КАК ИдентификаторВСервисе,
	|	СервисМобильнойПодписиСтатусы.НаименованиеДокумента КАК НаименованиеДокумента,
	|	СервисМобильнойПодписиСтатусы.ПодписанныйОбъект КАК ПодписанныйОбъект,
	|	СервисМобильнойПодписиСтатусы.Операция КАК Операция,
	|	СервисМобильнойПодписиСтатусы.Комментарий КАК Комментарий,
	|	СервисМобильнойПодписиСтатусы.Доверенность КАК Доверенность,
	|	СервисМобильнойПодписиСтатусы.НастройкаОтправкиВГосключ КАК НастройкаОтправкиВГосключ
	|ИЗ
	|	РегистрСведений.СервисМобильнойПодписиСтатусы КАК СервисМобильнойПодписиСтатусы
	|ГДЕ
	|	СервисМобильнойПодписиСтатусы.ИдентификаторВСервисе = &ИдентификаторЗадачи";
	
	Запрос.УстановитьПараметр("ИдентификаторЗадачи", ИдентификаторЗадачи);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Статус = Перечисления.СервисМобильнойПодписиСтатусы.Успешно Тогда
			
				Для Каждого Подпись Из Результат Цикл
					Если Выборка.НайтиСледующий(Подпись.title, "НаименованиеДокумента") Тогда
						Если ТипЗнч(Подпись.signature) = Тип("Строка") Тогда
							ДанныеПодписи = ЭлектроннаяПодпись.ПодписьВКодировкеDER(Base64Значение(Подпись.signature));
						Иначе
							ДанныеПодписи = Подпись.signature;
						КонецЕсли;
						ЗаписатьРезультатПодписи(Выборка, ДанныеПодписи, Статус, Подпись.title);
					КонецЕсли;
				КонецЦикла;
			
		Иначе
			
			Пока Выборка.Следующий() Цикл
				ИдентификаторДокумента = Выборка.ИдентификаторДокумента;
				
				НаборЗаписей = РегистрыСведений.СервисМобильнойПодписиСтатусы.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.ИдентификаторДокумента.Установить(ИдентификаторДокумента);
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СервисМобильнойПодписиСтатусы");
				ЭлементБлокировки.УстановитьЗначение("ИдентификаторДокумента", ИдентификаторДокумента);
				
				НачатьТранзакцию();
				Попытка
					Блокировка.Заблокировать();
					НаборЗаписей.Прочитать();
					
					Если НаборЗаписей.Выбран() И (НаборЗаписей[0].Статус <> Статус Или Статус
						= Перечисления.СервисМобильнойПодписиСтатусы.Ошибка Или Статус = Неопределено) Тогда

						НоваяЗапись = НаборЗаписей[0];
						Если Статус <> Неопределено Тогда
							НоваяЗапись.Статус = Статус;
						КонецЕсли;
						НоваяЗапись.Результат = Новый ХранилищеЗначения(Результат, Новый СжатиеДанных(9));
						НаборЗаписей.Записать();

					КонецЕсли;
					
					ЗафиксироватьТранзакцию();
				Исключение
					ОтменитьТранзакцию();
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Не удалось записать результат подписания %1(%2): %3'"), ИдентификаторДокумента,
						ИдентификаторЗадачи, ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
					ЗаписьЖурналаРегистрации(НСтр("ru='Сервис мобильной подписи.Запись результата подписания'",
						ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
				КонецПопытки;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ЗаписатьРезультатПодписи(Выборка, ДанныеПодписи, Статус, Наименование)
	
	ИдентификаторДокумента = Выборка.ИдентификаторДокумента;
	
	НаборЗаписей = РегистрыСведений.СервисМобильнойПодписиСтатусы.СоздатьНаборЗаписей();
	НаборЗаписей.Заполнить(Неопределено);
	НаборЗаписей.Отбор.ИдентификаторДокумента.Установить(ИдентификаторДокумента);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СервисМобильнойПодписиСтатусы");
	ЭлементБлокировки.УстановитьЗначение("ИдентификаторДокумента", ИдентификаторДокумента);

	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Выбран() И НаборЗаписей[0].Статус <> Статус Тогда
			Если ЗначениеЗаполнено(Выборка.ПодписанныйОбъект)
				И ЭлектроннаяПодписьСлужебный.ДоступнаЭлектроннаяПодпись(ТипЗнч(Выборка.ПодписанныйОбъект)) Тогда
				
				НовыеСвойстваПодписи = ЭлектроннаяПодписьКлиентСервер.НовыеСвойстваПодписи();
				НовыеСвойстваПодписи.Подпись = ДанныеПодписи;
				СвойстваПодписи = ЭлектроннаяПодпись.СвойстваПодписи(НовыеСвойстваПодписи.Подпись);
				
				Если СвойстваПодписи.Успех = Ложь Тогда
					ВызватьИсключение СвойстваПодписи.ТекстОшибки;
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(НовыеСвойстваПодписи, СвойстваПодписи);
				НовыеСвойстваПодписи.ИдентификаторПодписи = ИдентификаторДокумента;
				НовыеСвойстваПодписи.УстановившийПодпись = Выборка.Отправитель;
				НовыеСвойстваПодписи.ПодписанныйОбъект = Выборка.ПодписанныйОбъект;
				НовыеСвойстваПодписи.Комментарий = Выборка.Комментарий;
				Если Не ЗначениеЗаполнено(НовыеСвойстваПодписи.КомуВыданСертификат) Тогда
					НовыеСвойстваПодписи.КомуВыданСертификат = Выборка.ПодписантПредставление;
				КонецЕсли;
				ЭлектроннаяПодпись.ДобавитьПодпись(Выборка.ПодписанныйОбъект, НовыеСвойстваПодписи);
				Если ЗначениеЗаполнено(Выборка.Доверенность) И ОбщегоНазначения.ПодсистемаСуществует(
					"СтандартныеПодсистемы.МашиночитаемыеДоверенности") Тогда
					МодульМашиночитаемыеДоверенностиФНССлужебный = ОбщегоНазначения.ОбщийМодуль(
						"МашиночитаемыеДоверенностиФНССлужебный");
					НовыеСвойстваПодписи.РезультатПроверкиПодписиПоМЧД = 
						МодульМашиночитаемыеДоверенностиФНССлужебный.НовыйРезультатПроверкиПодписиПоМЧД(
						Выборка.Доверенность);
				КонецЕсли;
				
				НаборЗаписей.Очистить();
				НаборЗаписей.Записать();
			Иначе
				НоваяЗапись = НаборЗаписей[0];
				НоваяЗапись.Статус = Статус;
				НоваяЗапись.Результат = Новый ХранилищеЗначения(ДанныеПодписи,
						Новый СжатиеДанных(9));
				НаборЗаписей.Записать();
			КонецЕсли;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось записать подпись %1(%2): %3'"), Наименование, ИдентификаторДокумента,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Сервис мобильной подписи.Запись результата подписания'",
			ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ПредставлениеОшибки(ОтветHTTP)
	
	Попытка
		СтруктураОтвета = СтруктураОтвета(ОтветHTTP);
		ПредставлениеОшибки = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("ОшибкаИзСервиса"),
			СтруктураОтвета.ОшибкаИзСервиса, ОтветHTTP.ПолучитьТелоКакСтроку() + " (" + ОтветHTTP.КодСостояния + ")");
	Исключение
		ПредставлениеОшибки = ОтветHTTP.ПолучитьТелоКакСтроку() + " (" + ОтветHTTP.КодСостояния + ")";
	КонецПопытки;
	
	Возврат ПредставлениеОшибки;
	
КонецФункции

Функция СтатусОтклонено()
	Возврат "REJECTED";
КонецФункции

Функция СтатусОжидаетПодписания()
	Возврат "WAITING_FOR_SIGNING";
КонецФункции

Функция СтатусОшибка()
	Возврат "FAILED";
КонецФункции

Функция СтатусОтмена()
	Возврат "CANCELLED";
КонецФункции

Функция СтатусУспешно()
	Возврат "SUCCESS";
КонецФункции 

Функция СтатусДокументыПодписаныГосключ(Статус)
	Возврат Статус = 3;
КонецФункции 

Функция СтатусОтмененГосключ(Статус)
	Возврат Статус = 4;
КонецФункции 

Функция ОшибкаКлиентНеНайден()
	Возврат "Клиент с такими данными не найден"; // АПК:1297 Строка из сервиса ФНС
КонецФункции

Функция СтатусСогласиеНеОтправлено()
	Возврат НСтр("ru='Согласие не отправлено на подписание'");
КонецФункции

Функция АвторизоватьсяВСервисеМобильнойПодписи(ЦельАвторизации = "")
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыРезультатаАвторизации = ПараметрыСеанса.ПараметрыАвторизацииВСервисеМобильнойПодписи;
	Если ПараметрыРезультатаАвторизации <> Неопределено И ПараметрыРезультатаАвторизации.ДатаСеанса <> Неопределено
		И ПараметрыРезультатаАвторизации.РезультатАвторизации <> Неопределено Тогда
		
		ДатаСеанса = ТекущаяДатаСеанса();
		Если ПараметрыРезультатаАвторизации.ДатаСеанса >= ДатаСеанса - 60 * 60
			И ПараметрыРезультатаАвторизации.ДатаСеанса <= ДатаСеанса Тогда
			
			Возврат Новый Структура(ПараметрыРезультатаАвторизации.РезультатАвторизации);
		КонецЕсли;
	КонецЕсли;
	
	СвойстваСервисаМобильнойПодписи = СвойстваСервисаМобильнойПодписи();
	
	Результат = Новый Структура;
	Результат.Вставить("АдресСервера", СвойстваСервисаМобильнойПодписи.АдресСервера);
	Результат.Вставить("ТокенДоступа", "");
	Результат.Вставить("Ошибка");
	
	Если Не ЭлектроннаяПодпись.РазрешенДоступКИнтернетСервисам() Тогда
		ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Не удается авторизоваться в сервисе 1С:Мобильная подпись'"), ЦельАвторизации);
		Результат.Ошибка = ОшибкаЗапретаДоступаКИнтернетСервисам(ЗаголовокОшибки);
		Возврат Результат;
	КонецЕсли;
	
	РесурсНаСервере = СвойстваСервисаМобильнойПодписи.РесурсКорняAPI + "fns-ms/public-api/v1/auth-tokens/create";
	
	ПараметрыРесурсаНаСервере = "";
	ТикетАутентификацииИлиДанныеПользователя = ТикетАутентификацииИлиДанныеПользователяНаПорталеПоддержки();
		
	Если ТикетАутентификацииИлиДанныеПользователя = Неопределено Тогда
		ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Не удается авторизоваться на сайте интернет-поддержки'"), ЦельАвторизации);
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, НСтр("ru='Не удается получить тикет аутентификации или данные пользователя.'"), "");
		Возврат Результат;
	КонецЕсли;
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTP.Вставить("Proxy-Connection", "Keep-Alive");
	ЗаголовкиHTTP.Вставить("Pragma", "no-cache");
		
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере + ПараметрыРесурсаНаСервере, ЗаголовкиHTTP);
	СтруктураЗапроса = Новый Структура;
	СтруктураЗапроса.Вставить("ticket", ТикетАутентификацииИлиДанныеПользователя.Тикет);
	СтруктураЗапроса.Вставить("applicationGuid", ИдентификаторИнформационнойБазы());
	ТелоЗапроса = ОбщегоНазначения.ЗначениеВJSON(СтруктураЗапроса);
	ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса);
	
	ОтветHTTP = Неопределено;
	ВидОперации = НСтр("ru='Сервис мобильной подписи.Авторизация в сервисе мобильной подписи'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Попытка
		Соединение = СоединениеССерверомИнтернета(ВидОперации, СвойстваСервисаМобильнойПодписи.АдресСервера);
		СоединениеHTTP = Соединение.HTTPСоединение;
		ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("POST", ЗапросHTTP);
	
	Исключение
		ПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Не удалось авторизоваться в сервисе 1С:Мобильная подпись'"), ЦельАвторизации);
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, "", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецПопытки;

	Если ОтветHTTP.КодСостояния = 200 Тогда
		Попытка
			СтруктураОтвета = СтруктураОтвета(ОтветHTTP);
			Результат.ТокенДоступа = ?(ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("token"),
				СтруктураОтвета.token, "");
		Исключение
			ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Не удалось получить токен доступа в сервисе 1С:Мобильная подпись'"), ЦельАвторизации);
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, "", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			Возврат Результат;
		КонецПопытки;
	Иначе
		ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Отказ при получении токена доступа в сервисе 1С:Мобильная подпись'"), ЦельАвторизации);
		ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось получить токен доступа в сервисе 1С:Мобильная подпись: %1'"), ПредставлениеОшибки(ОтветHTTP));
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, "", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Результат.ТокенДоступа) Тогда
		ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Сервис 1С:Мобильная подпись не предоставил токен для авторизации'"), ЦельАвторизации);
		ПредставлениеОшибки = НСтр("ru='Сервис 1С:Мобильная подпись не предоставил токен для авторизации.'");
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, "", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецЕсли;
	
	ПараметрыРезультатаАвторизации = ПараметрыРезультатаАвторизацииВСервисеМобильнойПодписи();
	ПараметрыРезультатаАвторизации.ДатаСеанса = ТекущаяДатаСеанса();
	ПараметрыРезультатаАвторизации.РезультатАвторизации = Новый ФиксированнаяСтруктура(Результат);
	ПараметрыСеанса.ПараметрыАвторизацииВСервисеМобильнойПодписи = Новый ФиксированнаяСтруктура(ПараметрыРезультатаАвторизации);
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыРезультатаАвторизацииВСервисеМобильнойПодписи()
	
	ПараметрыРезультатаАвторизации = Новый Структура;
	ПараметрыРезультатаАвторизации.Вставить("ДатаСеанса", 			Неопределено);
	ПараметрыРезультатаАвторизации.Вставить("РезультатАвторизации", Неопределено);
	Возврат ПараметрыРезультатаАвторизации;
	
КонецФункции

Функция СтруктураОтвета(ОтветHTTP)
	
	ТелоОтвета = ОтветHTTP.ПолучитьТелоКакСтроку();
	
	Заголовки = СтандартныеПодсистемыСервер.HTTPЗаголовкиВНижнийРегистр(ОтветHTTP.Заголовки);
	
	Если СтрНачинаетсяС(НРег(Заголовки["content-type"]), "text/html") Тогда
		ФорматированныйДокумент = Новый ФорматированныйДокумент;
		ФорматированныйДокумент.УстановитьHTML(ТелоОтвета, Новый Структура);
		ФорматированнаяСтрока = ФорматированныйДокумент.ПолучитьФорматированнуюСтроку();
		СтруктураОтвета = Новый Структура("ОшибкаИзСервиса", ФорматированнаяСтрока);
	ИначеЕсли Не ЗначениеЗаполнено(ТелоОтвета) Тогда
		Если ОтветHTTP.КодСостояния >= 400 Тогда
			СтруктураОтвета = Новый Структура("ОшибкаИзСервиса", ТелоОтвета + " (" + ОтветHTTP.КодСостояния + ")");
		Иначе
			СтруктураОтвета = Новый Структура;
		КонецЕсли;
	ИначеЕсли НРег(Заголовки["content-type"]) = "application/xml;charset=UTF-8" Тогда
		СтруктураОтвета = Новый Структура("ОшибкаИзСервиса", ТелоОтвета + " (" + ОтветHTTP.КодСостояния + ")");
	Иначе
		СтруктураОтвета = ОбщегоНазначения.JSONВЗначение(ТелоОтвета,,Ложь);
		Если ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("error") И ЗначениеЗаполнено(СтруктураОтвета.error) Тогда
			ОбработатьОшибкуСервиса(СтруктураОтвета);
		ИначеЕсли ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("code")
			И ЗначениеЗаполнено(СтруктураОтвета.code) И СтруктураОтвета.code <> "OK" Тогда
			ОбработатьОшибкуГосключа(СтруктураОтвета);
		ИначеЕсли ОтветHTTP.КодСостояния > 202 Тогда
			Если ТипЗнч(СтруктураОтвета) = Тип("Структура") И СтруктураОтвета.Свойство("message") Тогда
			
				Если СтруктураОтвета.Свойство("details") Тогда
					ДеталиОшибки = ДеталиОшибки(СтруктураОтвета.details);
				Иначе
					ДеталиОшибки = "";
				КонецЕсли;
				
				Если ОтветHTTP.КодСостояния = 500 И СтруктураОтвета.Type = "INTERNAL" Тогда
					ТекстОшибки = НСтр("ru='Сервису 1С:Мобильная подпись не удалось обработать запрос. Попробуйте выполнить операцию позднее.'");
				ИначеЕсли СтруктураОтвета.Type = "MAXIMUM_FILE_SIZE_EXCEEDED" Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Превышен максимальный размер файла: %1.'"),
						ДеталиОшибки);
				ИначеЕсли СтруктураОтвета.Type = "INPUT_DATA_VALIDATION_FAILED" Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Данные не могут быть отправлены: %1.'"),
						ДеталиОшибки);
				ИначеЕсли СтруктураОтвета.Type = "TICKET_VERIFICATION_ERROR" Тогда
					ТекстОшибки = НСтр("ru='Некорректный тикет авторизации.'");
				Иначе
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)%3'"), 
						СтруктураОтвета.message, ОтветHTTP.КодСостояния, ДеталиОшибки);
				КонецЕсли;
					
			Иначе
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1: %2'"),
					ОтветHTTP.КодСостояния, ТелоОтвета);
			КонецЕсли;
			СтруктураОтвета = Новый Структура("ОшибкаИзСервиса", ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
КонецФункции

Процедура ОбработатьОшибкуСервиса(СтруктураОтвета)
	
	ИдентификаторОшибки = "";
	ТипОшибки = "";
	СообщениеОбОшибке = "";
	Ошибки = Новый Массив;
	Для Каждого КлючИЗначение Из СтруктураОтвета.error Цикл
		Если КлючИЗначение.Ключ = "details" Тогда
			Если ЗначениеЗаполнено(СтруктураОтвета.error.details) Тогда
				Ошибки.Добавить(ДеталиОшибки(СтруктураОтвета.error.details));
			КонецЕсли;
		ИначеЕсли КлючИЗначение.Ключ = "message" Тогда
			Если НРег(КлючИЗначение.Значение) = "see details" Тогда
				Продолжить;
			КонецЕсли;
			СообщениеОбОшибке = КлючИЗначение.Значение;
		ИначеЕсли КлючИЗначение.Ключ = "ueid" Тогда
			ИдентификаторОшибки = КлючИЗначение.Значение;
		ИначеЕсли КлючИЗначение.Ключ = "type" Тогда
			ТипОшибки = КлючИЗначение.Значение;
		ИначеЕсли КлючИЗначение.Ключ = "non_field_errors" Тогда
			Ошибки.Добавить(ДеталиОшибки(КлючИЗначение.Значение));
		Иначе
			Ошибки.Добавить(КлючИЗначение.Ключ + ": " + ДеталиОшибки(КлючИЗначение.Значение));
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) И ТипОшибки <> "FNS_ERROR" Тогда
		Ошибки.Вставить(0, СообщениеОбОшибке);
	КонецЕсли;
	
	Если ТипОшибки <> "FNS_SIMPLIFIED_SIGNING_ERROR" Тогда
		Если ЗначениеЗаполнено(ИдентификаторОшибки) Тогда
			Ошибки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Идентификатор ошибки: %1'"), ИдентификаторОшибки));
		КонецЕсли;
		Если ЗначениеЗаполнено(ТипОшибки) Тогда
			Если ТипОшибки = "INTERNAL_APPLICATION_ERROR" Тогда
				Ошибки.Вставить(0, НСтр("ru='Сервис не смог обработать запрос:'"));
			ИначеЕсли ТипОшибки = "FNS_ERROR" Тогда
				Ошибки.Вставить(0, НСтр("ru='Сервис ФНС не смог обработать запрос на подписание. Попробуйте переотправить документ позднее.'"));
			Иначе
				Ошибки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='Тип ошибки: %1'"), ТипОшибки));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураОтвета.Вставить("ОшибкаИзСервиса", СтрСоединить(Ошибки, Символы.ПС));
			
КонецПроцедуры

Функция ДеталиОшибки(Детали)
	
	Если ТипЗнч(Детали) = Тип("Структура") Тогда
		
		Ошибки = Новый Массив;
		
		Если Детали.Свойство("Field") И Детали.Свойство("RejectedValue") И Детали.Свойство("message") Тогда
			Ошибки.Добавить(Детали.RejectedValue + ": " + ДеталиОшибки(Детали.message));
		Иначе
			Для Каждого КлючИЗначение Из Детали Цикл
				ОшибкаСтрокой = ДеталиОшибки(КлючИЗначение.Значение);
				Если КлючИЗначение.Ключ = "non_field_errors" Тогда
					Ошибки.Добавить(ОшибкаСтрокой);
				Иначе
					Ошибки.Добавить(КлючИЗначение.Ключ + ": " + ОшибкаСтрокой);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Возврат СтрСоединить(Ошибки, ";" + Символы.ПС);
		
	ИначеЕсли ТипЗнч(Детали) = Тип("Массив") Тогда
		
		Ошибки = Новый Массив;
		Для Каждого Элемент Из Детали Цикл
			Ошибки.Добавить(ДеталиОшибки(Элемент));
		КонецЦикла;
		
		Возврат СтрСоединить(Ошибки, ";" + Символы.ПС);
		
	ИначеЕсли ТипЗнч(Детали) = Тип("Строка") Тогда
		
		Возврат Детали;
		
	КонецЕсли;
	
	Возврат ЗаписатьЗначениеJSON(Детали);
	
КонецФункции

Функция СвойстваСервисаМобильнойПодписи()
	
	Результат = Новый Структура;
	
	URLСервера = "https://signing-integration.1c.ru/";
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(URLСервера);
	Результат.Вставить("АдресСервера", СтруктураURI.Схема + "://" + СтруктураURI.ИмяСервера);
	Результат.Вставить("РесурсКорняAPI", "/" + СтруктураURI.ПутьНаСервере);
	Результат.Вставить("Схема", СтруктураURI.Схема);
	Результат.Вставить("Порт", СтруктураURI.Порт);
	Результат.Вставить("Хост", СтруктураURI.Хост);
	
	Возврат Результат;
	
КонецФункции

Функция ИдентификаторИнформационнойБазы()
	Возврат СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы();
КонецФункции

Функция ТикетАутентификацииИлиДанныеПользователяНаПорталеПоддержки()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПродолжитьПолучениеТикета = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");	
		ПродолжитьПолучениеТикета = МодульИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	КонецЕсли;
	
	Если ПродолжитьПолучениеТикета Тогда
			
			ВладелецТикета = ВладелецТикета();
			
			РезультатПолученияТикета =
				МодульИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки(ВладелецТикета);
			
			Если РезультатПолученияТикета = Неопределено Или Не ЗначениеЗаполнено(РезультатПолученияТикета.Тикет) Тогда
				Возврат Неопределено;
			Иначе
				Возврат РезультатПолученияТикета;
			КонецЕсли;
			
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ВладелецТикета()

	Возврат "signing-integration-onec-dss";

КонецФункции

Функция ОшибкаЗапретаДоступаКИнтернетСервисам(ЗаголовокОшибки)
	Возврат ОшибкаМобильногоСервиса(ЗаголовокОшибки,
		ЭлектроннаяПодписьСлужебныйКлиентСервер.ЗапрещенДоступКИнтернетСервисамТекстСообщения(), "");
КонецФункции

Функция ОшибкаМобильногоСервиса(ЗаголовокОшибки, ТекстОшибки, ИдентификаторЗапроса, СоединениеHTTP = Неопределено,
	ЗапросHTTP = Неопределено, ОтветHTTP = Неопределено)

	Результат = СервисМобильнойПодписиКлиентСервер.НовоеОписаниеОшибки(ЗаголовокОшибки, ТекстОшибки);
	Результат.ИдентификаторЗапроса = ИдентификаторЗапроса;
	Если СоединениеHTTP <> Неопределено Тогда
		ПараметрыФайловДляРасследования = ПараметрыФайловДляРасследования();
		ПараметрыФайловДляРасследования.СоединениеHTTP = СоединениеHTTP;
		ПараметрыФайловДляРасследования.Запрос = ЗапросHTTP;
		ПараметрыФайловДляРасследования.Ответ = ОтветHTTP;
		Результат.ДополнительныеФайлы = ФайлыДляРасследования(ЗапросHTTP, ОтветHTTP, СоединениеHTTP);
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ИдентификаторЗапроса()
	Возврат Строка(Новый УникальныйИдентификатор);
КонецФункции

Процедура ДобавитьФайлДляРасследования(Файлы, ДанныеФайла, ТипФайла, Расширение = "txt")
	
	СведенияОФайле = Новый Структура;
	Если ТипЗнч(ДанныеФайла) = Тип("Строка") Тогда
		СведенияОФайле.Вставить("Данные", Новый ДвоичныеДанные(ДанныеФайла));
	ИначеЕсли ТипЗнч(ДанныеФайла) = Тип("ДвоичныеДанные") Тогда
		СведенияОФайле.Вставить("Данные", ДанныеФайла);
	Иначе
		СведенияОФайле.Вставить("Данные", Неопределено);
	КонецЕсли;
	СведенияОФайле.Вставить("Имя", ТипФайла + "." + Расширение);
	СведенияОФайле.Вставить("ТипФайла", ТипФайла);
	Файлы.Добавить(СведенияОФайле);

КонецПроцедуры

Функция ПараметрыФайловДляРасследования()
	
	ПараметрыФайловДляРасследования = Новый Структура;
	ПараметрыФайловДляРасследования.Вставить("ИмяФайлаЗапроса", "");
	ПараметрыФайловДляРасследования.Вставить("ИмяФайлаОтвета", "");
	ПараметрыФайловДляРасследования.Вставить("Запрос", Неопределено);
	ПараметрыФайловДляРасследования.Вставить("Ответ", Неопределено);
	ПараметрыФайловДляРасследования.Вставить("СоединениеHTTP", Неопределено);
	Возврат ПараметрыФайловДляРасследования;

КонецФункции

Функция ФайлыДляРасследования(Запрос, Ответ, СоединениеHTTP)
	
	ДополнительныеФайлы = Новый Массив;
	
	Если Запрос <> Неопределено И СоединениеHTTP <> Неопределено Тогда
		RestЗапрос = RestПредставлениеЗапроса(Запрос, СоединениеHTTP);
		ДанныеФайла = ПолучитьДвоичныеДанныеИзСтроки(RestЗапрос);
		ДобавитьФайлДляРасследования(ДополнительныеФайлы, ДанныеФайла, НСтр("ru='Запрос http'"), "txt");
	КонецЕсли;
	
	Если Ответ <> Неопределено Тогда
		СтруктураЗначения = Новый Структура;
		СтруктураЗначения.Вставить("КодСостояния", Ответ.КодСостояния);
		Заголовки = Ответ.Заголовки;
		СтруктураЗначения.Вставить("Заголовки", Заголовки);
		СтруктураЗначения.Вставить("Тело", Ответ.ПолучитьТелоКакСтроку());
		ДанныеФайла = ПолучитьДвоичныеДанныеИзСтроки(ОбщегоНазначения.ЗначениеВJSON(СтруктураЗначения));
		ДобавитьФайлДляРасследования(ДополнительныеФайлы, ДанныеФайла, НСтр("ru='Ответ http'"), "txt");
	КонецЕсли;
	
	Возврат ДополнительныеФайлы;
КонецФункции

Функция RestПредставлениеЗапроса(Запрос, СоединениеHTTP)
	RestЗапрос = Новый Массив;
	RestЗапрос.Добавить("###");
	RestЗапрос.Добавить(Символы.ПС);
	АдресРесурса = ?(СоединениеHTTP.ЗащищенноеСоединение = Неопределено, "http://", "https://") + СоединениеHTTP.Сервер + Запрос.АдресРесурса;
	СтрокаЗапроса = ?(ЗначениеЗаполнено(Запрос.ПолучитьТелоКакСтроку()), "POST", "GET") + " " + АдресРесурса + " HTTP/1.1";
	RestЗапрос.Добавить(СтрокаЗапроса);
	Для Каждого Заголовок Из Запрос.Заголовки Цикл
		Если НРег(Заголовок.Ключ) = "authorization" Тогда
			RestЗапрос.Добавить(Заголовок.Ключ+": "+ Лев(Заголовок.Значение, 30) + "*");
		Иначе
			RestЗапрос.Добавить(Заголовок.Ключ+": "+Заголовок.Значение);
		КонецЕсли;
	КонецЦикла;
	
	RestЗапрос.Добавить(Символы.ПС);
	
	ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
	Если ЗначениеЗаполнено(ТелоЗапроса) Тогда
		Попытка
			ЗапросБезДанных = ПрочитатьЗначениеJSON(ТелоЗапроса);
			Если ЗапросБезДанных.Свойство("documents") Тогда
				Для Каждого Элемент Из ЗапросБезДанных.documents Цикл
					Элемент.Path = "*";
				КонецЦикла;
			КонецЕсли;
			Если ЗапросБезДанных.Свойство("phone") Тогда
				ЗапросБезДанных.phone = "*" + Прав(ЗапросБезДанных.phone, 4);
			КонецЕсли;
			RestЗапрос.Добавить(ЗаписатьЗначениеJSON(ЗапросБезДанных));
		Исключение
			RestЗапрос.Добавить(ТелоЗапроса);
		КонецПопытки;
		RestЗапрос.Добавить(Символы.ПС);
	КонецЕсли;
	RestЗапрос.Добавить("###");
	Возврат СтрСоединить(RestЗапрос, Символы.ПС);
КонецФункции

Функция ОтправитьНаПодписаниеВГосключ(НаборДанных, Подписант, Параметры, РеквизитыПодписанта = Неопределено)
	
	ЗаголовокОшибки = НСтр("ru='Не удалось отправить документы на подписание'");
	Результат = Новый Структура("Ошибка, НаборДанных");
	Результат.НаборДанных = НаборДанных;

	Если Не Параметры.Свойство("ТокенДоступа") Или ТекущаяУниверсальнаяДата() < Параметры.ТокенДоступаДействуетДо Тогда
		РезультатАвторизации = АвторизоватьсяВГосключе(Параметры.НастройкаОтправки, ЗаголовокОшибки);
		Если РезультатАвторизации.Ошибка <> Неопределено Тогда
			Результат.Ошибка = РезультатАвторизации.Ошибка;
			Возврат Результат;
		КонецЕсли;
		Параметры.Вставить("ТокенДоступа", РезультатАвторизации.ТокенДоступа);
		Параметры.Вставить("ТокенДоступаДействуетДо", РезультатАвторизации.ДействуетДо);
	КонецЕсли;
	
	ТокенДоступа = Параметры.ТокенДоступа;
	Если Не Параметры.Свойство("ПарольСертификата") Тогда
		УстановитьПривилегированныйРежим(Истина);
		Параметры.Вставить("ПарольСертификата", ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
			Параметры.НастройкаОтправки.Ссылка, "ПарольСертификата"));
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	СвойстваСервисаМобильнойПодписи = СвойстваСервисаГосключа(Параметры.НастройкаОтправки);
	
	Если РеквизитыПодписанта = Неопределено Тогда
		
		ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
		
		Если ТипЗнч(Подписант) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Подписант);
			Если Не ЗначениеЗаполнено(Подписант.Ссылка) Тогда
				Попытка
					Подписант.Ссылка = ДобавитьПодписантаВСправочник(ПараметрыПодписанта).Ссылка;
				Исключение
					Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Подписант: %1.'"), ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке())),
						"");
					Возврат Результат;
				КонецПопытки;
			КонецЕсли;
		Иначе
			РеквизитыПодписантаИзСправочника = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Подписант,
				"Ссылка, ВидПодписанта, ИНН, СНИЛС, ОГРН, НомерЗаписиОбАккредитации");
			ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, РеквизитыПодписантаИзСправочника);
		КонецЕсли;
		
		ПараметрыПодписанта.МояПодпись = Ложь;
		ПараметрыПодписанта.ГосключУКЭП = Параметры.ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.ГосключУКЭП;
		ПараметрыПодписанта.ГосключУНЭП = Параметры.ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.ГосключУНЭП;
		
		Попытка
			ПроверкаПараметровПодписанта(ПараметрыПодписанта);
			РеквизитыПодписанта = ПараметрыПодписанта;
		Исключение
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Подписант: %1.'"), ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке())), "");
			Возврат Результат;
		КонецПопытки;
	Иначе
		ПараметрыПодписанта = РеквизитыПодписанта;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ТипПодписи)
		И Параметры.ТипПодписи <> Перечисления.ТипыПодписиКриптографии.БазоваяCAdESBES
		И Параметры.ТипПодписи <> Перечисления.ТипыПодписиКриптографии.ОбычнаяCMS
		И Параметры.ТипПодписи <> Перечисления.ТипыПодписиКриптографии.СМеткойДоверенногоВремениCAdEST Тогда
		
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В Госключе не поддерживается тип подписи: %1'"),
			Параметры.ТипПодписи), "");
		Возврат Результат;
	КонецЕсли;
	
	ОшибкаНабораДанных = Неопределено;
	
	МенеджерКриптографии = Неопределено;
	Сертификат =  Параметры.НастройкаОтправки.Сертификат;
	
	РазмерЗапроса = 0;
	
	АрхивИнформации = Новый ЗаписьZipФайла();
	ВременныйКаталог = ФайловаяСистема.СоздатьВременныйКаталог();
	ВременныеФайлы = Новый Массив;
	
	Для Каждого Элемент Из НаборДанных Цикл
		Если Элемент.Свойство("ИдентификаторДокумента") И ЗначениеЗаполнено(Элемент.ИдентификаторДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭтоАдресВременногоХранилища(Элемент.Данные) Тогда
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(Элемент.Данные);
			Если ТипЗнч(ДвоичныеДанные) <> Тип("ДвоичныеДанные") Тогда
				Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Не поддерживается тип данных для отправки в сервис: %1 - %2'"),
						ТипЗнч(ДвоичныеДанные), Элемент.Представление), "");
				Прервать;
			КонецЕсли;

		ИначеЕсли ТипЗнч(Элемент.Данные) = Тип("ДвоичныеДанные") Тогда
			ДвоичныеДанные = Элемент.Данные;
		Иначе
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Не поддерживается тип данных для отправки в сервис: %1 - %2'"),
					ТипЗнч(Элемент.Данные), Элемент.Представление), "");
			Прервать;
		КонецЕсли;
		
		Размер = ДвоичныеДанные.Размер();
	
		Если Размер = 0 Тогда
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Пустые данные для подписи: %1'"),
					Элемент.Представление), "");
			Прервать;
		КонецЕсли;
		
		ПреобразоватьИмяФайла(Элемент.Представление, Истина);
		
		РазмерЗапроса = РазмерЗапроса + Размер;
			
		ДобавитьФайлВАрхив(АрхивИнформации, ДвоичныеДанные, Элемент.Представление,
			ВременныйКаталог, ВременныеФайлы);
		
		Если Элемент.Свойство("СвойстваПодписи") Тогда
			Если ЭтоАдресВременногоХранилища(Элемент.СвойстваПодписи) Тогда
				СвойстваПодписи = ПолучитьИзВременногоХранилища(Элемент.СвойстваПодписи);
			Иначе
				СвойстваПодписи = Элемент.СвойстваПодписи;
			КонецЕсли;
			Подпись = СвойстваПодписи.Подпись;
		Иначе
			
			Попытка
				Подпись = ПодписатьДанныеДляОтправкиВГосключ(
					ДвоичныеДанные, Сертификат, Параметры.ПарольСертификата, Ложь, МенеджерКриптографии);
			Исключение
				Подпись = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			КонецПопытки;
			
			Если ТипЗнч(Подпись) = Тип("Строка") Тогда
				Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, Подпись, "");
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
		Размер = Подпись.Размер();
	
		Если Размер = 0 Тогда
			Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Пустые данные подписи: %1'"),
				Элемент.Представление + ".sig"), "");
			Прервать;
		КонецЕсли;

		РазмерЗапроса = РазмерЗапроса + Размер;

		ДобавитьФайлВАрхив(АрхивИнформации, Подпись, Элемент.Представление + ".sig",
			ВременныйКаталог, ВременныеФайлы);
		
	КонецЦикла;
	
	Если Ошибка <> Неопределено Тогда
		Результат.Ошибка = Ошибка;
		Возврат Результат;
	КонецЕсли;
			
	Если Параметры.ЗапросНаОтправкуДокумента = Неопределено Тогда
		Запрос = ЗапросНаОтправкуДокумента(ПараметрыПодписанта, Параметры.НастройкаОтправки);
		ДанныеЗапроса = ПолучитьДвоичныеДанныеИзСтроки(Запрос);
		Попытка
			Подпись = ПодписатьДанныеДляОтправкиВГосключ(
				ДанныеЗапроса, Сертификат, Параметры.ПарольСертификата, Ложь, МенеджерКриптографии);
		Исключение
			Результат.Ошибка = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			Возврат Результат;
		КонецПопытки;
	Иначе
		ДанныеЗапроса = Параметры.ЗапросНаОтправкуДокумента.ДанныеЗапроса;
		СвойстваПодписи = Параметры.ЗапросНаОтправкуДокумента.СвойстваПодписи;
		Если ЭтоАдресВременногоХранилища(СвойстваПодписи) Тогда
			СвойстваПодписи = ПолучитьИзВременногоХранилища(СвойстваПодписи);
		КонецЕсли;
		Подпись = СвойстваПодписи.Подпись;
	КонецЕсли;
	
	ИмяФайлаЗапроса = "req.xml";
	ДобавитьФайлВАрхив(АрхивИнформации, ДанныеЗапроса, ИмяФайлаЗапроса,
		ВременныйКаталог, ВременныеФайлы);
	ДобавитьФайлВАрхив(АрхивИнформации, Подпись, ИмяФайлаЗапроса + ".sig",
		ВременныйКаталог, ВременныеФайлы);
	
	ДД = АрхивИнформации.ПолучитьДвоичныеДанные();
	ОчиститьВременныеФайлы(ВременныеФайлы, ВременныйКаталог);
	
	РазмерАрхива = ДД.Размер();
	
	Если РазмерАрхива > 1000 * 1000 * 50 Тогда
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Размер отправляемого архива с файлами не должен превышать 50 000 000 байт: %1 байт'"),
			РазмерЗапроса),
			"");
		Возврат Результат;
	КонецЕсли;
	
	Если РазмерЗапроса > 1024 * 1024 * 100 Тогда
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Размер отправляемых файлов в одном пакете не должен превышать 100 Мб: %1 байт'"),
			РазмерЗапроса),
			"");
		Возврат Результат;
	КонецЕсли;
		
	Попытка
		НомерЗадачи = ОтправитьЗапросВГосключ(ДД, Параметры.НастройкаОтправки, ПараметрыПодписанта, ТокенДоступа, СвойстваСервисаМобильнойПодписи.АдресСервера);
	Исключение
		ПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, "");
		Возврат Результат;
	КонецПопытки;
	
	Если ТипЗнч(НомерЗадачи) = Тип("Структура") Тогда
		Результат.Ошибка = НомерЗадачи;
		Возврат Результат;
	КонецЕсли;
	
	НомерЗадачи = Формат(НомерЗадачи, "ЧГ=0");
	
	ДатаОтправки = ТекущаяДатаСеанса();
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ОтправленныеДокументы = Новый Массив;
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого Элемент Из НаборДанных Цикл
		ПредставлениеОшибки = "";
		
		НачатьТранзакцию();
		Попытка
			ИдентификаторДокумента = Новый УникальныйИдентификатор;
			
			НаборЗаписей = РегистрыСведений.СервисМобильнойПодписиСтатусы.СоздатьНаборЗаписей();
			НаборЗаписей.Заполнить(Неопределено);
			НаборЗаписей.Отбор.ИдентификаторДокумента.Установить(ИдентификаторДокумента);
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.ИдентификаторДокумента = ИдентификаторДокумента;
			НоваяЗапись.ИдентификаторВСервисе = НомерЗадачи;
			НоваяЗапись.НаименованиеДокумента = Элемент.Представление;
			Если Элемент.Свойство("Объект") Тогда
				НоваяЗапись.ПодписанныйОбъект = Элемент.Объект;
			КонецЕсли;
			НоваяЗапись.Отправитель = ТекущийПользователь;
			НоваяЗапись.ДатаОтправки = ДатаОтправки;
			НоваяЗапись.Подписант = ПараметрыПодписанта.Ссылка;
			НоваяЗапись.Доверенность = Параметры.Доверенность;
			НоваяЗапись.Комментарий = Параметры.Комментарий;
			НоваяЗапись.Статус = Перечисления.СервисМобильнойПодписиСтатусы.ОжидаетОбработки;
			НоваяЗапись.Операция = Перечисления.СервисМобильнойПодписиОперации.Подписание;
			НоваяЗапись.НастройкаОтправкиВГосключ = Параметры.НастройкаОтправки.Ссылка;
			НаборЗаписей.Записать();
			
			Если Элемент.Свойство("Объект") И ЭлектроннаяПодписьСлужебный.ДоступнаЭлектроннаяПодпись(ТипЗнч(Элемент.Объект)) Тогда
				
				ВерсияОбъекта = Неопределено;
				Элемент.Свойство("ВерсияОбъекта", ВерсияОбъекта);
				ЗаблокироватьДанныеДляРедактирования(Элемент.Объект, ВерсияОбъекта, Параметры.ИдентификаторФормы);
				ОбъектДанных = Элемент.Объект.ПолучитьОбъект();
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить(ОбъектДанных.Метаданные().ПолноеИмя());
				ЭлементБлокировки.УстановитьЗначение("Ссылка", ОбъектДанных.Ссылка);
				
				Блокировка.Заблокировать();
				
				Если Не ОбъектДанных.ПодписанЭП Тогда
					ОбъектДанных.ПодписанЭП = Истина;
					// Чтобы определить, что это запись с целью добавления/удаления подписи.
					ОбъектДанных.ДополнительныеСвойства.Вставить("ЗаписьПодписанногоОбъекта", Истина);
					Если ОбъектДанных.Модифицированность() Тогда
						ОбъектДанных.Записать();
					КонецЕсли;
				КонецЕсли;
				
				РазблокироватьДанныеДляРедактирования(Элемент.Объект, Параметры.ИдентификаторФормы);
			КонецЕсли;
			
			Элемент.Вставить("ИдентификаторДокумента", ИдентификаторДокумента);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
			ЗаписьЖурналаРегистрации(НСтр("ru='Сервис мобильной подписи.Запись результата отправки на подписание'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,,, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		КонецПопытки;
		
		Если ЗначениеЗаполнено(ПредставлениеОшибки) Тогда
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, "");
			Возврат Результат;
		КонецЕсли;
		
	КонецЦикла;
	
	Результат.НаборДанных = НаборДанных;
	
	Возврат Результат;
	
КонецФункции

Функция АвторизоватьсяВГосключе(Знач Настройка, ЦельАвторизации = "", Подпись = Неопределено) Экспорт
	
	Если ТипЗнч(Настройка) = Тип("СправочникСсылка.НастройкиОтправкиВГосключ") Тогда
		Параметры = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписания();
		Параметры.ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.ГосключУКЭП;
		Параметры.НастройкаОтправки = Настройка;
		Настройка = НастройкаОтправки(Параметры, Истина);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ТокенДоступа", "");
	Результат.Вставить("ДействуетДо", Дата(1,1,1));
	Результат.Вставить("Ошибка");
	
	ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Не удается авторизоваться в Госключе'"), ЦельАвторизации);
	Если Не ЭлектроннаяПодпись.РазрешенДоступКИнтернетСервисам() Тогда
		Результат.Ошибка = ОшибкаЗапретаДоступаКИнтернетСервисам(ЗаголовокОшибки);
		Возврат Результат;
	КонецЕсли;
	
	СвойстваСервисаМобильнойПодписи = СвойстваСервисаАвторизацииГосключа(Настройка);
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыАвторизацииВГосключе = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Настройка.Ссылка, "ПараметрыАвторизацииВГосключе");
	УстановитьПривилегированныйРежим(Ложь);
		
	Если ТипЗнч(ПараметрыАвторизацииВГосключе) = Тип("Структура")
		И ПараметрыАвторизацииВГосключе.РезультатАвторизации <> Неопределено
		И ПараметрыАвторизацииВГосключе.РезультатАвторизации.АдресСервера = СвойстваСервисаМобильнойПодписи.АдресСервера Тогда
		
		Если ТекущаяУниверсальнаяДата() < ПараметрыАвторизацииВГосключе.РезультатАвторизации.ДействуетДо Тогда
			Результат = Новый Структура(ПараметрыАвторизацииВГосключе.РезультатАвторизации);
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	Результат.Вставить("АдресСервера", СвойстваСервисаМобильнойПодписи.АдресСервера);
	
	УстановитьПривилегированныйРежим(Истина);
	КлючДоступа = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Настройка.Ссылка, "КлючДоступа");
	ПарольСертификата = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Настройка.Ссылка, "ПарольСертификата");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ЗначениеЗаполнено(КлючДоступа) Тогда
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не указан ключ доступа в настройке %1'"), Настройка.Ссылка), "");
		Возврат Результат;
	КонецЕсли;
	
	Если Подпись = Неопределено Тогда
		Если Не Настройка.ПарольСертификатаУстановлен Тогда
			ОповеститьОНеобходимостиАвторизацииВГосключе(Настройка.Ссылка);
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не задан пароль сертификата для подписания ключа доступа в Госключ в настройке %1.'"), Настройка.Ссылка), "");
			Возврат Результат;
		КонецЕсли;
		
		Если Не Настройка.РазрешеноПодписаниеНаСервере Тогда
			ОповеститьОНеобходимостиАвторизацииВГосключе(Настройка.Ссылка);
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, 
				НСтр("ru='Не настроено подписание на сервере для подписания ключа доступа в Госключ в настройке %1.'"), "");
			Возврат Результат;
		КонецЕсли;
		
		Сертификат = Настройка.Сертификат;
		
		Попытка
			Подпись = ПодписатьДанныеДляОтправкиВГосключ(КлючДоступа, Сертификат, ПарольСертификата, Истина);
		Исключение
			Подпись = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецПопытки;
		
		Если ТипЗнч(Подпись) = Тип("Строка") Тогда
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='При подписании запроса на авторизацию: %1'"), Подпись), "");
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	ПодписьСтрока = UrlSafeBase64(Base64Строка(Подпись));
	
	РесурсНаСервере = "/esia-rs/api/public/v1/orgs/ext-app/"+ КлючДоступа + "/tkn?signature=" + ПодписьСтрока;
	
	ЗаголовкиHTTP = Новый Соответствие(); 
	
	ЗаголовкиHTTP.Вставить("Content-Type", "application/json");
	ЗаголовкиHTTP.Вставить("Connection", "Keep-Alive");
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	
	ОтветHTTP = Неопределено;
	ВидОперации = НСтр("ru='Сервис мобильной подписи.Авторизация в Госключе'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Попытка
		Соединение = СоединениеССерверомИнтернета(ВидОперации, СвойстваСервисаМобильнойПодписи.АдресСервера);
		СоединениеHTTP = Соединение.HTTPСоединение;
		ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("GET", ЗапросHTTP);
	
	Исключение
		ПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Не удалось авторизоваться в Госключе'"), ЦельАвторизации);
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, "", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецПопытки;

	Если ОтветHTTP.КодСостояния = 200 Тогда
		Попытка
			СтруктураОтвета = СтруктураОтвета(ОтветHTTP);
			Результат.ТокенДоступа = ?(СтруктураОтвета.Свойство("accessTkn"), СтруктураОтвета.accessTkn, "");
			
			Если Не ЗначениеЗаполнено(Результат.ТокенДоступа) И СтруктураОтвета.Свойство("ОшибкаИзСервиса") И ЗначениеЗаполнено(СтруктураОтвета.ОшибкаИзСервиса) Тогда
				ВызватьИсключение СтруктураОтвета.ОшибкаИзСервиса;
			КонецЕсли;
			
		Исключение
			ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Не удалось получить токен доступа в Госключе'"), ЦельАвторизации);
			Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, "", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
			Возврат Результат;
		КонецПопытки;
	Иначе
		ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Отказ при получении токена доступа в Госключе'"), ЦельАвторизации);
		ПредставлениеОшибки = ПредставлениеОшибки(ОтветHTTP);
		ЗаписьЖурналаРегистрации(ВидОперации, УровеньЖурналаРегистрации.Ошибка, , Настройка.Ссылка,
			ПредставлениеОшибки);
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, 
			НСтр("ru='Не удалось получить токен доступа в Госключе. Подробнее см. в журнале регистрации.'"), "", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Результат.ТокенДоступа) Тогда
		ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Госключ не предоставил токен для авторизации'"), ЦельАвторизации);
		ПредставлениеОшибки = НСтр("ru='Госключ не предоставил токен для авторизации.'");
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, "", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецЕсли;
	
	Попытка
		РезультатРазбора = РазобратьJWT(Результат.ТокенДоступа);
		ДействуетДо = Дата(1970,1,1) + РезультатРазбора.Сообщение["exp"];
	Исключение
		ЗаголовокОшибки = ?(ЦельАвторизации = "", НСтр("ru='Не удалось разобрать токен авторизации'"), ЦельАвторизации);
		ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.Ошибка = ОшибкаМобильногоСервиса(ЗаголовокОшибки, ПредставлениеОшибки, "", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Результат;
	КонецПопытки;
	
	ПараметрыРезультатаАвторизации = ПараметрыРезультатаАвторизацииВГосключе();
	Результат.ДействуетДо = ДействуетДо;
	ПараметрыРезультатаАвторизации.РезультатАвторизации = Новый ФиксированнаяСтруктура(Результат);
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Настройка.Ссылка, ПараметрыРезультатаАвторизации, "ПараметрыАвторизацииВГосключе");
	УстановитьПривилегированныйРежим(Ложь);

	Возврат Результат;
	
КонецФункции

Функция СвойстваСервисаАвторизацииГосключа(Настройка)
	
	URLСервера = Настройка.АдресЕСИА;
	Если Не ЗначениеЗаполнено(URLСервера) Тогда
		URLСервера = "https://esia.gosuslugi.ru";
	КонецЕсли;
	
	Результат = Новый Структура;
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(URLСервера);
	Результат.Вставить("АдресСервера", СтруктураURI.Схема + "://" + СтруктураURI.ИмяСервера);
	Результат.Вставить("РесурсКорняAPI", "/" + СтруктураURI.ПутьНаСервере);
	Результат.Вставить("Схема", СтруктураURI.Схема);
	Результат.Вставить("Порт", СтруктураURI.Порт);
	Результат.Вставить("Хост", СтруктураURI.Хост);
	
	Возврат Результат;
	
КонецФункции

Функция НастройкаОтправки(Параметры, ДляАвторизации = Ложь) Экспорт 
	
	НастройкаОтправки = Новый Структура("Госключ", Ложь);
	
	Если Не ЗначениеЗаполнено(Параметры.ВидМобильнойПодписи) 
		Или ((Параметры.ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.ГосключУКЭП
			Или Параметры.ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.ГосключУНЭП)
			И Не ЗначениеЗаполнено(Параметры.НастройкаОтправки)) Тогда
			
		Если Не ЗначениеЗаполнено(Параметры.ВидМобильнойПодписи) Тогда
			Ошибка = НСтр("ru = 'Не указан сервис для отправки документов на подписание.'");
		Иначе
			Ошибка = НСтр("ru = 'Недоступны настройки для отправки подписанту в приложение ""Госключ"", обратитесь к администратору.'");
		КонецЕсли;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Если Параметры.ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.ГосключУКЭП
			Или Параметры.ВидМобильнойПодписи = Перечисления.ВидыМобильныхПодписей.ГосключУНЭП Тогда
			
		НастройкаОтправки = НастройкаОтправкиРеквизиты(Параметры.НастройкаОтправки);
		НастройкаОтправки.Вставить("Госключ", Истина);
		
		РеквизитыОрганизации = СервисМобильнойПодписиКлиентСервер.РеквизитыОрганизации(НастройкаОтправки.Организация);
		
		Если ЗначениеЗаполнено(НастройкаОтправки.Организация) Тогда
			ПриЗаполненииРеквизитовОрганизации(РеквизитыОрганизации);
		Иначе
			РеквизитыОрганизации.ИНН = НастройкаОтправки.ИНН;
			РеквизитыОрганизации.НаименованиеОрганизации = НастройкаОтправки.НаименованиеОрганизации;
			РеквизитыОрганизации.ОГРН = НастройкаОтправки.ОГРН;
			РеквизитыОрганизации.КодОКАТО = НастройкаОтправки.КодОКАТО;
		КонецЕсли;
		
		Если Не ДляАвторизации Тогда

			Ошибки = Новый Массив;
			Если Не ЗначениеЗаполнено(РеквизитыОрганизации.НаименованиеОрганизации) Тогда
				Ошибки.Добавить(НСтр("ru = 'не указано наименование организации отправителя'"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(РеквизитыОрганизации.ИНН) Тогда
				Ошибки.Добавить(НСтр("ru = 'не указан ИНН организации отправителя'"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(РеквизитыОрганизации.ОГРН) Тогда
				Ошибки.Добавить(НСтр("ru = 'не указан ОГРН организации отправителя'"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(РеквизитыОрганизации.КодОКАТО) Тогда
				Ошибки.Добавить(НСтр("ru = 'не указан Код ОКАТО организации отправителя'"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(НастройкаОтправки.Сертификат) Тогда
				Ошибки.Добавить(НСтр("ru = 'не указан сертификат организации отправителя'"));
			КонецЕсли;
			Если Ошибки.Количество() > 0 Тогда
				Ошибка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В настройке отправки в Госключ ""%1"":
					 |%2.'"), Параметры.НастройкаОтправки, СтрСоединить(Ошибки, Символы.ПС));
				Возврат Ошибка;
			КонецЕсли;

		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(НастройкаОтправки, РеквизитыОрганизации, Истина);
		НастройкаОтправки.Вставить("РазрешеноПодписаниеНаСервере", РазрешеноПодписаниеНаСервере());
		
		УстановитьПривилегированныйРежим(Истина);
		ПарольСертификатаУстановлен = ЗначениеЗаполнено(
			ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Параметры.НастройкаОтправки.Ссылка, "ПарольСертификата"));
		НастройкаОтправки.Вставить("ПарольСертификатаУстановлен", ПарольСертификатаУстановлен);
		ПараметрыАвторизацииВГосключе = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
			Параметры.НастройкаОтправки.Ссылка, "ПараметрыАвторизацииВГосключе");
		Если ЗначениеЗаполнено(ПараметрыАвторизацииВГосключе) Тогда
			ПараметрыАвторизацииВГосключеУстановлены = ТекущаяУниверсальнаяДата()
				< ПараметрыАвторизацииВГосключе.РезультатАвторизации.ДействуетДо;
		Иначе
			ПараметрыАвторизацииВГосключеУстановлены = Ложь;
		КонецЕсли;
		НастройкаОтправки.Вставить("ПараметрыАвторизацииВГосключеУстановлены", ПараметрыАвторизацииВГосключеУстановлены);
		Если Не ПараметрыАвторизацииВГосключеУстановлены И (Не НастройкаОтправки.ПарольСертификатаУстановлен
			Или Не НастройкаОтправки.РазрешеноПодписаниеНаСервере) Тогда
			НастройкаОтправки.Вставить("Ключ", 
				ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Параметры.НастройкаОтправки.Ссылка, "КлючДоступа"));
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);

	КонецЕсли;
	
	Возврат НастройкаОтправки;
КонецФункции

Функция НастройкаОтправкиРеквизиты(НастройкаОтправки)
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаОтправки, "Ссылка, АдресСервиса, АдресЕСИА, Сертификат, Организация, ИНН, НаименованиеОрганизации, ОГРН, КодОКАТО");
КонецФункции

Процедура ПриЗаполненииРеквизитовОрганизации(Реквизиты) Экспорт
	
	СтандартнаяОбработка = Истина;
	СервисМобильнойПодписиПереопределяемый.ПриЗаполненииРеквизитовОрганизации(Реквизиты, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Организации") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Реквизиты.Ссылка) Тогда
		Возврат; // Реквизиты организации нельзя заполнить, если организация не выбрана.
	КонецЕсли;
	
	МодульОрганизацииСервер = ОбщегоНазначения.ОбщийМодуль("ОрганизацииСервер");
	СведенияОбОрганизации = МодульОрганизацииСервер.СведенияОбОрганизации(Реквизиты.Ссылка,, ТекущаяДатаСеанса());
	
	ЭтоИП = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ЭтоИП", Ложь);
	
	Если ЭтоИП Тогда
		
		Реквизиты.НаименованиеОрганизации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ИПНаименование", "");
		Реквизиты.ИНН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ИндивидуальныйПредпринимательИНН", "");
		Реквизиты.ОГРН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ОГРН", "");
		Реквизиты.ЛицоБезДоверенности = Неопределено;
	
	Иначе
	
		Реквизиты.ИНН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ИНН", "");
		Реквизиты.НаименованиеОрганизации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "НаименованиеСокращенное", "");
		Реквизиты.ОГРН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ОГРН", "");
		
		ЮридическийАдрес = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "АдресОрганизации", "");
		ЮридическийАдресЗначение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "АдресОрганизацииJSON", "");
		
	КонецЕсли;

КонецПроцедуры

Функция СвойстваСервисаГосключа(НастройкиПодписания)
	
	URLСервера = НастройкиПодписания.АдресСервиса;
	Если Не ЗначениеЗаполнено(URLСервера) Тогда
		URLСервера = "https://www.gosuslugi.ru/";
	КонецЕсли;
	
	Результат = Новый Структура;
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(URLСервера);
	Результат.Вставить("АдресСервера", СтруктураURI.Схема + "://" + СтруктураURI.ИмяСервера);
	Результат.Вставить("РесурсКорняAPI", "/" + СтруктураURI.ПутьНаСервере);
	Результат.Вставить("Схема", СтруктураURI.Схема);
	Результат.Вставить("Порт", СтруктураURI.Порт);
	Результат.Вставить("Хост", СтруктураURI.Хост);
	
	Возврат Результат;
	
КонецФункции

Функция РазрешеноПодписаниеНаСервере()
	Возврат ЭлектроннаяПодпись.ОбщиеНастройки().СоздаватьЭлектронныеПодписиНаСервере
		Или ОбщегоНазначения.ИнформационнаяБазаФайловая() И Не ОбщегоНазначения.КлиентПодключенЧерезВебСервер();
КонецФункции

Функция ПодписатьДанныеДляОтправкиВГосключ(Данные, Сертификат, Пароль, Присоединенная, МенеджерКриптографии = Неопределено)
	
	Если Не РазрешеноПодписаниеНаСервере() Тогда
		Возврат НСтр("ru='Не настроено подписание на сервере.'");
	КонецЕсли;
	
	ОписаниеОшибки = "";
	Если ТипЗнч(Сертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		ДанныеСертификата = ЭлектроннаяПодписьСлужебныйВызовСервера.ДанныеСертификата(Сертификат);
		Если ТипЗнч(ДанныеСертификата) <> Тип("ДвоичныеДанные") Тогда
			Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Сертификат %1 не содержит данных.'"), Сертификат);
		КонецЕсли;
		Сертификат = Новый СертификатКриптографии(ДанныеСертификата);
		МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("Подписание", Ложь, ОписаниеОшибки, ДанныеСертификата);
	ИначеЕсли МенеджерКриптографии = Неопределено Тогда
		ДанныеСертификата = Сертификат.Выгрузить();
		МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("Подписание", Ложь, ОписаниеОшибки, ДанныеСертификата);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Возврат ОписаниеОшибки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Пароль) Тогда
		МенеджерКриптографии.ПарольДоступаКЗакрытомуКлючу = Пароль;
	КонецЕсли;
	
	Если Присоединенная Тогда
		Параметры = ЭлектроннаяПодпись.ПараметрыCMS();
		Подпись = ЭлектроннаяПодписьСлужебный.ПодписатьCMS(Данные, Параметры, Сертификат, МенеджерКриптографии);
	Иначе
		Подпись = МенеджерКриптографии.Подписать(Данные, Неопределено, Сертификат, ТипПодписиКриптографии.CAdESBES);
	КонецЕсли;
	
	Возврат Подпись;
КонецФункции

Функция UrlSafeBase64(Строка)
	Строка = СтрЗаменить(Строка, Символы.ВК, "");
	Строка = СтрЗаменить(Строка, Символы.ПС, "");
	Строка = СтрЗаменить(СтрЗаменить(Строка, "+", "-"), "/", "_");
	Возврат СтрЗаменить(Строка, "=", "");
КонецФункции

Процедура ОбработатьОшибкуГосключа(СтруктураОтвета)
	
	Если СтруктураОтвета.code = "VALIDATION_ERROR" Тогда
		ОшибкаИзСервиса = СтруктураОтвета.message;
	ИначеЕсли СтруктураОтвета.code = "INTERNAL_ERROR" Тогда
		ОшибкаИзСервиса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Обратитесь в техническую поддержку Госключа или попробуйте позднее. %1 (%2).'"),СтруктураОтвета.message, СтруктураОтвета.code);
	Иначе
		ОшибкаИзСервиса = СтрШаблон("%1 (%2).", СтруктураОтвета.message, СтруктураОтвета.code);
	КонецЕсли;
	
	СтруктураОтвета.Вставить("ОшибкаИзСервиса", ОшибкаИзСервиса);
	
КонецПроцедуры

Функция РазобратьJWT(Знач Токен)
	
	Части = СтрРазделить(Токен, ".");
	Если Части.Количество() < 3 Тогда
		ВызватьИсключение НСтр("ru='Неверный формат JWT'");
	КонецЕсли;
	
	ЗаголовокBase64 = Части[0];
	СообщениеBase64 = Части[1];
	ПодписьBase64 = Части[2];
	
	ЗаголовокJSON = ИзUrlSafeBase64(ЗаголовокBase64);
	СообщениеJSON = ИзUrlSafeBase64(СообщениеBase64);
	
	Результат = Новый Структура;
	Результат.Вставить("Заголовок", ОбщегоНазначения.JSONВЗначение(ЗаголовокJSON));
	Результат.Вставить("Сообщение", ОбщегоНазначения.JSONВЗначение(СообщениеJSON));
	Результат.Вставить("Подпись", ПодписьBase64);
	
	Возврат Результат;
КонецФункции

Функция ИзUrlSafeBase64(Строка)
	
	Строка = СтрЗаменить(СтрЗаменить(Строка, "-", "+"), "_", "/");
	
	Пока СтрДлина(Строка) % 4 <> 0 Цикл
		Строка = Строка + "=";
	КонецЦикла;
	
	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзBase64Строки(Строка);
	Возврат ПолучитьСтрокуИзДвоичныхДанных(ДвоичныеДанные, "UTF-8");
	
КонецФункции

Функция ПараметрыРезультатаАвторизацииВГосключе()
	
	ПараметрыРезультатаАвторизации = Новый Структура;
	ПараметрыРезультатаАвторизации.Вставить("РезультатАвторизации", Неопределено);
	Возврат ПараметрыРезультатаАвторизации;
	
КонецФункции

Процедура ОчиститьВременныеФайлы(ВременныеФайлы, ВременныйКаталог)
	
	Для Каждого ВременныйФайл Из ВременныеФайлы Цикл
		ФайловаяСистема.УдалитьВременныйФайл(ВременныйФайл);
	КонецЦикла;
	
	ФайловаяСистема.УдалитьВременныйКаталог(ВременныйКаталог); 
	
КонецПроцедуры

Процедура ДобавитьФайлВАрхив(Архив, ДанныеФайла, ИмяФайла, ВременныйКаталог, ВременныеФайлы)
	
	Разделитель = ПолучитьРазделительПути();
	ВременныйФайл = ВременныйКаталог
		+ ?(СтрЗаканчиваетсяНа(ВременныйКаталог, Разделитель), "", Разделитель)
		+ ИмяФайла;
	ВременныеФайлы.Добавить(ВременныйФайл);
	ДанныеФайла.Записать(ВременныйФайл);
	Архив.Добавить(ВременныйФайл);
	
КонецПроцедуры

Функция ОтправитьЗапросВГосключ(ДД, Настройка, ПараметрыПодписанта, МаркерДоступа, АдресСервиса)

	РесурсНаСервере = "/api/gusmev/push";

	ЗаголовкиHTTP = Новый Соответствие(); 
	
	ЗаголовкиHTTP.Вставить("Content-Type", "multipart/form-data; boundary=My1cV8tyrx");
	ЗаголовкиHTTP.Вставить("Authorization", "Bearer " + МаркерДоступа);
	
	МассивДвоичныхДанных = Новый Массив();
	
	ШаблонФайла = "--My1cV8tyrx
		|Content-Disposition: form-data; name=""file""; filename=""req.zip""
		|Content-Type: application/octet-stream
		|
		|";
	СодержимоеФайла = СтрЗаменить(ШаблонФайла, Символы.ПС, Символы.ВК + Символы.ПС);
	
	МассивДвоичныхДанных.Добавить(ПолучитьДвоичныеДанныеИзСтроки(СодержимоеФайла)); 
	МассивДвоичныхДанных.Добавить(ДД);
		
	ШаблонМетаданных = "
		|--My1cV8tyrx
		|Content-Disposition: form-data; name=""meta""
		|Content-Type: application/json
		|
		|";
	
	СодержимоеФайла = СтрЗаменить(ШаблонМетаданных, Символы.ПС, Символы.ВК + Символы.ПС);
	
	МассивДвоичныхДанных.Добавить(ПолучитьДвоичныеДанныеИзСтроки(СодержимоеФайла));
	
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ОГРН) Тогда
		Код = "60025907"
	Иначе
		Код = "10000000374"
	КонецЕсли;
	
	Мета = ОбщегоНазначения.ЗначениеВJSON(
		Новый Структура("region, serviceCode, targetCode", Настройка.КодОКАТО, Код, "-"+ Код));
	МассивДвоичныхДанных.Добавить(ПолучитьДвоичныеДанныеИзСтроки(Мета));
	
	ШаблонФайла = "
		|--My1cV8tyrx--";
	СодержимоеФайла = СтрЗаменить(ШаблонФайла,  Символы.ПС, Символы.ВК + Символы.ПС);
	
	МассивДвоичныхДанных.Добавить(ПолучитьДвоичныеДанныеИзСтроки(СодержимоеФайла));
	
	ПередаваемыеДанные = СоединитьДвоичныеДанные(МассивДвоичныхДанных);
		
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере, ЗаголовкиHTTP);
	ЗапросHTTP.УстановитьТелоИзДвоичныхДанных(ПередаваемыеДанные);
	
	ВидОперации = НСтр("ru='Сервис мобильной подписи.Отправка запроса в Госключ'", ОбщегоНазначения.КодОсновногоЯзыка());
	ОтветHTTP = Неопределено;
	Попытка
		Соединение = СоединениеССерверомИнтернета(ВидОперации, АдресСервиса);
		СоединениеHTTP = Соединение.HTTPСоединение;
		ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("POST", ЗапросHTTP); 
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(ВидОперации, УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		Ошибка = ОшибкаМобильногоСервиса(НСтр("ru='Не удалось отправить запрос в Госключ'"),
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке),
			"", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Ошибка;
	КонецПопытки;
	
	Попытка	
		Структура = СтруктураОтвета(ОтветHTTP);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(ВидОперации, УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		Ошибка = ОшибкаМобильногоСервиса(НСтр("ru='Не удалось прочитать ответ Госключа'"),
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке), "", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Ошибка;
	КонецПопытки;
	
	Если Структура.Свойство("ОшибкаИзСервиса") И ЗначениеЗаполнено(Структура.ОшибкаИзСервиса) Тогда
		Ошибка = ОшибкаМобильногоСервиса(НСтр("ru='Не удалось отправить запрос в Госключ'"), Структура.ОшибкаИзСервиса, "");
		Возврат Ошибка;
	КонецЕсли;

	НомерЗаявки = ?(Структура.Свойство("orderID"), Структура.orderID, "");
	Если ЗначениеЗаполнено(НомерЗаявки) Тогда
		ЗаписьЖурналаРегистрации(ВидОперации, УровеньЖурналаРегистрации.Информация, , Настройка.Ссылка,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Номер заявки %1, настройка отправки: %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				Формат(НомерЗаявки, "ЧГ=0"), Настройка.Ссылка));
		Возврат НомерЗаявки;
	 Иначе 
		ПредставлениеОшибки = НСтр("ru='Не удалось получить идентификатор запроса в Госключе'");
		Ошибка = ОшибкаМобильногоСервиса(НСтр("ru='Госключ вернул пустой номер заявки.'"), ПредставлениеОшибки, "", СоединениеHTTP, ЗапросHTTP, ОтветHTTP);
		Возврат Ошибка;
	КонецЕсли;

КонецФункции

Функция ИдентификаторСхемыМЧД(ПараметрыПодписанта)
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ОГРН) Тогда
		Возврат "urn://www.1c.ru/ssl/gosuslugi/sign_document_ukep_legalperson/1.0.0";
	Иначе
		Возврат "urn://www.1c.ru/ssl/gosuslugi/sign_document/1.0.0";
	КонецЕсли;
	
КонецФункции

Функция ЦелевоеПространствоИмен(ПараметрыПодписанта)
	Если ПараметрыПодписанта.ГосключУНЭП = Истина Тогда
		Возврат "urn://mpkey.gosuslugi.ru/sign_document/1.0.0"
	Иначе
		Возврат "urn://mpkey.gosuslugi.ru/sign_document_ukep/1.0.0"
	КонецЕсли;
КонецФункции

Функция ЗапросНаОтправкуДокумента(ПараметрыПодписанта, ПараметрыОтправителя) Экспорт
	
	ИдентификаторСхемыМЧД = ИдентификаторСхемыМЧД(ПараметрыПодписанта);
	
	Запрос = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ИдентификаторСхемыМЧД, "SignRequestType"));
		
	СНИЛС = Лев(ПараметрыПодписанта.СНИЛС, 3) + "-" + Сред(ПараметрыПодписанта.СНИЛС, 4, 3) + "-" + Сред(
		ПараметрыПодписанта.СНИЛС, 7, 3) + " " + Прав(ПараметрыПодписанта.СНИЛС, 2);
	
	Если ЗначениеЗаполнено(ПараметрыПодписанта.ВидПодписанта) = Перечисления.ВидыПодписантовМобильногоСервиса.ФизическоеЛицо Тогда
		Запрос.Snils = СНИЛС;
	ИначеЕсли ЗначениеЗаполнено(ПараметрыПодписанта.ОГРН) Тогда
		ПараметрыОрганизации = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ИдентификаторСхемыМЧД, "RFOrgType"));
		ПараметрыОрганизации.ogrn = ПараметрыПодписанта.ОГРН;
		ПараметрыОрганизации.Snils = СНИЛС;
		Запрос.RFOrg = ПараметрыОрганизации;
	ИначеЕсли ЗначениеЗаполнено(ПараметрыПодписанта.НомерЗаписиОбАккредитации) Тогда
		ПараметрыОрганизации = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ИдентификаторСхемыМЧД, "ForeignOrgType"));
		ПараметрыОрганизации.RAFP = ПараметрыПодписанта.НомерЗаписиОбАккредитации;
		ПараметрыОрганизации.INN_user = ПараметрыПодписанта.ИННФЛ;
		Запрос.ForeignOrg = ПараметрыОрганизации;
	Иначе
		Запрос.Snils = СНИЛС;
	КонецЕсли;

	Запрос.Description = НСтр("ru='Документы на подпись'");
	Атрибут = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ИдентификаторСхемыМЧД, "AttributeType"));
	Атрибут.AttributeName = "orgName";
	Атрибут.AttributeValue = Лев(ПараметрыОтправителя.НаименованиеОрганизации, 250);
	Запрос.Attribute.Добавить(Атрибут);
	Атрибут = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ИдентификаторСхемыМЧД, "AttributeType"));
	Атрибут.AttributeName = "orgINN";
	Атрибут.AttributeValue = ПараметрыОтправителя.ИНН;
	Запрос.Attribute.Добавить(Атрибут);

	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, Запрос, "SignRequest");
	ЗапросНаОтправкуДокументаXML = ЗаписьXML.Закрыть();
	
	ЗапросНаОтправкуДокументаXML = СтрЗаменить(ЗапросНаОтправкуДокументаXML, """" + ИдентификаторСхемыМЧД(ПараметрыПодписанта) + """",
		 """" + ЦелевоеПространствоИмен(ПараметрыПодписанта)  + """");
	
	Возврат ЗапросНаОтправкуДокументаXML;
	
КонецФункции

Процедура ПреобразоватьИмяФайла(ИмяФайла, Госключ = Истина)
	
	Если Госключ Тогда
		ИмяФайла = СтроковыеФункции.СтрокаЛатиницей(ИмяФайла);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла);
	МаксимальнаяДлина = ?(Госключ, 150, 250);
	Если СтрДлина(ИмяФайла) > МаксимальнаяДлина Тогда
		Файл = Новый Файл(ИмяФайла);
		ТребуемаяДлина = МаксимальнаяДлина - СтрДлина(Файл.Расширение) - 1;
		ИмяБезРасширения = ОбщегоНазначения.СократитьСтрокуКонтрольнойСуммой(Файл.ИмяБезРасширения, ТребуемаяДлина);
		ИмяФайла = ИмяБезРасширения + Файл.Расширение;
	КонецЕсли;
	
	ИмяФайла = СтрЗаменитьПоРегулярномуВыражению(ИмяФайла, "[^\.\(\)\w,\s-]", "_");
	Если Не Госключ Тогда
		ИмяФайла = СтрЗаменить(ИмяФайла, "№", "N");
	Иначе
		ИмяФайла = СтрЗаменить(ИмяФайла, " ", "_");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОповеститьОНеобходимостиАвторизацииВГосключе(Настройка)

	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);

	ТекстЗапроса = "ВЫБРАТЬ
	               |	НастройкиОтправкиВГосключПользователи.Пользователь.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
	               |ИЗ
	               |	Справочник.НастройкиОтправкиВГосключ.Пользователи КАК НастройкиОтправкиВГосключПользователи
	               |ГДЕ
	               |	НастройкиОтправкиВГосключПользователи.Ссылка = &Ссылка
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НастройкиОтправкиВГосключ.Ответственный.ИдентификаторПользователяИБ
	               |ИЗ
	               |	Справочник.НастройкиОтправкиВГосключ КАК НастройкиОтправкиВГосключ
	               |ГДЕ
	               |	НастройкиОтправкиВГосключ.Ссылка = &Ссылка";

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Настройка);
	Выборка = Запрос.Выполнить().Выбрать();

	Адресаты = Новый Соответствие;

	Пока Выборка.Следующий() Цикл
		Адресаты.Вставить(Выборка.ИдентификаторПользователяИБ, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("*"));
	КонецЦикла;

	СерверныеОповещения.ОтправитьСерверноеОповещение(СервисМобильнойПодписиКлиентСервер.ИмяСерверногоОповещения(),
		Настройка, Адресаты);
	УстановитьПривилегированныйРежим(Ложь);
	УстановитьОтключениеБезопасногоРежима(Ложь);
	
КонецПроцедуры

// См. СтандартныеПодсистемыСервер.ПриОпределенииМетодовРазрешенныхДляВызоваКакПроизвольныйКод
Процедура ПриОпределенииМетодовРазрешенныхДляВызоваКакПроизвольныйКод(Методы) Экспорт
	
	Методы.Вставить("УстановитьПараметрСеансаПараметрыАвторизацииВСервисеМобильнойПодписи");
	Методы.Вставить("ОтправитьНаПодписание", Истина);
	Методы.Вставить("ПолучениеРезультатовИзСервисаМобильнойПодписи", Истина);
	
КонецПроцедуры

#КонецОбласти

