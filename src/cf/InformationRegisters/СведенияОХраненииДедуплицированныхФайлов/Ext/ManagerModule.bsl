///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает параметры хранения дедуплицированных файлов для переданного элемента справочника "ХранилищеДвоичныхДанных"
// и вида хранения.
//
// Параметры:
//  ХранилищеДвоичныхДанныхСсылка	- СправочникСсылка.ХранилищеДвоичныхДанных - значение измерения "ХранилищеДвоичныхДанных".
//  ВидХраненияФайлов				- ПеречислениеСсылка.ВидыХраненияФайлов, Неопределено - значение измерения "ВидХраненияФайлов".
//
// Возвращаемое значение:
//  - Структура - если параметры хранения существуют, то содержит параметров хранения "ТипХраненияФайла", "Том" и "ПутьКФайлу".
//  - Неопределено - если параметры хранения отсутствуют в регистре.
//
Функция СведенияОХраненииДедуплицированныхФайлов(ХранилищеДвоичныхДанныхСсылка, ВидХраненияФайлов = Неопределено) Экспорт

	Если ВидХраненияФайлов = Неопределено Тогда
		ВидХраненияФайлов = Перечисления.ВидыХраненияФайлов.ОперационноеХранение;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ХранилищеДвоичныхДанных"	, ХранилищеДвоичныхДанныхСсылка);
	Запрос.УстановитьПараметр("ВидХраненияФайлов"		, ВидХраненияФайлов);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОХраненииДедуплицированныхФайлов.ТипХраненияФайла КАК ТипХраненияФайла,
		|	СведенияОХраненииДедуплицированныхФайлов.Том КАК Том,
		|	"""" КАК ПутьКФайлу
		|ИЗ
		|	РегистрСведений.СведенияОХраненииДедуплицированныхФайлов КАК СведенияОХраненииДедуплицированныхФайлов
		|ГДЕ
		|	СведенияОХраненииДедуплицированныхФайлов.ХранилищеДвоичныхДанных = &ХранилищеДвоичныхДанных
		|	И СведенияОХраненииДедуплицированныхФайлов.ВидХраненияФайлов = &ВидХраненияФайлов";

	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда

		Результат = Новый Структура("ТипХраненияФайла,Том,ПутьКФайлу");
		ЗаполнитьЗначенияСвойств(Результат, ВыборкаДанных);

	Иначе
		Результат = Неопределено;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Добавляет запись в регистр сведений.
//
// Параметры:
//  ХранилищеДвоичныхДанныхСсылка	- СправочникСсылка.ХранилищеДвоичныхДанных - значение измерения "ХранилищеДвоичныхДанных".
//  ПараметрыЗаписиВИБ				- Структура - см. РаботаСФайловымАрхивомСервер.ПараметрыЗаписиВИнформационнуюБазу
//
Процедура ДобавитьЗапись(ХранилищеДвоичныхДанныхСсылка, ПараметрыЗаписиВИБ) Экспорт

	Если ПараметрыЗаписиВИБ.ЭтоЗаписьВФайловыйАрхив Тогда
		ВидХраненияФайлов = Перечисления.ВидыХраненияФайлов.АрхивноеХранение;
	Иначе
		ВидХраненияФайлов = Перечисления.ВидыХраненияФайлов.ОперационноеХранение;
	КонецЕсли;

	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.ХранилищеДвоичныхДанных.Установить(ХранилищеДвоичныхДанныхСсылка);
	Набор.Отбор.ВидХраненияФайлов.Установить(ВидХраненияФайлов);

	Запись = Набор.Добавить();
	Запись.ХранилищеДвоичныхДанных	= ХранилищеДвоичныхДанныхСсылка;
	Запись.ВидХраненияФайлов		= ВидХраненияФайлов;
	Запись.ТипХраненияФайла			= ПараметрыЗаписиВИБ.ТипХраненияФайла;
	Запись.Том						= ПараметрыЗаписиВИБ.Том;

	Набор.Записать();

КонецПроцедуры

// Удаляет запись из регистра сведений.
//
// Параметры:
//  ХранилищеДвоичныхДанныхСсылка	- СправочникСсылка.ХранилищеДвоичныхДанных - значение измерения "ХранилищеДвоичныхДанных".
//  ВидХраненияФайлов				- ПеречислениеСсылка.ВидыХраненияФайлов, Неопределено - значение измерения "ВидХраненияФайлов".
//
Процедура УдалитьЗапись(ХранилищеДвоичныхДанныхСсылка, ВидХраненияФайлов = Неопределено) Экспорт
	
	Блокировка = Новый БлокировкаДанных();
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СведенияОХраненииДедуплицированныхФайлов");
	ЭлементБлокировки.УстановитьЗначение("ХранилищеДвоичныхДанных", ХранилищеДвоичныхДанныхСсылка);

	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ХранилищеДвоичныхДанных.Установить(ХранилищеДвоичныхДанныхСсылка);
	
	Если ЗначениеЗаполнено(ВидХраненияФайлов) Тогда
		НаборЗаписей.Отбор.ВидХраненияФайлов.Установить(ВидХраненияФайлов);	
	
		ЭлементБлокировки.УстановитьЗначение("ВидХраненияФайлов", ВидХраненияФайлов);
	
	КонецЕсли;	

	НачатьТранзакцию();
	Попытка

		Блокировка.Заблокировать();

		НаборЗаписей.Записать();

		ЗафиксироватьТранзакцию();

	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

#Область ОбработчикиОбновления

// Регистрирует на плане обмена ОбновлениеИнформационнойБазы объекты,
// для которых необходимо добавить записи в регистре.
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РезультатОбъединения.Ссылка КАК Ссылка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ХранилищеФайлов.ХранилищеДвоичныхДанных КАК Ссылка,
	|		1 КАК ИзХранилищеФайлов,
	|		0 КАК ИзСведенияОХраненииДедуплицированныхФайлов
	|	ИЗ
	|		РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СведенияОХраненииДедуплицированныхФайлов.ХранилищеДвоичныхДанных,
	|		0,
	|		1
	|	ИЗ
	|		РегистрСведений.СведенияОХраненииДедуплицированныхФайлов КАК СведенияОХраненииДедуплицированныхФайлов) КАК РезультатОбъединения
	|
	|СГРУППИРОВАТЬ ПО
	|	РезультатОбъединения.Ссылка
	|
	|ИМЕЮЩИЕ
	|	СУММА(РезультатОбъединения.ИзХранилищеФайлов) > 0 И
	|	СУММА(РезультатОбъединения.ИзСведенияОХраненииДедуплицированныхФайлов) = 0";

	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, РезультатЗапроса.ВыгрузитьКолонку("Ссылка"));

КонецПроцедуры

// Обработчик обновления на новую версию:
// - Выполняет добавление записей в регистр сведений "СведенияОХраненииДедуплицированныхФайлов" для используемых
// элементов справочника "ХранилищеДвоичныхДанных".
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ХранилищаДвоичныхДанных = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, "Справочник.ХранилищеДвоичныхДанных");

	Если ХранилищаДвоичныхДанных.Количество() > 0 Тогда

		МетаданныеРегистра		= Метаданные.РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов;
		ПредставлениеРегистра	= МетаданныеРегистра.Представление();		
		ИмяПроцедуры			= МетаданныеРегистра.ПолноеИмя() + "." + "ОбработатьДанныеДляПереходаНаНовуюВерсию";

		ОбъектовОбработано = 0;
		ПроблемныхОбъектов = 0;

		Пока ХранилищаДвоичныхДанных.Следующий() Цикл

			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СведенияОХраненииДедуплицированныхФайлов");
			ЭлементБлокировкиДанных.УстановитьЗначение("ХранилищеДвоичныхДанных", ХранилищаДвоичныхДанных.Ссылка);
			ЭлементБлокировкиДанных.УстановитьЗначение("ВидХраненияФайлов"		, Перечисления.ВидыХраненияФайлов.ОперационноеХранение);

			ПараметрыЗаписиВИБ = РаботаСФайловымАрхивомСервер.ПараметрыЗаписиВИнформационнуюБазу();
			ПараметрыЗаписиВИБ.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе;			

			НачатьТранзакцию();
			Попытка		

				БлокировкаДанных.Заблокировать();		

				ДобавитьЗапись(ХранилищаДвоичныхДанных.Ссылка, ПараметрыЗаписиВИБ);

				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ХранилищаДвоичныхДанных.Ссылка);
				ОбъектовОбработано = ОбъектовОбработано + 1;
				ЗафиксироватьТранзакцию();
				
			Исключение

				ОтменитьТранзакцию();

				ПроблемныхОбъектов = ПроблемныхОбъектов + 1;

				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось добавить запись в регистр сведений ""Сведения о хранении дедуплицированных файлов"" 
					|для элемента справочника ""Хранилище двоичных данных"" %1 по причине:
					|%2'"), 
					ХранилищаДвоичныхДанных.Ссылка, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

				ОбновлениеИнформационнойБазы.ЗаписатьОшибкуВЖурналРегистрации(
					МетаданныеРегистра,
					ПредставлениеРегистра,
					ТекстСообщения);

			КонецПопытки;
		КонецЦикла;

		Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать некоторые элементы справочника ""Хранилище двоичных данных"" (пропущены): %1'"), 
				ПроблемныхОбъектов);
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;

		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Информация, МетаданныеРегистра, ,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура %1 обработала очередную порцию записей: %2'"),
				ИмяПроцедуры,
				ОбъектовОбработано));
	КонецЕсли;

	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, "Справочник.ХранилищеДвоичныхДанных");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли