///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет запись в регистр сведений.
//
// Параметры:
//  ХранилищеДвоичныхДанныхСсылка	- СправочникСсылка.ХранилищеДвоичныхДанных - значение измерения "ХранилищеДвоичныхДанных".
//  ЗначенияАтрибутовЗаписи			- Структура
//
Процедура ДобавитьИзменитьЗапись(ХранилищеДвоичныхДанныхСсылка, ЗначенияАтрибутовЗаписи) Экспорт

	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.ХранилищеДвоичныхДанных.Установить(ХранилищеДвоичныхДанныхСсылка);

	Блокировка = Новый БлокировкаДанных;
	Блокировка.Добавить("РегистрСведений.МестаХраненияДвоичныхДанных").УстановитьЗначение("ХранилищеДвоичныхДанных", ХранилищеДвоичныхДанныхСсылка);

	НачатьТранзакцию();	
	Попытка

		Блокировка.Заблокировать();	
		
		Набор.Прочитать();
		Если Набор.Количество() = 0 Тогда
			Запись = Набор.Добавить();
		Иначе
			Запись = Набор[0];
		КонецЕсли;
		
		Запись.ХранилищеДвоичныхДанных	= ХранилищеДвоичныхДанныхСсылка;
		ЗаполнитьЗначенияСвойств(Запись, ЗначенияАтрибутовЗаписи);

		Набор.Записать();

		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

// Удаляет запись из регистра сведений.
//
// Параметры:
//  ХранилищеДвоичныхДанныхСсылка	- СправочникСсылка.ХранилищеДвоичныхДанных - значение измерения "ХранилищеДвоичныхДанных".
//
Процедура УдалитьЗапись(ХранилищеДвоичныхДанныхСсылка) Экспорт

	Запись = СоздатьМенеджерЗаписи();
	Запись.ХранилищеДвоичныхДанных = ХранилищеДвоичныхДанныхСсылка;

	Запись.Удалить();	

КонецПроцедуры

#КонецОбласти

#КонецЕсли