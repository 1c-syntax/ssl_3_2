///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимостьЭлементовФормы();
	
	Если ЗначениеЗаполнено(Запись.ВидТранспортаСообщенийОбменаПоУмолчанию) Тогда
		
		ИмяСтраницы = "НастройкиТранспорта[ВидТранспорта]";
		ИмяСтраницы = СтрЗаменить(ИмяСтраницы, "[ВидТранспорта]"
		, ОбщегоНазначения.ИмяЗначенияПеречисления(Запись.ВидТранспортаСообщенийОбменаПоУмолчанию));
		
		Если Элементы[ИмяСтраницы].Видимость Тогда
			
			Элементы.СтраницыВидовТранспорта.ТекущаяСтраница = Элементы[ИмяСтраницы];
			
		КонецЕсли;
	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.КонечнаяТочкаКорреспондента) Тогда
		УстановитьПривилегированныйРежим(Истина);
		Пароли = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Запись.КонечнаяТочкаКорреспондента, "FTPСоединениеПарольОбластейДанных, ПарольАрхиваСообщенияОбменаОбластейДанных");
		FTPСоединениеПароль = ?(ЗначениеЗаполнено(Пароли.FTPСоединениеПарольОбластейДанных), УникальныйИдентификатор, "");
		ПарольАрхиваСообщенияОбмена = ?(ЗначениеЗаполнено(Пароли.ПарольАрхиваСообщенияОбменаОбластейДанных), УникальныйИдентификатор, "");
		УстановитьПривилегированныйРежим(Ложь); 
		
		Если ЗначениеЗаполнено(Пароли.FTPСоединениеПарольОбластейДанных) Тогда
			Элементы.ПарольАрхиваСообщенияОбмена1.КнопкаВыбора = Ложь;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Пароли.ПарольАрхиваСообщенияОбменаОбластейДанных) Тогда
			Элементы.ПарольАрхиваСообщенияОбмена.КнопкаВыбора = Ложь;
		КонецЕсли;    
		
		Если ЗначениеЗаполнено(FTPСоединениеПароль) Тогда
			Элементы.FTPСоединениеПароль.КнопкаВыбора = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаПарольАрхива.Доступность = Запись.FTPСжиматьФайлИсходящегоСообщения;
	Элементы.ГруппаПарольАрхиваСообщенияОбмена.Доступность = Запись.FILEСжиматьФайлИсходящегоСообщения;

КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьПривилегированныйРежим(Истина);
	Если FTPСоединениеПарольИзменен Тогда
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Запись.КонечнаяТочкаКорреспондента, FTPСоединениеПароль, "FTPСоединениеПарольОбластейДанных");
	КонецЕсли;
	Если ПарольАрхиваСообщенияОбменаИзменен Тогда
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Запись.КонечнаяТочкаКорреспондента, ПарольАрхиваСообщенияОбмена, "ПарольАрхиваСообщенияОбменаОбластейДанных");
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура FILEКаталогОбменаИнформациейНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ОбработчикВыбораФайловогоКаталога(Запись, "FILEКаталогОбменаИнформацией", СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура FILEКаталогОбменаИнформациейОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ОбработчикОткрытияФайлаИлиКаталога(Запись, "FILEКаталогОбменаИнформацией", СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольАрхиваСообщенияОбменаПриИзменении(Элемент)
	ПарольАрхиваСообщенияОбменаИзменен = Истина;
КонецПроцедуры

&НаКлиенте
Процедура FTPСоединениеПарольПриИзменении(Элемент)
	FTPСоединениеПарольИзменен = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПарольАрхиваСообщенияОбмена1ПриИзменении(Элемент)
	ПарольАрхиваСообщенияОбменаИзменен = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПарольАрхиваСообщенияОбменаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ПолеПароляНачалоВыбора(Элемент, ПарольАрхиваСообщенияОбменаFTP, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольАрхиваСообщенияОбмена1НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ПолеПароляНачалоВыбора(Элемент, ПарольАрхиваСообщенияОбмена, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура FTPСоединениеПарольНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ПолеПароляНачалоВыбора(Элемент, FTPСоединениеПароль, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура FTPСжиматьФайлИсходящегоСообщенияПриИзменении(Элемент)
	
	Элементы.ГруппаПарольАрхива.Доступность = Запись.FTPСжиматьФайлИсходящегоСообщения;
	
КонецПроцедуры

&НаКлиенте
Процедура FILEСжиматьФайлИсходящегоСообщенияПриИзменении(Элемент)
	
	Элементы.ГруппаПарольАрхиваСообщенияОбмена.Доступность = Запись.FILEСжиматьФайлИсходящегоСообщения;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьПодключениеОбщаяКоманда(Команда)

	Если Элементы.СтраницыВидовТранспорта.ТекущаяСтраница = Элементы.НастройкиТранспортаFILE Тогда
		ПроверитьПодключение("FILE");
	Иначе
		ПроверитьПодключение("FTP");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СгенерироватьПароль(Команда)
	
	ОткрытьФормуСозданиеПароля();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолеПароляНачалоВыбора(Элемент, Реквизит, СтандартнаяОбработка)
	
	ТранспортСообщенийОбменаКлиент.ПолеПароляНачалоВыбора(Элемент, Реквизит, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключение(ВидТранспортаСтрокой)
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	ПроверитьПодключениеНаСервере(Отказ, ВидТранспортаСтрокой);
	
	ТекстПредупреждения = ?(Отказ, НСтр("ru = 'Не удалось установить подключение.'"), НСтр("ru = 'Подключение успешно установлено.'"));
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьПодключениеНаСервере(Отказ, ВидТранспортаСтрокой)
	
	ПаролиДляПроверки = Неопределено;
	
	Если FTPСоединениеПарольИзменен Тогда
		ПаролиДляПроверки = Новый Структура("FTPСоединениеПароль", FTPСоединениеПароль);
	КонецЕсли;
	
	ТранспортСообщенийОбмена.ПроверитьПодключениеОбработкиТранспортаСообщенийОбмена(
		Отказ, Запись, Перечисления.ВидыТранспортаСообщенийОбмена[ВидТранспортаСтрокой], , ПаролиДляПроверки);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	ИспользуемыеТранспорты = Новый Массив;
	ИспользуемыеТранспорты.Добавить(Перечисления.ВидыТранспортаСообщенийОбмена.FILE);
	ИспользуемыеТранспорты.Добавить(Перечисления.ВидыТранспортаСообщенийОбмена.FTP);
	
	Элементы.ВидТранспортаСообщенийОбменаПоУмолчанию.СписокВыбора.Очистить();
	
	Для Каждого Элемент Из ИспользуемыеТранспорты Цикл
		
		Элементы.ВидТранспортаСообщенийОбменаПоУмолчанию.СписокВыбора.Добавить(Элемент, Строка(Элемент));
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСозданиеПароля()
	
	ОписаниеОповещенияДляЗавершенияПароля = Новый ОписаниеОповещения("ЗавершениеСозданияПароля", ЭтотОбъект);
	
	ОткрытьФорму("РегистрСведений.НастройкиТранспортаОбменаОбластейДанных.Форма.ФормаНовыйПароль",, ЭтотОбъект, 
		УникальныйИдентификатор,,, 
		ОписаниеОповещенияДляЗавершенияПароля, 
		РежимБлокировкиПриОткрытииОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеСозданияПароля(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат <> Неопределено Тогда
		Если Элементы.СтраницыВидовТранспорта.ТекущаяСтраница = Элементы.НастройкиТранспортаFILE Тогда
			ПарольАрхиваСообщенияОбмена = Результат;
			ПарольАрхиваСообщенияОбменаИзменен = Истина;
		Иначе
			ПарольАрхиваСообщенияОбменаFTP = Результат;
			ПарольАрхиваСообщенияОбменаFTPИзменен = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти




