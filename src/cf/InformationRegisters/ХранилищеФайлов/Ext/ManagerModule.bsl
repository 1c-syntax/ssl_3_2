///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаписатьДвоичныеДанные(Знач Файл, Знач ДвоичныеДанные, Знач ПараметрыЗаписиВИБ = Неопределено) Экспорт
	
	Если ПараметрыЗаписиВИБ = Неопределено Тогда
		ПараметрыЗаписиВИБ = РаботаСФайловымАрхивомСервер.ЗаполненныеПараметрыЗаписиВИнформационнуюБазу(Файл);
	КонецЕсли;

	РезультатХеширования = РаботаСФайловымАрхивомСервер.РезультатХешированияДвоичныхДанных(ДвоичныеДанные);
	ЭтоПустыеДвоичныеДанные = РезультатХеширования.ЭтоПустыеДвоичныеДанные;
	Размер					= РезультатХеширования.Размер;
	Хеш						= РезультатХеширования.Хеш;

	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.ХранилищеДвоичныхДанных");
		ЭлементБлокировки.УстановитьЗначение("Хеш", Хеш);
		Блокировка.Заблокировать();

		УдалитьДвоичныеДанные(Файл);

		ДоступныХранилищаДвоичныхДанных = РаботаСФайловымАрхивомСервер.ДоступныХранилищаДвоичныхДанных();

		СведенияОХранилищеДвоичныхДанных = РаботаСФайловымАрхивомСервер.СведенияОбЭлементеХранилищаДвоичныхДанныхПоХешуИРазмеру(Хеш, Размер);
		ХранилищеДвоичныхДанныхСсылка = СведенияОХранилищеДвоичныхДанных.ХранилищеДвоичныхДанныхСсылка;

		Если ЗначениеЗаполнено(ХранилищеДвоичныхДанныхСсылка) Тогда

			Если СведенияОХранилищеДвоичныхДанных.РазмерХранимыхДанных = 0 Тогда
				ХранилищеДвоичныхДанныхОбъект = ХранилищеДвоичныхДанныхСсылка.ПолучитьОбъект();
				ХранилищеДвоичныхДанныхОбъект.ДвоичныеДанные = ?(ЭтоПустыеДвоичныеДанные,
					Неопределено, Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9)));
				ХранилищеДвоичныхДанныхОбъект.Записать();
			КонецЕсли;			

			Если Не ПараметрыЗаписиВИБ.ЭтоЗаписьВФайловыйАрхив
				И Не ПараметрыЗаписиВИБ.НеИзменятьДвоичныеДанные
				И ДоступныХранилищаДвоичныхДанных
				И РаботаСФайловымАрхивомСервер.ХранилищеДвоичныхДанныхПрисутствуетВФайловомАрхиве(ХранилищеДвоичныхДанныхСсылка) Тогда

				ИмяРеквизитаДляЗаписиДвоичныхДанных = РаботаСФайламиВХранилищеДвоичныхДанныхСлужебный.ОпределитьРеквизитХраненияДвоичныхДанныхПоТипуХраненияФайлов(ПараметрыЗаписиВИБ.ТипХраненияФайла);

				ДвоичныеДанныеДляЗаписи = ?(ЭтоПустыеДвоичныеДанные, Неопределено, Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9)));				

				Если ИмяРеквизитаДляЗаписиДвоичныхДанных = "ДвоичныеДанные" Тогда
					ХранилищеДвоичныхДанныхОбъект = ХранилищеДвоичныхДанныхСсылка.ПолучитьОбъект();
					ХранилищеДвоичныхДанныхОбъект.ДвоичныеДанные = ДвоичныеДанныеДляЗаписи;
					ХранилищеДвоичныхДанныхОбъект.Записать();
				Иначе
					ЗначениеДвоичныхДанных = Новый Структура(ИмяРеквизитаДляЗаписиДвоичныхДанных, ДвоичныеДанныеДляЗаписи);
					РегистрыСведений.МестаХраненияДвоичныхДанных.ДобавитьИзменитьЗапись(ХранилищеДвоичныхДанныхСсылка, ЗначениеДвоичныхДанных);
				КонецЕсли;

				РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов.ДобавитьЗапись(ХранилищеДвоичныхДанныхСсылка, ПараметрыЗаписиВИБ);

			КонецЕсли;				

		Иначе
			ХранилищеДвоичныхДанныхОбъект = Справочники.ХранилищеДвоичныхДанных.СоздатьЭлемент();
			ХранилищеДвоичныхДанныхОбъект.Размер = Размер;
			ХранилищеДвоичныхДанныхОбъект.Хеш = Хеш;

			Если ДоступныХранилищаДвоичныхДанных И ПараметрыЗаписиВИБ.ЭтоЗаписьВФайловыйАрхив Тогда
				ДвоичныеДанныеДляЗаписи = ?(ЗначениеЗаполнено(ПараметрыЗаписиВИБ.ДвоичныеДанныеАрхива), 
												Новый ХранилищеЗначения(ПараметрыЗаписиВИБ.ДвоичныеДанныеАрхива, Новый СжатиеДанных(9)),
												Неопределено);
			Иначе
				ДвоичныеДанныеДляЗаписи = ?(ЭтоПустыеДвоичныеДанные, Неопределено, Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9)));
			КонецЕсли;

			Если ДоступныХранилищаДвоичныхДанных Тогда
				ИмяРеквизитаДляЗаписиДвоичныхДанных = РаботаСФайламиВХранилищеДвоичныхДанныхСлужебный.ОпределитьРеквизитХраненияДвоичныхДанныхПоТипуХраненияФайлов(ПараметрыЗаписиВИБ.ТипХраненияФайла, ПараметрыЗаписиВИБ.ЭтоЗаписьВФайловыйАрхив);
			Иначе
				ИмяРеквизитаДляЗаписиДвоичныхДанных = "ДвоичныеДанные";
			КонецЕсли;
			
			Если ИмяРеквизитаДляЗаписиДвоичныхДанных = "ДвоичныеДанные" Тогда
				ХранилищеДвоичныхДанныхОбъект[ИмяРеквизитаДляЗаписиДвоичныхДанных] = ДвоичныеДанныеДляЗаписи;			
			КонецЕсли;

			ХранилищеДвоичныхДанныхОбъект.Записать();
			ХранилищеДвоичныхДанныхСсылка = ХранилищеДвоичныхДанныхОбъект.Ссылка;

			Если ИмяРеквизитаДляЗаписиДвоичныхДанных <> "ДвоичныеДанные" Тогда

				ЗначениеДвоичныхДанных = Новый Структура(ИмяРеквизитаДляЗаписиДвоичныхДанных, ДвоичныеДанныеДляЗаписи);
				РегистрыСведений.МестаХраненияДвоичныхДанных.ДобавитьИзменитьЗапись(ХранилищеДвоичныхДанныхСсылка, ЗначениеДвоичныхДанных);

			КонецЕсли;

			РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов.ДобавитьЗапись(ХранилищеДвоичныхДанныхСсылка, ПараметрыЗаписиВИБ);
		КонецЕсли;

		ДобавитьЗапись(Файл, ХранилищеДвоичныхДанныхСсылка, Ложь);

		ЗафиксироватьТранзакцию();	
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Процедура УдалитьДвоичныеДанные(Файл) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Файл", Файл);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ХранилищеФайлов.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанных,
	|	ХранилищеФайлов.ХранилищеДвоичныхДанных.Хеш КАК Хеш
	|ИЗ
	|	РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
	|ГДЕ
	|	ХранилищеФайлов.Файл = &Файл";

	НачатьТранзакцию();
	Попытка
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ХранилищеДвоичныхДанных");
			ЭлементБлокировки.УстановитьЗначение("Хеш", Выборка.Хеш);
			Блокировка.Заблокировать();
			
			ХранилищеФайла = СоздатьМенеджерЗаписи();
			ХранилищеФайла.Файл = Файл;
			ХранилищеФайла.Удалить();
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ХранилищеДвоичныхДанных", Выборка.ХранилищеДвоичныхДанных);
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА КАК Проверка
			|ИЗ
			|	РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
			|ГДЕ
			|	ХранилищеФайлов.ХранилищеДвоичныхДанных = &ХранилищеДвоичныхДанных";
			Если Запрос.Выполнить().Пустой() Тогда

                РегистрыСведений.МестаХраненияДвоичныхДанных.УдалитьЗапись(Выборка.ХранилищеДвоичныхДанных);

				РегистрыСведений.СведенияОХраненииДедуплицированныхФайлов.УдалитьЗапись(Выборка.ХранилищеДвоичныхДанных);

				ХранилищеДанных = Выборка.ХранилищеДвоичныхДанных.ПолучитьОбъект();
				ХранилищеДанных.ОбменДанными.Загрузка = Истина;
				ХранилищеДанных.Удалить();
			КонецЕсли;
		КонецЕсли;
		
		Если Не РаботаСФайламиСлужебныйПовтИсп.ДедупликацияВыполнена() Тогда
			ХранилищеФайла = РегистрыСведений.УдалитьДвоичныеДанныеФайлов.СоздатьМенеджерЗаписи();
			ХранилищеФайла.Файл = Файл;
			ХранилищеФайла.Удалить();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();	
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Функция НавигационнаяСсылкаФайла(Файл) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Файл", Файл);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ХранилищеФайлов.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанных
	|ИЗ
	|	РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
	|ГДЕ
	|	ХранилищеФайлов.Файл = &Файл";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат ПолучитьНавигационнуюСсылку(Выборка.ХранилищеДвоичныхДанных, "ДвоичныеДанные");
	КонецЕсли;
	
	КлючЗаписи = РегистрыСведений.УдалитьДвоичныеДанныеФайлов.СоздатьКлючЗаписи(Новый Структура("Файл", Файл));
	
	Возврат ПолучитьНавигационнуюСсылку(КлючЗаписи, "ДвоичныеДанныеФайла");
		
КонецФункции

Процедура ПеренестиДанные(СообщатьПрогресс = Ложь, АдресРезультата = Неопределено) Экспорт
	
	ШаблонПрогресса = НСтр("ru = 'Обработано %1 (%2 Мб) файлов из %3 (%4 Мб)'");
	ВсегоЗаписей = 0;
	ВсегоРазмерМб = 0;
	Если СообщатьПрогресс Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК ВсегоЗаписей
		|ИЗ
		|	РегистрСведений.УдалитьДвоичныеДанныеФайлов КАК УдалитьДвоичныеДанныеФайлов";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ВсегоЗаписей = Выборка.ВсегоЗаписей;
		КонецЕсли;
		
		ВключитьОбъекты = Новый Массив();
		ВключитьОбъекты.Добавить(Метаданные.РегистрыСведений.УдалитьДвоичныеДанныеФайлов);
		ВсегоРазмерМб = Окр(ПолучитьРазмерДанныхБазыДанных(, ВключитьОбъекты) / 1024 / 1024, 0);
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПрогресса, 
			0, 0, ВсегоЗаписей, ВсегоРазмерМб);
		ДлительныеОперации.СообщитьПрогресс(0, Текст);
	КонецЕсли;
	
	Выборка = РегистрыСведений.УдалитьДвоичныеДанныеФайлов.Выбрать();
	ЗаписейОбработано = 0;
	РазмерОбработано = 0;
	Ошибки = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Файл = Выборка.Файл;
		НаименованиеФайла = Строка(Файл);
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных();
			Блокировка.Добавить("РегистрСведений.УдалитьДвоичныеДанныеФайлов").УстановитьЗначение("Файл", Файл);
			Блокировка.Добавить("РегистрСведений.ХранилищеФайлов").УстановитьЗначение("Файл", Файл);
			Блокировка.Заблокировать();
			
			ДвоичныеДанные = Выборка.ДвоичныеДанныеФайла.Получить();
			Если ТипЗнч(ДвоичныеДанные) = Тип("Картинка") Тогда

				ДвоичныеДанные = ДвоичныеДанные.ПолучитьДвоичныеДанные();

			ИначеЕсли ТипЗнч(ДвоичныеДанные) <> Тип("ДвоичныеДанные") Тогда
				ТекстОшибки = НСтр("ru = 'В регистре сведений ""%1"" для файла ""%2"" хранятся данные типа ""%3"", а ожидалось ""%4"".'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки,
					Метаданные.РегистрыСведений.УдалитьДвоичныеДанныеФайлов.Имя,
					НаименованиеФайла,
					ТипЗнч(ДвоичныеДанные),
					Тип("ДвоичныеДанные"));
				ОписаниеОшибки = Новый Структура;
				ОписаниеОшибки.Вставить("ИмяФайла", ОбщегоНазначения.ПредметСтрокой(Файл));
				ОписаниеОшибки.Вставить("Ошибка", ТекстОшибки);
				ОписаниеОшибки.Вставить("ПодробноеПредставлениеОшибки", ТекстОшибки);
				ОписаниеОшибки.Вставить("Версия", Файл);
				Ошибки.Добавить(ОписаниеОшибки);
				
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Файлы.Ошибка дедупликации файла.'", ОбщегоНазначения.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Ошибка, , Файл, ТекстОшибки);
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			// @skip-check query-in-loop - порционная обработка больших объемов данных. 
			Если Не ЗаписьСуществует(Файл) Тогда
				ПараметрыЗаписиВИБ = РаботаСФайловымАрхивомСервер.ЗаполненныеПараметрыЗаписиВИнформационнуюБазу(Файл);
				ЗаписатьДвоичныеДанные(Файл, ДвоичныеДанные, ПараметрыЗаписиВИБ);
			КонецЕсли;
			
			Запись = РегистрыСведений.УдалитьДвоичныеДанныеФайлов.СоздатьМенеджерЗаписи();
			Запись.Файл = Файл;
			Запись.Удалить();
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
		
		Если СообщатьПрогресс Тогда
			ЗаписейОбработано = ЗаписейОбработано + 1;
			РазмерОбработано = РазмерОбработано + ДвоичныеДанные.Размер();
			Если ЗаписейОбработано % 100 = 0 Тогда
				Процент = Окр(ЗаписейОбработано * 100 / ВсегоЗаписей);
				Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПрогресса, 
					ЗаписейОбработано, Окр(РазмерОбработано / 1024 / 1024), ВсегоЗаписей, ВсегоРазмерМб);
				ДлительныеОперации.СообщитьПрогресс(Процент, Текст);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	
	Если ЗначениеЗаполнено(АдресРезультата) И Ошибки.Количество() > 0 Тогда
		ПоместитьВоВременноеХранилище(Ошибки, АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЗапись(Файл, ХранилищеДвоичныхДанныхСсылка, Замещать = Истина) Экспорт

	Запись = СоздатьМенеджерЗаписи();
	Запись.Файл = Файл;
	Запись.ХранилищеДвоичныхДанных = ХранилищеДвоичныхДанныхСсылка;
	Запись.Записать(Замещать);	

КонецПроцедуры

#Область ОбработчикиОбновления

// Регистрирует на плане обмена ОбновлениеИнформационнойБазы объекты,
// для которых необходимо обновить записи в регистре.
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УдалитьДвоичныеДанныеФайлов.Файл КАК Ссылка
	|ИЗ
	|	РегистрСведений.УдалитьДвоичныеДанныеФайлов КАК УдалитьДвоичныеДанныеФайлов
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(УдалитьДвоичныеДанныеФайлов.Файл) = &ТипФайла";
	
	Запрос.УстановитьПараметр("ТипФайла", ТипЗнч(Справочники.Файлы.ПустаяСсылка()));
	
	МассивСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, МассивСсылок);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, "Справочник.Файлы");
	Если Выборка.Количество() > 0 Тогда
		СоздатьНедостающиеВерсииФайлов(Выборка);
	КонецЕсли;
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, "Справочник.ВерсииФайлов")
		И ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, "Справочник.Файлы");
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьНедостающиеВерсииФайлов(Выборка)
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		ФайлСсылка = Выборка.Ссылка; // СправочникСсылка.Файлы
		ПредставлениеСсылки = Строка(ФайлСсылка);
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.Файлы");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ФайлСсылка);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УдалитьДвоичныеДанныеФайлов");
		ЭлементБлокировки.УстановитьЗначение("Файл", ФайлСсылка);
		
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
			
			ФайлОбъект = ФайлСсылка.ПолучитьОбъект();
			
			Если ФайлОбъект <> Неопределено Тогда
				
				Версия = Справочники.ВерсииФайлов.СоздатьЭлемент();
				Версия.УстановитьНовыйКод();
				
				НаборСвойств = "Автор,Владелец,ДатаМодификацииУниверсальная,ДатаСоздания,ИндексКартинки,
				|Наименование,ПометкаУдаления, ПутьКФайлу,Размер,Расширение,СтатусИзвлеченияТекста,
				|ТекстХранилище, ТипХраненияФайла, Том";
				
				ЗаполнитьЗначенияСвойств(Версия, ФайлОбъект, НаборСвойств);
				Версия.НомерВерсии = 1;
				Версия.Владелец = ФайлСсылка;
				
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Версия);
				
				ФайлОбъект.ТекущаяВерсия = Версия.Ссылка;
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ФайлОбъект);
				
				ДвоичныеДанныеФайлов = РегистрыСведений.УдалитьДвоичныеДанныеФайлов.СоздатьМенеджерЗаписи();
				ДвоичныеДанныеФайлов.Файл = ФайлСсылка;
				ДвоичныеДанныеФайлов.Прочитать();
				Если ДвоичныеДанныеФайлов.Выбран() Тогда
					ПараметрыЗаписиВИБ = РаботаСФайловымАрхивомСервер.ЗаполненныеПараметрыЗаписиВИнформационнуюБазу(Версия);
					// @skip-check query-in-loop - порционная обработка больших объемов данных.
					ЗаписатьДвоичныеДанные(Версия.Ссылка, ДвоичныеДанныеФайлов.ДвоичныеДанныеФайла.Получить(), ПараметрыЗаписиВИБ);
					ДвоичныеДанныеФайлов.Удалить();
				КонецЕсли;
				
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
		Исключение
			
			ОтменитьТранзакцию();
			// Если не удалось обработать какой-либо файл, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОшибкуВЖурналРегистрации(
				ФайлСсылка,
				ПредставлениеСсылки,
				ИнформацияОбОшибке());
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось обработать некоторые файлы (пропущены): %1'"), 
			ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Справочники.ВерсииФайлов,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Обработана очередная порция файлов: %1'"),
				ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

Функция ЗаписьСуществует(Файл)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Файл", Файл);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Проверка
	|ИЗ
	|	РегистрСведений.ХранилищеФайлов КАК ХранилищеФайлов
	|ГДЕ
	|	ХранилищеФайлов.Файл = &Файл";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти


#КонецЕсли