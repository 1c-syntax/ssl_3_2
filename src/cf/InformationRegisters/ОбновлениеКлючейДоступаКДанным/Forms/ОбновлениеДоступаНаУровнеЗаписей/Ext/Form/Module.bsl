///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем АдресРезультата, АдресХранимыхДанных, ИдентификаторЗаданияОбновленияПрогресса,
		ТекстОшибкиОбновленияДоступа, ПрогрессОбновленПослеКомандыЗапуститьСейчас,
		ОбновитьПрогрессПриАктивизацииФормы, ДатаИзмененияПланаОбновленияДоступа,
		ОграничиватьДоступНаУровнеЗаписейУниверсальноБыло,
		ОграничиватьДоступНаУровнеЗаписейУниверсальноВключеноБыло;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	НавигационнаяСсылка = "e1cib/app/РегистрСведений.ОбновлениеКлючейДоступаКДанным.Форма.ОбновлениеДоступаНаУровнеЗаписей";
	
	УправлениеДоступомСлужебный.ПроверитьОграничениеДоступаОтключено();
	
	ПериодОбновленияПрогресса = 3;
	АвтообновлениеПрогресса = Не Параметры.ВыключитьАвтообновлениеПрогресса;
	
	ПоляСортировки.Добавить("ЗадержкаОбработкиСписка", "Возр");
	МаксимальнаяДата = УправлениеДоступомСлужебный.МаксимальнаяДата();
	УстановитьКартинкуСортировки(Элементы.СпискиЗадержкаОбработкиСписка, Ложь);
	
	Если Параметры.ПоказатьПрогрессПоОтдельнымСпискам Тогда
		Элементы.ГруппаДополнительно.Показать();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	КоличествоПотоковОбновленияДоступа = Константы.КоличествоПотоковОбновленияДоступа.Получить();
	Если УправлениеДоступомСлужебный.ДоступнаБалансировкаНагрузкиНаДиск() Тогда
		БалансировкаНагрузкиНаДиск = УправлениеДоступомСлужебный.БалансировкаНагрузкиНаДиск();
	Иначе
		БалансировкаНагрузкиНаДиск = Ложь;
		Элементы.БалансировкаНагрузкиНаДиск1.Доступность = Ложь;
		Элементы.БалансировкаНагрузкиНаДиск2.Доступность = Ложь;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	Если КоличествоПотоковОбновленияДоступа = 0 Тогда
		КоличествоПотоковОбновленияДоступа = 1;
	КонецЕсли;
	Элементы.КоличествоПотоковОбновленияДоступа1.Подсказка =
		Метаданные.Константы.КоличествоПотоковОбновленияДоступа.Подсказка;
	Элементы.КоличествоПотоковОбновленияДоступа2.Подсказка =
		Метаданные.Константы.КоличествоПотоковОбновленияДоступа.Подсказка;
	
	Элементы.БалансировкаНагрузкиНаДиск1.Подсказка =
		НСтр("ru = 'Если при обновлении доступа скорость передачи данных
		           |с устройства HDD уменьшается до 2-10 Мб/сек вместо
		           |40-150 мб/сек и устройство занято на 100% в течении
		           |5-10 минут или более, то диск медленный.
		           |Прим: устройство SSD считается быстрым.'");
	Элементы.БалансировкаНагрузкиНаДиск2.Подсказка =
		Элементы.БалансировкаНагрузкиНаДиск1.Подсказка;
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая()
	 Или ОбщегоНазначения.РазделениеВключено() Тогда
		
		Элементы.ГруппаКоличествоПотоковОбновленияДоступа1.Видимость = Ложь;
		Элементы.ГруппаКоличествоПотоковОбновленияДоступа2.Видимость = Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		Возврат;
	КонецЕсли;
	
	Положение = ПоложениеЗаголовкаЭлементаФормы.Верх;
	Элементы.КоличествоПотоковОбновленияДоступа1.ПоложениеЗаголовка = Положение;
	Элементы.КоличествоПотоковОбновленияДоступа2.ПоложениеЗаголовка = Положение;
	Элементы.БалансировкаНагрузкиНаДиск1.ПоложениеЗаголовка = Положение;
	Элементы.БалансировкаНагрузкиНаДиск2.ПоложениеЗаголовка = Положение;
	
	Панель = Элементы.Добавить("КоманднаяПанельТакси", Тип("ГруппаФормы"), Элементы.ВерхняяПанельФормы);
	Панель.Вид = ВидГруппыФормы.КоманднаяПанель;
	
	СтандартныеПодсистемыСервер.ПереместитьЭлементыВГруппу(КоманднаяПанель, Панель);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьПрогрессПриАктивизацииФормы = Ложь;
	ПрогрессОбновленПослеКомандыЗапуститьСейчас = Истина;
	ПодключитьОбработчикОжидания("ПриОткрытииОбработчикОжидания", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()
	
	ПодключитьОбработчикОжидания("ПриПовторномОткрытииОбработчикОжидания", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияОбновленияПрогресса) Тогда
		ОтменитьОбновлениеПрогрессаНаСервере(ИдентификаторЗаданияОбновленияПрогресса);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОбновлениеКлючейДоступаКДанным"
	 Или ИмяСобытия = "Запись_ОбновлениеКлючейДоступаПользователей" Тогда
		
		НачатьОбновлениеПрогресса(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КоличествоПотоковОбновленияДоступаПриИзменении(Элемент)
	
	Если КоличествоПотоковОбновленияДоступа = 0 Тогда
		КоличествоПотоковОбновленияДоступа = 1;
	КонецЕсли;
	
	УстановитьКоличествоПотоковОбновленияДоступаНаСервере(КоличествоПотоковОбновленияДоступа);
	
	ОбновитьЗаголовокГруппыКоличествоПотоковОбновленияДоступа();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПотоковОбновленияДоступаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Текст) Тогда
		КоличествоПотоковОбновленияДоступа = Число(Текст);
	Иначе
		КоличествоПотоковОбновленияДоступа = 1;
	КонецЕсли;
	
	КоличествоПотоковОбновленияДоступаПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура БалансировкаНагрузкиНаДискПриИзменении(Элемент)
	
	БалансировкаНагрузкиНаДискПриИзмененииНаСервере(БалансировкаНагрузкиНаДиск);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоследнееЗавершениеОбновленияДоступаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПоказатьТекстОшибки" Тогда
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(ТекстОшибкиОбновленияДоступа);
		ТекстовыйДокумент.Показать(НСтр("ru = 'Ошибка обновления доступа'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПрогрессНажатие(Элемент)
	
	НачатьОбновлениеПрогресса(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОбновлениеПрогрессаНажатие(Элемент)
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияОбновленияПрогресса) Тогда
		ОтменитьОбновлениеПрогрессаНаСервере(ИдентификаторЗаданияОбновленияПрогресса);
		Если АвтообновлениеПрогресса Тогда
			АвтообновлениеПрогресса = Ложь;
			Пояснение = НСтр("ru = 'Автообновление состояния отключено'");
		Иначе
			Пояснение = "";
		КонецЕсли;
		ПоказатьОповещениеПользователя(НСтр("ru = 'Обновление состояния отменено'"),,
			Пояснение);
	КонецЕсли;
	
	Элементы.ОбновлениеПрогресса.ТекущаяСтраница = Элементы.ОбновлениеПрогрессаЗавершено;
	Элементы.ОтменитьОбновлениеПрогресса.Доступность = Ложь;
	
	ОбновитьЗаголовокГруппыАвтообновление();
	
КонецПроцедуры

&НаКлиенте
Процедура АвтообновлениеПрогрессаПриИзменении(Элемент)
	
	ОбновитьЗаголовокГруппыАвтообновление();
	
	НачатьОбновлениеПрогресса();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОбновленияПрогрессаПриИзменении(Элемент)
	
	ОбновитьЗаголовокГруппыАвтообновление();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитыватьПоКоличествуДанныхПриИзменении(Элемент)
	
	Элементы.СпискиКоличествоЭлементов.Видимость                 = РассчитыватьПоКоличествуДанных;
	Элементы.СпискиКоличествоОбработанныхЭлементов.Видимость     = РассчитыватьПоКоличествуДанных;
	Элементы.СпискиКоличествоКлючейДоступа.Видимость             = РассчитыватьПоКоличествуДанных;
	Элементы.СпискиКоличествоОбработанныхКлючейДоступа.Видимость = РассчитыватьПоКоличествуДанных;
	
	ЭтоПовторноеОбновлениеПрогресса = Ложь;
	НачатьОбновлениеПрогресса(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапуститьОбновлениеДоступаСейчас(Команда)
	
	ПрогрессОбновленПослеКомандыЗапуститьСейчас = Ложь;
	ПодключитьОбработчикОжидания("ЗапуститьОбновлениеДоступаСейчасОбработчикОжидания", 0.1, Истина);
	Элементы.ЗапуститьОбновлениеДоступаСейчас.Доступность = Ложь;
	ОбновитьСостояниеЗаданияОбновленияДоступаЧерезТриСекунды();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьОбновлениеДоступа(Команда)
	
	ВключитьОбновлениеДоступаНаСервере();
	
	Элементы.ОбновлениеДоступаЗапрещено.Видимость = Ложь;
	Элементы.РегламентноеЗаданиеОтключено.Видимость = Ложь;
	
	НачатьОбновлениеПрогресса(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстановитьИЗапретитьОбновлениеДоступа(Команда)
	
	ПодключитьОбработчикОжидания("ОстановитьИЗапретитьОбновлениеДоступаОбработчикОжидания", 0.1, Истина);
	Элементы.ОстановитьИЗапретитьОбновлениеДоступа.Доступность = Ложь;
	ОбновитьСостояниеЗаданияОбновленияДоступаЧерезТриСекунды();
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаРучноеУправление(Команда)
	
	ОткрытьФорму("РегистрСведений.ОбновлениеКлючейДоступаКДанным.Форма.ОбновлениеДоступаРучноеУправление");
	
КонецПроцедуры

&НаКлиенте
Процедура СпискиПоказыватьОбработанныеСписки(Команда)
	
	ПоказыватьОбработанныеСписки = Не ПоказыватьОбработанныеСписки;
	Элементы.СпискиПоказыватьОбработанныеСписки.Пометка = ПоказыватьОбработанныеСписки;
	
	НачатьОбновлениеПрогресса(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СпискиСортироватьСписокПоВозрастанию(Команда)
	
	СортироватьСписок();
	
КонецПроцедуры

&НаКлиенте
Процедура СпискиСортироватьСписокПоУбыванию(Команда)
	
	СортироватьСписок(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СпискиЗадержкаОбработкиСписка.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Списки.ЗадержкаОбработкиСписка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 999999;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Списки.ОбработаноЭлементов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 100;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Списки.ОбработаноКлючейДоступа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 100;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "--- ---");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииОбработчикОжидания()
	
	ОбновитьЗаголовокГруппыКоличествоПотоковОбновленияДоступа();
	ОбновитьЗаголовокГруппыАвтообновление();
	
	ПриПовторномОткрытииОбработчикОжидания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытииОбработчикОжидания()
	
	ОбновитьСостояниеЗаданияОбновленияДоступа(, Истина);
	
	НачатьОбновлениеПрогресса(Истина, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьКоличествоПотоковОбновленияДоступаНаСервере(Знач Количество)
	
	Если Константы.КоличествоПотоковОбновленияДоступа.Получить() <> Количество Тогда
		Константы.КоличествоПотоковОбновленияДоступа.Установить(Количество);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура БалансировкаНагрузкиНаДискПриИзмененииНаСервере(БалансировкаНагрузкиНаДиск)
	
	УправлениеДоступомСлужебный.УстановитьБалансировкуНагрузкиНаДиск(БалансировкаНагрузкиНаДиск);
	БалансировкаНагрузкиНаДиск = УправлениеДоступомСлужебный.БалансировкаНагрузкиНаДиск();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьДействиеИнформационногоСообщения(ЭлементИлиКоманда,
			НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	ОбщегоНазначенияКлиент.ОбработатьДействиеИнформационногоСообщения(ЭтотОбъект,
		ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокГруппыКоличествоПотоковОбновленияДоступа()
	
	Элементы.ГруппаКоличествоПотоковОбновленияДоступа1.Заголовок =
		Формат(КоличествоПотоковОбновленияДоступа, "ЧГ=") + " "
			+ ПользователиСлужебныйКлиентСервер.ПредметЦелогоЧисла(КоличествоПотоковОбновленияДоступа,
				"", НСтр("ru = 'поток,потока,потоков,,,,,,0'"));
	
	Элементы.ГруппаКоличествоПотоковОбновленияДоступа2.Заголовок =
		Элементы.ГруппаКоличествоПотоковОбновленияДоступа1.Заголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеЗаданияОбновленияДоступаЧерезТриСекунды()
	
	ОтключитьОбработчикОжидания("ОбновитьСостояниеЗаданияОбновленияДоступаОбработчикОжидания");
	ПодключитьОбработчикОжидания("ОбновитьСостояниеЗаданияОбновленияДоступаОбработчикОжидания", 3.5);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеЗаданияОбновленияДоступаОбработчикОжидания()
	
	ОбновитьСостояниеЗаданияОбновленияДоступа();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеЗаданияОбновленияДоступа(Состояние = Неопределено, ПриОткрытии = Ложь)
	
	Если Не ПриОткрытии И Не ФормаАктивна() Тогда
		ОтключитьОбработчикОжидания("ОбновитьСостояниеЗаданияОбновленияДоступаОбработчикОжидания");
		ПодключитьОбработчикОжидания("ОбновитьСостояниеЗаданияОбновленияДоступаОбработчикОжидания", 1);
		Возврат;
	КонецЕсли;
	
	Если Состояние = Неопределено Тогда
		Состояние = СостояниеЗаданияОбновленияДоступа();
	КонецЕсли;
	
	Универсально = Состояние.ОграничиватьДоступНаУровнеЗаписейУниверсально;
	Элементы.РазрешитьОбновлениеДоступа.Доступность  = Универсально;
	Элементы.ВключитьРегламентноеЗадание.Доступность = Универсально;
	
	Если ОграничиватьДоступНаУровнеЗаписейУниверсальноБыло = Неопределено
	   И Не Универсально
	 Или ОграничиватьДоступНаУровнеЗаписейУниверсальноБыло <> Неопределено
	   И ОграничиватьДоступНаУровнеЗаписейУниверсальноБыло <> Универсально
	 Или Универсально
	   И ОграничиватьДоступНаУровнеЗаписейУниверсальноВключеноБыло = Неопределено
	   И Не Состояние.ОграничиватьДоступНаУровнеЗаписейУниверсальноВключено
	 Или Универсально
	   И ОграничиватьДоступНаУровнеЗаписейУниверсальноВключеноБыло <> Неопределено
	   И ОграничиватьДоступНаУровнеЗаписейУниверсальноВключеноБыло
	      <> Состояние.ОграничиватьДоступНаУровнеЗаписейУниверсальноВключено Тогда
		
		ОграничиватьДоступНаУровнеЗаписейУниверсальноБыло = Универсально;
		ОграничиватьДоступНаУровнеЗаписейУниверсальноВключеноБыло =
			Состояние.ОграничиватьДоступНаУровнеЗаписейУниверсальноВключено;
		ВывестиВажныеСообщения(Универсально, Состояние.ОграничиватьДоступНаУровнеЗаписейУниверсальноВключено);
	КонецЕсли;
	
	Элементы.ОбновлениеДоступаЗапрещено.Видимость = Состояние.ОбновлениеДоступаЗапрещено;
	Элементы.РегламентноеЗаданиеОтключено.Видимость = Не Состояние.ОбновлениеДоступаЗапрещено
		И Состояние.РегламентноеЗаданиеОтключено И Не Состояние.ОбновлениеДоступаВыполняется И ВсегоОбновлено <> 100;
	
	ТекстОшибкиОбновленияДоступа = Состояние.ТекстОшибкиОбновленияДоступа;
	
	Если ЗначениеЗаполнено(Состояние.ПоследнееЗавершениеОбновленияДоступа) Тогда
		Если Состояние.ОбновлениеОтмененоНештатно Тогда
			Если Состояние.ПоследнееЗавершениеСегодня Тогда
				Шаблон = НСтр("ru = '<b><span style=""color: %3"">отменено нештатно</span></b> после запуска в %1'");
			Иначе
				Шаблон = НСтр("ru = '<b><span style=""color: %3"">отменено нештатно</span></b> после запуска %1'");
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(ТекстОшибкиОбновленияДоступа) Тогда
			Если Состояние.ОбновлениеОтменено Тогда
				Если Состояние.ПоследнееЗавершениеСегодня Тогда
					Шаблон = НСтр("ru = '<b><span style=""color: %3"">отменено</span></b> <a href = ""%4"">с ошибкой</a> в %1 через %2'");
				Иначе
					Шаблон = НСтр("ru = '<b><span style=""color: %3"">отменено</span></b> <a href = ""%4"">с ошибкой</a> %1 через %2'");
				КонецЕсли;
			Иначе
				Если Состояние.ПоследнееЗавершениеСегодня Тогда
					Шаблон = НСтр("ru = '<b><span style=""color: %3"">завершено</span></b> <a href = ""%4"">с ошибкой</a> в %1 через %2'");
				Иначе
					Шаблон = НСтр("ru = '<b><span style=""color: %3"">завершено</span></b> <a href = ""%4"">с ошибкой</a> %1 через %2'");
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если Состояние.ОбновлениеОтменено Тогда
				Если Состояние.ПоследнееЗавершениеСегодня Тогда
					Шаблон = НСтр("ru = 'отменено в %1 через %2'");
				Иначе
					Шаблон = НСтр("ru = 'отменено %1 через %2'");
				КонецЕсли;
			Иначе
				Если Состояние.ПоследнееЗавершениеСегодня Тогда
					Шаблон = НСтр("ru = 'завершено в %1 за %2'");
				Иначе
					Шаблон = НСтр("ru = 'завершено %1 за %2'");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если Состояние.ПоследнееЗавершениеСегодня Тогда
			ПредставлениеДаты = Формат(Состояние.ПоследнееЗавершениеОбновленияДоступа, "ДЛФ=T");
			Шаблон = СтрЗаменить(Шаблон, "%1", "<b>%1</b>");
		Иначе
			ПредставлениеДаты = Формат(Состояние.ПоследнееЗавершениеОбновленияДоступа, "ДЛФ=DT");
		КонецЕсли;
		Шаблон = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон,
			ПредставлениеДаты,
			Состояние.ПоследнееВыполнениеПродолжительность,
			"ЦветОсобогоТекста",
			"ПоказатьТекстОшибки");
		ПоследнееЗавершение = СтроковыеФункцииКлиент.ФорматированнаяСтрока("(" + Шаблон + ")");
	Иначе
		ПоследнееЗавершение = "(" + ?(Состояние.ОбновлениеДоступаВыполняется,
			НСтр("ru = 'не завершалось'"), НСтр("ru = 'не запускалось'")) + ")";
	КонецЕсли;
	Элементы.ПоследнееЗавершениеОбновленияДоступа.Заголовок = ПоследнееЗавершение;
	
	ВыполнениеЗаданияЗавершено = ЗначениеЗаполнено(Состояние.ПоследнееЗавершениеОбновленияДоступа)
		И ПоследнееЗавершениеОбновленияДоступа <> Состояние.ПоследнееЗавершениеОбновленияДоступа;
	ПоследнееЗавершениеОбновленияДоступа = Состояние.ПоследнееЗавершениеОбновленияДоступа;
	
	СостояниеВыполненияЗаданияИзменилось = Состояние.ОбновлениеДоступаВыполняется
		<> Элементы.ОбновлениеДоступаВыполняется.Видимость;
	Элементы.ОбновлениеДоступаВыполняется.Видимость   =    Состояние.ОбновлениеДоступаВыполняется;
	Элементы.ОбновлениеДоступаНеВыполняется.Видимость = Не Состояние.ОбновлениеДоступаВыполняется;
	
	Элементы.ОстановитьИЗапретитьОбновлениеДоступа.Доступность =    Состояние.ОбновлениеДоступаВыполняется;
	Элементы.ЗапуститьОбновлениеДоступаСейчас.Доступность      = Не Состояние.ОбновлениеДоступаВыполняется И Универсально;
	
	Элементы.КартинкаФоновоеЗаданиеВыполняется.Видимость       =    Состояние.ФоновоеЗаданиеВыполняется;
	Элементы.КартинкаФоновоеЗаданиеОжидаетВыполнения.Видимость = Не Состояние.ФоновоеЗаданиеВыполняется;
	
	ПланыОбновленияИзменились = Не Состояние.ОбновлениеДоступаВыполняется
		И ДатаИзмененияПланаОбновленияДоступа <> Состояние.ДатаИзмененияПланаОбновления;
	ДатаИзмененияПланаОбновленияДоступа = Состояние.ДатаИзмененияПланаОбновления;
	
	Если Не Состояние.ОбновлениеДоступаВыполняется Тогда
		Элементы.ВремяВыполненияФоновогоЗадания1.Заголовок = "";
		Элементы.ВремяВыполненияФоновогоЗадания2.Заголовок = "";
	Иначе
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Выполняется %1'"),
			ВремяВыполненияСтрокой(Состояние.ВыполняетсяСекунд));
		
		Элементы.ВремяВыполненияФоновогоЗадания1.Заголовок = ТекстЗаголовка;
		Элементы.ВремяВыполненияФоновогоЗадания2.Заголовок = ТекстЗаголовка;
		
		ВидимостьПервого = Элементы.ВремяВыполненияФоновогоЗадания1.Видимость;
		ВидимостьПервого = Не ВидимостьПервого;
		
		Элементы.ВремяВыполненияФоновогоЗадания1.Видимость =    ВидимостьПервого;
		Элементы.ВремяВыполненияФоновогоЗадания2.Видимость = Не ВидимостьПервого;
	КонецЕсли;
	
	Если ОбновитьПрогрессПриАктивизацииФормы = Истина Тогда
		НачатьОбновлениеПрогресса(Истина, Истина);
		ОбновитьПрогрессПриАктивизацииФормы = Ложь;
		
	ИначеЕсли Не ПриОткрытии
	        И Не АвтообновлениеПрогресса
	        И (ВыполнениеЗаданияЗавершено
	           Или ПрогрессОбновленПослеКомандыЗапуститьСейчас <> Истина
	           Или СостояниеВыполненияЗаданияИзменилось
	           Или ПланыОбновленияИзменились) Тогда
		
		НачатьОбновлениеПрогресса(Истина);
	КонецЕсли;
	
	ОбновитьСостояниеЗаданияОбновленияДоступаЧерезТриСекунды();
	
КонецПроцедуры

&НаСервере
Процедура ВывестиВажныеСообщения(УниверсальноеОграничение, УниверсальноеОграничениеВключено)
	
	Если УниверсальноеОграничение Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьСтатусИлиВажноеСообщение(ЭтотОбъект, "ПроизводительныйВариантОтключен");
	Иначе
		ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
		ПараметрыСообщения.Идентификатор = "ПроизводительныйВариантОтключен";
		ПараметрыСообщения.ТипСообщения = "Предупреждение";
		ПараметрыСообщения.КраткоеПредставление = НСтр("ru = 'Производительный вариант отключен'");
		ПараметрыСообщения.Заголовок = ПараметрыСообщения.КраткоеПредставление;
		ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'Используется стандартный вариант (см. вариант работы ограничения доступа в панели <b>Настройки пользователей и прав</b>).'"));
		ОбщегоНазначения.ДобавитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения);
		ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);
	КонецЕсли;
	
	Если Не УниверсальноеОграничение Или УниверсальноеОграничениеВключено Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьСтатусИлиВажноеСообщение(ЭтотОбъект, "ПроизводительныйВариантВключается");
	Иначе
		ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
		ПараметрыСообщения.Идентификатор = "ПроизводительныйВариантВключается";
		ПараметрыСообщения.ТипСообщения = "Предупреждение";
		ПараметрыСообщения.КраткоеПредставление = НСтр("ru = 'Производительный вариант включается'");
		ПараметрыСообщения.Заголовок = ПараметрыСообщения.КраткоеПредставление;
		ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'До первого завершения обновления используется стандартный вариант (см. константу <b>Первое обновление доступа завершилось</b>).'"));
		ОбщегоНазначения.ДобавитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения);
		ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВремяВыполненияСтрокой(ВремяВСекундах)
	
	ВсегоМинут = Цел(ВремяВСекундах / 60);
	Секунд = ВремяВСекундах - ВсегоМинут * 60;
	ВсегоЧасов = Цел(ВсегоМинут / 60);
	Минут = ВсегоМинут - ВсегоЧасов * 60;
	
	Если ВсегоЧасов > 0 Тогда
		Шаблон = НСтр("ru = '%3 ч %2 мин %1 сек'");
		
	ИначеЕсли Минут > 0 Тогда
		Шаблон = НСтр("ru = '%2 мин %1 сек'");
	Иначе
		Шаблон = НСтр("ru = '%1 сек'");
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон,
		Формат(Секунд, "ЧН=0; ЧГ="), Формат(Минут, "ЧН=0; ЧГ="), Формат(ВсегоЧасов, "ЧГ="));
	
КонецФункции

&НаСервереБезКонтекста
Функция СостояниеЗаданияОбновленияДоступа()
	
	ПоследнееОбновлениеДоступа = УправлениеДоступомСлужебный.ПоследнееОбновлениеДоступа();
	ТекущаяДатаНаСервере = УправлениеДоступомСлужебный.ТекущаяДатаНаСервере();
	
	Состояние = Новый Структура;
	
	Состояние.Вставить("ОграничиватьДоступНаУровнеЗаписейУниверсально",
		УправлениеДоступомСлужебный.ОграничиватьДоступНаУровнеЗаписейУниверсально(Истина));
	
	Состояние.Вставить("ОграничиватьДоступНаУровнеЗаписейУниверсальноВключено",
		УправлениеДоступомСлужебный.ОграничиватьДоступНаУровнеЗаписейУниверсально(Истина, Истина));
	
	Состояние.Вставить("ПоследнееЗавершениеОбновленияДоступа",
		?(ЗначениеЗаполнено(ПоследнееОбновлениеДоступа.ДатаПолногоЗавершения),
			ПоследнееОбновлениеДоступа.ДатаПолногоЗавершения,
			ПоследнееОбновлениеДоступа.ДатаЗавершенияНаСервере));
	
	Состояние.Вставить("ПоследнееВыполнениеПродолжительность",
		ВремяВыполненияСтрокой(ПоследнееОбновлениеДоступа.ПоследнееВыполнениеСекунд));
	
	Состояние.Вставить("ПоследнееЗавершениеСегодня",
		ЭтоСегодняшняяДата(ТекущаяДатаНаСервере, Состояние.ПоследнееЗавершениеОбновленияДоступа));
	
	Состояние.Вставить("ОбновлениеОтменено",
		ПоследнееОбновлениеДоступа.ОбновлениеОтменено);
	
	Состояние.Вставить("ТекстОшибкиОбновленияДоступа",
		ПоследнееОбновлениеДоступа.ТекстОшибкиЗавершения);
	
	Состояние.Вставить("ОбновлениеДоступаЗапрещено",
		ПоследнееОбновлениеДоступа.ОбновлениеДоступаЗапрещено);
	
	Состояние.Вставить("ВыполняетсяСекунд", 0);
	Состояние.Вставить("ОбновлениеОтмененоНештатно", Ложь);
	
	Исполнитель = УправлениеДоступомСлужебный.ИсполнительОбновленияДоступа(ПоследнееОбновлениеДоступа);
	
	Если Исполнитель = Неопределено Тогда
		Состояние.Вставить("ОбновлениеДоступаВыполняется", Ложь);
		Состояние.Вставить("ФоновоеЗаданиеВыполняется", Ложь);
		Если ПоследнееОбновлениеДоступа.ДатаЗапускаНаСервере > ПоследнееОбновлениеДоступа.ДатаЗавершенияНаСервере Тогда
			Состояние.ОбновлениеОтмененоНештатно = Истина;
			Состояние.ПоследнееЗавершениеОбновленияДоступа = ПоследнееОбновлениеДоступа.ДатаЗапускаНаСервере;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Исполнитель) = Тип("ФоновоеЗадание")
	        И (Исполнитель.УникальныйИдентификатор <> ПоследнееОбновлениеДоступа.ИдентификаторФоновогоЗадания
	           Или ОбщегоНазначения.ИнформационнаяБазаФайловая()
	             И Не СеансФоновогоЗаданияСуществует(Исполнитель)) Тогда
		
		Состояние.Вставить("ОбновлениеДоступаВыполняется", Истина);
		ОжидаетВыполненияСекунд = ТекущаяДатаНаСервере - Исполнитель.Начало;
		ОжидаетВыполненияСекунд = ?(ОжидаетВыполненияСекунд < 0, 0, ОжидаетВыполненияСекунд);
		Состояние.Вставить("ФоновоеЗаданиеВыполняется", ОжидаетВыполненияСекунд < 2);
	Иначе
		Состояние.Вставить("ОбновлениеДоступаВыполняется", Истина);
		Состояние.Вставить("ФоновоеЗаданиеВыполняется", Истина);
		ВыполняетсяСекунд = ТекущаяДатаНаСервере - ПоследнееОбновлениеДоступа.ДатаЗапускаНаСервере;
		Состояние.Вставить("ВыполняетсяСекунд", ?(ВыполняетсяСекунд < 0, 0, ВыполняетсяСекунд));
	КонецЕсли;
	
	Состояние.Вставить("РегламентноеЗаданиеОтключено", Ложь);
	Состояние.Вставить("ДатаИзмененияПланаОбновления", '00010101');
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОбновлениеКлючейДоступаКДанным.ДатаИзмененияЗаписиРегистра КАК ДатаИзменения
	|ИЗ
	|	РегистрСведений.ОбновлениеКлючейДоступаКДанным КАК ОбновлениеКлючейДоступаКДанным
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОбновлениеКлючейДоступаКДанным.ДатаИзмененияЗаписиРегистра УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОбновлениеКлючейДоступаПользователей.ДатаИзмененияЗаписиРегистра КАК ДатаИзменения
	|ИЗ
	|	РегистрСведений.ОбновлениеКлючейДоступаПользователей КАК ОбновлениеКлючейДоступаПользователей
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОбновлениеКлючейДоступаПользователей.ДатаИзмененияЗаписиРегистра УБЫВ";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатыЗапроса[0].Пустой() И РезультатыЗапроса[1].Пустой() Тогда
		Возврат Состояние;
	КонецЕсли;
	
	Если Не РезультатыЗапроса[0].Пустой() Тогда
		Состояние.ДатаИзмененияПланаОбновления = РезультатыЗапроса[0].Выгрузить()[0].ДатаИзменения;
	КонецЕсли;
	Если Не РезультатыЗапроса[1].Пустой() Тогда
		ДатаИзменения = РезультатыЗапроса[1].Выгрузить()[0].ДатаИзменения;
		Если ДатаИзменения > Состояние.ДатаИзмененияПланаОбновления Тогда
			Состояние.ДатаИзмененияПланаОбновления = ДатаИзменения;
		КонецЕсли;
	КонецЕсли;
	
	Отбор = Новый Структура("Метаданные", Метаданные.РегламентныеЗадания.ОбновлениеДоступаНаУровнеЗаписей);
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	Включено = Ложь;
	Для Каждого Задание Из Задания Цикл
		Если Задание.Использование Тогда
			Включено = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не Включено Тогда
		Состояние.РегламентноеЗаданиеОтключено = Истина;
	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции

&НаСервереБезКонтекста
Функция СеансФоновогоЗаданияСуществует(ФоновоеЗадание)
	
	Сеансы = ПолучитьСеансыИнформационнойБазы();
	Для Каждого Сеанс Из Сеансы Цикл
		Если Сеанс.ИмяПриложения <> "BackgroundJob" Тогда
			Продолжить;
		КонецЕсли;
		ФоновоеЗаданиеСеанса = Сеанс.ПолучитьФоновоеЗадание();
		Если ФоновоеЗаданиеСеанса = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ФоновоеЗаданиеСеанса.УникальныйИдентификатор = ФоновоеЗадание.УникальныйИдентификатор Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоСегодняшняяДата(ТекущаяДата, Дата)
	
	Возврат ТекущаяДата < Дата + 12 * 60 * 60;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьОбновлениеДоступаСейчасОбработчикОжидания()
	
	СостояниеЗаданияОбновленияДоступа = Неопределено;
	
	ТекстПредупреждения = ЗапуститьОбновлениеДоступаСейчасНаСервере(СостояниеЗаданияОбновленияДоступа);
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
	ОбновитьСостояниеЗаданияОбновленияДоступа(СостояниеЗаданияОбновленияДоступа);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапуститьОбновлениеДоступаСейчасНаСервере(СостояниеЗаданияОбновленияДоступа)
	
	Результат = УправлениеДоступомСлужебный.ЗапуститьОбновлениеДоступаНаУровнеЗаписей(Истина);
	
	СостояниеЗаданияОбновленияДоступа = СостояниеЗаданияОбновленияДоступа();
	
	Возврат Результат.ТекстПредупреждения;
	
КонецФункции

&НаКлиенте
Функция ФормаАктивна()
	
	ФормаАктивна = АктивноеОкно() = Окно Или ВводДоступен();
	
	Если Элементы.ВсегоОбновлено.ОтображатьПроценты <> ФормаАктивна Тогда
		Элементы.ВсегоОбновлено.ОтображатьПроценты = ФормаАктивна;
	КонецЕсли;
	
	Возврат ФормаАктивна;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьЗаголовокГруппыАвтообновление()
	
	Если АвтообновлениеПрогресса Тогда
		Элементы.Автообновление.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Автообновление, сек: %1'"), ПериодОбновленияПрогресса);
	Иначе
		Элементы.Автообновление.Заголовок = НСтр("ru = 'Автообновление: Нет'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОбновлениеПрогресса(ЗапускВручную = Ложь, ПриАктивизацииФормы = Ложь)
	
	Если ЗапускВручную И ЗначениеЗаполнено(ИдентификаторЗаданияОбновленияПрогресса) Тогда
		ОтменитьОбновлениеПрогрессаНаСервере(ИдентификаторЗаданияОбновленияПрогресса);
		
	ИначеЕсли Не АвтообновлениеПрогресса И Не ЗапускВручную
	 Или Элементы.ОбновлениеПрогресса.ТекущаяСтраница = Элементы.ОбновлениеПрогрессаВыполняется Тогда
		
		Возврат;
	КонецЕсли;
	
	Если Не ПриАктивизацииФормы И Не ФормаАктивна() Тогда
		ОбновитьПрогрессПриАктивизацииФормы = Истина;
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбновитьПрогрессОбработчикОжидания", 0.1, Истина);
	ОбновитьСостояниеЗаданияОбновленияДоступаЧерезТриСекунды();
	
	Элементы.ОбновлениеПрогресса.ТекущаяСтраница = Элементы.ОбновлениеПрогрессаВыполняется;
	Элементы.ОтменитьОбновлениеПрогресса.Доступность = Ложь;
	Элементы.КартинкаОбновлениеПрогресса.Видимость = Истина;
	Элементы.КартинкаОжиданиеОбновленияПрогресса.Видимость = Ложь;
	
	ПрогрессОбновленПослеКомандыЗапуститьСейчас = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОбновлениеПрогрессаОбработчикОжидания()
	
	НачатьОбновлениеПрогресса();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПрогрессОбработчикОжидания()
	
	Контекст = Новый Структура;
	Контекст.Вставить("РассчитыватьПоКоличествуДанных",  РассчитыватьПоКоличествуДанных);
	Контекст.Вставить("ПоказыватьОбработанныеСписки",    ПоказыватьОбработанныеСписки);
	Контекст.Вставить("ЭтоПовторноеОбновлениеПрогресса", ЭтоПовторноеОбновлениеПрогресса);
	Контекст.Вставить("ВсегоОбновлено",                  ВсегоОбновлено);
	Контекст.Вставить("ПериодОбновленияПрогресса",       ПериодОбновленияПрогресса);
	Контекст.Вставить("АвтообновлениеПрогресса",         АвтообновлениеПрогресса);
	Контекст.Вставить("ДобавленныеСтроки",               Новый Массив);
	Контекст.Вставить("УдаленныеСтроки",                 Новый Соответствие);
	Контекст.Вставить("ИзмененныеСтроки",                Новый Соответствие);
	Контекст.Вставить("ОбновлениеДоступаВыполняется",    Элементы.ОбновлениеДоступаВыполняется.Видимость);
	
	Попытка
		Статус = НачатьОбновлениеПрогрессаНаСервере(Контекст, АдресРезультата, АдресХранимыхДанных,
			УникальныйИдентификатор, ИдентификаторЗаданияОбновленияПрогресса);
	Исключение
		АвтообновлениеПрогресса = Ложь;
		Элементы.ОбновлениеПрогресса.ТекущаяСтраница = Элементы.ОбновлениеПрогрессаЗавершено;
		Элементы.ОтменитьОбновлениеПрогресса.Доступность = Ложь;
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(ИнформацияОбОшибке());
		Возврат;
	КонецПопытки;
	Элементы.ОтменитьОбновлениеПрогресса.Доступность = Истина;
	
	Если Статус = "Выполнено" Или Статус = "Ошибка" Тогда
		ОбновитьПрогрессПослеПолученияДанных(Контекст);
		
	ИначеЕсли Статус = "Выполняется" Тогда
		ОбновлениеПрогрессаВыполняется = Ложь;
		ПодключитьОбработчикОжидания("ЗавершитьОбновлениеПрогрессаОбработчикОжидания", 1, Истина);
		ОбновитьСостояниеЗаданияОбновленияДоступаЧерезТриСекунды();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьОбновлениеПрогрессаОбработчикОжидания()
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗаданияОбновленияПрогресса) Тогда
		Возврат;
	КонецЕсли;
	
	Контекст = Новый Структура;
	ЗаданиеВыполнено = ЗавершитьОбновлениеПрогрессаНаСервере(Контекст, АдресРезультата,
		АдресХранимыхДанных, ИдентификаторЗаданияОбновленияПрогресса);
	
	Если Не ЗаданиеВыполнено Тогда
		Если Контекст.ОбновлениеПрогрессаВыполняется Тогда
			ОбновлениеПрогрессаВыполняется = Истина;
		КонецЕсли;
		Элементы.КартинкаОбновлениеПрогресса.Видимость         =    ОбновлениеПрогрессаВыполняется;
		Элементы.КартинкаОжиданиеОбновленияПрогресса.Видимость = Не ОбновлениеПрогрессаВыполняется;
		
		ОбновитьСостояниеЗаданияОбновленияДоступа(Контекст.СостояниеЗаданияОбновленияДоступа);
		
		ПодключитьОбработчикОжидания("ЗавершитьОбновлениеПрогрессаОбработчикОжидания", 1, Истина);
		Возврат;
	КонецЕсли;
	
	ОбновитьПрогрессПослеПолученияДанных(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПрогрессПослеПолученияДанных(Контекст)
	
	Если Контекст.Свойство("ИнформацияОбОшибке") Тогда
		АвтообновлениеПрогресса = Ложь;
		ЗавершитьОбновлениеПрогрессаНаКлиенте(Контекст);
		Если ТипЗнч(Контекст.ИнформацияОбОшибке) = Тип("Строка") Тогда
			ПоказатьПредупреждение(, Контекст.ИнформацияОбОшибке);
		Иначе
			СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Контекст.ИнформацияОбОшибке);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ВсегоОбновлено = Контекст.ВсегоОбновлено;
	Если Контекст.Свойство("ПериодОбновленияПрогресса") Тогда
		ПериодОбновленияПрогресса = Контекст.ПериодОбновленияПрогресса;
	КонецЕсли;
	Если Контекст.Свойство("АвтообновлениеПрогресса") Тогда
		АвтообновлениеПрогресса = Контекст.АвтообновлениеПрогресса;
	КонецЕсли;
	
	ТекущийМомент = ОбщегоНазначенияКлиент.ДатаСеанса();
	ОбновитьЗадержку = ПоляСортировки[0].Значение = "ЗадержкаОбработкиСписка";
	
	Индекс = Списки.Количество() - 1;
	Пока Индекс >= 0 Цикл
		Строка = Списки.Получить(Индекс);
		Если Контекст.УдаленныеСтроки.Получить(Строка.Список) <> Неопределено Тогда
			Списки.Удалить(Индекс);
		Иначе
			ИзмененнаяСтрока = Контекст.ИзмененныеСтроки.Получить(Строка.Список);
			Если ИзмененнаяСтрока <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(Строка, ИзмененнаяСтрока);
			КонецЕсли;
			Если ОбновитьЗадержку Тогда
				ОбновитьЗадержкуОбновленияСписка(Строка, ТекущийМомент);
			КонецЕсли;
		КонецЕсли;
		Индекс = Индекс - 1;
	КонецЦикла;
	Для Каждого ДобавленнаяСтрока Из Контекст.ДобавленныеСтроки Цикл
		НоваяСтрока = Списки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДобавленнаяСтрока);
		Если ОбновитьЗадержку Тогда
			ОбновитьЗадержкуОбновленияСписка(НоваяСтрока, ТекущийМомент);
		КонецЕсли;
	КонецЦикла;
	
	Если Контекст.ДобавленныеСтроки.Количество() > 0
	 Или СортировкаСПереходомКНачалу()
	   И Контекст.ИзмененныеСтроки.Количество() > 0  Тогда
		
		СортироватьСписокПоПолям();
	КонецЕсли;
	
	ЗавершитьОбновлениеПрогрессаНаКлиенте(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьОбновлениеПрогрессаНаКлиенте(Контекст)
	
	Если АвтообновлениеПрогресса Тогда
		ПодключитьОбработчикОжидания("НачатьОбновлениеПрогрессаОбработчикОжидания",
			ПериодОбновленияПрогресса, Истина);
	КонецЕсли;
	
	Элементы.ОбновлениеПрогресса.ТекущаяСтраница = Элементы.ОбновлениеПрогрессаЗавершено;
	
	Если Не Контекст.Свойство("ИнформацияОбОшибке") Тогда
		ЭтоПовторноеОбновлениеПрогресса = Истина;
	КонецЕсли;
	
	ОбновитьСостояниеЗаданияОбновленияДоступа(Контекст.СостояниеЗаданияОбновленияДоступа);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗадержкуОбновленияСписка(Строка, ТекущийМомент)
	
	Делитель = 5;
	
	Если ЗначениеЗаполнено(Строка.ПоследнееОбновление) Тогда
		Строка.ЗадержкаОбработкиСписка =
			Цел((ТекущийМомент - Строка.ПоследнееОбновление) / Делитель) * Делитель;
		
	ИначеЕсли Строка.ПервоеПланированиеОбновления < МаксимальнаяДата Тогда
		Строка.ЗадержкаОбработкиСписка =
			Цел((ТекущийМомент - Строка.ПервоеПланированиеОбновления) / Делитель) * Делитель;
	Иначе
		Строка.ЗадержкаОбработкиСписка = 999999;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НачатьОбновлениеПрогрессаНаСервере(Контекст, АдресРезультата, АдресХранимыхДанных,
			ИдентификаторФормы, ИдентификаторЗаданияОбновленияПрогресса)
	
	Если ЗначениеЗаполнено(АдресХранимыхДанных) Тогда
		ЗапускПриОткрытии = Ложь;
		ХранимыеДанные = ПолучитьИзВременногоХранилища(АдресХранимыхДанных);
	Иначе
		ЗапускПриОткрытии = Истина;
		ХранимыеДанные = УправлениеДоступомСлужебный.НовыеХранимыеДанныеОбновленияПрогресса();
		АдресХранимыхДанных = ПоместитьВоВременноеХранилище(ХранимыеДанные, ИдентификаторФормы);
	КонецЕсли;
	
	ФиксированныйКонтекст = Новый ФиксированнаяСтруктура(Контекст);
	ПараметрыПроцедуры = Новый Структура(ФиксированныйКонтекст);
	
	АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, ИдентификаторФормы);
	ПараметрыПроцедуры.Вставить("ХранимыеДанные", ХранимыеДанные);
	ПараметрыПроцедуры.Вставить("Версия", 1);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	Если Не ОбщегоНазначения.ИнформационнаяБазаФайловая()
	 Или ОбщегоНазначения.РазделениеВключено()
	 Или ЗапускПриОткрытии И Не Контекст.ОбновлениеДоступаВыполняется Тогда
		ПараметрыВыполнения.СРасширениямиБазыДанных = Истина;
	Иначе
		ПараметрыВыполнения.ЗапуститьНеВФоне = Истина;
	КонецЕсли;
	ПараметрыВыполнения.АдресРезультата = АдресРезультата;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Управление доступом: Получение состояния обновления доступа'");
	ПараметрыВыполнения.УточнениеОшибки =
		НСтр("ru = 'Не удалось обновить состояние по причине:'");

	РезультатЗапуска = ДлительныеОперации.ВыполнитьВФоне("УправлениеДоступомСлужебный.ОбновитьПрогрессВФоне",
		ПараметрыПроцедуры, ПараметрыВыполнения);
	
	ИдентификаторЗаданияОбновленияПрогресса = Неопределено;
	
	Если РезультатЗапуска.Статус = "Выполнено" Тогда
		ЗавершитьОбновлениеПрогрессаНаСервере(Контекст, АдресРезультата,
			АдресХранимыхДанных, Неопределено);
		
	ИначеЕсли РезультатЗапуска.Статус = "Выполняется" Тогда
		ИдентификаторЗаданияОбновленияПрогресса = РезультатЗапуска.ИдентификаторЗадания;
		
	ИначеЕсли РезультатЗапуска.Статус = "Ошибка" Тогда
		Контекст.Вставить("ИнформацияОбОшибке", РезультатЗапуска.ИнформацияОбОшибке);
		Контекст.Вставить("СостояниеЗаданияОбновленияДоступа", СостояниеЗаданияОбновленияДоступа());
	КонецЕсли;
	
	Возврат РезультатЗапуска.Статус;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОтменитьОбновлениеПрогрессаНаСервере(ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	ИдентификаторЗадания = Неопределено;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗавершитьОбновлениеПрогрессаНаСервере(Контекст, Знач АдресРезультата, Знач АдресХранимыхДанных,
			ИдентификаторЗаданияОбновленияПрогресса)
	
	Если ИдентификаторЗаданияОбновленияПрогресса <> Неопределено Тогда
		Результат = ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗаданияОбновленияПрогресса, Истина);
		Если Результат.Статус = "Выполняется" Тогда
			Контекст = Новый Структура("СостояниеЗаданияОбновленияДоступа", СостояниеЗаданияОбновленияДоступа());
			Контекст.Вставить("ОбновлениеПрогрессаВыполняется",
				ДлительныеОперации.ПрочитатьПрогресс(ИдентификаторЗаданияОбновленияПрогресса) <> Неопределено);
			Возврат Ложь;
		ИначеЕсли Результат.Статус <> "Выполнено" Тогда
			ИдентификаторЗаданияОбновленияПрогресса = Неопределено;
			Контекст = Новый Структура("СостояниеЗаданияОбновленияДоступа", СостояниеЗаданияОбновленияДоступа());
			Контекст.Вставить("ИнформацияОбОшибке", Результат.ИнформацияОбОшибке);
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	ИдентификаторЗаданияОбновленияПрогресса = Неопределено;
	
	Контекст = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если Не Контекст.Свойство("ИнформацияОбОшибке") Тогда
		ПоместитьВоВременноеХранилище(Контекст.ХранимыеДанные, АдресХранимыхДанных);
		Контекст.Удалить("ХранимыеДанные");
	КонецЕсли;
	
	Контекст.Вставить("СостояниеЗаданияОбновленияДоступа", СостояниеЗаданияОбновленияДоступа());
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура СортироватьСписок(ПоУбыванию = Ложь)
	
	ТекущаяКолонка = Элементы.Списки.ТекущийЭлемент;
	
	Если ТекущаяКолонка = Неопределено
	 Или Не СтрНачинаетсяС(ТекущаяКолонка.Имя, "Списки") Тогда
		
		ПоказатьПредупреждение(,
			НСтр("ru = 'Выберите колонку для сортировки'"));
		Возврат;
	КонецЕсли;
	
	ОчиститьКартинкуСортировки(Элементы["Списки" + ПоляСортировки[0].Значение]);
	
	ПоляСортировки.Очистить();
	
	Поле = Сред(ТекущаяКолонка.Имя, СтрДлина("Списки") + 1);
	ПоляСортировки.Добавить(Поле, ?(ПоУбыванию, "Убыв", "Возр"));
	Если Поле <> "СписокПредставление" Тогда
		ПоляСортировки.Добавить("СписокПредставление", "Возр");
	КонецЕсли;
	
	УстановитьКартинкуСортировки(ТекущаяКолонка, ПоУбыванию);
	
	СортироватьСписокПоПолям();
	
	ПоказатьОповещениеПользователя(
		?(ПоУбыванию, НСтр("ru = 'Сортировка по убыванию'"),
			НСтр("ru = 'Сортировка по возрастанию'")),,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Колонка ""%1""'"),
			СтрЗаменить(ТекущаяКолонка.Заголовок, Символы.ПС, " ")));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКартинкуСортировки(Элемент, ПоУбыванию)
	
	ДобавкаШирины = 3;
	
	Если Элемент.Ширина > 0 Тогда
		Элемент.Ширина = Элемент.Ширина + ДобавкаШирины;
	КонецЕсли;
	Элемент.КартинкаШапки = ?(ПоУбыванию,
		БиблиотекаКартинок.СортироватьСписокПоУбыванию,
		БиблиотекаКартинок.СортироватьСписокПоВозрастанию);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьКартинкуСортировки(Элемент)
	
	ДобавкаШирины = 3;
	
	Элемент.КартинкаШапки = Новый Картинка;
	Если Элемент.Ширина > ДобавкаШирины Тогда
		Элемент.Ширина = Элемент.Ширина - ДобавкаШирины;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СортировкаСПереходомКНачалу()
	
	Возврат ПоляСортировки[0].Значение <> "СписокПредставление";
	
КонецФункции

&НаКлиенте
Процедура СортироватьСписокПоПолям(ИндексПоляСортировки = 0, СтрокиСписка = Неопределено)
	
	Если ИндексПоляСортировки >= ПоляСортировки.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ПолеСортировки = ПоляСортировки[ИндексПоляСортировки].Значение;
	Если СтрокиСписка = Неопределено Тогда
		СтрокиСписка = Новый СписокЗначений;
		Если Списки.Количество() < 2 Тогда
			Возврат;
		КонецЕсли;
		Для Каждого Строка Из Списки Цикл
			СтрокиСписка.Добавить(Строка,
				ПредставлениеДляСортировки(Строка[ПолеСортировки]));
		КонецЦикла;
	ИначеЕсли СтрокиСписка.Количество() < 2 Тогда
		Возврат;
	Иначе
		Для Каждого ЭлементСписка Из СтрокиСписка Цикл
			ЭлементСписка.Представление =
				ПредставлениеДляСортировки(ЭлементСписка.Значение[ПолеСортировки]);
		КонецЦикла;
	КонецЕсли;
	
	НачальныйИндекс = Списки.Индекс(СтрокиСписка[0].Значение);
	СтрокиСписка.СортироватьПоПредставлению(
		НаправлениеСортировки[ПоляСортировки[ИндексПоляСортировки].Представление]);
	
	ТекущееПредставление = Неопределено;
	Подстроки = Неопределено;
	НовыйИндекс = НачальныйИндекс;
	Для Каждого ЭлементСписка Из СтрокиСписка Цикл
		ТекущийИндекс = Списки.Индекс(ЭлементСписка.Значение);
		Если ТекущийИндекс <> НовыйИндекс Тогда
			Списки.Сдвинуть(ТекущийИндекс, НовыйИндекс - ТекущийИндекс);
		КонецЕсли;
		Если ТекущееПредставление <> ЭлементСписка.Представление Тогда
			Если Подстроки <> Неопределено Тогда
				СортироватьСписокПоПолям(ИндексПоляСортировки + 1, Подстроки);
			КонецЕсли;
			Подстроки = Новый СписокЗначений;
			ТекущееПредставление = ЭлементСписка.Представление;
		КонецЕсли;
		Подстроки.Добавить(ЭлементСписка.Значение);
		НовыйИндекс = НовыйИндекс + 1;
	КонецЦикла;
	
	Если Подстроки <> Неопределено Тогда
		СортироватьСписокПоПолям(ИндексПоляСортировки + 1, Подстроки);
	КонецЕсли;
	
	Если ИндексПоляСортировки = 0
	   И СортировкаСПереходомКНачалу()
	   И Списки.Количество() > 0 Тогда
		
		Элементы.Списки.ТекущаяСтрока = Списки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПредставлениеДляСортировки(Значение)
	
	Возврат Формат(Значение, "ЧЦ=15; ЧДЦ=4; ЧН=00000000000,0000; ЧВН=; ЧГ=");
	
КонецФункции

&НаСервереБезКонтекста
Процедура ВключитьОбновлениеДоступаНаСервере()
	
	УправлениеДоступомСлужебный.УстановитьЗапретОбновленияДоступа(Ложь);
	УправлениеДоступомСлужебный.УстановитьОбновлениеДоступа(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстановитьИЗапретитьОбновлениеДоступаОбработчикОжидания()
	
	ОстановитьИЗапретитьОбновлениеДоступаНаСервере();
	
	Элементы.ОбновлениеДоступаЗапрещено.Видимость = Истина;
	Элементы.РегламентноеЗаданиеОтключено.Видимость = Ложь;
	
	НачатьОбновлениеПрогресса(Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОстановитьИЗапретитьОбновлениеДоступаНаСервере()
	
	УправлениеДоступомСлужебный.УстановитьОбновлениеДоступа(Ложь);
	УправлениеДоступомСлужебный.ОтменитьОбновлениеДоступаНаУровнеЗаписей();
	
КонецПроцедуры

#КонецОбласти

