///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не Параметры.РазрешеноОткрытиеФормы Тогда
		ТекстОшибки = НСтр("ru = 'Произвольное открытие формы не предусмотрено.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ЗаполнитьТипыОбъектовВДеревеЗначений();

	ДоступенРучнойЗапускПереносаФайлов = Не ОбщегоНазначения.РазделениеВключено() И ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РегламентныеЗадания");
	
	Элементы.ГруппаСостояниеВыполненияПереносаФайлов.Видимость = ДоступенРучнойЗапускПереносаФайлов;
	Элементы.ПеренестиФайлы.Видимость = ДоступенРучнойЗапускПереносаФайлов;

	Если ДоступенРучнойЗапускПереносаФайлов Тогда

		Элементы.НадписьТекущееСостояние.Заголовок = ПолучитьПредставлениеТекущегоСостоянияВыполненияОперацийПереносаФайлов();

	КонецЕсли;

КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоОбъектовМетаданных

&НаКлиенте
Процедура ДеревоОбъектовМетаданныхПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)

	Отказ = Истина;
	Если НЕ Копирование Тогда
		ПодключитьОбработчикОжидания("ДобавитьНастройкуРаботыСФайловымАрхивом", 0.1, Истина);
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовМетаданныхПриАктивизацииПоля(Элемент)

	Элемент.ТекущийЭлемент.ТолькоПросмотр = Не ТаблицаФормыПолучитьДоступностьЭлементаДляРедактирования(Элемент);

КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовМетаданныхДействиеПриИзменении(Элемент)

	ЗаписатьТекущиеНастройки();

КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовМетаданныхПеренестиВФайловыйАрхивЧерезДнейПриИзменении(Элемент)

	Если Не НастройкаЗаполненаКорректно() Тогда
		Возврат;		
	КонецЕсли;
	
	ЗаписатьТекущиеНастройки();

КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовМетаданныхДействиеОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;	
	УстановитьДействиеДляВыбранныхОбъектов(ПредопределенноеЗначение("Перечисление.ДействияВНастройкахРаботыСФайловымАрхивом.НеПереноситьВАрхив"));

КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовМетаданныхПередНачаломИзменения(Элемент, Отказ)

	Отказ = Не ТаблицаФормыПолучитьДоступностьЭлементаДляРедактирования(Элемент); // АПК:144 Функция возвращает значение типа Булево.

КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовМетаданныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ДобавитьНастройкиПоВладельцу(ВыбранноеЗначение);

КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовМетаданныхПередУдалением(Элемент, Отказ)

	Отказ = Истина;

	НастройкаДляУдаления = ДеревоОбъектовМетаданных.НайтиПоИдентификатору(Элементы.ДеревоОбъектовМетаданных.ТекущаяСтрока);
	Если НастройкаДляУдаления <> Неопределено Тогда

		НастройкаДляУдаленияРодителя = НастройкаДляУдаления.ПолучитьРодителя();

		Если НастройкаДляУдаленияРодителя <> Неопределено И НастройкаДляУдаленияРодителя.ВозможностьДетализации Тогда

			ТекстВопроса = НСтр("ru = 'Удаление настройки приведет к прекращению использования
				|заданных в ней правил. Продолжить?'");
			ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьНастройкуЗавершение", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, НСтр("ru = 'Предупреждение'"));
			Возврат;

		КонецЕсли;

	КонецЕсли;

	ТекстСообщения = НСтр("ru = 'Расширенная настройка работы с файловым архивом не предусмотрена для этого объекта.'");
	ПоказатьПредупреждение(, ТекстСообщения);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиФайлы(Команда)
	ПрерватьФоновоеЗадание();
	ЗапуститьРегламентноеЗадание();

	ПодключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания", 2, Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ЗаполнитьТипыОбъектовВДеревеЗначений()

	НастройкиРаботыСФайловымАрхивом = РегистрыСведений.НастройкиРаботыСФайловымАрхивом.ТекущиеНастройкиРаботыСФайловымАрхивом();

	ДеревоОМ = РеквизитФормыВЗначение("ДеревоОбъектовМетаданных");
	ДеревоОМ.Строки.Очистить();

	МетаданныеСправочники = Метаданные.Справочники;

	ТаблицаТипов = Новый ТаблицаЗначений;
	ТаблицаТипов.Колонки.Добавить("ВладелецФайла");
	ТаблицаТипов.Колонки.Добавить("ТипВладельцаФайла");
	ТаблицаТипов.Колонки.Добавить("ИмяВладельцаФайла");
	ТаблицаТипов.Колонки.Добавить("ЭтоФайл", Новый ОписаниеТипов("Булево"));
	ТаблицаТипов.Колонки.Добавить("ВозможностьДетализации"  , Новый ОписаниеТипов("Булево"));

	Для Каждого Справочник Из МетаданныеСправочники Цикл

		Если Справочник.Реквизиты.Найти("ВладелецФайла") = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ТипыВладельцевФайлов = Справочник.Реквизиты.ВладелецФайла.Тип.Типы();
		Для Каждого ТипВладельца Из ТипыВладельцевФайлов Цикл
			
			МетаданныеВладельца = Метаданные.НайтиПоТипу(ТипВладельца);
			
			НоваяСтрока = ТаблицаТипов.Добавить();
			НоваяСтрока.ВладелецФайла = ТипВладельца;
			НоваяСтрока.ТипВладельцаФайла = Справочник;
			НоваяСтрока.ИмяВладельцаФайла = МетаданныеВладельца.ПолноеИмя();
			НоваяСтрока.ВозможностьДетализации = Истина;
			
			Если Не СтрЗаканчиваетсяНа(Справочник.Имя, РаботаСФайламиСлужебный.СуффиксСправочникаПрисоединенныеФайлы()) Тогда
				НоваяСтрока.ЭтоФайл = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

	ВсеСправочники = Справочники.ТипВсеСсылки();

	ВсеДокументы = Документы.ТипВсеСсылки();
	УзелСправочники = Неопределено;
	УзелДокументы = Неопределено;
	УзелБизнесПроцессы = Неопределено;

	УзелОбщиеНастройки = ДеревоОМ.Строки.Добавить();
	УзелОбщиеНастройки.СинонимНаименованияОбъекта	= НСтр("ru = 'Общие настройки'");	

	ОбщиеНастройки = НастройкиРаботыСФайловымАрхивом.НайтиСтроки(Новый Структура("ВладелецФайла, ТипВладельцаФайла", Неопределено, Неопределено));
	Если ОбщиеНастройки.Количество() > 0 Тогда

		УзелОбщиеНастройки.ПеренестиВФайловыйАрхивЧерезДней	= ОбщиеНастройки[0].ПеренестиВФайловыйАрхивЧерезДней;

	КонецЕсли;

	ВладельцыФайлов = Новый Массив;
	Для Каждого Тип Из ТаблицаТипов Цикл
		
		ТипВладельцаФайла = Тип.ТипВладельцаФайла; // ОбъектМетаданныхСправочник
		Если СтрНачинаетсяС(ТипВладельцаФайла.Имя, "Удалить")
			Или ВладельцыФайлов.Найти(Тип.ИмяВладельцаФайла) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ВладельцыФайлов.Добавить(Тип.ИмяВладельцаФайла);

		Если ВсеСправочники.СодержитТип(Тип.ВладелецФайла) Тогда
			Если УзелСправочники = Неопределено Тогда
				УзелСправочники = ДеревоОМ.Строки.Добавить();
				УзелСправочники.СинонимНаименованияОбъекта = НСтр("ru = 'Справочники'");
			КонецЕсли;
			НоваяСтрокаТаблицы = УзелСправочники.Строки.Добавить();
		ИначеЕсли ВсеДокументы.СодержитТип(Тип.ВладелецФайла) Тогда
			Если УзелДокументы = НеОпределено Тогда
				УзелДокументы = ДеревоОМ.Строки.Добавить();
				УзелДокументы.СинонимНаименованияОбъекта = НСтр("ru = 'Документы'");
			КонецЕсли;
			НоваяСтрокаТаблицы = УзелДокументы.Строки.Добавить();
		ИначеЕсли БизнесПроцессы.ТипВсеСсылки().СодержитТип(Тип.ВладелецФайла) Тогда
			Если УзелБизнесПроцессы = Неопределено Тогда
				УзелБизнесПроцессы = ДеревоОМ.Строки.Добавить();
				УзелБизнесПроцессы.СинонимНаименованияОбъекта = НСтр("ru = 'Бизнес-процессы'");
			КонецЕсли;
			НоваяСтрокаТаблицы = УзелБизнесПроцессы.Строки.Добавить();
		КонецЕсли;
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(Тип.ВладелецФайла);
		НоваяСтрокаТаблицы.ВладелецФайла = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип.ВладелецФайла);
		НоваяСтрокаТаблицы.ТипВладельцаФайла = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип.ТипВладельцаФайла);
		НоваяСтрокаТаблицы.СинонимНаименованияОбъекта = МетаданныеОбъекта.Синоним;
		НоваяСтрокаТаблицы.ЭтоФайл = Тип.ЭтоФайл;
		НоваяСтрокаТаблицы.ВозможностьДетализации = Тип.ВозможностьДетализации;
		
		ИдентификаторОбъекта		= ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип.ВладелецФайла);
		ДетализированныеНастройки	= НастройкиРаботыСФайловымАрхивом.НайтиСтроки(Новый Структура(
										"ИдентификаторВладельца, ЭтоФайл", ИдентификаторОбъекта, Тип.ЭтоФайл));

		Если ДетализированныеНастройки.Количество() > 0 Тогда
			Для Каждого Настройка Из ДетализированныеНастройки Цикл
				ДетализированнаяНастройка = НоваяСтрокаТаблицы.Строки.Добавить();
				ДетализированнаяНастройка.ВладелецФайла						= Настройка.ВладелецФайла;
				ДетализированнаяНастройка.ТипВладельцаФайла					= Настройка.ТипВладельцаФайла;
				ДетализированнаяНастройка.СинонимНаименованияОбъекта		= Настройка.ВладелецФайла;
				ДетализированнаяНастройка.Действие							= Настройка.Действие;
				ДетализированнаяНастройка.ПеренестиВФайловыйАрхивЧерезДней	= Настройка.ПеренестиВФайловыйАрхивЧерезДней;
				ДетализированнаяНастройка.ЭтоФайл							= Настройка.ЭтоФайл;
			КонецЦикла;
		КонецЕсли;		
		
		НайденныеНастройки = НастройкиРаботыСФайловымАрхивом.НайтиСтроки(Новый Структура("ВладелецФайла, ЭтоФайл", НоваяСтрокаТаблицы.ВладелецФайла, Тип.ЭтоФайл));

		Если НайденныеНастройки.Количество() > 0 Тогда
			НоваяСтрокаТаблицы.Действие							= НайденныеНастройки[0].Действие;
			НоваяСтрокаТаблицы.ПеренестиВФайловыйАрхивЧерезДней	= НайденныеНастройки[0].ПеренестиВФайловыйАрхивЧерезДней;
		Иначе
			НоваяСтрокаТаблицы.Действие							= Перечисления.ДействияВНастройкахРаботыСФайловымАрхивом.НеПереноситьВАрхив;
			НоваяСтрокаТаблицы.ПеренестиВФайловыйАрхивЧерезДней	= 0;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого УзелВерхнегоУровня Из ДеревоОМ.Строки Цикл
		УзелВерхнегоУровня.Строки.Сортировать("СинонимНаименованияОбъекта");
	КонецЦикла;
	ЗначениеВРеквизитФормы(ДеревоОМ, "ДеревоОбъектовМетаданных");
	
КонецПроцедуры

&НаКлиенте
Функция ТаблицаФормыПолучитьДоступностьЭлементаДляРедактирования(Элемент)

	Результат = Истина;

	Если Элемент.ТекущийЭлемент.Имя = "ДеревоОбъектовМетаданныхСинонимНаименованияОбъекта" Тогда
		Результат = Ложь;
	ИначеЕсли Элемент.ТекущиеДанные.ТипВладельцаФайла = Неопределено Тогда
		Результат = Элемент.ТекущийЭлемент.Имя = "ДеревоОбъектовМетаданныхПеренестиВФайловыйАрхивЧерезДней";
	КонецЕсли;

	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ДобавитьВладельцаФайла(ВыбранныеВладельцы, СтрокаДереваОбъектов)
	Если ТипЗнч(СтрокаДереваОбъектов.ВладелецФайла) = Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных")
	   Или ТипЗнч(СтрокаДереваОбъектов.ВладелецФайла) = Тип("СправочникСсылка.ИдентификаторыОбъектовРасширений")
	   Или СтрокаДереваОбъектов.ВладелецФайла = Неопределено Тогда
		ПодчиненныеСтроки = СтрокаДереваОбъектов.ПолучитьЭлементы();
		Для Каждого СтрокаДерева Из ПодчиненныеСтроки Цикл
			ДобавитьВладельцаФайла(ВыбранныеВладельцы, СтрокаДерева);
		КонецЦикла;
	Иначе
		Если ВыбранныеВладельцы.Найти(СтрокаДереваОбъектов.ВладелецФайла) = Неопределено Тогда
			ВыбранныеВладельцы.Добавить(СтрокаДереваОбъектов.ВладелецФайла);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНастройкуЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ОчиститьДанныеНастройки();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНастройкуРаботыСФайловымАрхивом()

	СтрокаДерева = Элементы.ДеревоОбъектовМетаданных.ТекущиеДанные;

	Если Не СтрокаДерева.ВозможностьДетализации Тогда
		ТекстСообщения = НСтр("ru = 'Расширенная настройка работы с файловым архивом не предусмотрена для этого объекта.'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;

	ПараметрыФормыВыбора = Новый Структура;

	ПараметрыФормыВыбора.Вставить("ВыборГруппИЭлементов"			, ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
	ПараметрыФормыВыбора.Вставить("ЗакрыватьПриВыборе"				, Истина);
	ПараметрыФормыВыбора.Вставить("ЗакрыватьПриЗакрытииВладельца"	, Истина);
	ПараметрыФормыВыбора.Вставить("МножественныйВыбор"				, Истина);
	ПараметрыФормыВыбора.Вставить("РежимВыбора"						, Истина);

	ПараметрыФормыВыбора.Вставить("РежимОткрытияОкна"		, РежимБлокировкиПриОткрытииОкнаФормы.БлокироватьВладельца);
	ПараметрыФормыВыбора.Вставить("ВыборГрупп"				, Истина);
	ПараметрыФормыВыбора.Вставить("ВыборГруппПользователей"	, Истина);

	ПараметрыФормыВыбора.Вставить("РасширенныйПодбор"		, Истина);
	ПараметрыФормыВыбора.Вставить("ЗаголовокФормыПодбора"	, НСтр("ru = 'Подбор элементов настроек'"));

	// Исключим из списка выбора уже существующие настройки.
	СуществующиеНастройки = СтрокаДерева.ПолучитьЭлементы();
	ФиксированныеНастройки = Новый НастройкиКомпоновкиДанных;
	ЭлементНастройки = ФиксированныеНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементНастройки.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
	ЭлементНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	СписокСуществующих = Новый Массив;
	Для Каждого Настройка Из СуществующиеНастройки Цикл
		СписокСуществующих.Добавить(Настройка.ВладелецФайла);
	КонецЦикла;
	ЭлементНастройки.ПравоеЗначение = СписокСуществующих;
	ЭлементНастройки.Использование = Истина;
	ЭлементНастройки.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ПараметрыФормыВыбора.Вставить("ФиксированныеНастройки", ФиксированныеНастройки);

	ОткрытьФорму(ПутьФормыВыбора(СтрокаДерева.ВладелецФайла), ПараметрыФормыВыбора, Элементы.ДеревоОбъектовМетаданных);

КонецПроцедуры

&НаСервере
Функция ПутьФормыВыбора(Знач ВладелецФайла)

	ОбъектМетаданных = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ВладелецФайла);
	Возврат ОбъектМетаданных.ПолноеИмя() + ".ФормаВыбора";

КонецФункции

&НаСервере
Процедура ДобавитьНастройкиПоВладельцу(Знач ВыбранноеЗначение)

	СтрокаВладелец = ДеревоОбъектовМетаданных.НайтиПоИдентификатору(Элементы.ДеревоОбъектовМетаданных.ТекущаяСтрока);


	СтруктураЗаписиРегистра = РегистрыСведений.НастройкиРаботыСФайловымАрхивом.СтруктуруЗаписи();
	ЗаполнитьЗначенияСвойств(СтруктураЗаписиРегистра, СтрокаВладелец);
	РегистрыСведений.НастройкиРаботыСФайловымАрхивом.ДобавитьЗапись(СтруктураЗаписиРегистра);

	ЭлементВладелец = СтрокаВладелец.ПолучитьЭлементы();
	Для Каждого Настройка Из ВыбранноеЗначение Цикл

		СтруктураЗаписиРегистра = РегистрыСведений.НастройкиРаботыСФайловымАрхивом.СтруктуруЗаписи();
		СтруктураЗаписиРегистра.ВладелецФайла						= Настройка;
		СтруктураЗаписиРегистра.ТипВладельцаФайла					= СтрокаВладелец.ТипВладельцаФайла;
		СтруктураЗаписиРегистра.Действие							= Перечисления.ДействияВНастройкахРаботыСФайловымАрхивом.НеПереноситьВАрхив;
		СтруктураЗаписиРегистра.ПеренестиВФайловыйАрхивЧерезДней	= СтрокаВладелец.ПеренестиВФайловыйАрхивЧерезДней;
		СтруктураЗаписиРегистра.ЭтоФайл								= СтрокаВладелец.ЭтоФайл;

		РегистрыСведений.НастройкиРаботыСФайловымАрхивом.ДобавитьЗапись(СтруктураЗаписиРегистра);

		ДетализированнаяНастройка = ЭлементВладелец.Добавить();
		ЗаполнитьЗначенияСвойств(ДетализированнаяНастройка, СтруктураЗаписиРегистра);
		ДетализированнаяНастройка.СинонимНаименованияОбъекта = Настройка;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьДанныеНастройки()

	НастройкаДляУдаления = ДеревоОбъектовМетаданных.НайтиПоИдентификатору(Элементы.ДеревоОбъектовМетаданных.ТекущаяСтрока);

	РегистрыСведений.НастройкиРаботыСФайловымАрхивом.УдалитьЗапись(НастройкаДляУдаления.ВладелецФайла, НастройкаДляУдаления.ТипВладельцаФайла);

	РодительЭлементаНастроек = НастройкаДляУдаления.ПолучитьРодителя();
	Если РодительЭлементаНастроек <> Неопределено Тогда
		РодительЭлементаНастроек.ПолучитьЭлементы().Удалить(НастройкаДляУдаления);
	Иначе
		ДеревоОбъектовМетаданных.ПолучитьЭлементы().Удалить(НастройкаДляУдаления);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьДействиеДляВыбранныхОбъектов(Знач Действие)

	Для Каждого ИдентификаторСтроки Из Элементы.ДеревоОбъектовМетаданных.ВыделенныеСтроки Цикл
		ЭлементДерева = ДеревоОбъектовМетаданных.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ЭлементДерева.ПолучитьРодителя() = Неопределено Тогда
			Для Каждого ПодчиненныйЭлементДерева Из ЭлементДерева.ПолучитьЭлементы() Цикл
				УстановитьДействиеВыбранногоОбъектаСРекурсией(ПодчиненныйЭлементДерева, Действие);
			КонецЦикла;
		Иначе
			УстановитьДействиеВыбранногоОбъектаСРекурсией(ЭлементДерева, Действие);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьДействиеВыбранногоОбъектаСРекурсией(ВыбранныйОбъект, Знач Действие)

	УстановитьДействиеВыбранногоОбъекта(ВыбранныйОбъект, Действие);
	Для Каждого ДочернийОбъект Из ВыбранныйОбъект.ПолучитьЭлементы() Цикл
		УстановитьДействиеВыбранногоОбъекта(ДочернийОбъект, Действие);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьДействиеВыбранногоОбъекта(ВыбранныйОбъект, Знач Действие)

	ВыбранныйОбъект.Действие = Действие;
	ЗаписатьТекущиеНастройкиПоОбъекту(
		ВыбранныйОбъект.ВладелецФайла,
		ВыбранныйОбъект.ТипВладельцаФайла,
		Действие,
		ВыбранныйОбъект.ПеренестиВФайловыйАрхивЧерезДней,
		ВыбранныйОбъект.ЭтоФайл);

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьТекущиеНастройки()

	ТекущиеДанные = Элементы.ДеревоОбъектовМетаданных.ТекущиеДанные;
	ЗаписатьТекущиеНастройкиПоОбъекту(
		ТекущиеДанные.ВладелецФайла,
		ТекущиеДанные.ТипВладельцаФайла,
		ТекущиеДанные.Действие,
		ТекущиеДанные.ПеренестиВФайловыйАрхивЧерезДней,
		ТекущиеДанные.ЭтоФайл);

КонецПроцедуры

&НаСервере
Процедура ЗаписатьТекущиеНастройкиПоОбъекту(Знач ВладелецФайла, Знач ТипВладельцаФайла, Знач Действие, Знач ПеренестиВФайловыйАрхивЧерезДней, Знач ЭтоФайл)

	СтруктураЗаписиРегистра = РегистрыСведений.НастройкиРаботыСФайловымАрхивом.СтруктуруЗаписи();
	СтруктураЗаписиРегистра.ВладелецФайла						= ВладелецФайла;
	СтруктураЗаписиРегистра.ТипВладельцаФайла					= ТипВладельцаФайла;
	СтруктураЗаписиРегистра.Действие							= Действие;
	СтруктураЗаписиРегистра.ПеренестиВФайловыйАрхивЧерезДней	= ПеренестиВФайловыйАрхивЧерезДней;
	СтруктураЗаписиРегистра.ЭтоФайл								= ЭтоФайл;
	
	РегистрыСведений.НастройкиРаботыСФайловымАрхивом.ДобавитьЗапись(СтруктураЗаписиРегистра);

КонецПроцедуры

&НаСервере
Процедура ЗапуститьРегламентноеЗадание()

	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РегламентныеЗадания") Тогда
		Возврат;
	КонецЕсли;
	
	МодульРегламентныеЗаданияСлужебный = ОбщегоНазначения.ОбщийМодуль("РегламентныеЗаданияСлужебный");
	
	ВызватьИсключениеЕслиНетПраваАдминистрирования(МодульРегламентныеЗаданияСлужебный);

	РегламентноеЗаданиеМетаданные = Метаданные.РегламентныеЗадания.ПереносФайловМеждуОперационнымХранилищемИФайловымАрхивом;

	Отбор = Новый Структура;
	ИмяМетода = РегламентноеЗаданиеМетаданные.ИмяМетода;
	Отбор.Вставить("ИмяМетода", ИмяМетода);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	ФоновыеЗаданияПереносаФайлов = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	Если ФоновыеЗаданияПереносаФайлов.Количество() > 0 Тогда
		ИдентификаторФоновогоЗадания = ФоновыеЗаданияПереносаФайлов[0].УникальныйИдентификатор;
	Иначе

		РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(РегламентноеЗаданиеМетаданные); // АПК:453 Используется только в неразделенном режиме работы с данными

		ПараметрыВыполнения = МодульРегламентныеЗаданияСлужебный.ВыполнитьРегламентноеЗаданиеВручную(РегламентноеЗадание.УникальныйИдентификатор);		

		ИдентификаторФоновогоЗадания = Новый УникальныйИдентификатор(ПараметрыВыполнения.ИдентификаторФоновогоЗадания);

		Элементы.НадписьТекущееСостояние.Заголовок = ПолучитьПредставлениеТекущегоСостоянияВыполненияОперацийПереносаФайлов(ИдентификаторФоновогоЗадания)

	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПрерватьФоновоеЗадание()
	ОтменитьВыполнениеЗадания(ИдентификаторФоновогоЗадания);
	ИдентификаторФоновогоЗадания = "";
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(Знач ИдентификаторФоновогоЗадания)

	Если ЗначениеЗаполнено(ИдентификаторФоновогоЗадания) И ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РегламентныеЗадания") Тогда

		ВызватьИсключениеЕслиНетПраваАдминистрирования();

		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторФоновогоЗадания);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция НастройкаЗаполненаКорректно()

	Результат = Истина;

	ТекущиеДанные = Элементы.ДеревоОбъектовМетаданных.ТекущиеДанные;

	Если ТекущиеДанные.ВладелецФайла = Неопределено И ТекущиеДанные.ТипВладельцаФайла = Неопределено Тогда
		Если ТекущиеДанные.ПеренестиВФайловыйАрхивЧерезДней = 0 Тогда

			Результат = Ложь;

			ТекущиеДанные.ПеренестиВФайловыйАрхивЧерезДней = ПолучитьТекущееЗначениеНастройкиПоУмолчанию();

			ТекстСообщения = НСтр("ru = 'Значение общих настроек хранения файлов должно быть обязательно заполнено
									|Восстановлено старое значение.'");
			ПоказатьПредупреждение(,ТекстСообщения);

		КонецЕсли;
	КонецЕсли;

	Возврат Результат;	

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТекущееЗначениеНастройкиПоУмолчанию()

	Возврат РегистрыСведений.НастройкиРаботыСФайловымАрхивом.ПолучитьКоличествоДнейХраненияИзНастройкиПоУмолчанию();

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПредставлениеТекущегоСостоянияВыполненияОперацийПереносаФайлов(Знач ИдентификаторФоновогоЗадания = Неопределено)
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РегламентныеЗадания") Тогда
		Возврат "";
	КонецЕсли;

	ВызватьИсключениеЕслиНетПраваАдминистрирования();

	СведенияДляФормированияПредставления = Новый Структура;
	СведенияДляФормированияПредставления.Вставить("Состояние"	, Неопределено);
	СведенияДляФормированияПредставления.Вставить("Начало"		, Дата(1,1,1));
	СведенияДляФормированияПредставления.Вставить("Конец"		, Дата(1,1,1));	

	Если ИдентификаторФоновогоЗадания = Неопределено Тогда

		РегламентноеЗаданиеМетаданные = Метаданные.РегламентныеЗадания.ПереносФайловМеждуОперационнымХранилищемИФайловымАрхивом;

		Отбор = Новый Структура;
		Отбор.Вставить("ИмяМетода", РегламентноеЗаданиеМетаданные.ИмяМетода);
		Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
		ФоновыеЗаданияПереносаФайлов = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
		
		Если ФоновыеЗаданияПереносаФайлов.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(СведенияДляФормированияПредставления, ФоновыеЗаданияПереносаФайлов[0]);
		Иначе

			Отбор.Удалить("Состояние");

			ФоновыеЗаданияПереносаФайлов = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);

			Если ФоновыеЗаданияПереносаФайлов.Количество() > 0 Тогда
				ПоследнееФоновоеЗадание = ФоновыеЗаданияПереносаФайлов[0];
				Для Каждого ФоновоеЗадание Из ФоновыеЗаданияПереносаФайлов Цикл
					Если ПоследнееФоновоеЗадание.Конец < ФоновоеЗадание.Конец Тогда
						ПоследнееФоновоеЗадание = ФоновоеЗадание;
					КонецЕсли;
				КонецЦикла;

				ЗаполнитьЗначенияСвойств(СведенияДляФормированияПредставления, ПоследнееФоновоеЗадание);
			КонецЕсли;	
		КонецЕсли;
	Иначе

		НайденноеФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторФоновогоЗадания);

		Если НайденноеФоновоеЗадание <> Неопределено Тогда

			ЗаполнитьЗначенияСвойств(СведенияДляФормированияПредставления, НайденноеФоновоеЗадание);
		КонецЕсли;
	КонецЕсли;

	Возврат ПолучитьПредставлениеСостоянияВыполненияФоновогоЗадания(СведенияДляФормированияПредставления);

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПредставлениеСостоянияВыполненияФоновогоЗадания(СведенияОСостоянииФоновогоЗадания)

	Результат = "";

	Если СведенияОСостоянииФоновогоЗадания.Состояние = Неопределено Тогда

		Результат = НСтр("ru = 'Нет информации'");

	ИначеЕсли СведенияОСостоянииФоновогоЗадания.Состояние = СостояниеФоновогоЗадания.Активно Тогда

		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Выполняется. Начато %1'"), 
																			Формат(СведенияОСостоянииФоновогоЗадания.Начало, "ДЛФ=DT"));

	Иначе

		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 %2", 
																			СведенияОСостоянииФоновогоЗадания.Состояние, 
																			Формат(СведенияОСостоянииФоновогоЗадания.Конец, "ДЛФ=DT"));

	КонецЕсли;

	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ПроверитьВыполнениеФоновогоЗадания()
	Если ЗначениеЗаполнено(ИдентификаторФоновогоЗадания) И Не ЗаданиеЗавершено(ИдентификаторФоновогоЗадания) Тогда
		ПодключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания", 5, Истина);
	Иначе
		Элементы.НадписьТекущееСостояние.Заголовок = ПолучитьПредставлениеТекущегоСостоянияВыполненияОперацийПереносаФайлов(ИдентификаторФоновогоЗадания);
		ИдентификаторФоновогоЗадания = "";
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеЗавершено(Знач ИдентификаторФоновогоЗадания)

	Результат = Истина;
	
	РезультатВыполненияЗадания = ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторФоновогоЗадания, Истина);
	
	Если РезультатВыполненияЗадания.Статус = "Выполняется" Тогда
		Результат = Ложь;
	КонецЕсли;

	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Процедура ВызватьИсключениеЕслиНетПраваАдминистрирования(Знач МодульРегламентныеЗаданияСлужебный = Неопределено)
	
	Если МодульРегламентныеЗаданияСлужебный = Неопределено Тогда
		МодульРегламентныеЗаданияСлужебный = ОбщегоНазначения.ОбщийМодуль("РегламентныеЗаданияСлужебный");
	КонецЕсли;
	
	МодульРегламентныеЗаданияСлужебный.ВызватьИсключениеЕслиНетПраваАдминистрирования();
	
КонецПроцедуры

#КонецОбласти