///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает таблицу значений, содержащую настройки для работы с файловым архивом.
// Используется для выполнения регламентного задания "РезервноеКопированиеВстроенногоХранилищаДвоичныхДанных".
//
// Возвращаемое значение:
//  ТаблицаЗначений
//
Функция ПолучитьНастройкиРаботыСФайловымАрхивомДляПереносаФайлов() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаТекущиеНастройкиРаботыСФайловымАрхивом(Истина);

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Возвращает количество дней хранения из настройки по умолчанию.
//
// Возвращаемое значение:
//  Число
//
Функция ПолучитьКоличествоДнейХраненияИзНастройкиПоУмолчанию() Экспорт

	Результат = 0;

	Запрос = Новый Запрос;
	Запрос.Текст =
       "ВЫБРАТЬ
       |	НастройкиРаботыСФайловымАрхивом.ПеренестиВФайловыйАрхивЧерезДней КАК ПеренестиВФайловыйАрхивЧерезДней
       |ИЗ
       |	РегистрСведений.НастройкиРаботыСФайловымАрхивом КАК НастройкиРаботыСФайловымАрхивом
       |ГДЕ
       |	НастройкиРаботыСФайловымАрхивом.ВладелецФайла = НЕОПРЕДЕЛЕНО
       |	И НастройкиРаботыСФайловымАрхивом.ТипВладельцаФайла = НЕОПРЕДЕЛЕНО";

	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда
		Результат = ВыборкаДанных.ПеренестиВФайловыйАрхивЧерезДней;
	КонецЕсли;

	Если Результат = 0 Тогда
		Результат = ПолучитьЗначениеПоУмолчаниюДляПеренестиВФайловыйАрхивЧерезДней();
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет запись в регистр сведений.
// 
Процедура ДобавитьЗапись(СтруктураЗаписи) Экспорт

	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВладелецФайла.Установить(СтруктураЗаписи["ВладелецФайла"]);
	НаборЗаписей.Отбор.ТипВладельцаФайла.Установить(СтруктураЗаписи["ТипВладельцаФайла"]);

	Запись = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(Запись, СтруктураЗаписи);

	НаборЗаписей.Записать(Истина);

КонецПроцедуры

// Удаляет запись из регистра сведений.
//
Процедура УдалитьЗапись(ВладелецФайла, ТипВладельцаФайла) Экспорт
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.ВладелецФайла.Установить(ВладелецФайла);
	Набор.Отбор.ТипВладельцаФайла.Установить(ТипВладельцаФайла);
	Набор.Записать();	
	
КонецПроцедуры 

Функция СтруктуруЗаписи() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ВладелецФайла");
	Результат.Вставить("ТипВладельцаФайла");
	Результат.Вставить("Действие");
	Результат.Вставить("ПеренестиВФайловыйАрхивЧерезДней");
	Результат.Вставить("ЭтоФайл");
	
	Возврат Результат;
	
КонецФункции

Процедура ОбновитьНастройкиРаботыСФайловымАрхивом()
	
	МетаданныеСправочники = Метаданные.Справочники;
	
	ТаблицаВладельцевФайлов = Новый ТаблицаЗначений;
	ТаблицаВладельцевФайлов.Колонки.Добавить("ВладелецФайла",     Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	ТаблицаВладельцевФайлов.Колонки.Добавить("ТипВладельцаФайла", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	ТаблицаВладельцевФайлов.Колонки.Добавить("ЭтоФайл",           Новый ОписаниеТипов("Булево"));
	
	Для Каждого Справочник Из МетаданныеСправочники Цикл
		
		Если Справочник.Реквизиты.Найти("ВладелецФайла") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТипыВладельцевФайлов = Справочник.Реквизиты.ВладелецФайла.Тип.Типы();
		Для Каждого ТипВладельца Из ТипыВладельцевФайлов Цикл
			
			НоваяСтрока = ТаблицаВладельцевФайлов.Добавить();
			НоваяСтрока.ВладелецФайла = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипВладельца);
			НоваяСтрока.ТипВладельцаФайла = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Справочник);
			Если Не СтрЗаканчиваетсяНа(Справочник.Имя, РаботаСФайламиСлужебный.СуффиксСправочникаПрисоединенныеФайлы()) Тогда
				НоваяСтрока.ЭтоФайл = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

	Запрос = Новый Запрос; Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаВладельцевФайлов.ВладелецФайла КАК ВладелецФайла,
		|	ТаблицаВладельцевФайлов.ТипВладельцаФайла КАК ТипВладельцаФайла,
		|	ТаблицаВладельцевФайлов.ЭтоФайл КАК ЭтоФайл
		|ПОМЕСТИТЬ ТаблицаВладельцевФайлов
		|ИЗ
		|	&ТаблицаВладельцевФайлов КАК ТаблицаВладельцевФайлов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВладелецФайла,
		|	ЭтоФайл
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиРаботыСФайловымАрхивом.ВладелецФайла КАК ВладелецФайла,
		|	НастройкиРаботыСФайловымАрхивом.ТипВладельцаФайла КАК ТипВладельцаФайла,
		|	НастройкиРаботыСФайловымАрхивом.ЭтоФайл КАК ЭтоФайл,
		|	ИдентификаторыОбъектовМетаданных.Ссылка КАК ИдентификаторОбъекта
		|ПОМЕСТИТЬ ПодчиненныеНастройки
		|ИЗ
		|	РегистрСведений.НастройкиРаботыСФайловымАрхивом КАК НастройкиРаботыСФайловымАрхивом
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
		|		ПО НастройкиРаботыСФайловымАрхивом.ВладелецФайла <> Неопределено
		|			И НастройкиРаботыСФайловымАрхивом.ТипВладельцаФайла <> Неопределено
		|			И (ТИПЗНАЧЕНИЯ(НастройкиРаботыСФайловымАрхивом.ВладелецФайла) = ТИПЗНАЧЕНИЯ(ИдентификаторыОбъектовМетаданных.ЗначениеПустойСсылки))
		|ГДЕ
		|	НастройкиРаботыСФайловымАрхивом.ВладелецФайла <> Неопределено
		|		И НастройкиРаботыСФайловымАрхивом.ТипВладельцаФайла <> Неопределено		
		|		И ТИПЗНАЧЕНИЯ(НастройкиРаботыСФайловымАрхивом.ВладелецФайла) <> ТИП(Справочник.ИдентификаторыОбъектовМетаданных)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторОбъекта,
		|	ЭтоФайл,
		|	ТипВладельцаФайла
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиРаботыСФайловымАрхивом.ВладелецФайла КАК ВладелецФайла,
		|	НастройкиРаботыСФайловымАрхивом.ТипВладельцаФайла КАК ТипВладельцаФайла,
		|	НастройкиРаботыСФайловымАрхивом.ЭтоФайл КАК ЭтоФайл,
		|	ЛОЖЬ КАК НоваяНастройка
		|ИЗ
		|	РегистрСведений.НастройкиРаботыСФайловымАрхивом КАК НастройкиРаботыСФайловымАрхивом
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаВладельцевФайлов КАК ТаблицаВладельцевФайлов
		|		ПО НастройкиРаботыСФайловымАрхивом.ВладелецФайла = ТаблицаВладельцевФайлов.ВладелецФайла
		|			И НастройкиРаботыСФайловымАрхивом.ЭтоФайл = ТаблицаВладельцевФайлов.ЭтоФайл
		|			И НастройкиРаботыСФайловымАрхивом.ТипВладельцаФайла = ТаблицаВладельцевФайлов.ТипВладельцаФайла
		|ГДЕ
		|	ТаблицаВладельцевФайлов.ВладелецФайла ЕСТЬ NULL
		|	И ТИПЗНАЧЕНИЯ(НастройкиРаботыСФайловымАрхивом.ВладелецФайла) = ТИП(Справочник.ИдентификаторыОбъектовМетаданных)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПодчиненныеНастройки.ВладелецФайла,
		|	ПодчиненныеНастройки.ТипВладельцаФайла,
		|	ПодчиненныеНастройки.ЭтоФайл,
		|	ЛОЖЬ
		|ИЗ
		|	ПодчиненныеНастройки КАК ПодчиненныеНастройки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаВладельцевФайлов КАК ТаблицаВладельцевФайлов
		|		ПО ПодчиненныеНастройки.ТипВладельцаФайла = ТаблицаВладельцевФайлов.ТипВладельцаФайла
		|			И ПодчиненныеНастройки.ЭтоФайл = ТаблицаВладельцевФайлов.ЭтоФайл
		|			И ПодчиненныеНастройки.ИдентификаторОбъекта = ТаблицаВладельцевФайлов.ВладелецФайла
		|ГДЕ
		|	ТаблицаВладельцевФайлов.ВладелецФайла ЕСТЬ NULL";
	
	Запрос.Параметры.Вставить("ТаблицаВладельцевФайлов", ТаблицаВладельцевФайлов);
	ОбщаяТаблицаНастроек = Запрос.Выполнить().Выгрузить();
	
	НастройкиДляУдаления = ОбщаяТаблицаНастроек.НайтиСтроки(Новый Структура("НоваяНастройка", Ложь));
	Для Каждого Настройка Из НастройкиДляУдаления Цикл

		УдалитьЗапись(Настройка.ВладелецФайла, Настройка.ТипВладельцаФайла);

	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьЗначениеПоУмолчаниюДляПеренестиВФайловыйАрхивЧерезДней()

	Возврат 365;

КонецФункции

// Возвращаемое значение:
//   ТаблицаЗначений:
//   * ВладелецФайла						- ЛюбаяСсылка
//   * ИдентификаторВладельца				- СправочникСсылка.ИдентификаторыОбъектовМетаданных
//   * ЭтоДетальнаяНастройкаПереносаФайлов	- Булево
//   * ТипВладельцаФайла					- СправочникСсылка.ИдентификаторыОбъектовМетаданных
//   * ПеренестиВФайловыйАрхивЧерезДней		- Число
//   * Действие								- ПеречислениеСсылка.ДействияВНастройкахРаботыСФайловымАрхивом
//   * ЭтоФайл								- Булево
//
Функция ТекущиеНастройкиРаботыСФайловымАрхивом() Экспорт

	УстановитьПривилегированныйРежим(Истина);

	ОбновитьНастройкиРаботыСФайловымАрхивом();

	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапросаТекущиеНастройкиРаботыСФайловымАрхивом();

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ПолучитьТекстЗапросаТекущиеНастройкиРаботыСФайловымАрхивом(ЗапросДляРегламентногоЗадания = Ложь)

	Результат = 
		"ВЫБРАТЬ
		|	НастройкиРаботыСФайловымАрхивом.ВладелецФайла КАК ВладелецФайла,
		|	ИдентификаторыОбъектовМетаданных.Ссылка КАК ИдентификаторВладельца,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ИдентификаторыОбъектовМетаданных.Ссылка) <> ТИПЗНАЧЕНИЯ(НастройкиРаботыСФайловымАрхивом.ВладелецФайла)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоДетальнаяНастройкаПереносаФайлов,
		|	НастройкиРаботыСФайловымАрхивом.ТипВладельцаФайла КАК ТипВладельцаФайла,
		|	НастройкиРаботыСФайловымАрхивом.ПеренестиВФайловыйАрхивЧерезДней КАК ПеренестиВФайловыйАрхивЧерезДней,
		|	НастройкиРаботыСФайловымАрхивом.Действие КАК Действие,
		|	&ДополнительноеПоле,
		|	НастройкиРаботыСФайловымАрхивом.ЭтоФайл КАК ЭтоФайл
		|ИЗ
		|	РегистрСведений.НастройкиРаботыСФайловымАрхивом КАК НастройкиРаботыСФайловымАрхивом
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
		|		ПО НастройкиРаботыСФайловымАрхивом.ВладелецФайла <> Неопределено
		|		И НастройкиРаботыСФайловымАрхивом.ТипВладельцаФайла <> Неопределено
		|		И (ТИПЗНАЧЕНИЯ(НастройкиРаботыСФайловымАрхивом.ВладелецФайла) = ТИПЗНАЧЕНИЯ(ИдентификаторыОбъектовМетаданных.ЗначениеПустойСсылки))";

	Если Не ЗапросДляРегламентногоЗадания Тогда
		Результат = СтрЗаменить(Результат, "&ДополнительноеПоле,", "");
	КонецЕсли;

	Если ЗапросДляРегламентногоЗадания Тогда

		Результат = СтрЗаменить(Результат, "&ДополнительноеПоле,", 
		"	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ИдентификаторыОбъектовМетаданных.Ссылка) <> ТИПЗНАЧЕНИЯ(НастройкиРаботыСФайловымАрхивом.ВладелецФайла)
		|			ТОГДА ИдентификаторыОбъектовМетаданных.Ссылка
		|		ИНАЧЕ НастройкиРаботыСФайловымАрхивом.ВладелецФайла
		|	КОНЕЦ КАК ОбщийИдентификаторВладельца,");

		Результат = Результат + "
		|ГДЕ
		|	НастройкиРаботыСФайловымАрхивом.ВладелецФайла <> НЕОПРЕДЕЛЕНО
		|	И НастройкиРаботыСФайловымАрхивом.ТипВладельцаФайла <> НЕОПРЕДЕЛЕНО"
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#КонецЕсли