///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = НСтр("ru = 'Настройка почты'");
	ВариантАутентификации = "OAuth";
	
	Элементы.КнопкаПерейтиКНастройкам.Видимость = Ложь;
	Элементы.КнопкаНазад.Видимость = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПрофилиБезопасности") Тогда
		МодульРаботаВБезопасномРежиме = ОбщегоНазначения.ОбщийМодуль("РаботаВБезопасномРежиме");
		ИспользуютсяПрофилиБезопасности = МодульРаботаВБезопасномРежиме.ИспользуютсяПрофилиБезопасности();
	Иначе
		ИспользуютсяПрофилиБезопасности = Ложь;
	КонецЕсли;
	
	Если ИспользуютсяПрофилиБезопасности Тогда
		СпособНастройки = "Вручную";
	Иначе
		СпособНастройки = "Автоматически";
	КонецЕсли;
	
	ДоступноПолучениеПисем = РаботаСПочтовымиСообщениямиСлужебный.НастройкиПодсистемы().ДоступноПолучениеПисем;
	ИспользоватьПротоколPOP3 = РаботаСПочтовымиСообщениямиСлужебный.НастройкиПодсистемы().ИспользоватьПротоколPOP3;
	
	Если Не ИспользоватьПротоколPOP3 Тогда
		Протокол = "IMAP";
	КонецЕсли;
	
	КонтекстныйРежим = Параметры.КонтекстныйРежим;
	Перенастройка = Параметры.Перенастройка;
	
	ИспользоватьДляПолучения = Не КонтекстныйРежим И ДоступноПолучениеПисем;
	ИспользоватьДляОтправки = Истина;
	ТребуетсяАвторизацияПриОтправкеПисем = Истина;
	
	Элементы.ИмяОтправителяПисем.Видимость = Не КонтекстныйРежим;
	Элементы.ИспользоватьДляПолучения.Видимость = Не КонтекстныйРежим И ДоступноПолучениеПисем;
	Элементы.Страницы.ТекущаяСтраница = Элементы.ВводАдресаЭлектроннойПочты;
	
	КлючСохраненияПоложенияОкна = ?(КонтекстныйРежим, "КонтекстныйРежим", "НеконтекстныйРежим");
	
	Если ЗначениеЗаполнено(Параметры.Ключ) Тогда
		УчетнаяЗаписьСсылка = Параметры.Ключ;
		ВариантАутентификации = "Пароль";
		ЗаполнитьСвойстваУчетнойЗаписи();
	Иначе
		СсылкаНовойУчетнойЗаписи = Справочники.УчетныеЗаписиЭлектроннойПочты.ПолучитьСсылку();
		
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
			
			МодульУправлениеКонтактнойИнформацией = ОбщегоНазначения.ОбщийМодуль("УправлениеКонтактнойИнформацией");
			ТипАдресЭлектроннойПочты = МодульУправлениеКонтактнойИнформацией.ТипАдресЭлектроннойПочты();
			КонтактнаяИнформацияОбъекта = МодульУправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
				Пользователи.ТекущийПользователь(), ТипАдресЭлектроннойПочты, , Ложь);
			
			Для Каждого Контакт Из КонтактнаяИнформацияОбъекта Цикл
				Адрес = Контакт.Представление;
				Если Справочники.УчетныеЗаписиЭлектроннойПочты.НайтиПоРеквизиту("АдресЭлектроннойПочты", Адрес).Пустая() Тогда
					АдресЭлектроннойПочты = Адрес;
					ИмяОтправителяПисем = Строка(Пользователи.ТекущийПользователь());
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	Элементы.ВидУчетнойЗаписи.Видимость = ЭтоПолноправныйПользователь И Не КонтекстныйРежим;
	ВидУчетнойЗаписи = ?(ЭтоПолноправныйПользователь, "Общая", "Персональная");
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Элементы.Пароль.РастягиватьПоГоризонтали = Истина;
		Элементы.Пароль.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	КонецЕсли;
	
	Элементы.ГруппаТребуетсяПомощь.Видимость = Ложь;
	
	// СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку") Тогда
		
		МодульОбращенияВТехническуюПоддержкуСлужебный = ОбщегоНазначения.ОбщийМодуль(
			"ОбращенияВТехническуюПоддержкуСлужебный");
		
		МодульОбращенияВТехническуюПоддержкуСлужебный.ПриСозданииНаСервере(ЭтотОбъект);
		
	Иначе
		Элементы.ГруппаТребуетсяПомощь.Видимость = Ложь;
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("НастроитьЭлементыТекущейСтраницыПриОткрытии", 0.1, Истина);
	ПодключитьОбработчикОжидания("ПроверитьДоступностьСервисаОткрытойАвторизации", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ТребуетсяПодтверждениеЗакрытияФормы Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ПоказатьВопросПередЗакрытиемФормы", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия <> "ОткрытаяАвторизацияПочтовогоСервиса" Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	АдресПолученияКлюча = НастройкиАвторизации.АдресПолученияКлюча;
	
	Если ИдентификаторЗапроса <> Строка(Параметр.ИдентификаторЗапроса) Тогда
		СообщенияОбОшибках = НСтр("ru = 'Не удалось авторизоваться на почтовом сервере. Неверный идентификатор ответа.'");
		ПроверкаЗавершиласьСОшибками = Истина;
	ИначеЕсли Не ПолучитьКлючиДоступаКПочтовомуСерверу(Параметр.КодАвторизации, АдресПолученияКлюча) Тогда
		ПроверкаЗавершиласьСОшибками = Истина;
	КонецЕсли;
	
	Если ПроверкаЗавершиласьСОшибками Тогда
		ЗаполнитьПояснения();
	КонецЕсли;
	
	ПерейтиНаСледующуюСтраницу();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ПарольДляОтправкиПисем = ПарольДляПолученияПисем;
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСПочтовымиСообщениямиКлиент.ПолеПароляНачалоВыбора(Элемент, ПарольДляПолученияПисем, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресЭлектроннойПочтыПриИзменении(Элемент)
	
	НастройкиЗаполнены = Ложь;
	ТребуетсяПодтверждениеЗакрытияФормы = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяОтправителяПисемПриИзменении(Элемент)
	
	ТребуетсяПодтверждениеЗакрытияФормы = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияТехническиеПодробностиНажатие(Элемент)
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.ТехническиеПодробности;
	Элементы.КнопкаНазад.Видимость = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	ПерейтиНаСледующуюСтраницу(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	ТекущаяСтраница = Элементы.Страницы.ТекущаяСтраница;
	
	ПредыдущаяСтраница = Неопределено;
	
	Если ТекущаяСтраница = Элементы.НастройкиУчетнойЗаписи Тогда
		ПредыдущаяСтраница = Элементы.ВводАдресаЭлектроннойПочты;
	ИначеЕсли ТекущаяСтраница = Элементы.Авторизация Или ТекущаяСтраница = Элементы.ВводПароляУчетнойЗаписи Тогда
		
		Если Не КонтекстныйРежим Тогда
			ПредыдущаяСтраница = Элементы.НастройкиУчетнойЗаписи;
		Иначе
			ПредыдущаяСтраница = Элементы.ВводАдресаЭлектроннойПочты;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ПриПроверкеОбнаруженыОшибки Тогда
		
		Если ЗначениеЗаполнено(ИдентификаторПриложения) Тогда
			ПредыдущаяСтраница = Элементы.НастройкиУчетнойЗаписи;
		Иначе
			ПредыдущаяСтраница = Элементы.ВводПароляУчетнойЗаписи;
			ПроверкаЗавершиласьСОшибками = Ложь;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ТехническиеПодробности Тогда
		ПредыдущаяСтраница = Элементы.ПриПроверкеОбнаруженыОшибки;
	КонецЕсли;
	
	Если ПредыдущаяСтраница <> Неопределено Тогда
		Элементы.Страницы.ТекущаяСтраница = ПредыдущаяСтраница;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.ВводАдресаЭлектроннойПочты;
	КонецЕсли;
	
	НастроитьЭлементыТекущейСтраницы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКНастройкам(Команда)
	
	ТекущаяСтраница = Элементы.Страницы.ТекущаяСтраница;
	
	Если Не КонтекстныйРежим И ТекущаяСтраница = Элементы.УчетнаяЗаписьУспешноНастроена Тогда
		ПоказатьЗначение(,УчетнаяЗаписьСсылка);
		Закрыть(Истина);
	Иначе
		ОткрытьРучныеНастройки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросВПоддержку(Команда)
	
	// СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку") Тогда
		
		МодульОбращенияВТехническуюПоддержкуСлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ОбращенияВТехническуюПоддержкуСлужебныйКлиент");
		
		Если ЗначениеЗаполнено(КраткоеОписаниеОшибки) Тогда
			ОписаниеДляТемыОбращения = КраткоеОписаниеОшибки;
		Иначе
			ОписаниеДляТемыОбращения = СообщенияОбОшибках;
		КонецЕсли;
		
		ПараметрыОбращения = МодульОбращенияВТехническуюПоддержкуСлужебныйКлиент.ПараметрыОбращения();
		ПараметрыОбращения.ТехнологическаяИнформация = СообщенияОбОшибках;
		ПараметрыОбращения.ОтборЖурналаРегистрации.Вставить("ДатаНачала", ВремяРегистрацииОшибки);
		
		ПараметрыОбращения.Тема = РаботаСПочтовымиСообщениямиСлужебныйКлиент.ТемаОбращенияВПоддержку(
			ОписаниеДляТемыОбращения);
		
		ПараметрыОбращения.Сообщение = РаботаСПочтовымиСообщениямиСлужебныйКлиент.ТекстОбращенияВПоддержку(
			АдресЭлектроннойПочты,
			ОписаниеДляТемыОбращения);
		
		МодульОбращенияВТехническуюПоддержкуСлужебныйКлиент.ОтправитьВопросВПоддержку(
			ЭтотОбъект,
			ПараметрыОбращения);
		
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
	
КонецПроцедуры

&НаКлиенте
Процедура ИнформацияДляОтправкиВПоддержку(Команда)
	
	// СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку") Тогда
		
		МодульОбращенияВТехническуюПоддержкуСлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ОбращенияВТехническуюПоддержкуСлужебныйКлиент");
		
		ПараметрыОбращения = МодульОбращенияВТехническуюПоддержкуСлужебныйКлиент.ПараметрыОбращения();
		ПараметрыОбращения.ТехнологическаяИнформация = СообщенияОбОшибках;
		ПараметрыОбращения.ОтборЖурналаРегистрации.Вставить("ДатаНачала", ВремяРегистрацииОшибки);
		
		МодульОбращенияВТехническуюПоддержкуСлужебныйКлиент.СкачатьИнформациюДляОтправкиВПоддержку(
			ЭтотОбъект,
			ПараметрыОбращения);
		
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьРучныеНастройки()
	
	ПараметрыУчетнойЗаписи = ПараметрыУчетнойЗаписи();
	ОбработчикЗавершения = Новый ОписаниеОповещения("ПослеРучнойНастройкиУчетнойЗаписи", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.УчетныеЗаписиЭлектроннойПочты.Форма.ФормаЭлемента", ПараметрыУчетнойЗаписи, , , , ,
		ОбработчикЗавершения, РежимБлокировкиПриОткрытииОкнаФормы.БлокироватьВладельца);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвойстваУчетнойЗаписи()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УчетныеЗаписиЭлектроннойПочты.АдресЭлектроннойПочты КАК АдресЭлектроннойПочты,
	|	УчетныеЗаписиЭлектроннойПочты.ИмяПользователя КАК ИмяОтправителяПисем,
	|	УчетныеЗаписиЭлектроннойПочты.Наименование КАК НазваниеУчетнойЗаписи,
	|	УчетныеЗаписиЭлектроннойПочты.АвторизацияСПомощьюПочтовогоСервиса
	|ИЗ
	|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
	|ГДЕ
	|	УчетныеЗаписиЭлектроннойПочты.Ссылка = &Ссылка";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", УчетнаяЗаписьСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	ТолькоАвторизация = Параметры.ТолькоАвторизация;
	
	Если Выборка.АвторизацияСПомощьюПочтовогоСервиса Или ТолькоАвторизация Тогда
		
		ВариантАутентификации = "OAuth";
		
		Если ТолькоАвторизация Тогда
			
			НастройкиАвторизацииНаПочтовомСервере = НастройкиАвторизацииНаПочтовомСервере();
			НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
			
			Если ЗначениеЗаполнено(НастройкиАвторизации) И ЗначениеЗаполнено(НастройкиАвторизации.ИдентификаторПриложения) Тогда
				
				ИдентификаторПриложения = НастройкиАвторизации.ИдентификаторПриложения;
				АдресПеренаправления = НастройкиАвторизации.АдресПеренаправления;
				
				Если ЭтоВебКлиент() И Не ОткрытаяАвторизацияПочтовогоСервисаОпубликована Тогда
					АдресПеренаправления = НастройкиАвторизации.АдресПеренаправленияВебКлиент;
				КонецЕсли;
				
				ПарольПриложения = НастройкиАвторизации.ПарольПриложения;
				Элементы.Страницы.ТекущаяСтраница = Элементы.Авторизация;
				
				Если ЭтоВебКлиент() И Не ОткрытаяАвторизацияПочтовогоСервисаОпубликована Тогда
					Элементы.ВариантыАвторизации.ТекущаяСтраница = Элементы.БраузерОперационнойСистемы;
					ТекущийЭлемент = Элементы.ВебСтраница;
				Иначе
					Элементы.ВариантыАвторизации.ТекущаяСтраница = Элементы.ВстроенныйБраузер;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросПередЗакрытиемФормы()
	
	ТекстВопроса = НСтр("ru = 'Введенные данные не записаны. Закрыть форму?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗакрытиеФормыПодтверждено", ЭтотОбъект);
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("Закрыть", НСтр("ru = 'Закрыть'"));
	Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Не закрывать'"));
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Отмена, НСтр("ru = 'Настройка почты'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФормыПодтверждено(РезультатВопроса, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ТребуетсяПодтверждениеЗакрытияФормы = Ложь;
	Закрыть(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСледующуюСтраницу(Команда = Неопределено)
	
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	
	ТекущаяСтраница = Элементы.Страницы.ТекущаяСтраница;
	СледующаяСтраница = Неопределено;
	Отказ = Ложь;
	
	Если ТекущаяСтраница = Элементы.ВводАдресаЭлектроннойПочты Тогда
		
		ПроверкаЗавершиласьСОшибками = Ложь;
		ПроверитьЗаполнениеАдресаЭлектроннойПочты(Отказ);
		
		Если Не Отказ И Не НастройкиЗаполнены Тогда
			ЗаполнитьНастройкиУчетнойЗаписи();
		КонецЕсли;
		
		Если Не Отказ Тогда
			
			НастройкиАвторизацииНаПочтовомСервере = НастройкиАвторизацииНаПочтовомСервере();
			НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
			
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Настройка %1'"), АдресЭлектроннойПочты);
			
			Если Не КонтекстныйРежим Тогда
				СледующаяСтраница = Элементы.НастройкиУчетнойЗаписи;
			Иначе
				ОпределитьВариантАутентификации(СледующаяСтраница, НастройкиАвторизации);
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.НастройкиУчетнойЗаписи Тогда
		
		ОпределитьВариантАутентификации(СледующаяСтраница, НастройкиАвторизации);
		
	ИначеЕсли ТекущаяСтраница = Элементы.Авторизация Тогда
		
		Если ЭтоВебКлиент() И Не ОткрытаяАвторизацияПочтовогоСервисаОпубликована
			И Не ЗначениеЗаполнено(НастройкиАвторизации.АдресРегистрацииУстройства) Тогда
			
			Если Не ПолучитьКлючиДоступаКПочтовомуСерверу(КодПодтверждения, НастройкиАвторизации.АдресПолученияКлюча, КлиентскоеПриложение.ПолучитьКраткийЗаголовок()) Тогда
				ПроверкаЗавершиласьСОшибками = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТолькоАвторизация Тогда
			ТребуетсяПодтверждениеЗакрытияФормы = Ложь;
			Если ПроверкаЗавершиласьСОшибками Тогда
				Закрыть(Ложь);
			Иначе
				Закрыть(ЗаписатьНастройкиАвторизации());
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		Если ПроверкаЗавершиласьСОшибками Тогда
			СледующаяСтраница = Элементы.ПриПроверкеОбнаруженыОшибки;
		Иначе
			СледующаяСтраница = Элементы.ПроверкаНастроекУчетнойЗаписи;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ВводПароляУчетнойЗаписи Тогда
		
		Если ПустаяСтрока(ПарольДляПолученияПисем) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Введите пароль учетной записи'"), , "Пароль");
			Возврат;
		КонецЕсли;
		
		СледующаяСтраница = Элементы.ПроверкаНастроекУчетнойЗаписи;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ПроверкаНастроекУчетнойЗаписи Тогда
		
		Если Команда = Неопределено Тогда
			Если ПроверкаЗавершиласьСОшибками Тогда
				СледующаяСтраница = Элементы.ПриПроверкеОбнаруженыОшибки;
				ЗаполнитьПояснения();
			ИначеЕсли ПроверкаПропущена И СпособНастройки = "Автоматически" Тогда
				ОткрытьРучныеНастройки();
				Возврат;
			Иначе
				СледующаяСтраница = Элементы.УчетнаяЗаписьУспешноНастроена;
			КонецЕсли;
			ПроверкаПропущена = Ложь;
		Иначе
			ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
			ИдентификаторЗадания = "";
			ПроверкаПропущена = Истина;
			НастроитьЭлементыТекущейСтраницы();
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ПриПроверкеОбнаруженыОшибки Тогда
		СледующаяСтраница = Элементы.ПроверкаНастроекУчетнойЗаписи;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если СледующаяСтраница = Неопределено Тогда
		Закрыть(Истина);
	Иначе
		Элементы.Страницы.ТекущаяСтраница = СледующаяСтраница;
		НастроитьЭлементыТекущейСтраницы();
	КонецЕсли;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ПроверкаНастроекУчетнойЗаписи Тогда
		Если СпособНастройки = "Автоматически" Тогда
			ПодключитьОбработчикОжидания("НастроитьПараметрыПодключенияАвтоматически", 0.1, Истина);
		Иначе
			ПодключитьОбработчикОжидания("ВыполнитьПроверкуНастроек", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьВариантАутентификации(СледующаяСтраница, НастройкиАвторизации)
	
	ИдентификаторПриложения = НастройкиАвторизации.ИдентификаторПриложения;
	ПарольПриложения = НастройкиАвторизации.ПарольПриложения;
	
	Если ВариантАутентификации = "OAuth" И ЗначениеЗаполнено(ИдентификаторПриложения) Тогда
		
		Если ЭтоВебКлиент() И Не ОткрытаяАвторизацияПочтовогоСервисаОпубликована И Не ДоступнаАвторизацияПоКоду() Тогда
			ВариантАутентификации = "Пароль";
			СледующаяСтраница = Элементы.ВводПароляУчетнойЗаписи;
			ЗаполнитьПодсказкуВводаПароля();
		Иначе
			
			СледующаяСтраница = Элементы.Авторизация;
			АдресПеренаправления = НастройкиАвторизации.АдресПеренаправления;
			
			Если ЭтоВебКлиент() И Не ОткрытаяАвторизацияПочтовогоСервисаОпубликована Тогда
				АдресПеренаправления = НастройкиАвторизации.АдресПеренаправленияВебКлиент;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(АдресПеренаправления) Тогда
				АдресПеренаправления = НастройкиАвторизации.АдресПеренаправленияПоУмолчанию;
			КонецЕсли;
			
			ПодключитьОбработчикОжидания("АвторизоватьсяНаПочтовомСервере", 0.1, Истина);
			
		КонецЕсли
	Иначе
		ВариантАутентификации = "Пароль";
		СледующаяСтраница = Элементы.ВводПароляУчетнойЗаписи;
		ЗаполнитьПодсказкуВводаПароля();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодсказкуВводаПароля();
	
	НастройкиАвторизацииНаПочтовомСервере = НастройкиАвторизацииНаПочтовомСервере();
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	
	Если Не ЗначениеЗаполнено(НастройкиАвторизации) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НастройкиАвторизации.ПодсказкаВводаПароля) Тогда
		Элементы.Пароль.РасширеннаяПодсказка.Заголовок = НастройкиАвторизации.ПодсказкаВводаПароля;
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроверкуНастроек()
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыполнитьПроверкуНастроекЗапросНаРазрешенияВыполнен", ЭтотОбъект);
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПрофилиБезопасности") Тогда
		
		Запрос = СоздатьЗапросНаИспользованиеВнешнихРесурсов();
		
		МодульРаботаВБезопасномРежимеКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаВБезопасномРежимеКлиент");
		МодульРаботаВБезопасномРежимеКлиент.ПрименитьЗапросыНаИспользованиеВнешнихРесурсов(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Запрос), ЭтотОбъект, ОповещениеОЗакрытии);
		
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеОЗакрытии, КодВозвратаДиалога.ОК);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроверкуНастроекЗапросНаРазрешенияВыполнен(РезультатЗапроса, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатЗапроса = КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьНастройкиУчетнойЗаписи();
	
	Если ЗначениеЗаполнено(УчетнаяЗаписьСсылка) Тогда 
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(УчетнаяЗаписьСсылка);
	КонецЕсли;
	
	ПерейтиНаСледующуюСтраницу();
	
КонецПроцедуры

&НаСервере
Функция СоздатьЗапросНаИспользованиеВнешнихРесурсов()
	
	МодульРаботаВБезопасномРежиме = ОбщегоНазначения.ОбщийМодуль("РаботаВБезопасномРежиме");
	
	Возврат МодульРаботаВБезопасномРежиме.ЗапросНаИспользованиеВнешнихРесурсов(
		Разрешения(), СсылкаНовойУчетнойЗаписи);
	
КонецФункции

&НаСервере
Функция Разрешения()
	
	Результат = Новый Массив;
	
	МодульРаботаВБезопасномРежиме = ОбщегоНазначения.ОбщийМодуль("РаботаВБезопасномРежиме");
	
	Если ИспользоватьДляОтправки Тогда
		Результат.Добавить(
			МодульРаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
				"SMTP",
				СерверИсходящейПочты,
				ПортСервераИсходящейПочты,
				НСтр("ru = 'Электронная почта.'")));
	КонецЕсли;
	
	Если ИспользоватьДляПолучения Тогда
		Результат.Добавить(
			МодульРаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
				Протокол,
				СерверВходящейПочты,
				ПортСервераВходящейПочты,
				НСтр("ru = 'Электронная почта.'")));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеАдресаЭлектроннойПочты(Отказ)
	
	ОчиститьСообщения();
	
	Если ПустаяСтрока(АдресЭлектроннойПочты) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Введите адрес электронной почты'"), , "АдресЭлектроннойПочты", , Отказ);
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(АдресЭлектроннойПочты, Истина) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Адрес электронной почты введен неверно'"), , "АдресЭлектроннойПочты", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьЭлементыТекущейСтраницы()
	
	ТекущаяСтраница = Элементы.Страницы.ТекущаяСтраница;
	
	Если ТекущаяСтраница = Элементы.УчетнаяЗаписьУспешноНастроена Тогда
		Если КонтекстныйРежим Тогда
			ЗаголовокКнопкиДалее = НСтр("ru = 'Далее'");
		Иначе
			ЗаголовокКнопкиДалее = НСтр("ru = 'Закрыть'");
		КонецЕсли;
	Иначе
		Если ТекущаяСтраница = Элементы.ПроверкаНастроекУчетнойЗаписи Тогда
			ЗаголовокКнопкиДалее = НСтр("ru = 'Пропустить проверку'");
		Иначе
			ЗаголовокКнопкиДалее = НСтр("ru = 'Далее'");
		КонецЕсли;
	КонецЕсли;
	
	Если Не КонтекстныйРежим И ТекущаяСтраница = Элементы.УчетнаяЗаписьУспешноНастроена Тогда
		Элементы.КнопкаПерейтиКНастройкам.Заголовок = НСтр("ru = 'Перейти к настройкам'");
	Иначе
		Элементы.КнопкаПерейтиКНастройкам.Заголовок = НСтр("ru = 'Настроить вручную'");
	КонецЕсли;
	
	Элементы.КнопкаДалее.Заголовок = ЗаголовокКнопкиДалее;
	Элементы.КнопкаДалее.КнопкаПоУмолчанию = ТекущаяСтраница <> Элементы.ПроверкаНастроекУчетнойЗаписи;
	Элементы.КнопкаДалее.Доступность = Не (ТекущаяСтраница = Элементы.ПроверкаНастроекУчетнойЗаписи И ПроверкаПропущена);
	Элементы.КнопкаДалее.Видимость = Не (ТекущаяСтраница = Элементы.ПроверкаНастроекУчетнойЗаписи И СпособНастройки = "Вручную")
		И Не (ТекущаяСтраница = Элементы.Авторизация И Не ЭтоВебКлиент())
		И Не ТекущаяСтраница = Элементы.ПриПроверкеОбнаруженыОшибки;
	
	Элементы.КнопкаНазад.Видимость = ТекущаяСтраница <> Элементы.ВводАдресаЭлектроннойПочты
		И ТекущаяСтраница <> Элементы.УчетнаяЗаписьУспешноНастроена
		И ТекущаяСтраница <> Элементы.ПроверкаНастроекУчетнойЗаписи
		И Не ТолькоАвторизация;
	
	Элементы.КнопкаПерейтиКНастройкам.Видимость = Не ИспользуютсяПрофилиБезопасности И (ТекущаяСтраница = Элементы.ПриПроверкеОбнаруженыОшибки
		И ПроверкаЗавершиласьСОшибками Или Не КонтекстныйРежим И Не Перенастройка И ТекущаяСтраница = Элементы.УчетнаяЗаписьУспешноНастроена);
	
	Если ТекущаяСтраница = Элементы.УчетнаяЗаписьУспешноНастроена Тогда
		Элементы.НадписьУчетнаяЗаписьУспешноНастроена.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Настройка почты
				|%1 успешно завершена.'"), АдресЭлектроннойПочты);
	КонецЕсли;
	
	Если ТекущаяСтраница = Элементы.Авторизация И Элементы.ВариантыАвторизации.ТекущаяСтраница = Элементы.ВстроенныйБраузер Тогда
		Активизировать();
		ПодключитьОбработчикОжидания("ПроверитьРезультатАвторизации", 5, Истина );
	КонецЕсли;
	
	Если ТекущаяСтраница <> Элементы.ПриПроверкеОбнаруженыОшибки Тогда
		СкрытьРазделТребуетсяПомощь();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СкрытьРазделТребуетсяПомощь()
	
	// СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку") Тогда
		
		МодульОбращенияВТехническуюПоддержкуСлужебный = ОбщегоНазначения.ОбщийМодуль(
		"ОбращенияВТехническуюПоддержкуСлужебный");
		
		МодульОбращенияВТехническуюПоддержкуСлужебный.СкрытьРазделТребуетсяПомощь(Элементы);
		
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНастройкиУчетнойЗаписи()
	
	НастройкиПоУмолчанию = НастройкиПоУмолчанию(АдресЭлектроннойПочты, ПарольДляПолученияПисем);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиПоУмолчанию);
	
	Если ПустаяСтрока(НазваниеУчетнойЗаписи) Тогда
		НазваниеУчетнойЗаписи = АдресЭлектроннойПочты;
	КонецЕсли;
	
	НастройкиЗаполнены = Истина;
	
	ШифрованиеПриОтправкеПочты = ?(ИспользоватьЗащищенноеСоединениеДляИсходящейПочты, "SSL", "Авто");
	ШифрованиеПриПолученииПочты = ?(ИспользоватьЗащищенноеСоединениеДляВходящейПочты, "SSL", "Авто");
	
	ЗаполнитьОписаниеНастроекПоУмолчанию(НастройкиПоУмолчанию);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкиПоУмолчанию(АдресЭлектроннойПочты, Пароль)
	
	Позиция = СтрНайти(АдресЭлектроннойПочты, "@");
	ИмяСервераВУчетнойЗаписи = Сред(АдресЭлектроннойПочты, Позиция + 1);
	
	Настройки = Новый Структура;
	
	Настройки.Вставить("ИмяПользователяДляПолученияПисем", АдресЭлектроннойПочты);
	Настройки.Вставить("ИмяПользователяДляОтправкиПисем", АдресЭлектроннойПочты);
	
	Настройки.Вставить("ПарольДляОтправкиПисем", Пароль);
	Настройки.Вставить("ПарольДляПолученияПисем", Пароль);
	
	Настройки.Вставить("Протокол", "IMAP");
	Настройки.Вставить("СерверВходящейПочты", "imap." + ИмяСервераВУчетнойЗаписи);
	Настройки.Вставить("ПортСервераВходящейПочты", 993);
	Настройки.Вставить("ИспользоватьЗащищенноеСоединениеДляВходящейПочты", Истина);
	
	Настройки.Вставить("СерверИсходящейПочты", "smtp." + ИмяСервераВУчетнойЗаписи);
	Настройки.Вставить("ПортСервераИсходящейПочты", 587);
	Настройки.Вставить("ИспользоватьЗащищенноеСоединениеДляИсходящейПочты", Ложь);
	
	Настройки.Вставить("ДлительностьОжиданияСервера", 10);
	Настройки.Вставить("ОставлятьКопииПисемНаСервере", Истина);
	Настройки.Вставить("ПериодХраненияСообщенийНаСервере", 10);
	
	НастройкиIMAPПоУмолчанию = Справочники.УчетныеЗаписиЭлектроннойПочты.ВариантыНастройкиПодключенияКСерверуIMAP(АдресЭлектроннойПочты)[0];
	НастройкиSMTPПоУмолчанию = Справочники.УчетныеЗаписиЭлектроннойПочты.ВариантыНастройкиПодключенияКСерверуSMTP(АдресЭлектроннойПочты)[0];
	
	ЗаполнитьЗначенияСвойств(Настройки, НастройкиIMAPПоУмолчанию);
	ЗаполнитьЗначенияСвойств(Настройки, НастройкиSMTPПоУмолчанию);
	
	Возврат Настройки;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьОписаниеНастроекПоУмолчанию(НастройкиПоУмолчанию)
	
	ШаблонОписанияНастроекПоУмолчанию = НСтр("ru = 'Используются настройки по умолчанию.
		|Протокол: %1
		|
		|Сервер входящей почты: %2
		|Порт сервера входящей почты: %3
		|Использовать SSL-соединение для входящей почты: %4
		|
		|Сервер исходящей почты: %5
		|Порт сервера исходящей почты: %6
		|Использовать SSL-соединение для исходящей почты: %7'");
	
	ОписаниеНастроекПоУмолчанию = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонОписанияНастроекПоУмолчанию,
		НастройкиПоУмолчанию.Протокол,
		НастройкиПоУмолчанию.СерверВходящейПочты,
		НастройкиПоУмолчанию.ПортСервераВходящейПочты,
		НастройкиПоУмолчанию.ИспользоватьЗащищенноеСоединениеДляВходящейПочты,
		НастройкиПоУмолчанию.СерверИсходящейПочты,
		НастройкиПоУмолчанию.ПортСервераИсходящейПочты,
		НастройкиПоУмолчанию.ИспользоватьЗащищенноеСоединениеДляИсходящейПочты);
	
	Элементы.ОписаниеНастроекПоУмолчанию.Заголовок = ОписаниеНастроекПоУмолчанию;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНастройкиУчетнойЗаписи()
	
	ПрофильИсходящейПочты = Неопределено;
	Если ИспользоватьДляОтправки Тогда
		ПрофильИсходящейПочты = ИнтернетПочтовыйПрофиль(Ложь);
	КонецЕсли;
	
	ПрофильВходящейПочты = Неопределено;
	Если ИспользоватьДляПолучения Тогда
		ПрофильВходящейПочты = ИнтернетПочтовыйПрофиль(Истина);
	КонецЕсли;
	
	РезультатПроверки = Справочники.УчетныеЗаписиЭлектроннойПочты.ПроверитьНастройкиПрофилей(
		ПрофильИсходящейПочты, ПрофильВходящейПочты, АдресЭлектроннойПочты);
	
	СообщенияОбОшибках = РезультатПроверки.ОшибкиПодключения;
	ПроверкаЗавершиласьСОшибками = ЗначениеЗаполнено(СообщенияОбОшибках);
	
	Если Не ПроверкаЗавершиласьСОшибками И Не ПроверкаПропущена Тогда
		Попытка
			СоздатьУчетнуюЗапись();
		Исключение
			ПроверкаЗавершиласьСОшибками = Истина;
			СообщенияОбОшибках = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецПопытки;
	КонецЕсли;
	
	Если ПроверкаЗавершиласьСОшибками Тогда
		
		ВремяРегистрацииОшибки = ТекущаяДатаСеанса();
		ЗаполнитьПояснения();
		
		// СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку") Тогда
			
			МодульОбращенияВТехническуюПоддержкуСлужебный = ОбщегоНазначения.ОбщийМодуль(
				"ОбращенияВТехническуюПоддержкуСлужебный");
			
			МодульОбращенияВТехническуюПоддержкуСлужебный.ПоказатьРазделТребуетсяПомощь(Элементы);
			
		КонецЕсли;
		// Конец СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьУчетнуюЗапись()
	
	ПреобразоватьНастройкиИзPunycode();
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	
	СистемнаяУчетнаяЗапись = Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты;
	НастроитьСистемнуюУчетнуюЗапись = КонтекстныйРежим И ВидУчетнойЗаписи = "Общая"
		И Не РаботаСПочтовымиСообщениями.УчетнаяЗаписьНастроена(СистемнаяУчетнаяЗапись)
		И Справочники.УчетныеЗаписиЭлектроннойПочты.ИзменениеРазрешено(СистемнаяУчетнаяЗапись);
	
	Если НастроитьСистемнуюУчетнуюЗапись Тогда
		УчетнаяЗаписьСсылка = СистемнаяУчетнаяЗапись;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.УчетныеЗаписиЭлектроннойПочты");
	Если Не УчетнаяЗаписьСсылка.Пустая() Тогда
		ЭлементБлокировки.УстановитьЗначение("Ссылка", УчетнаяЗаписьСсылка);
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка.Заблокировать();
		
		Если УчетнаяЗаписьСсылка.Пустая() Тогда
			УчетнаяЗапись = Справочники.УчетныеЗаписиЭлектроннойПочты.СоздатьЭлемент();
			УчетнаяЗапись.УстановитьСсылкуНового(СсылкаНовойУчетнойЗаписи);
		Иначе
			УчетнаяЗапись = УчетнаяЗаписьСсылка.ПолучитьОбъект();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(УчетнаяЗапись, ЭтотОбъект);
		УчетнаяЗапись.ИмяПользователя = ИмяОтправителяПисем;
		УчетнаяЗапись.Пользователь = ИмяПользователяДляПолученияПисем;
		УчетнаяЗапись.ПользовательSMTP = ИмяПользователяДляОтправкиПисем;
		УчетнаяЗапись.ВремяОжидания = ДлительностьОжиданияСервера;
		УчетнаяЗапись.ОставлятьКопииСообщенийНаСервере = ОставлятьКопииПисемНаСервере;
		УчетнаяЗапись.ПериодХраненияСообщенийНаСервере = ?(ОставлятьКопииПисемНаСервере И УдалятьПисьмаССервера И Протокол = "POP", ПериодХраненияСообщенийНаСервере, 0);
		УчетнаяЗапись.ПротоколВходящейПочты = Протокол;
		УчетнаяЗапись.Наименование = НазваниеУчетнойЗаписи;
		УчетнаяЗапись.ПриОтправкеПисемТребуетсяАвторизация = ЗначениеЗаполнено(УчетнаяЗапись.ПользовательSMTP);
		
		Если ВидУчетнойЗаписи = "Персональная" Тогда
			УчетнаяЗапись.ВладелецУчетнойЗаписи = Пользователи.ТекущийПользователь();
		Иначе
			УчетнаяЗапись.ВладелецУчетнойЗаписи = Справочники.Пользователи.ПустаяСсылка();
		КонецЕсли;
		
		Если ВариантАутентификации = "OAuth" Тогда
			УчетнаяЗапись.АвторизацияСПомощьюПочтовогоСервиса = Истина;
			УчетнаяЗапись.ИмяПочтовогоСервиса = НастройкиАвторизации.ИмяИнтернетСервиса;
		КонецЕсли;
		
		УчетнаяЗапись.ДополнительныеСвойства.Вставить("НеПроверятьИзменениеНастроек");
		УчетнаяЗапись.Записать();
		
		УчетнаяЗаписьСсылка = УчетнаяЗапись.Ссылка;
		ТребуетсяПодтверждениеЗакрытияФормы = Ложь;
		
		УстановитьПривилегированныйРежим(Истина);
		
		ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(УчетнаяЗаписьСсылка);
		
		Если ВариантАутентификации = "OAuth" Тогда
			ЗаписатьНастройкиАвторизации();
		Иначе
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(УчетнаяЗаписьСсылка, ПарольДляПолученияПисем);
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(УчетнаяЗаписьСсылка, ПарольДляОтправкиПисем, "ПарольSMTP");
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Работа с почтовыми сообщениями'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, , УчетнаяЗаписьСсылка, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьНастройкиАвторизации()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(УчетнаяЗаписьСсылка, ТокенДоступа, "ТокенДоступа");
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(УчетнаяЗаписьСсылка, СрокДействияТокенаДоступа, "СрокДействияТокенаДоступа");
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(УчетнаяЗаписьСсылка, ТокенОбновления, "ТокенОбновления");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ОбщегоНазначения.КонтрольнаяСуммаСтрокой(ТокенДоступа + ТокенОбновления);
	
КонецФункции

&НаСервере
Функция ИнтернетПочтовыйПрофиль(ДляПолучения = Ложь)
	
	Профиль = Новый ИнтернетПочтовыйПрофиль;
	
	Если ДляПолучения Или ТребуетсяВходНаСерверПередОтправкой Тогда
		Если Протокол = "IMAP" Тогда
			Профиль.АдресСервераIMAP = СерверВходящейПочты;
			Профиль.ИспользоватьSSLIMAP = ИспользоватьЗащищенноеСоединениеДляВходящейПочты;
			Профиль.ПарольIMAP = ПарольДляПолученияПисем;
			Профиль.ПользовательIMAP = ИмяПользователяДляПолученияПисем;
			Профиль.ПортIMAP = ПортСервераВходящейПочты;
		Иначе
			Профиль.АдресСервераPOP3 = СерверВходящейПочты;
			Профиль.ИспользоватьSSLPOP3 = ИспользоватьЗащищенноеСоединениеДляВходящейПочты;
			Профиль.Пароль = ПарольДляПолученияПисем;
			Профиль.Пользователь = ИмяПользователяДляПолученияПисем;
			Профиль.ПортPOP3 = ПортСервераВходящейПочты;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ДляПолучения Тогда
		Профиль.POP3ПередSMTP = ТребуетсяВходНаСерверПередОтправкой;
		Профиль.АдресСервераSMTP = СерверИсходящейПочты;
		Профиль.ИспользоватьSSLSMTP = ИспользоватьЗащищенноеСоединениеДляИсходящейПочты;
		Профиль.ПарольSMTP = ПарольДляОтправкиПисем;
		Профиль.ПользовательSMTP = ИмяПользователяДляОтправкиПисем;
		Профиль.ПортSMTP = ПортСервераИсходящейПочты;
	КонецЕсли;
	
	Профиль.Таймаут = ДлительностьОжиданияСервера;
	
	Если ВариантАутентификации = "OAuth" Тогда
		Профиль.АутентификацияПоТокену = ИспользованиеАутентификацииИнтернетПочтыПоТокену.Использовать;
		Профиль.ТокенДоступа = ТокенДоступа;
	КонецЕсли;
	
	Возврат Профиль;
	
КонецФункции

&НаКлиенте
Процедура НастроитьПараметрыПодключенияАвтоматически()
	
	СообщенияОбОшибках = НСтр("ru = 'Не удалось определить настройки подключения.
		|Настройте параметры подключения вручную.'");
	
	ПроверкаЗавершиласьСОшибками = Ложь;
	
	ДлительнаяОперация = НачатьПоискНастроекУчетнойЗаписи();
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииПоискаНастроек", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция НачатьПоискНастроекУчетнойЗаписи()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Поиск настроек почтового сервера'");
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Справочники.УчетныеЗаписиЭлектроннойПочты.ОпределитьНастройкиУчетнойЗаписи",
		АдресЭлектроннойПочты, ПарольДляПолученияПисем, ИспользоватьДляОтправки, ИспользоватьДляПолучения);
	
КонецФункции

// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ПриЗавершенииПоискаНастроек(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Если ПроверкаПропущена И Открыта() Тогда
			ПерейтиНаСледующуюСтраницу();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ПроверкаЗавершиласьСОшибками = Истина;
		ПерейтиНаСледующуюСтраницу();
		
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
		
		Возврат;
	КонецЕсли;
	
	НайденныеНастройки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	ПроверкаЗавершиласьСОшибками = ИспользоватьДляОтправки И Не НайденныеНастройки.ДляОтправки 
		Или ИспользоватьДляПолучения И Не НайденныеНастройки.ДляПолучения;
		
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НайденныеНастройки);
	ШифрованиеПриОтправкеПочты = ?(ИспользоватьЗащищенноеСоединениеДляИсходящейПочты, "SSL", "Авто");
	ШифрованиеПриПолученииПочты = ?(ИспользоватьЗащищенноеСоединениеДляВходящейПочты, "SSL", "Авто");
	
	Если Не НайденныеНастройки.ПроверкаНастроекВыполнена Тогда
		ПодключитьОбработчикОжидания("ВыполнитьПроверкуНастроек", 0.1, Истина);
		Возврат;
	КонецЕсли;
	
	Если Не ПроверкаЗавершиласьСОшибками И Не ПроверкаПропущена Тогда
		Попытка
			СоздатьУчетнуюЗапись();
		Исключение
			ПроверкаЗавершиласьСОшибками = Истина;
			СообщенияОбОшибках = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецПопытки;
		Если Не ПроверкаЗавершиласьСОшибками Тогда
			ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(СсылкаНовойУчетнойЗаписи);	
		КонецЕсли;
	КонецЕсли;
	
	ПерейтиНаСледующуюСтраницу();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(ИдентификаторЗадания)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда 
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПереходПоАдресу(Знач АдресПерехода)
	
	СтруктураАдреса = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресПерехода);
	
	Если Не ЗначениеЗаполнено(СтруктураАдреса.Схема) Тогда
		АдресПерехода = "http://" + АдресПерехода;
	КонецЕсли;
	
	Если ЭтоВебКлиент() Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(АдресПерехода);
		Возврат;
	КонецЕсли;
	
	ВебСтраница = 
	"<!DOCTYPE html>
	|<html>
	|	<body>
	|		<script language = 'javascript'>
	|			document.location.href='" + АдресПерехода + "';
	|		</script>
	|		<p>Загрузка...</p>
	|	</body>
	|</html>";
	
КонецПроцедуры

&НаКлиенте
Процедура АвторизоватьсяНаПочтовомСервере()
	
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	СтрокаЗапросаАвторизации = СтрокаЗапросаАвторизации();
	
	Если ЭтоВебКлиент() И Не ОткрытаяАвторизацияПочтовогоСервисаОпубликована Тогда
		
		Если ЗначениеЗаполнено(НастройкиАвторизации.АдресРегистрацииУстройства) Тогда
			Если ПолучитьПараметрыАвторизацииВВебКлиенте(НастройкиАвторизации.АдресРегистрацииУстройства) Тогда
				СтрокаЗапросаАвторизации = АдресАвторизацииПользователя;
				Если Не ЗначениеЗаполнено(ИнтервалВыполненияЗапросов) Тогда
					ИнтервалВыполненияЗапросов = 5;
				КонецЕсли;
				ПодключитьОбработчикОжидания("ПолучитьКлючиДоступаВебКлиент", ИнтервалВыполненияЗапросов, Истина);
			Иначе
				ЗаполнитьПояснения();
				ПерейтиНаСледующуюСтраницу();
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ПояснениеПоКодуПодтверждения.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(НСтр(
			"ru = 'Авторизуйтесь на <a href=""%1"">странице почтового сервиса</a> и введите полученный код в поле ниже:'"),
			СтрокаЗапросаАвторизации);
		
		Элементы.ВариантыАвторизации.ТекущаяСтраница = Элементы.БраузерОперационнойСистемы;
		
	Иначе
		Элементы.ВариантыАвторизации.ТекущаяСтраница = Элементы.ВстроенныйБраузер;
		ВыполнитьПереходПоАдресу(СтрокаЗапросаАвторизации);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СтрокаЗапросаАвторизации()
	
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(НастройкиАвторизации.АдресАвторизации);
	АдресСервера = СтруктураURI.Хост;
	АдресРесурса = "/" + СтруктураURI.ПутьНаСервере;
	
	ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор());
	ПроверочныйКод = РаботаСПочтовымиСообщениямиСлужебный.СгенерироватьПроверочныйКод();
	
	Если ЭтоВебКлиент() Тогда
		Если Не ЗначениеЗаполнено(НастройкиАвторизации.АдресРегистрацииУстройства) Тогда
			ИдентификаторУстройства = Строка(Новый УникальныйИдентификатор());
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыЗапроса = ПараметрыЗапросаАвторизации();
	HTTPЗапрос = РаботаСПочтовымиСообщениямиСлужебный.ПодготовитьHTTPЗапрос(АдресРесурса, ПараметрыЗапроса, Ложь);
	
	СтрокаЗапроса = "https://" + АдресСервера + HTTPЗапрос.АдресРесурса;
	Возврат СтрокаЗапроса; 
	
КонецФункции

&НаСервере
Функция ПараметрыЗапросаАвторизации()
	
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("client_id", ИдентификаторПриложения);
	ПараметрыЗапроса.Вставить("response_type", "code");
	ПараметрыЗапроса.Вставить("redirect_uri", АдресПеренаправления);
	
	Если ЗначениеЗаполнено(НастройкиАвторизации["ЗапрашиваемыеРазрешения"]) Тогда
		ПараметрыЗапроса.Вставить("scope", НастройкиАвторизации["ЗапрашиваемыеРазрешения"]);
	КонецЕсли;
	
	Если ОткрытаяАвторизацияПочтовогоСервисаОпубликована Тогда
		ПараметрыЗапроса.Вставить("state", ВложенныйАдресПеренаправления());
	Иначе
		ПараметрыЗапроса.Вставить("state", ИдентификаторЗапроса);
	КонецЕсли;
	
	ПараметрыЗапроса.Вставить("login_hint", АдресЭлектроннойПочты);
	
	Если НастройкиАвторизации.ИспользоватьКлючПроверкиПодлинностиPKCE Тогда
		ПараметрыЗапроса.Вставить("code_challenge", РаботаСПочтовымиСообщениямиСлужебный.КодироватьСтрокуМетодомS256(ПроверочныйКод));
		ПараметрыЗапроса.Вставить("code_challenge_method", "S256");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НастройкиАвторизации["ДополнительныеПараметрыАвторизации"]) Тогда
		Для Каждого Элемент Из СтрРазделить(НастройкиАвторизации["ДополнительныеПараметрыАвторизации"], " ", Ложь) Цикл
			ОписаниеПараметра = СтрРазделить(Элемент, "=", Истина);
			ИмяПараметра = ОписаниеПараметра[0];
			ЗначениеПараметра = "";
			Если ОписаниеПараметра.Количество() = 2 Тогда
				ЗначениеПараметра = ОписаниеПараметра[1];
			КонецЕсли;
			ПараметрыЗапроса.Вставить(ИмяПараметра, ЗначениеПараметра);
		КонецЦикла;
	КонецЕсли;
	
	Если ЭтоВебКлиент() Тогда
		Если Не ЗначениеЗаполнено(НастройкиАвторизации.АдресРегистрацииУстройства) Тогда
			ПараметрыЗапроса.Вставить("device_name", НСтр("ru = '1С:Предприятие'"));
		КонецЕсли;
		ПараметрыЗапроса.Вставить("device_id", ИдентификаторУстройства);
	КонецЕсли;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

&НаСервере
Функция ВложенныйАдресПеренаправления()
	
	ШаблонВложенногоАдресаПеренаправления = "%1/hs/oauth2_mail/callback/?zone=%2&session=%3&request_id=%4&user_id=%5";
	ВложенныйАдресПеренаправления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонВложенногоАдресаПеренаправления,
		ОбщегоНазначения.АдресПубликацииИнформационнойБазыВИнтернете(),
		XMLСтрока(ЗначениеРазделителяСеанса()),
		XMLСтрока(НомерСеансаИнформационнойБазы()),
		ИдентификаторЗапроса,
		Строка(ПользователиИнформационнойБазы.ТекущийПользователь().УникальныйИдентификатор));
	
	Возврат ВложенныйАдресПеренаправления;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеРазделителяСеанса()
	
	ЗначениеРазделителяСеанса = 0;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса.БазоваяФункциональность") Тогда
		МодульРаботаВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервиса");
		ЗначениеРазделителяСеанса = МодульРаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
	КонецЕсли;
	
	Возврат ЗначениеРазделителяСеанса;
	
КонецФункции

&НаКлиенте
Процедура ВебСтраницаДокументСформирован(Элемент)
	
	Если ЗначениеЗаполнено(АдресПеренаправления) Тогда
		ПроверитьРезультатАвторизации();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРезультатАвторизации()
	
	Если ОткрытаяАвторизацияПочтовогоСервисаОпубликована Тогда
		СерверныеОповещенияКлиент.ПодключитьОбработчикПроверкиПолученияСерверныхОповещений(1, Истина);
		Возврат;
	КонецЕсли;
	
#Если Не ВебКлиент Тогда
	ОтключитьОбработчикОжидания("ПроверитьРезультатАвторизации");
	
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	АдресПолученияКлюча = НастройкиАвторизации.АдресПолученияКлюча;
	
	ОтветСервера = "";
	Если СтрНачинаетсяС(НРег(Элементы.ВебСтраница.Документ.URL), НРег(АдресПеренаправления)) Тогда
		ОтветСервера = Элементы.ВебСтраница.Документ.URL;
	ИначеЕсли СтрНачинаетсяС(ВебСтраница, "{") И СтрЗаканчиваетсяНа(ВебСтраница, "}") Тогда
		ОтветСервера = ВебСтраница;
	ИначеЕсли Элементы.ВебСтраница.Документ.contentType = "application/json"
		И Элементы.ВебСтраница.Документ.body <> Неопределено Тогда
		ОтветСервера = Элементы.ВебСтраница.Документ.body.innerText;
	ИначеЕсли Элементы.ВебСтраница.Документ.body <> Неопределено 
		И СтрНайти(Элементы.ВебСтраница.Документ.body.innerText, АдресПеренаправления) Тогда
		ЧастиСтроки = СтрРазделить(Элементы.ВебСтраница.Документ.body.innerText, " ", Ложь);
		Для Каждого ЧастьСтроки Из ЧастиСтроки Цикл
			Если СтрНачинаетсяС(ЧастьСтроки, АдресПеренаправления) Тогда
				ОтветСервера = ЧастьСтроки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.Авторизация Тогда
			ПодключитьОбработчикОжидания("ПроверитьРезультатАвторизации", 5, Истина);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ПриПолученииОтветаПочтовогоСервера(ОтветСервера, АдресПолученияКлюча);
	ПерейтиНаСледующуюСтраницу();
#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииОтветаПочтовогоСервера(СтрокаПараметров, АдресПолученияКлюча)
	
	Если СтрНачинаетсяС(СтрокаПараметров, "{") Тогда
		Ответ = ОбщегоНазначения.JSONВЗначение(СтрокаПараметров);
	Иначе
		Ответ = ПараметрыИзСтрокиURI(СтрокаПараметров);
	КонецЕсли;
	
	КодАвторизации = Ответ["code"];
	КодОшибки = Ответ["error"];
	ТекстОшибки = Ответ["error_description"];
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ТекстОшибки = РаскодироватьСтроку(ТекстОшибки, СпособКодированияСтроки.КодировкаURL);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодОшибки) Тогда
		СообщенияОбОшибках = ОписаниеОшибкиАвторизацииПочтовогоСервера(КодОшибки, ТекстОшибки);
		ПроверкаЗавершиласьСОшибками = Истина;
	ИначеЕсли ИдентификаторЗапроса <> Ответ["state"] Тогда
		СообщенияОбОшибках = НСтр("ru = 'Не удалось авторизоваться на почтовом сервере. Неверный идентификатор ответа.'");
		ПроверкаЗавершиласьСОшибками = Истина;
	ИначеЕсли Не ПолучитьКлючиДоступаКПочтовомуСерверу(КодАвторизации, АдресПолученияКлюча) Тогда
		ПроверкаЗавершиласьСОшибками = Истина;
	КонецЕсли;
	
	Если ПроверкаЗавершиласьСОшибками Тогда
		
		ВремяРегистрацииОшибки = ТекущаяДатаСеанса();
		ЗаполнитьПояснения();
		
		// СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку") Тогда
			
			МодульОбращенияВТехническуюПоддержкуСлужебный = ОбщегоНазначения.ОбщийМодуль(
				"ОбращенияВТехническуюПоддержкуСлужебный");
			
			МодульОбращенияВТехническуюПоддержкуСлужебный.ПоказатьРазделТребуетсяПомощь(Элементы);
			
		КонецЕсли;
		// Конец СтандартныеПодсистемы.ОбращенияВТехническуюПоддержку
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКлючиДоступаКПочтовомуСерверу(КодАвторизации, АдресПолученияКлюча, ЗаголовокПриложения = "")
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресПолученияКлюча);
	АдресСервера = СтруктураURI.Хост;
	АдресРесурса = "/" + СтруктураURI.ПутьНаСервере;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("client_id", ИдентификаторПриложения);
	
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	
	Если ЗначениеЗаполнено(НастройкиАвторизации["ЗапрашиваемыеРазрешения"]) Тогда
		ПараметрыЗапроса.Вставить("scope", НастройкиАвторизации["ЗапрашиваемыеРазрешения"]);
	КонецЕсли;
	
	ПараметрыЗапроса.Вставить("code", КодАвторизации);
	ПараметрыЗапроса.Вставить("redirect_uri", АдресПеренаправления);
	ПараметрыЗапроса.Вставить("grant_type", "authorization_code");
	
	Если НастройкиАвторизации.ИспользоватьКлючПроверкиПодлинностиPKCE Тогда
		ПараметрыЗапроса.Вставить("code_verifier", ПроверочныйКод);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПарольПриложения) Тогда
		ПараметрыЗапроса.Вставить("client_secret", ПарольПриложения);
	КонецЕсли;
	
	Если ЭтоВебКлиент() Тогда
		ПараметрыЗапроса.Вставить("device_id", ИдентификаторУстройства);
		ПараметрыЗапроса.Вставить("device_name", ЗаголовокПриложения);
	КонецЕсли;
	
	ВремяЗапроса = ТекущаяДатаСеанса();
	РезультатЗапроса = РаботаСПочтовымиСообщениямиСлужебный.ВыполнитьЗапрос(АдресСервера, АдресРесурса, ПараметрыЗапроса);
	
	Попытка
		ПараметрыОтвета = ОбщегоНазначения.JSONВЗначение(РезультатЗапроса.ОтветСервера);
	Исключение
		ПараметрыОтвета = Новый Соответствие;
	КонецПопытки;
	
	ТокенДоступа = ПараметрыОтвета["access_token"];
	СрокДействияТокенаДоступа = ПараметрыОтвета["expires_in"];
	ТокенОбновления = ПараметрыОтвета["refresh_token"];
	КодОшибки = ПараметрыОтвета["error"];
	ТекстОшибки = ПараметрыОтвета["error_description"];
	
	Если ЗначениеЗаполнено(КодОшибки) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить ключи доступа к учетной записи почты %1 по причине:
			|%2'"), АдресЭлектроннойПочты, ТекстОшибки);
			
		ТехническиеПодробности = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ответ сервера:
			|%1'"), РезультатЗапроса.ОтветСервера);
			
		ЗаписьЖурналаРегистрации(РаботаСПочтовымиСообщениямиСлужебный.ИмяСобытияАвторизацияПоПротоколуOAuth(),
			УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки + Символы.ПС + ТехническиеПодробности);
		СообщенияОбОшибках = ТекстОшибки;
		Возврат Ложь;
	ИначеЕсли Не РезультатЗапроса.ЗапросВыполнен Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить ключи доступа к учетной записи почты %1 по причине:
			|Запрос не выполнен.'"), АдресЭлектроннойПочты);
			
		ТехническиеПодробности = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ответ сервера:
			|%1'"), РезультатЗапроса.ОтветСервера);
			
		ЗаписьЖурналаРегистрации(РаботаСПочтовымиСообщениямиСлужебный.ИмяСобытияАвторизацияПоПротоколуOAuth(),
			УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки + Символы.ПС + ТехническиеПодробности);
		СообщенияОбОшибках = ТекстОшибки;
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СрокДействияТокенаДоступа) Тогда
		СрокДействияТокенаДоступа = ВремяЗапроса + СрокДействияТокенаДоступа;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПараметрыИзСтрокиURI(СтрокаURI)
	
	ЗначенияПараметров = Новый Соответствие;
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(СтрокаURI);
	
	Для Каждого ЧастьСтроки Из СтрРазделить(СтруктураURI.ПутьНаСервере, "?&") Цикл
		ПараметрИЗначение = СтрРазделить(ЧастьСтроки, "=", Истина);
		ИмяПараметра = ПараметрИЗначение[0];
		Если ПараметрИЗначение.Количество() > 1 Тогда
			ЗначениеПараметра = РаскодироватьСтроку(ПараметрИЗначение[1], СпособКодированияСтроки.КодировкаURL);
			ЗначенияПараметров.Вставить(ИмяПараметра, ЗначениеПараметра);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗначенияПараметров;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОписаниеОшибкиАвторизацииПочтовогоСервера(Знач КодОшибки, Знач ТекстОшибки)
	
	Результат = Строка(ТекстОшибки) + Символы.ПС + КодОшибки;
	Результат = СокрЛП(Результат);
	
	Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
		"ru = 'Не удалось авторизоваться на почтовом сервере:
		|%1'"), Результат);
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//   см. Справочники.УчетныеЗаписиЭлектроннойПочты.НастройкиПодключенияПоАдресуЭлектроннойПочты
//
&НаСервере
Функция НастройкиПодключенияПоАдресуЭлектроннойПочты()
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Справочники.УчетныеЗаписиЭлектроннойПочты.НастройкиПодключенияПоАдресуЭлектроннойПочты(АдресЭлектроннойПочты, ПарольДляПолученияПисем);
	
КонецФункции

// Возвращаемое значение:
//   см. Справочники.НастройкиАвторизацииИнтернетСервисов.НастройкиАвторизацииИнтернетСервиса
//
&НаСервере
Функция НастройкиАвторизацииНаПочтовомСервере()
	
	Возврат НастройкиПодключенияПоАдресуЭлектроннойПочты().НастройкиАвторизации;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоВебКлиент()
	
#Если ВебКлиент Тогда
	Возврат Истина;
#КонецЕсли
	
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Возврат ОбщегоНазначения.ЭтоВебКлиент();
#КонецЕсли
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция ПолучитьПараметрыАвторизацииВВебКлиенте(АдресПолученияКлюча)
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресПолученияКлюча);
	АдресСервера = СтруктураURI.Хост;
	АдресРесурса = "/" + СтруктураURI.ПутьНаСервере;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("client_id", ИдентификаторПриложения);
	
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	
	Если ЗначениеЗаполнено(НастройкиАвторизации["ЗапрашиваемыеРазрешения"]) Тогда
		ПараметрыЗапроса.Вставить("scope", НастройкиАвторизации["ЗапрашиваемыеРазрешения"]);
	КонецЕсли;
	
	ВремяЗапроса = ТекущаяДатаСеанса();
	РезультатЗапроса = РаботаСПочтовымиСообщениямиСлужебный.ВыполнитьЗапрос(АдресСервера, АдресРесурса, ПараметрыЗапроса);
	
	Попытка
		ПараметрыОтвета = ОбщегоНазначения.JSONВЗначение(РезультатЗапроса.ОтветСервера);
	Исключение
		ПараметрыОтвета = Новый Соответствие;
	КонецПопытки;
	
	ИдентификаторУстройства = ПараметрыОтвета["device_code"];
	СрокДействияИдентификатораУстройства = ПараметрыОтвета["expires_in"];
	ИнтервалВыполненияЗапросов = ПараметрыОтвета["interval"];
	КодПодтверждения = ПараметрыОтвета["user_code"];
	
	АдресАвторизацииПользователя = ПараметрыОтвета["verification_url"];
	Если Не ЗначениеЗаполнено(АдресАвторизацииПользователя) Тогда
		АдресАвторизацииПользователя = ПараметрыОтвета["verification_uri"];
	КонецЕсли;
	
	КодОшибки = ПараметрыОтвета["error"];
	ТекстОшибки = ПараметрыОтвета["error_description"];
	ТекстОшибки = ОписаниеОшибкиАвторизацииПочтовогоСервера(КодОшибки, ТекстОшибки);
	
	Если ЗначениеЗаполнено(КодОшибки) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить ключи доступа к учетной записи почты %1 по причине:
			|%2'"), АдресЭлектроннойПочты, ТекстОшибки);
			
		ТехническиеПодробности = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ответ сервера:
			|%1'"), РезультатЗапроса.ОтветСервера);
			
		ЗаписьЖурналаРегистрации(РаботаСПочтовымиСообщениямиСлужебный.ИмяСобытияАвторизацияПоПротоколуOAuth(),
			УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки + Символы.ПС + ТехническиеПодробности);
		СообщенияОбОшибках = ТекстОшибки;
		ПроверкаЗавершиласьСОшибками = Истина;
		Возврат Ложь;
	ИначеЕсли Не РезультатЗапроса.ЗапросВыполнен Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить ключи доступа к учетной записи почты %1 по причине:
			|Запрос не выполнен.'"), АдресЭлектроннойПочты);
			
		ТехническиеПодробности = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ответ сервера:
			|%1'"), РезультатЗапроса.ОтветСервера);
			
		ЗаписьЖурналаРегистрации(РаботаСПочтовымиСообщениямиСлужебный.ИмяСобытияАвторизацияПоПротоколуOAuth(),
			УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки + Символы.ПС + ТехническиеПодробности);
		СообщенияОбОшибках = ТекстОшибки;
		ПроверкаЗавершиласьСОшибками = Истина;
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СрокДействияИдентификатораУстройства) Тогда
		СрокДействияИдентификатораУстройства = ВремяЗапроса + СрокДействияИдентификатораУстройства;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьКлючиДоступаВебКлиент()
	
	Если ПолучитьКлючДоступаУстройства() = Неопределено Тогда
		ПодключитьОбработчикОжидания("ПолучитьКлючиДоступаВебКлиент", ИнтервалВыполненияЗапросов, Истина);
		Возврат;
	КонецЕсли;
	
	ПерейтиНаСледующуюСтраницу();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКлючДоступаУстройства()
	
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(НастройкиАвторизации.АдресПолученияКлюча);
	АдресСервера = СтруктураURI.Хост;
	АдресРесурса = "/" + СтруктураURI.ПутьНаСервере;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("client_id", ИдентификаторПриложения);
	ПараметрыЗапроса.Вставить("client_secret", ПарольПриложения);
	ПараметрыЗапроса.Вставить("device_code", ИдентификаторУстройства);
	ПараметрыЗапроса.Вставить("grant_type", "urn:ietf:params:oauth:grant-type:device_code");
	
	ВремяЗапроса = ТекущаяДатаСеанса();
	РезультатЗапроса = РаботаСПочтовымиСообщениямиСлужебный.ВыполнитьЗапрос(АдресСервера, АдресРесурса, ПараметрыЗапроса);
	
	Попытка
		ПараметрыОтвета = ОбщегоНазначения.JSONВЗначение(РезультатЗапроса.ОтветСервера);
	Исключение
		ПараметрыОтвета = Новый Соответствие;
	КонецПопытки;
	
	ТокенДоступа = ПараметрыОтвета["access_token"];
	СрокДействияТокенаДоступа = ПараметрыОтвета["expires_in"];
	ТокенОбновления = ПараметрыОтвета["refresh_token"];
	КодОшибки = ПараметрыОтвета["error"];
	ТекстОшибки = ПараметрыОтвета["error_description"];
	
	Если КодОшибки = "authorization_pending" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодОшибки) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить ключи доступа к учетной записи почты %1 по причине:
			|%2'"), АдресЭлектроннойПочты, ТекстОшибки);
			
		ТехническиеПодробности = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ответ сервера:
			|%1'"), РезультатЗапроса.ОтветСервера);
			
		ЗаписьЖурналаРегистрации(РаботаСПочтовымиСообщениямиСлужебный.ИмяСобытияАвторизацияПоПротоколуOAuth(),
			УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки + Символы.ПС + ТехническиеПодробности);
		СообщенияОбОшибках = ТекстОшибки;
		Возврат Ложь;
	ИначеЕсли Не РезультатЗапроса.ЗапросВыполнен Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить ключи доступа к учетной записи почты %1 по причине:
			|Запрос не выполнен.'"), АдресЭлектроннойПочты);
			
		ТехническиеПодробности = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ответ сервера:
			|%1'"), РезультатЗапроса.ОтветСервера);
			
		ЗаписьЖурналаРегистрации(РаботаСПочтовымиСообщениямиСлужебный.ИмяСобытияАвторизацияПоПротоколуOAuth(),
			УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки + Символы.ПС + ТехническиеПодробности);
		СообщенияОбОшибках = ТекстОшибки;
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СрокДействияТокенаДоступа) Тогда
		СрокДействияТокенаДоступа = ВремяЗапроса + СрокДействияТокенаДоступа;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция ДоступнаАвторизацияПоКоду()
	
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	
	Возврат ЗначениеЗаполнено(НастройкиАвторизации)
		И (ЗначениеЗаполнено(НастройкиАвторизации.АдресРегистрацииУстройства) 
		Или ЗначениеЗаполнено(НастройкиАвторизации.АдресПеренаправленияВебКлиент));
	
КонецФункции

&НаКлиенте
Процедура НастроитьЭлементыТекущейСтраницыПриОткрытии()
	
	НастройкиАвторизации = НастройкиАвторизацииНаПочтовомСервере; // см. НастройкиАвторизацииНаПочтовомСервере
	
	Если ТолькоАвторизация Тогда
		
		Если Не ЗначениеЗаполнено(НастройкиАвторизации) Тогда
			Закрыть(НСтр("ru = 'Не найдены настройки авторизации для указанного адреса электронной почты.
				|Используйте авторизацию по логину и паролю.'"));
			Возврат;
		КонецЕсли;
		
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.Авторизация Тогда
			АвторизоватьсяНаПочтовомСервере();
		КонецЕсли;
		
	КонецЕсли;
	
	НастроитьЭлементыТекущейСтраницы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПояснения()
	
	ПоясненияПоОшибке = РаботаСПочтовымиСообщениямиСлужебный.ПоясненияПоОшибке(СообщенияОбОшибках, , Истина);
	
	ВозможныеПричины = РаботаСПочтовымиСообщениямиСлужебный.ФорматированныйСписок(ПоясненияПоОшибке.ВозможныеПричины);
	СпособыУстранения = РаботаСПочтовымиСообщениямиСлужебный.ФорматированныйСписок(ПоясненияПоОшибке.СпособыУстранения);
	
	Элементы.ДекорацияРекомендации.Заголовок = Строка(СпособыУстранения);
	Элементы.ДекорацияВозможныеПричины.Заголовок = Строка(ВозможныеПричины);
	
	ЗаполнитьКраткоеОписаниеОшибки();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКраткоеОписаниеОшибки()
	
	ОграничениеДлины = 150;
	
	Если СтрДлина(СообщенияОбОшибках) = ОграничениеДлины Тогда
		НачалоОписанияОшибки = СтрНайти(СообщенияОбОшибках, Символы.ПС);
		КраткоеОписаниеОшибки = Лев(СообщенияОбОшибках, НачалоОписанияОшибки);
	Иначе
		КраткоеОписаниеОшибки = СообщенияОбОшибках;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КраткоеОписаниеОшибки) Тогда
		КраткоеОписаниеОшибки = НСтр("ru = 'Не удалось авторизоваться на почтовом сервере.'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПреобразоватьНастройкиИзPunycode()
	
	СерверВходящейПочты = РаботаСПочтовымиСообщениямиСлужебный.PunycodeВСтроку(СерверВходящейПочты);
	СерверИсходящейПочты = РаботаСПочтовымиСообщениямиСлужебный.PunycodeВСтроку(СерверИсходящейПочты);
	ИмяПользователяДляОтправкиПисем = РаботаСПочтовымиСообщениямиСлужебный.PunycodeВСтроку(ИмяПользователяДляОтправкиПисем);
	ИмяПользователяДляПолученияПисем = РаботаСПочтовымиСообщениямиСлужебный.PunycodeВСтроку(ИмяПользователяДляПолученияПисем);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеРучнойНастройкиУчетнойЗаписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроверкаПропущена Тогда
		ПроверкаПропущена = Ложь;
		Если ВариантАутентификации = "Пароль" Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.ВводПароляУчетнойЗаписи;
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.Авторизация;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
	ПарольДляПолученияПисем = Результат.Пароль;
	
	ПерейтиНаСледующуюСтраницу();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДоступностьСервисаОткрытойАвторизации()
	
	ОткрытаяАвторизацияПочтовогоСервисаОпубликована = ОткрытаяАвторизацияПочтовогоСервисаОпубликована();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОткрытаяАвторизацияПочтовогоСервисаОпубликована()
	
	Возврат РаботаСПочтовымиСообщениямиСлужебный.ОткрытаяАвторизацияПочтовогоСервисаОпубликована();
	
КонецФункции

&НаСервере
Функция ПараметрыУчетнойЗаписи()
	
	ПараметрыУчетнойЗаписи = Справочники.УчетныеЗаписиЭлектроннойПочты.ПараметрыУчетнойЗаписи();
	ЗаполнитьЗначенияСвойств(ПараметрыУчетнойЗаписи, ЭтотОбъект);
	
	ПараметрыУчетнойЗаписи.Наименование = НазваниеУчетнойЗаписи;
	ПараметрыУчетнойЗаписи.ВремяОжидания =  ДлительностьОжиданияСервера;
	ПараметрыУчетнойЗаписи.ИмяПользователя = ИмяОтправителяПисем;
	ПараметрыУчетнойЗаписи.ОставлятьКопииСообщенийНаСервере = ОставлятьКопииПисемНаСервере;
	
	ПериодХранения = ?(ОставлятьКопииПисемНаСервере И УдалятьПисьмаССервера И Протокол = "POP", ПериодХраненияСообщенийНаСервере, 0);
	ПараметрыУчетнойЗаписи.ПериодХраненияСообщенийНаСервере = ПериодХранения;
	
	ПараметрыУчетнойЗаписи.Пользователь = ИмяПользователяДляПолученияПисем;
	ПараметрыУчетнойЗаписи.ПользовательSMTP = ИмяПользователяДляОтправкиПисем;
	ПараметрыУчетнойЗаписи.ПротоколВходящейПочты = Протокол;
	ПараметрыУчетнойЗаписи.ПриОтправкеПисемТребуетсяАвторизация = ЗначениеЗаполнено(ИмяПользователяДляОтправкиПисем);
	
	Если ВидУчетнойЗаписи = "Персональная" Тогда
		ПараметрыУчетнойЗаписи.ВладелецУчетнойЗаписи = Пользователи.ТекущийПользователь();
	Иначе
		ПараметрыУчетнойЗаписи.ВладелецУчетнойЗаписи = Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;
	
	Если ВариантАутентификации = "OAuth" Тогда
		ПараметрыУчетнойЗаписи.АвторизацияСПомощьюПочтовогоСервиса = Истина;
		ПараметрыУчетнойЗаписи.ИмяПочтовогоСервиса = НастройкиАвторизацииНаПочтовомСервере.ИмяИнтернетСервиса;
	Иначе
		ПараметрыУчетнойЗаписи.Пароль = ПарольДляПолученияПисем;
	КонецЕсли;
	
	ПараметрыУчетнойЗаписи.ВариантАутентификации = ВариантАутентификации;
	ПараметрыУчетнойЗаписи.СозданиеУчетнойЗаписиЧерезПомощник = Истина;
	
	Возврат ПараметрыУчетнойЗаписи;
	
КонецФункции

#КонецОбласти
