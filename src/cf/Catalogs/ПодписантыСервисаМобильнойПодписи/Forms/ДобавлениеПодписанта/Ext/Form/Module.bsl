///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщиеНастройки = ЭлектроннаяПодпись.ОбщиеНастройки();
	ОрганизацияИспользуется = ОбщиеНастройки.ОрганизацияИспользуется;
	Элементы.ФизическоеЛицо.Видимость = ОбщиеНастройки.ФизическоеЛицоИспользуется;

	ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Параметры);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыПодписанта);
	
	ИННФЛ = СервисМобильнойПодписиКлиентСервер.ТолькоЦифры(ПараметрыПодписанта.ИННФЛ);
	ИНН = СервисМобильнойПодписиКлиентСервер.ТолькоЦифры(ПараметрыПодписанта.ИНН);
	Если ЗначениеЗаполнено(ПараметрыПодписанта.НомерТелефона) Тогда
		НомерТелефона = СервисМобильнойПодписиКлиентСервер.ТолькоЦифры(ПараметрыПодписанта.НомерТелефона);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидПодписанта) Тогда
		Если ЗначениеЗаполнено(Параметры.ИННФЛ) И Не ЗначениеЗаполнено(Параметры.ИНН) Тогда
			Если Не ИспользоватьГосключ Или ЗначениеЗаполнено(Параметры.ОГРН) Тогда
				ВидПодписанта = Перечисления.ВидыПодписантовМобильногоСервиса.ИндивидуальныйПредприниматель;
			Иначе
				ВидПодписанта = Перечисления.ВидыПодписантовМобильногоСервиса.ФизическоеЛицо;
			КонецЕсли;
		Иначе
			ВидПодписанта = Перечисления.ВидыПодписантовМобильногоСервиса.РуководительЮЛ;
		КонецЕсли;
	КонецЕсли;
	
	ПриИзмененииНастроек();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_НаборКонстант" И (Источник = "ИспользоватьГосключ" Или Источник = "ИспользоватьСервисМобильнойПодписи") Тогда
		ПриИзмененииНастроек();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидПодписантаПриИзменении(Элемент)
	
	Если ВидПодписанта = ПредопределенноеЗначение("Перечисление.ВидыПодписантовМобильногоСервиса.ФизическоеЛицо") Тогда
		СервисМобильнойПодписи = Ложь;
	Иначе
		ГосключУНЭП = Ложь;
	КонецЕсли;
	
	ПриИзмененииНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)

	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		РеквизитыФизическогоЛица = РеквизитыФизическогоЛица(ФизическоеЛицо);
		ЗаполненныеРеквизиты = ЗаполнитьРеквизитыФизическогоЛица(РеквизитыФизическогоЛица);
		ЗаполнитьЗначенияСвойств(РеквизитыФизическогоЛица, ЗаполненныеРеквизиты);
	Иначе
		РеквизитыФизическогоЛица = РеквизитыФизическогоЛица();
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыФизическогоЛица);

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		РеквизитыОрганизации = СервисМобильнойПодписиКлиентСервер.РеквизитыОрганизации(Организация);
		ЗаполненныеРеквизиты = ЗаполнитьРеквизитыОрганизации(РеквизитыОрганизации);
		ЗаполнитьЗначенияСвойств(РеквизитыОрганизации, ЗаполненныеРеквизиты);
		
		ИННИП = Неопределено;
		Если СтрДлина(РеквизитыОрганизации.ИНН) = 12 Тогда
			ВидПодписанта = ПредопределенноеЗначение("Перечисление.ВидыПодписантовМобильногоСервиса.ИндивидуальныйПредприниматель");
			ИННИП = РеквизитыОрганизации.ИНН;
			РеквизитыОрганизации.ИНН = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РеквизитыОрганизации.ФизическиеЛица) Тогда
			Если РеквизитыОрганизации.ФизическиеЛица.Количество() > 0 Тогда
				РеквизитыФизическогоЛица = РеквизитыФизическогоЛица();
				ЗаполнитьЗначенияСвойств(РеквизитыФизическогоЛица, РеквизитыОрганизации.ФизическиеЛица[0]);
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыФизическогоЛица);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИННИП) Тогда
			ИННФЛ = ИННИП;
			Должность = "";
		КонецЕсли;
	Иначе
		РеквизитыОрганизации = СервисМобильнойПодписиКлиентСервер.РеквизитыОрганизации();
	КонецЕсли;
	
  	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыОрганизации);
	УстановитьВидимостьИДоступность(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура МояПодписьПриИзменении(Элемент)
	УстановитьВидимостьИДоступность(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура УНЭППриИзменении(Элемент)
	УстановитьВидимостьИДоступность(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура УКЭППриИзменении(Элемент)
	УстановитьВидимостьИДоступность(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент) 
	УстановитьВидимостьИДоступность(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	
	Если ВидПодписанта = "ЮридическоеЛицо" И Не ЗначениеЗаполнено(ИНН) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Не заполнен ИНН юридического лица.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, ЭтотОбъект);
	ПараметрыПодписанта.НомерТелефона = ?(СтрНачинаетсяС(НомерТелефона, "+7"), НомерТелефона, "+7" + НомерТелефона);
	
	Попытка
		ПроверитьПараметрыПодписанта(ПараметрыПодписанта);
	Исключение
		ОбщегоНазначенияКлиент.СообщитьПользователю(ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	Результат = ДобавитьПодписанта(ПараметрыПодписанта);
	
	ОповеститьОЗаписиНового(Результат.Ссылка);
	Оповестить("Запись_ПодписантыСервисаМобильнойПодписи", Новый Структура("ЭтоНовый", Результат.ЭтоНовый), Результат.Ссылка);
	
	Если Не Результат.ЭтоНовый Тогда
		Если Результат.ОбновленыДанные Тогда
			ТекстОповещения = НСтр("ru = 'Обновлены данные подписанта'");
		Иначе
			ТекстОповещения = НСтр("ru = 'Подписант с такими данными уже есть в справочнике'");
		КонецЕсли;
		ПоказатьОповещениеПользователя(ТекстОповещения, ПолучитьНавигационнуюСсылку(Результат.Ссылка), Строка(
			Результат.Ссылка), БиблиотекаКартинок.ДиалогИнформация);
	КонецЕсли;
	
	Закрыть(Результат.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриИзмененииНастроек()
	
	ИспользоватьГосключ = ПолучитьФункциональнуюОпцию("ИспользоватьГосключ");
	ИспользоватьСервисМобильнойПодписи = ПолучитьФункциональнуюОпцию("ИспользоватьСервисМобильнойПодписи");
	
	ВидПодписантаФизическоеЛицо = Перечисления.ВидыПодписантовМобильногоСервиса.ФизическоеЛицо;
	Элемент = Элементы.ВидПодписанта.СписокВыбора.НайтиПоЗначению(ВидПодписантаФизическоеЛицо);
	
	Если Не ИспользоватьГосключ Тогда
		Если ВидПодписанта = ВидПодписантаФизическоеЛицо Тогда
			ВидПодписанта = Перечисления.ВидыПодписантовМобильногоСервиса.РуководительЮЛ;
		КонецЕсли;
		Если Элемент <> Неопределено Тогда
			Элементы.ВидПодписанта.СписокВыбора.Удалить(Элемент);
		КонецЕсли;
	ИначеЕсли Элемент = Неопределено Тогда
		Элементы.ВидПодписанта.СписокВыбора.Добавить(ВидПодписантаФизическоеЛицо, НСтр("ru = 'Физическое лицо'"));
	КонецЕсли;
	
	МояПодпись = ИспользоватьСервисМобильнойПодписи И Не ИспользоватьГосключ;
	ГосключУКЭП = ИспользоватьГосключ И Не ИспользоватьСервисМобильнойПодписи И ВидПодписанта <> ВидПодписантаФизическоеЛицо;
	УстановитьВидимостьИДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПроверитьПараметрыПодписанта(ПараметрыПодписанта)
	СервисМобильнойПодписиСлужебный.ПроверкаПараметровПодписанта(ПараметрыПодписанта);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьПодписанта(ПараметрыПодписанта)
	Возврат СервисМобильнойПодписиСлужебный.ДобавитьПодписантаВСправочник(ПараметрыПодписанта);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыФизическогоЛица(ФизическоеЛицо = Неопределено)
	РеквизитыФизическогоЛица = Новый Структура;
	РеквизитыФизическогоЛица.Вставить("ФизическоеЛицо", ФизическоеЛицо);
	РеквизитыФизическогоЛица.Вставить("ИННФЛ");
	РеквизитыФизическогоЛица.Вставить("Фамилия");
	РеквизитыФизическогоЛица.Вставить("Имя");
	РеквизитыФизическогоЛица.Вставить("Отчество");
	РеквизитыФизическогоЛица.Вставить("Должность");
	РеквизитыФизическогоЛица.Вставить("Телефон");
	РеквизитыФизическогоЛица.Вставить("СНИЛС");
	Возврат РеквизитыФизическогоЛица;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьИДоступность(Форма)
	
	ЭтоЮрЛицо = Форма.ВидПодписанта = ПредопределенноеЗначение("Перечисление.ВидыПодписантовМобильногоСервиса.РуководительЮЛ");
	ЭтоФизЛицо = Форма.ВидПодписанта = ПредопределенноеЗначение("Перечисление.ВидыПодписантовМобильногоСервиса.ФизическоеЛицо");
	
	Форма.Элементы.ГруппаГосключ.Видимость = Форма.ИспользоватьГосключ;
	Форма.Элементы.ГосключУКЭП.Видимость = ЭтоФизЛицо Или Форма.ИспользоватьСервисМобильнойПодписи;
	Форма.Элементы.ГосключУНЭП.Видимость = ЭтоФизЛицо;
	Форма.Элементы.МояПодпись.Видимость = Форма.ИспользоватьГосключ И Форма.ИспользоватьСервисМобильнойПодписи И Не ЭтоФизЛицо;
	
	ИностраннаяОрганизация = СтрНачинаетсяС(Форма.ИНН, "9909");
	
	Форма.Элементы.ИННФЛ.Видимость = Не ЭтоЮрЛицо Или ИностраннаяОрганизация;
	Форма.Элементы.ИНН.Видимость = ЭтоЮрЛицо;
	Форма.Элементы.НомерТелефона.Видимость = Форма.МояПодпись;
	Форма.Элементы.СНИЛС.Видимость = ЭтоФизЛицо Или Форма.ГосключУКЭП;
	
	Форма.Элементы.ГруппаОрганизация.Видимость = Не ЭтоФизЛицо;
	Форма.Элементы.НаименованиеОрганизации.Видимость = ЭтоЮрЛицо;
	Форма.Элементы.Организация.Видимость = Не ЭтоФизЛицо И Форма.ОрганизацияИспользуется;
	Форма.Элементы.ИНН.Видимость = ЭтоЮрЛицо;
	Форма.Элементы.Должность.Видимость = ЭтоЮрЛицо;
	
	Форма.Элементы.НомерЗаписиОбАккредитации.Видимость = ИностраннаяОрганизация;
	Форма.Элементы.ОГРН.Видимость = Не ИностраннаяОрганизация;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполнитьРеквизитыОрганизации(Знач Реквизиты)
	
	СтандартнаяОбработка = Истина;
	
	СервисМобильнойПодписиПереопределяемый.ПриЗаполненииРеквизитовОрганизации(Реквизиты, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда

		Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Организации") Тогда
			Возврат Реквизиты;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(Реквизиты.Организация) Тогда
			Возврат Реквизиты; // Реквизиты организации нельзя заполнить, если организация не выбрана.
		КонецЕсли;

		МодульОрганизацииСервер = ОбщегоНазначения.ОбщийМодуль("ОрганизацииСервер");
		СведенияОбОрганизации = МодульОрганизацииСервер.СведенияОбОрганизации(Реквизиты.Организация,,
			ТекущаяДатаСеанса());

		ЭтоИП = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ЭтоИП", Ложь);
		
		Если ЭтоИП Тогда

			Реквизиты.ИНН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации,
				"ИндивидуальныйПредпринимательИНН", "");
				
			ФИО = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ФизическоеЛицоНаименование",
				"");
			
			РеквизитыФизическогоЛица = РеквизитыФизическогоЛица();
			Если ЗначениеЗаполнено(ФИО) Тогда
				ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(ФИО);
				РеквизитыФизическогоЛица.Фамилия = ФИО.Фамилия;
				РеквизитыФизическогоЛица.Имя = ФИО.Имя;
				РеквизитыФизическогоЛица.Отчество = ФИО.Имя;
			КонецЕсли;
			
			Реквизиты.ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РеквизитыФизическогоЛица);
			
		Иначе
			Реквизиты.ОГРН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ОГРН", "");
			Реквизиты.ИНН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ИНН", "");
			Реквизиты.НаименованиеОрганизации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации,
				"НаименованиеСокращенное", "");

		КонецЕсли;

	КонецЕсли;

	Возврат Реквизиты;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьРеквизитыФизическогоЛица(Знач Параметры)
	СервисМобильнойПодписиПереопределяемый.ПриЗаполненииРеквизитовФизическогоЛица(Параметры);
	Возврат Параметры;
КонецФункции

#КонецОбласти
