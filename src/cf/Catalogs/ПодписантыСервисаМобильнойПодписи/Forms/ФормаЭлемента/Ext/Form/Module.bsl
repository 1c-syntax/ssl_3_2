///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	НомерТелефона = Прав(Объект.НомерТелефона, 10);
	ИспользуетсяАвтоНаименование = ИспользуетсяАвтоНаименование(Объект);
	ЕстьПраваНаПросмотр = Пользователи.ЭтоПолноправныйПользователь() Или Пользователи.ТекущийПользователь() = Объект.Добавил;

	Если ЗначениеЗаполнено(Параметры.Ключ) Тогда

		ОбщиеНастройки = ЭлектроннаяПодпись.ОбщиеНастройки();
		ПроверитьОтправленныеДокументы();

		Если Объект.ОтправленВСервис Или ЕстьОтправленныеДокументыВСервис Или ЕстьОтправленныеДокументыВГосуслуги Тогда
			Если Не ЕстьОтправленныеДокументыВСервис И Не ЕстьОтправленныеДокументыВГосуслуги Тогда
				ЗаголовокСообщения = НСтр("ru = 'Была отправка в сервис 1С:Мобильная подпись.'");
			ИначеЕсли ЕстьОтправленныеДокументыВСервис И ЕстьОтправленныеДокументыВГосуслуги Тогда
				ЗаголовокСообщения  = НСтр(
					"ru = 'Была отправка документов в мобильные приложения ""Моя подпись"" и ""Госключ"".'");
			ИначеЕсли ЕстьОтправленныеДокументыВГосуслуги Тогда
				ЗаголовокСообщения  = НСтр(
					"ru = 'Была отправка документов в мобильное приложение ""Госключ"".'");
			Иначе
				ЗаголовокСообщения  = НСтр(
					"ru = 'Была отправка документов в мобильное приложение ""Моя подпись"".'");
			КонецЕсли;

			ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
			ПараметрыСообщения.ТипСообщения = "Предупреждение";
			ПараметрыСообщения.Идентификатор = "БылаОтправкаВСервисМобильнойПодписи";
			ПараметрыСообщения.Сообщение = ЗаголовокСообщения;
			ПараметрыСообщения.КраткоеПредставление = НСтр("ru = 'Была отправка документов'");

			ОбщегоНазначения.ДобавитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения);
			ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);

		КонецЕсли;

		Элементы.Организация.Видимость = ОбщиеНастройки.ОрганизацияИспользуется;
		Элементы.ФизическоеЛицо.Видимость = ОбщиеНастройки.ФизическоеЛицоИспользуется;
		
	КонецЕсли;
	ОбновитьСписокПользователей();
	ПриИзмененииНастроек();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Отказ = Истина;
		СтандартныеПодсистемыКлиент.УстановитьХранениеФормы(ЭтотОбъект, Истина);
		ПодключитьОбработчикОжидания("ОбработчикОжиданияДобавитьПодписанта", 0.1, Истина);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если Объект.ВидПодписанта = ПредопределенноеЗначение("Перечисление.ВидыПодписантовМобильногоСервиса.РуководительЮЛ")
		И Не ЗначениеЗаполнено(Объект.ИНН) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнен ИНН юридического лица.'"),,"Объект.ИНН",,Отказ);
	КонецЕсли;
	
	ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Объект);
	ПараметрыПодписанта.НомерТелефона = ?(СтрНачинаетсяС(НомерТелефона, "+7"), НомерТелефона, "+7" + НомерТелефона);
	
	Попытка
		ПроверитьПараметрыПодписанта(ПараметрыПодписанта);
	Исключение
		ОбщегоНазначенияКлиент.СообщитьПользователю(ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()),,,,Отказ);
		Возврат;
	КонецПопытки;
	
	Объект.НомерТелефона = ПараметрыПодписанта.НомерТелефона;
	НомерТелефона = Прав(Объект.НомерТелефона, 10);
	Объект.ИНН = ПараметрыПодписанта.ИНН;
	Объект.ИННФЛ = ПараметрыПодписанта.ИННФЛ;
	Объект.СНИЛС = ПараметрыПодписанта.СНИЛС;
	
	Если ИспользуетсяАвтонаименование Или Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		Объект.Наименование = АвтоНаименование(ПараметрыПодписанта);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ИНН) И ЗначениеЗаполнено(Объект.ОГРН) Тогда
		ОГРН = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_ПодписантыСервисаМобильнойПодписи", Новый Структура("ЭтоНовый", Ложь), Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_НаборКонстант" И (Источник = "ИспользоватьГосключ" Или Источник = "ИспользоватьСервисМобильнойПодписи") Тогда
		ПриИзмененииНастроек();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	ИспользуетсяАвтоНаименование = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПодписантаПриИзменении(Элемент)
	
	Если Объект.ВидПодписанта = ПредопределенноеЗначение("Перечисление.ВидыПодписантовМобильногоСервиса.ФизическоеЛицо") Тогда
		Объект.МояПодпись = Ложь;
	Иначе
		Объект.ГосключУНЭП = Ложь;
	КонецЕсли;

	ПриИзмененииНастроек();
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователейНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("РежимВыбора", Истина);
	ПараметрыПодбора.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыПодбора.Вставить("МножественныйВыбор", Истина);
	ПараметрыПодбора.Вставить("РасширенныйПодбор", Истина);
	ПараметрыПодбора.Вставить("СкрытьПользователейБезПользователяИБ", Истина);
	
	Выбранные = Новый Массив;
	Для Каждого Строка Из Объект.Пользователи Цикл
		Выбранные.Добавить(Строка.Пользователь);
	КонецЦикла;
	
	ПараметрыПодбора.Вставить("ВыбранныеПользователи", Выбранные);
	ПараметрыПодбора.Вставить("ЗаголовокФормыПодбора",
		НСтр("ru = 'Выберите пользователей, которые могут отправлять документы подписанту'"));
	ПараметрыПодбора.Вставить("ЗаголовокКнопкиЗавершенияПодбора", НСтр("ru = 'Выбрать'"));
	
	Обработчик = Новый ОписаниеОповещения("ДобавитьПользователей", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыПодбора,,,,, Обработчик);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьДействиеИнформационногоСообщения(ЭлементИлиКоманда,
	НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	ОбщегоНазначенияКлиент.ОбработатьДействиеИнформационногоСообщения(ЭтотОбъект,
	ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	ИностраннаяОрганизация = СтрНачинаетсяС(Объект.ИНН, "9909");
	Если ИностраннаяОрганизация Тогда
		Объект.ОГРН = "";
	Иначе
		Объект.НомерЗаписиОбАккредитации = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура МояПодписьПриИзменении(Элемент)
	УстановитьВидимостьИДоступность(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ГосключУНЭППриИзменении(Элемент)
	УстановитьВидимостьИДоступность(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ГосключУКЭППриИзменении(Элемент)
	УстановитьВидимостьИДоступность(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСписокПользователей()
	Массив = Объект.Пользователи.Выгрузить().ВыгрузитьКолонку("Пользователь");
	СписокПользователей.ЗагрузитьЗначения(Массив);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПользователей(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Пользователи.Очистить();
	Для Каждого Пользователь Из Результат Цикл
		НоваяСтрока = Объект.Пользователи.Добавить();
		НоваяСтрока.Пользователь = Пользователь;
	КонецЦикла;
	
	ОбновитьСписокПользователей();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииНастроек()
	
	ИспользоватьГосключ = ПолучитьФункциональнуюОпцию("ИспользоватьГосключ");
	ИспользоватьСервисМобильнойПодписи = ПолучитьФункциональнуюОпцию("ИспользоватьСервисМобильнойПодписи");
	
	ВидПодписантаФизическоеЛицо = Перечисления.ВидыПодписантовМобильногоСервиса.ФизическоеЛицо;
	Элемент = Элементы.ВидПодписанта.СписокВыбора.НайтиПоЗначению(ВидПодписантаФизическоеЛицо);
	
	Если Не ИспользоватьГосключ Тогда
		Если Объект.ВидПодписанта <> ВидПодписантаФизическоеЛицо И Элемент <> Неопределено Тогда
			Элементы.ВидПодписанта.СписокВыбора.Удалить(Элемент);
		КонецЕсли;
	ИначеЕсли Элемент = Неопределено Тогда
		Элементы.ВидПодписанта.СписокВыбора.Добавить(ВидПодписантаФизическоеЛицо, НСтр("ru = 'Физическое лицо'"));
	КонецЕсли;
	
	УстановитьВидимостьИДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияДобавитьПодписанта()
	
	СтандартныеПодсистемыКлиент.УстановитьХранениеФормы(ЭтотОбъект, Ложь);
	ПараметрыДобавления = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	СервисМобильнойПодписиКлиент.ДобавитьПодписанта(ПараметрыДобавления);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьИДоступность(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Если Не Форма.ТолькоПросмотр И (Форма.ЕстьОтправленныеДокументыВСервис Или Форма.ЕстьОтправленныеДокументыВГосуслуги Или Форма.Объект.ОтправленВСервис) Тогда
		Элементы.ВидПодписанта.ТолькоПросмотр = Истина;
		Элементы.ОГРН.ТолькоПросмотр = Форма.ЕстьОтправленныеДокументыВСервис Или Форма.Объект.ОтправленВСервис;
		Элементы.ИНН.ТолькоПросмотр = Форма.ЕстьОтправленныеДокументыВСервис Или Форма.Объект.ОтправленВСервис;
		Элементы.ИННФЛ.ТолькоПросмотр = Форма.ЕстьОтправленныеДокументыВСервис Или Форма.Объект.ОтправленВСервис;
		Элементы.НомерТелефона.ТолькоПросмотр = Форма.ЕстьОтправленныеДокументыВСервис Или Форма.Объект.ОтправленВСервис;
		Элементы.СНИЛС.ТолькоПросмотр = Форма.ЕстьОтправленныеДокументыВГосуслуги;
	КонецЕсли;
	
	ЭтоЮрЛицо = Форма.Объект.ВидПодписанта = ПредопределенноеЗначение("Перечисление.ВидыПодписантовМобильногоСервиса.РуководительЮЛ");
	ЭтоФизЛицо = Форма.Объект.ВидПодписанта = ПредопределенноеЗначение("Перечисление.ВидыПодписантовМобильногоСервиса.ФизическоеЛицо");
	
	Форма.Элементы.ГруппаГосключ.Видимость = Форма.ИспользоватьГосключ;
	Форма.Элементы.ГосключУКЭП.Видимость = ЭтоФизЛицо Или Форма.ИспользоватьСервисМобильнойПодписи;
	Форма.Элементы.ГосключУНЭП.Видимость = ЭтоФизЛицо;
	Форма.Элементы.МояПодпись.Видимость = Форма.ИспользоватьГосключ И Форма.ИспользоватьСервисМобильнойПодписи И Не ЭтоФизЛицо;
	
	Элементы.ВидПодписанта.Видимость = Не Элементы.ВидПодписанта.ТолькоПросмотр;
	Элементы.ГруппаОрганизация.Видимость = ЭтоЮрЛицо;
	
	ИностраннаяОрганизация = СтрНачинаетсяС(Объект.ИНН, "9909");
	
	Элементы.ИННФЛ.Видимость = Форма.ЕстьПраваНаПросмотр 
		И (Не ЭтоЮрЛицо И Объект.МояПодпись Или ЗначениеЗаполнено(Объект.ИННФЛ) Или ИностраннаяОрганизация);
	Элементы.СНИЛС.Видимость = Форма.ЕстьПраваНаПросмотр И (ЭтоФизЛицо Или Объект.ГосключУКЭП Или ЗначениеЗаполнено(Объект.СНИЛС));
	Элементы.НомерТелефона.Видимость = Форма.ЕстьПраваНаПросмотр И (Объект.МояПодпись Или ЗначениеЗаполнено(Объект.НомерТелефона));
	
	Элементы.НаименованиеОрганизации.Видимость = ЭтоЮрЛицо И (Не ЗначениеЗаполнено(Объект.Организация)
		Или ЗначениеЗаполнено(Объект.НаименованиеОрганизации));
	
	Форма.Элементы.НомерЗаписиОбАккредитации.Видимость = ИностраннаяОрганизация;
	Форма.Элементы.ОГРН.Видимость = Не ИностраннаяОрганизация;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуетсяАвтоНаименование(Объект)
	
	Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Объект.Наименование = Справочники.ПодписантыСервисаМобильнойПодписи.АвтоНаименование(Объект);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПроверитьПараметрыПодписанта(ПараметрыПодписанта)
	СервисМобильнойПодписиСлужебный.ПроверкаПараметровПодписанта(ПараметрыПодписанта);
КонецПроцедуры

&НаСервереБезКонтекста
Функция АвтоНаименование(ПараметрыПодписанта)
	Возврат Справочники.ПодписантыСервисаМобильнойПодписи.АвтоНаименование(ПараметрыПодписанта);
КонецФункции

&НаСервере
Процедура ПроверитьОтправленныеДокументы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	СервисМобильнойПодписиСтатусы.НастройкаОтправкиВГосключ КАК НастройкаОтправкиВГосключ
	|ИЗ
	|	РегистрСведений.СервисМобильнойПодписиСтатусы КАК СервисМобильнойПодписиСтатусы
	|ГДЕ
	|	СервисМобильнойПодписиСтатусы.Подписант = &Подписант
	|	И СервисМобильнойПодписиСтатусы.НастройкаОтправкиВГосключ = ЗНАЧЕНИЕ(Справочник.НастройкиОтправкиВГосключ.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента,
	|	СервисМобильнойПодписиСтатусы.НастройкаОтправкиВГосключ
	|ИЗ
	|	РегистрСведений.СервисМобильнойПодписиСтатусы КАК СервисМобильнойПодписиСтатусы
	|ГДЕ
	|	СервисМобильнойПодписиСтатусы.Подписант = &Подписант
	|	И СервисМобильнойПодписиСтатусы.НастройкаОтправкиВГосключ <> ЗНАЧЕНИЕ(Справочник.НастройкиОтправкиВГосключ.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Подписант", Объект.Ссылка);
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЕстьОтправленныеДокументыВСервис = Не ЗначениеЗаполнено(Выборка.НастройкаОтправкиВГосключ);
		ЕстьОтправленныеДокументыВГосуслуги = ЗначениеЗаполнено(Выборка.НастройкаОтправкиВГосключ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
