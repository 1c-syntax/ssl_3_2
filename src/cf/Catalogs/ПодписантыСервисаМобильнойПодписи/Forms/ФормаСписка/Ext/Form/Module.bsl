///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщиеНастройки = ЭлектроннаяПодпись.ОбщиеНастройки();
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	Элементы.Организация.Видимость = ОбщиеНастройки.ОрганизацияИспользуется;
	Элементы.ФизическоеЛицоОтбор.Видимость = ОбщиеНастройки.ФизическоеЛицоИспользуется;
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		ОрганизацияОтбор = Параметры.Организация;
		Элементы.ОрганизацияОтбор.ТолькоПросмотр = Истина;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Организация", Параметры.Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ФизическоеЛицо) Тогда
		ФизическоеЛицоОтбор = Параметры.ФизическоеЛицо;
		Элементы.ФизическоеЛицоОтбор.ТолькоПросмотр = Истина;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ФизическоеЛицо", Параметры.ФизическоеЛицо);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ВРег(ИмяСобытия) = ВРег("Запись_ПодписантыСервисаМобильнойПодписи") Тогда
		Элементы.Список.Обновить();
		Если ЗначениеЗаполнено(Параметр) Тогда
			Элементы.Список.ТекущаяСтрока = Источник;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	Если Не Копирование Тогда
		
		ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
		ПараметрыПодписанта.Организация = ОрганизацияОтбор;
		СервисМобильнойПодписиКлиент.ДобавитьПодписанта(ПараметрыПодписанта, ЭтотОбъект);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОтборПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Организация", ОрганизацияОтбор,,,ЗначениеЗаполнено(ОрганизацияОтбор));
КонецПроцедуры

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ФизическоеЛицо", ФизическоеЛицоОтбор,,,ЗначениеЗаполнено(ФизическоеЛицоОтбор));
КонецПроцедуры

#КонецОбласти
