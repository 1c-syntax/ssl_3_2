///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	ЭтоАвторизованныйПользователь(Добавил) ИЛИ ЭтоАвторизованныйПользователь(Пользователи.Пользователь)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЭтоАвторизованныйПользователь(Добавил) ИЛИ ЭтоАвторизованныйПользователь(Пользователи.Пользователь)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики обновления.

// Регистрирует на плане обмена ОбновлениеИнформационнойБазы объекты,
// в которых необходимо заполнить реквизиты.
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПодписантыСервисаМобильнойПодписи.Ссылка
	|ИЗ
	|	Справочник.ПодписантыСервисаМобильнойПодписи КАК ПодписантыСервисаМобильнойПодписи
	|ГДЕ
	|	ПодписантыСервисаМобильнойПодписи.ВидПодписанта = &ПустаяСсылка";
	
	Запрос.УстановитьПараметр("ПустаяСсылка", Перечисления.ВидыПодписантовМобильногоСервиса.ПустаяСсылка());
	Результат = Запрос.Выполнить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Результат); 
	
КонецПроцедуры

// Заполняет реквизиты.
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь,
		"Справочник.ПодписантыСервисаМобильнойПодписи");
		
	Пока Выборка.Следующий() Цикл
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.ПодписантыСервисаМобильнойПодписи");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
			
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			
			Объект.МояПодпись = Истина;
			
			Если ЗначениеЗаполнено(Объект.ИНН) Тогда
				Объект.ВидПодписанта = Перечисления.ВидыПодписантовМобильногоСервиса.РуководительЮЛ;
			Иначе
				Объект.ВидПодписанта = Перечисления.ВидыПодписантовМобильногоСервиса.ИндивидуальныйПредприниматель;
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ВызватьИсключение;
		КонецПопытки;
		
	КонецЦикла;
		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, "Справочник.ПодписантыСервисаМобильнойПодписи");
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось обработать некоторых подписантов сервиса мобильной подписи (пропущены): %1'"),
			ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация, Метаданные.Справочники.ПодписантыСервисаМобильнойПодписи,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Обработана очередная порция подписантов сервиса мобильной подписи: %1'"),
			ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

Функция АвтоНаименование(Объект) Экспорт
	
	Наименование = Новый Массив;
	
	ФИО = "";
	Если ЗначениеЗаполнено(Объект.Фамилия) Тогда
		СтруктураФИО = Новый Структура("Фамилия, Имя, Отчество", Объект.Фамилия, Объект.Имя, Объект.Отчество);
		ФИО = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(СтруктураФИО);
	ИначеЕсли ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
		ФИО = Объект.ФизическоеЛицо;
	ИначеЕсли ЗначениеЗаполнено(Объект.ИННФЛ) Тогда
		ФИО = Объект.ИННФЛ;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФИО) Тогда
		Наименование.Добавить(ФИО);
	КонецЕсли;
	
	Организация = Неопределено;
	Если ЗначениеЗаполнено(Объект.ИНН) Или ФИО = Объект.ИННФЛ Тогда
		Если ЗначениеЗаполнено(Объект.НаименованиеОрганизации) Тогда
			Организация = СокрЛП(Объект.НаименованиеОрганизации);
		ИначеЕсли ЗначениеЗаполнено(Объект.Организация) Тогда
			Организация = СокрЛП(Объект.Организация);
		Иначе
			Организация = Объект.ИНН;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Наименование.Добавить(Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Должность) Тогда
		Наименование.Добавить(Объект.Должность);
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.НомерТелефона) Тогда
		Наименование.Добавить("*" + Прав(Объект.НомерТелефона, 4));
	КонецЕсли;
	Наименование = Лев(СтрСоединить(Наименование, ", "), 150);
	Возврат Наименование;
	
КонецФункции

// См. СтандартныеПодсистемыСервер.ПриОпределенииМетодовРазрешенныхДляВызоваКакПроизвольныйКод
Процедура ПриОпределенииМетодовРазрешенныхДляВызоваКакПроизвольныйКод(Методы) Экспорт
	
	Методы.Вставить("ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию");
	Методы.Вставить("ОбработатьДанныеДляПереходаНаНовуюВерсию");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
