///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Поле = Элементы.ИспользоватьРегламентноеЗадание Тогда
		
		СтандартнаяОбработка = Ложь;
		
		КоллекцияСценариев = Новый Массив(1);
		КоллекцияСценариев[0] = ТекущиеДанные.Ссылка;
		
		ВключитьОтключитьРегламентноеЗаданиеПакетноНаСервере(КоллекцияСценариев, Неопределено);
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьОтключитьРегламентноеЗадание(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КоллекцияСценариев = Новый Массив;
	КоллекцияСценариев.Добавить(ТекущиеДанные.Ссылка);
	
	ИспользоватьРегламентноеЗадание = Не ТекущиеДанные.ИспользоватьРегламентноеЗадание;
	
	ВключитьОтключитьРегламентноеЗаданиеПакетноНаСервере(КоллекцияСценариев, ИспользоватьРегламентноеЗадание);
	
	// Обновляем данные списка.
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьРасписание(Команда)
	
	ИзменитьИспользованиеРегламентныхЗаданийВыделенныхСценариев(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьРасписание(Команда)
	
	ИзменитьИспользованиеРегламентныхЗаданийВыделенныхСценариев(Ложь)
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьИспользованиеРегламентныхЗаданийВыделенныхСценариев(Использование)
	
	ВыделенныеСтроки = Элементы.Список.ПолучитьСтрокиДляОбработки();
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КоллекцияСценариев = Новый Массив;
	Для Каждого Сценарий Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(Сценарий);
		Если ДанныеСтроки.ПометкаУдаления Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		КоллекцияСценариев.Добавить(Сценарий);
		
	КонецЦикла;
	
	ВключитьОтключитьРегламентноеЗаданиеПакетноНаСервере(КоллекцияСценариев, Неопределено);
	
	// Обновляем данные списка.
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВключитьОтключитьРегламентноеЗаданиеПакетноНаСервере(КоллекцияСценариев, ИспользоватьРегламентноеЗадание)
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		Для Каждого Сценарий Из КоллекцияСценариев Цикл
			
			ЭлементБлокировки = Блокировка.Добавить("Справочник.СценарииОбменовДанными");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Сценарий);
			
		КонецЦикла;
		Блокировка.Заблокировать();
		
		Для Каждого Сценарий Из КоллекцияСценариев Цикл
			
			ЗаблокироватьДанныеДляРедактирования(Сценарий);
			
			Если ТипЗнч(ИспользоватьРегламентноеЗадание) <> Тип("Булево") Тогда
				
				ИспользоватьРегламентноеЗадание = НЕ Сценарий.ИспользоватьРегламентноеЗадание;
				
			КонецЕсли;
			
			СценарийОбъект = Сценарий.ПолучитьОбъект(); // СправочникОбъект.СценарииОбменовДанными
			СценарийОбъект.ИспользоватьРегламентноеЗадание = ИспользоватьРегламентноеЗадание;
			СценарийОбъект.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
