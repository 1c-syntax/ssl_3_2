///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбменДаннымиСервер.АктуализироватьРегламентныеЗадания();
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "УзелИнформационнойБазы", Параметры.УзелИнформационнойБазы, Истина);
	
	Заголовок = НСтр("ru = 'Сценарии синхронизации данных для: [УзелИнформационнойБазы]'");
	Заголовок = СтрЗаменить(Заголовок, "[УзелИнформационнойБазы]", Строка(Параметры.УзелИнформационнойБазы));
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		Элементы.ВключитьОтключитьРегламентноеЗадание.Видимость = Ложь;
		
	КонецЕсли;
	
	ТекущийВариантИнтерфейса = КлиентскоеПриложение.ТекущийВариантИнтерфейса();

	Если ТекущийВариантИнтерфейса = ВариантИнтерфейсаКлиентскогоПриложения.Такси Тогда
		
		Элементы.СписокКонтекстноеМенюВыполнитьСценарийРежимОтладки.Видимость = Истина;
		
	Иначе
		
		Элементы.СписокКонтекстноеМенюВыполнитьСценарийРежимОтладки.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СценарииОбменовДанными" Тогда
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекущиеСтроки = Новый Массив;
	ТекущиеСтроки.Добавить(ТекущиеДанные.Ссылка);
	
	Если Поле = Элементы.ФлагИспользованияЗагрузки Тогда
		
		ФлагИспользования = НЕ ТекущиеДанные.ФлагИспользованияЗагрузки;
		ВключитьОтключитьЗагрузкуНаСервере(ТекущиеСтроки, Параметры.УзелИнформационнойБазы, ФлагИспользования);
		
	ИначеЕсли Поле = Элементы.ФлагИспользованияВыгрузки Тогда
		
		ФлагИспользования = НЕ ТекущиеДанные.ФлагИспользованияВыгрузки;
		ВключитьОтключитьВыгрузкуНаСервере(ТекущиеСтроки, Параметры.УзелИнформационнойБазы, ФлагИспользования);
		
	ИначеЕсли Поле = Элементы.ИспользоватьРегламентноеЗадание Тогда
		
		ВключитьОтключитьРегламентноеЗаданиеНаСервере(ТекущиеДанные.Ссылка);
		
	Иначе
		
		ИзменитьСценарийОбменаДанными(ТекущиеДанные.Ссылка);
		
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьСценарийВФоне(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	РезультатЗапуска = Новый Структура;
	РезультатЗапуска.Вставить("ЗапускВыполнен", Ложь);
	РезультатЗапуска.Вставить("МоментЗапуска", Неопределено);
	РезультатЗапуска.Вставить("ИдентификаторФоновогоЗадания", Неопределено);
	РезультатЗапуска.Вставить("СообщениеОбОшибке", "");
	
	ВыполнитьСценарийВФонеНаСервере(ТекущиеДанные.Ссылка, РезультатЗапуска);
	
	Если НЕ ПустаяСтрока(РезультатЗапуска.СообщениеОбОшибке) Тогда
		
		ПоказатьПредупреждение(Неопределено, РезультатЗапуска.СообщениеОбОшибке, 20);
		
	ИначеЕсли РезультатЗапуска.ЗапускВыполнен = Истина Тогда
		
		ТекстСостояния = НСтр("ru ='Выполнен запуск сценария обмена.'");
		Состояние(ТекстСостояния);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьСценарийРежимОтладки(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Отказ = Ложь;
	
	// Запускаем выполнение обмена.
	ОбменДаннымиВызовСервера.ВыполнитьОбменДаннымиПоСценариюОбменаДанными(Отказ, ТекущиеДанные.Ссылка);
	
	Если Отказ Тогда
		Сообщение = НСтр("ru = 'Сценарий синхронизации выполнен с ошибками.'");
		Картинка = БиблиотекаКартинок.Ошибка32;
	Иначе
		Сообщение = НСтр("ru = 'Сценарий синхронизации успешно выполнен.'");
		Картинка = Неопределено;
	КонецЕсли;
	ПоказатьОповещениеПользователя(Сообщение,,,Картинка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРегламентноеЗадание(Команда)
	
	ВключитьОтключитьРегламентноеЗаданиеНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура Создать(Команда)
	
	// Добавлена отдельная команда, чтобы пробросить узел плана обмена в форму создания нового сценария
	
	ПараметрыФормы = Новый Структура("УзелИнформационнойБазы", Параметры.УзелИнформационнойБазы);
	
	ОткрытьФорму("Справочник.СценарииОбменовДанными.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВключитьОтключитьРегламентноеЗаданиеНаКлиенте()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;

	ВключитьОтключитьРегламентноеЗаданиеНаСервере(ТекущиеДанные.Ссылка);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВключитьОтключитьЗагрузкуНаСервере(Знач ТекущиеСтроки, Знач УзелИБ, Знач ФлагИспользования)
	
	Если ТипЗнч(ТекущиеСтроки) <> Тип("Массив") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Для каждого СценарийОбменаДанными Из ТекущиеСтроки Цикл
		
		Если ФлагИспользования Тогда
			
			Справочники.СценарииОбменовДанными.ДобавитьЗагрузкуВСценарииОбменаДанными(СценарийОбменаДанными, УзелИБ);
			
		Иначе
			
			Справочники.СценарииОбменовДанными.УдалитьЗагрузкуВСценарииОбменаДанными(СценарийОбменаДанными, УзелИБ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВключитьОтключитьВыгрузкуНаСервере(Знач ТекущиеСтроки, Знач УзелИБ, Знач ФлагИспользования)
	
	Если ТипЗнч(ТекущиеСтроки) <> Тип("Массив") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Для каждого СценарийОбменаДанными Из ТекущиеСтроки Цикл
		
		Если ФлагИспользования Тогда
			
			Справочники.СценарииОбменовДанными.ДобавитьВыгрузкуВСценарииОбменаДанными(СценарийОбменаДанными, УзелИБ);
			
		Иначе
			
			Справочники.СценарииОбменовДанными.УдалитьВыгрузкуВСценарииОбменаДанными(СценарийОбменаДанными, УзелИБ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//   СценарийОбменаДанными - СправочникСсылка.СценарииОбменовДанными - сценарий обмена.
//
&НаСервереБезКонтекста
Процедура ВключитьОтключитьРегламентноеЗаданиеНаСервере(Знач СценарийОбменаДанными)
	
	Если НЕ ЗначениеЗаполнено(СценарийОбменаДанными) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.СценарииОбменовДанными");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", СценарийОбменаДанными);
		Блокировка.Заблокировать();
		
		ЗаблокироватьДанныеДляРедактирования(СценарийОбменаДанными);
		
		СценарийОбъект = СценарийОбменаДанными.ПолучитьОбъект();
		СценарийОбъект.ИспользоватьРегламентноеЗадание = НЕ СценарийОбъект.ИспользоватьРегламентноеЗадание;
		
		Отказ = Ложь;
		Расписание = Справочники.СценарииОбменовДанными.ПолучитьРасписаниеВыполненияОбменаДанными(СценарийОбменаДанными);
		Справочники.СценарииОбменовДанными.ОбновитьДанныеРегламентногоЗадания(Отказ, Расписание, СценарийОбъект);
		СценарийОбъект.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСценарийОбменаДанными(Сценарий)
	
	Если Сценарий = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Сценарий);
	
	ОткрытьФорму("Справочник.СценарииОбменовДанными.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Процедура ЗапуститьСценарийСинхронизации(Знач СценарийСсылка, РезультатЗапуска)
	
	РегламентноеЗаданиеGUID = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СценарийСсылка, "РегламентноеЗаданиеGUID");
	
	Если НЕ ЗначениеЗаполнено(РегламентноеЗаданиеGUID) Тогда
		
		РезультатЗапуска.СообщениеОбОшибке = НСтр("ru = 'У сценария не заполнен идентификатор. Запуск сценария в ручном режиме не возможен.'");
		Возврат;
		
	КонецЕсли;
	
	ЗаданиеGUID = Новый УникальныйИдентификатор(РегламентноеЗаданиеGUID);
	РегЗаданиеСценария = РегламентныеЗаданияСервер.ПолучитьРегламентноеЗадание(ЗаданиеGUID);
	
	СвойстваПоследнегоЗадания = РегламентныеЗаданияСервер.СвойстваПоследнегоЗадания(РегЗаданиеСценария);
	ЗаданиеВыполняется = (СвойстваПоследнегоЗадания <> Неопределено)
		И (СвойстваПоследнегоЗадания.Состояние = СостояниеФоновогоЗадания.Активно);
		
	Если ЗаданиеВыполняется Тогда
		
		РезультатЗапуска.СообщениеОбОшибке = НСтр("ru = 'Задание уже выполняется. Запуск в текущий момент не возможен.'");
		Возврат;
		
	КонецЕсли;
	
	НаименованиеФоновогоЗадания = СтрШаблон(НСтр("ru = 'Запуск вручную: %1'"), РегЗаданиеСценария.Наименование);
	
	// Не используются длительные операции, т.к. делается вызов метода регламентного задания.
	ФоновоеЗадание = ФоновыеЗадания.Выполнить(РегЗаданиеСценария.Метаданные.ИмяМетода, РегЗаданиеСценария.Параметры, Строка(РегЗаданиеСценария.УникальныйИдентификатор), НаименованиеФоновогоЗадания);
	РезультатЗапуска.ИдентификаторФоновогоЗадания = Строка(ФоновоеЗадание.УникальныйИдентификатор);
	РезультатЗапуска.МоментЗапуска = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ФоновоеЗадание.УникальныйИдентификатор).Начало;
	РезультатЗапуска.ЗапускВыполнен = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьСценарийВФонеНаСервере(Знач СценарийСсылка, РезультатЗапуска)
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		ТекстСообщения = НСтр("ru ='Ручной запуск сценария в сервисе не поддерживается. Синхронизация будет выполнена согласно указанного расписания.'");
		РезультатЗапуска.СообщениеОбОшибке = ТекстСообщения;
		
	Иначе
		
		ЗапуститьСценарийСинхронизации(СценарийСсылка, РезультатЗапуска);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти