///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Имя = Параметры.Имя;
	Отчество = Параметры.Отчество;
	ИмяИОтчество = Параметры.Имя + ?(ЗначениеЗаполнено(Параметры.Отчество)," " + Параметры.Отчество, "");
	НовоеИмяИОтчество = ИмяИОтчество;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяПриИзменении(Элемент)
	НовоеИмяИОтчество();
КонецПроцедуры

&НаКлиенте
Процедура ОтчествоПриИзменении(Элемент)
	НовоеИмяИОтчество();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если НовоеИмяИОтчество = ИмяИОтчество Тогда
		Закрыть(Новый Структура("Имя, Отчество", СокрП(Имя), СокрП(Отчество)));
	Иначе
		ПоказатьПредупреждение( , СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Имя и отчество должны быть равны %1'"), ИмяИОтчество));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НовоеИмяИОтчество()
	
	НовоеИмяИОтчество = СокрП(Имя) + ?(ПустаяСтрока(Отчество), "", " " + СокрП(Отчество));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НовоеИмяИОтчество.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Элементы.НовоеИмяИОтчество.Имя);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = ИмяИОтчество;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаПалитры.Красный);

КонецПроцедуры

#КонецОбласти
