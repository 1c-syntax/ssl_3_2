///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = Истина;
	
	Элементы.РасшифровкаСоставаПолей.Заголовок =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Расшифровка количества первых полей,
			           |задействованных в шапке и табличных частях ключа:
			           |%1'"),
			РасшифровкаСоставаПолей(Объект.СоставПолей));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ФормаВключитьВозможностьРедактирования(Команда)
	
	ТолькоПросмотр = Ложь;
	
	ПоказатьПредупреждение(,
		НСтр("ru = 'Ключ доступа не следует изменять, так как он сопоставлен с разными объектами.
		           |Чтобы исправить нестандартную проблему следует удалить ключ доступа или связь с ним в регистрах и выполнить процедуру обновления доступа.'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция РасшифровкаСоставаПолей(СоставПолей, ЭтоПодсказка = Ложь)
	
	ТекущееЧисло = СоставПолей;
	Расшифровка = Новый Массив;
	РазмерностьКлючаДоступа = УправлениеДоступомСлужебныйПовтИсп.РазмерностьКлючаДоступа();
	КоличествоТабличныхЧастей = РазмерностьКлючаДоступа.КоличествоТабличныхЧастей;
	
	НомерТабличнойЧасти = 0;
	Пока НомерТабличнойЧасти <= КоличествоТабличныхЧастей Цикл
		Остаток = ТекущееЧисло - Цел(ТекущееЧисло / 16) * 16;
		Если НомерТабличнойЧасти = 0 Тогда
			Расшифровка.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Шапка: %1'"), Формат(Остаток, "ЧН=0; ЧГ=")));
		Иначе
			Расшифровка.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Табличная часть %1: %2'"), Формат(НомерТабличнойЧасти, "ЧГ="), Формат(Остаток, "ЧН=0; ЧГ=")));
		КонецЕсли;
		ТекущееЧисло = Цел(ТекущееЧисло / 16);
		НомерТабличнойЧасти = НомерТабличнойЧасти + 1;
	КонецЦикла;
	
	Возврат "- " + СтрСоединить(Расшифровка, Символы.ПС + "- ");
	
КонецФункции

#КонецОбласти
