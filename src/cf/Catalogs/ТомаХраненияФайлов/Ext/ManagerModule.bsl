///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив из Строка
//
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	РедактируемыеРеквизиты = Новый Массив;
	РедактируемыеРеквизиты.Добавить("Комментарий");
	
	Возврат РедактируемыеРеквизиты;
	
КонецФункции

// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// СтандартныеПодсистемы.ВариантыОтчетов

// Определяет список команд отчетов.
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Отчеты.ПроверкаЦелостностиТома) Тогда
		Возврат;
	КонецЕсли;
	
	Команда = КомандыОтчетов.Добавить();
	Команда.КлючВарианта       = "Основной";
	Команда.Представление      = НСтр("ru = 'Проверка целостности тома'");
	Команда.Идентификатор      = "ПроверкаЦелостностиТома";
	Команда.Менеджер           = "Отчет.ПроверкаЦелостностиТома";
	Команда.МножественныйВыбор = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Только для внутреннего использования.
// 
// Параметры:
//   Запросы - Массив
//   ВидТомаХраненияФайлов - ПеречислениеСсылка.ВидыХраненияФайлов, Неопределено - значение по умолчанию - Неопределено.
//
Процедура ДобавитьЗапросыНаИспользованиеВнешнихРесурсовВсехТомов(Запросы, Знач ВидТомаХраненияФайлов = Неопределено) Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидТомаХраненияФайлов = Неопределено Тогда
		ВидТомаХраненияФайлов = Перечисления.ВидыХраненияФайлов.ОперационноеХранение;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТомаХраненияФайлов.Ссылка КАК Ссылка,
	|	ТомаХраненияФайлов.ПолныйПутьLinux КАК ПолныйПутьLinux,
	|	ТомаХраненияФайлов.ПолныйПутьWindows КАК ПолныйПутьWindows,
	|	ТомаХраненияФайлов.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Справочник.ТомаХраненияФайлов КАК ТомаХраненияФайлов
	|ГДЕ
	|	ТомаХраненияФайлов.ПометкаУдаления = ЛОЖЬ
	|	И ТомаХраненияФайлов.ВидТомаХраненияФайлов = &ВидТомаХраненияФайлов
	|	И ТомаХраненияФайлов.СпособХраненияФайлов = ЗНАЧЕНИЕ(Перечисление.СпособыХраненияФайлов.ВСетевыхКаталогах)";

	Запрос.УстановитьПараметр("ВидТомаХраненияФайлов", ВидТомаХраненияФайлов);

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		Запросы.Добавить(ЗапросНаИспользованиеВнешнихРесурсовДляТома(
			Выборка.Ссылка, Выборка.ПолныйПутьWindows, Выборка.ПолныйПутьLinux));
	КонецЦикла;

КонецПроцедуры

// Только для внутреннего использования.
//
// Параметры:
//   Запросы - Массив
//   ВидТомаХраненияФайлов - ПеречислениеСсылка.ВидыХраненияФайлов, Неопределено - значение по умолчанию - Неопределено.
//
Процедура ДобавитьЗапросыНаОтменуИспользованияВнешнихРесурсовВсехТомов(Запросы, Знач ВидТомаХраненияФайлов = Неопределено) Экспорт

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПрофилиБезопасности") Тогда
		МодульРаботаВБезопасномРежиме = ОбщегоНазначения.ОбщийМодуль("РаботаВБезопасномРежиме");

		Если ВидТомаХраненияФайлов = Неопределено Тогда
			ВидТомаХраненияФайлов = Перечисления.ВидыХраненияФайлов.ОперационноеХранение;
		КонецЕсли;

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТомаХраненияФайлов.Ссылка КАК Ссылка,
		|	ТомаХраненияФайлов.ПолныйПутьLinux КАК ПолныйПутьLinux,
		|	ТомаХраненияФайлов.ПолныйПутьWindows КАК ПолныйПутьWindows,
		|	ТомаХраненияФайлов.ПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	Справочник.ТомаХраненияФайлов КАК ТомаХраненияФайлов
		|ГДЕ
		|	ТомаХраненияФайлов.ВидТомаХраненияФайлов = &ВидТомаХраненияФайлов
		|	И ТомаХраненияФайлов.СпособХраненияФайлов = ЗНАЧЕНИЕ(Перечисление.СпособыХраненияФайлов.ВСетевыхКаталогах)";

		Запрос.УстановитьПараметр("ВидТомаХраненияФайлов", ВидТомаХраненияФайлов);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Запросы.Добавить(МодульРаботаВБезопасномРежиме.ЗапросНаОчисткуРазрешенийИспользованияВнешнихРесурсов(
				Выборка.Ссылка));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования.
Функция ЗапросНаИспользованиеВнешнихРесурсовДляТома(Том, ПолныйПутьWindows, ПолныйПутьLinux) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПрофилиБезопасности") Тогда
		МодульРаботаВБезопасномРежиме = ОбщегоНазначения.ОбщийМодуль("РаботаВБезопасномРежиме");
	
		Разрешения = Новый Массив;
		
		Если ЗначениеЗаполнено(ПолныйПутьWindows) Тогда
			Разрешения.Добавить(МодульРаботаВБезопасномРежиме.РазрешениеНаИспользованиеКаталогаФайловойСистемы(
				ПолныйПутьWindows, Истина, Истина));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПолныйПутьLinux) Тогда
			Разрешения.Добавить(МодульРаботаВБезопасномРежиме.РазрешениеНаИспользованиеКаталогаФайловойСистемы(
				ПолныйПутьLinux, Истина, Истина));
		КонецЕсли;
		
		Возврат МодульРаботаВБезопасномРежиме.ЗапросНаИспользованиеВнешнихРесурсов(Разрешения, Том);
	КонецЕсли;
	
КонецФункции

// Проверяет, что том хранения использует способ хранения - "ВСетевыхКаталогах".
//
// Параметры:
//  ТомХраненияСсылка - СправочникСсылка.ТомаХраненияФайлов
//
// Возвращаемое значение:
//  Булево
//
Функция ЭтоТомХраненияНаДисках(ТомХраненияСсылка) Экспорт

	СпособХраненияФайлов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТомХраненияСсылка, "СпособХраненияФайлов");

	Возврат СпособХраненияФайлов = Перечисления.СпособыХраненияФайлов.ВСетевыхКаталогах;

КонецФункции

#Область ОбработчикиОбновления

// Обработчик обновления на новую версию:
// - заполняет реквизиты "ВидТомаХраненияФайлов" и "СпособХраненияФайлов" в справочнике "Тома хранения файлов".
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТомаХраненияФайлов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ТомаХраненияФайлов КАК ТомаХраненияФайлов
	|ГДЕ
	|	ТомаХраненияФайлов.СпособХраненияФайлов = ЗНАЧЕНИЕ(Перечисление.СпособыХраненияФайлов.ПустаяСсылка)";

	ТомаХраненияФайлов = Запрос.Выполнить().Выбрать();	
	
	Пока ТомаХраненияФайлов.Следующий() Цикл

		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.ТомаХраненияФайлов");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ТомаХраненияФайлов.Ссылка);

		ПредставлениеСсылки = Строка(ТомаХраненияФайлов.Ссылка);

		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
			СправочникОбъект = ТомаХраненияФайлов.Ссылка.ПолучитьОбъект();

			Если СправочникОбъект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;

			СправочникОбъект.ВидТомаХраненияФайлов	= Перечисления.ВидыХраненияФайлов.ОперационноеХранение;
			СправочникОбъект.СпособХраненияФайлов	= Перечисления.СпособыХраненияФайлов.ВСетевыхКаталогах;

			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
			ЗафиксироватьТранзакцию();

		Исключение
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазы.ЗаписатьОшибкуВЖурналРегистрации(ТомаХраненияФайлов.Ссылка,
				ПредставлениеСсылки, ИнформацияОбОшибке());

			ВызватьИсключение;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// См. СтандартныеПодсистемыСервер.ПриОпределенииМетодовРазрешенныхДляВызоваКакПроизвольныйКод
Процедура ПриОпределенииМетодовРазрешенныхДляВызоваКакПроизвольныйКод(Методы) Экспорт
	
	Методы.Вставить("ОбработатьДанныеДляПереходаНаНовуюВерсию");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
