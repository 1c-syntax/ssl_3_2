///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если Не ДанныеЗаполнения.Свойство("ВидТомаХраненияФайлов") Тогда
			ВидТомаХраненияФайлов = Перечисления.ВидыХраненияФайлов.ОперационноеХранение;
		КонецЕсли;
	КонецЕсли;

	СпособХраненияФайлов = Перечисления.СпособыХраненияФайлов.ВСетевыхКаталогах;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПрофилиБезопасности") Тогда
		
		МодульРаботаВБезопасномРежиме   = ОбщегоНазначения.ОбщийМодуль("РаботаВБезопасномРежиме");
		ИспользуютсяПрофилиБезопасности = МодульРаботаВБезопасномРежиме.ИспользуютсяПрофилиБезопасности();
		
	Иначе
		ИспользуютсяПрофилиБезопасности = Ложь;
	КонецЕсли;

	Если Не ДополнительныеСвойства.Свойство("ПропуститьОсновнуюПроверкуЗаполнения") Тогда

		Если Не ЗначениеЗаполнено(ВидТомаХраненияФайлов) Тогда
			ТекстОшибки = НСтр("ru = 'Не заполнен вид тома хранения файлов'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "ВидТомаХраненияФайлов", "Объект", Отказ);
			Возврат
		КонецЕсли;

		Если Не ЗначениеЗаполнено(СпособХраненияФайлов) Тогда
			ТекстОшибки = НСтр("ru = 'Не заполнен способ хранения файлов'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "СпособХраненияФайлов", "Объект", Отказ);
			Возврат;
		КонецЕсли;

		Если СпособХраненияФайлов = Перечисления.СпособыХраненияФайлов.ВоВнешнемХранилищеПоПротоколуS3
				И ПустаяСтрока(ИмяХранилищаДвоичныхДанных) Тогда
			ТекстОшибки = НСтр("ru = 'Не заполнено имя хранилища двоичных данных'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "ИмяХранилищаДвоичныхДанных", "Объект", Отказ);
			Возврат;
		КонецЕсли;

		Если Не НомерПорядкаУникален(ПорядокЗаполнения, Ссылка, ВидТомаХраненияФайлов) Тогда
			ТекстОшибки = НСтр("ru = 'Порядок заполнения не уникален - в системе уже есть том с таким порядком'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "ПорядокЗаполнения", "Объект", Отказ);
		КонецЕсли;

		Если Не ПроверитьУникальностьТомаХраненияРазмещенногоВХранилищеДвоичныхДанных(Ссылка, ВидТомаХраненияФайлов, СпособХраненияФайлов) Тогда
			
			// АПК:1297-выкл Локализация строки выполняется в коде ниже.
			ТекстОшибки = ТекстОшибкиНеуникальностиТомаХранилищаДвоичныхДанных();
			// АПК:1297-вкл			
					
			ОбщегоНазначения.СообщитьПользователю(НСтр(ТекстОшибки), , "СпособХраненияФайлов", "Объект", Отказ);
		КонецЕсли;		
		
		Если МаксимальныйРазмер <> 0 Тогда
			ТекущийРазмерВБайтах = 0;
			Если Не Ссылка.Пустая() Тогда
				ТекущийРазмерВБайтах = РаботаСФайламиВТомахСлужебный.ОбъемТома(Ссылка);
			КонецЕсли;
			ТекущийРазмер = ТекущийРазмерВБайтах / (1024 * 1024);
			
			Если МаксимальныйРазмер < ТекущийРазмер Тогда
				ТекстОшибки = НСтр("ru = 'Максимальный размер тома меньше, чем текущий размер'");
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "МаксимальныйРазмер", "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;

		Если СпособХраненияФайлов = Перечисления.СпособыХраненияФайлов.ВСетевыхКаталогах Тогда
			Если ПустаяСтрока(ПолныйПутьWindows) И ПустаяСтрока(ПолныйПутьLinux) Тогда
				ТекстОшибки = НСтр("ru = 'Не заполнен полный путь'");
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "ПолныйПутьWindows", "Объект", Отказ);
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "ПолныйПутьLinux",   "Объект", Отказ);
				Возврат;
			КонецЕсли;

			ПутиКТомам = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ПолныйПутьLinux, ПолныйПутьWindows");
			ПроверятьУникальностьПутей = ПутиКТомам.ПолныйПутьLinux <> ПолныйПутьLinux 
											ИЛИ ПутиКТомам.ПолныйПутьWindows <> ПолныйПутьWindows;
			Если ПроверятьУникальностьПутей Тогда
				ПроверитьУникальностьПутиКТомам(Отказ); 
			КонецЕсли;
			
			Если Не ИспользуютсяПрофилиБезопасности
			   И Не ПустаяСтрока(ПолныйПутьWindows)
			   И (    Лев(ПолныйПутьWindows, 2) <> "\\"
			      ИЛИ СтрНайти(ПолныйПутьWindows, ":") <> 0 ) Тогда
				
				ТекстОшибки = НСтр("ru = 'Путь к тому должен быть в формате UNC (\\servername\resource).'");
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "ПолныйПутьWindows", "Объект", Отказ);
				Возврат;
			КонецЕсли;
		КонецЕсли
	КонецЕсли;

	Если Не ДополнительныеСвойства.Свойство("ПропуститьПроверкуДоступаКПапке") Тогда
		Если СпособХраненияФайлов = Перечисления.СпособыХраненияФайлов.ВСетевыхКаталогах Тогда		
			ИмяПоляСПолнымПутем = ?(ОбщегоНазначения.ЭтоWindowsСервер(), "ПолныйПутьWindows", "ПолныйПутьLinux");
			Если ОбщегоНазначения.РазделениеВключено() Тогда
				МодульРаботаВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервиса");
				ЗначениеРазделителя = ?(МодульРаботаВМоделиСервиса.ИспользованиеРазделителяСеанса(),
					МодульРаботаВМоделиСервиса.ЗначениеРазделителяСеанса(), "");
			Иначе
				ЗначениеРазделителя = "";
			КонецЕсли;

			ПолныйПутьТома = СтрЗаменить(ЭтотОбъект[ИмяПоляСПолнымПутем], "%z", ЗначениеРазделителя);
			ИмяКаталогаТестовое = ПолныйПутьТома + "ПроверкаДоступа" + ПолучитьРазделительПути();

			Попытка
				СоздатьКаталог(ИмяКаталогаТестовое);
				УдалитьФайлы(ИмяКаталогаТестовое);
			Исключение
				ИнформацияОбОшибке = ИнформацияОбОшибке();

				Если ИспользуютсяПрофилиБезопасности Тогда
					ШаблонОшибки =
						НСтр("ru = 'Некорректный путь к тому или сервер с томом в данный момент недоступен.
						           |Проверьте корректность пути к общей папке и доступность сервера.
						           |Возможно не настроены разрешения в профилях безопасности,
						           |или учетная запись, от лица которой работает
						           |сервер 1С:Предприятия, не имеет прав доступа к каталогу тома.
						           |
						           |%1'");
				Иначе
					ШаблонОшибки =
						НСтр("ru = 'Некорректный путь к тому или сервер с томом в данный момент недоступен.
						           |Проверьте корректность пути к общей папке и доступность сервера.
						           |Возможно учетная запись, от лица которой работает
						           |сервер 1С:Предприятия, не имеет прав доступа к каталогу тома.
						           |
						           |%1'");
				КонецЕсли;

				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонОшибки, ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
				
				ОбщегоНазначения.СообщитьПользователю(
					ТекстОшибки, , ИмяПоляСПолнымПутем, "Объект", Отказ);
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСХранилищамиДвоичныхДанныхСервер.ИзменитьСоставХранимыхДанных(ВидТомаХраненияФайлов, СпособХраненияФайлов, ИмяХранилищаДвоичныхДанных)

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПометкаУдаления Тогда
		Если Не ПроверитьУникальностьТомаХраненияРазмещенногоВХранилищеДвоичныхДанных(Ссылка, ВидТомаХраненияФайлов, СпособХраненияФайлов) Тогда

			// АПК:1297-выкл Локализация строки выполняется в коде ниже.
			ТекстОшибки = ТекстОшибкиНеуникальностиТомаХранилищаДвоичныхДанных(Истина);
			// АПК:1297-вкл

			ВызватьИсключение НСтр(ТекстОшибки);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает Ложь, если есть том с таким порядком.
Функция НомерПорядкаУникален(ПорядокЗаполнения, СсылкаНаТом, ВидТомаХраненияФайлов)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(Тома.ПорядокЗаполнения) КАК Количество
	|ИЗ
	|	Справочник.ТомаХраненияФайлов КАК Тома
	|ГДЕ
	|	Тома.ПорядокЗаполнения = &ПорядокЗаполнения
	|	И Тома.ВидТомаХраненияФайлов = &ВидТомаХраненияФайлов
	|	И Тома.Ссылка <> &СсылкаНаТом";

	Запрос.Параметры.Вставить("ПорядокЗаполнения"		, ПорядокЗаполнения);
	Запрос.Параметры.Вставить("СсылкаНаТом"				, СсылкаНаТом);
	Запрос.Параметры.Вставить("ВидТомаХраненияФайлов"	, ВидТомаХраненияФайлов);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Количество = 0;
	КонецЕсли;

	Возврат Истина;

КонецФункции

Процедура ПроверитьУникальностьПутиКТомам(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПроверяемыйТом", Ссылка);

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТомаХраненияФайлов.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(ТомаХраненияФайлов.Ссылка) КАК Представление,
		|	ТомаХраненияФайлов.ПолныйПутьWindows КАК ПолныйПутьWindows,
		|	ТомаХраненияФайлов.ПолныйПутьLinux КАК ПолныйПутьLinux
		|ИЗ
		|	Справочник.ТомаХраненияФайлов КАК ТомаХраненияФайлов
		|ГДЕ
		|	НЕ ТомаХраненияФайлов.ПометкаУдаления
		|	И ТомаХраненияФайлов.СпособХраненияФайлов = ЗНАЧЕНИЕ(Перечисление.СпособыХраненияФайлов.ВСетевыхКаталогах)
		|	И ТомаХраненияФайлов.Ссылка <> &ПроверяемыйТом";
		
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаРезультата = РезультатЗапроса.Выгрузить();
	ТаблицаРезультата.Индексы.Добавить("ПолныйПутьLinux");
	ТаблицаРезультата.Индексы.Добавить("ПолныйПутьWindows");
	
	ШаблонОшибки = НСтр("ru = 'Путь к тому должен быть уникален. Каталог указан в томе %1.'");
	Если ЗначениеЗаполнено(ПолныйПутьLinux) Тогда
		Для каждого Том Из ТаблицаРезультата.НайтиСтроки(Новый Структура("ПолныйПутьLinux", ПолныйПутьLinux)) Цикл
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, Том.Представление);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "ПолныйПутьLinux", "Объект", Отказ);
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПолныйПутьWindows) Тогда
		Для каждого Том Из ТаблицаРезультата.НайтиСтроки(Новый Структура("ПолныйПутьWindows", ПолныйПутьWindows)) Цикл
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, Том.Представление);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "ПолныйПутьWindows", "Объект", Отказ);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Функция ПроверитьУникальностьТомаХраненияРазмещенногоВХранилищеДвоичныхДанных(СсылкаНаТом, ВидТомаХраненияФайлов, СпособХраненияФайлов)

	Если СпособХраненияФайлов = Перечисления.СпособыХраненияФайлов.ВСетевыхКаталогах Тогда
		Возврат Истина;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(Тома.СпособХраненияФайлов) КАК Количество
	|ИЗ
	|	Справочник.ТомаХраненияФайлов КАК Тома
	|ГДЕ
	|	Тома.ВидТомаХраненияФайлов = &ВидТомаХраненияФайлов
	|	И Тома.СпособХраненияФайлов = &СпособХраненияФайлов
	|	И Тома.Ссылка <> &СсылкаНаТом
	|	И НЕ Тома.ПометкаУдаления";

	Запрос.Параметры.Вставить("ВидТомаХраненияФайлов"	, ВидТомаХраненияФайлов);
	Запрос.Параметры.Вставить("СпособХраненияФайлов"	, СпособХраненияФайлов);
	Запрос.Параметры.Вставить("СсылкаНаТом"				, СсылкаНаТом);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Количество = 0;
	КонецЕсли;

КонецФункции

Функция ПредставлениеСпособаХРаненияФайловДляСообщения(СпособХраненияФайлов)

	// АПК:1297-выкл Результат функции локализуется в процедурах ОбработкаПроверкиЗаполнения и ПриЗаписи.
	Если СпособХраненияФайлов = Перечисления.СпособыХраненияФайлов.ВоВнешнемХранилищеПоПротоколуS3 Тогда
		Результат = "во внешнем хранилище двоичных данных (по протоколу S3)";
	ИначеЕсли СпособХраненияФайлов = Перечисления.СпособыХраненияФайлов.ВоВстроенномХранилище Тогда
        Результат = "во встроенном хранилище двоичных данных";
	Иначе
		Результат = Строка(СпособХраненияФайлов);
	КонецЕсли;
	// АПК:1297-вкл

	Возврат Результат;

КонецФункции

Функция ТекстОшибкиНеуникальностиТомаХранилищаДвоичныхДанных(ПроверкаПриЗаписи = Ложь)
	
	// АПК:1297-выкл Локализация строки выполняется в коде процедур ОбработкаПроверкиЗаполнения и ПриЗаписи.
	ДополнениеПриПроверкеПриЗаписи = "";
	Если ПроверкаПриЗаписи Тогда
		ДополнениеПриПроверкеПриЗаписи = " и не помеченный на удаление";
	КонецЕсли;
	
	Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ru = 'В системе уже существует том с видом %1, размещающий данные %2%3'",
					НРег(ВидТомаХраненияФайлов),
					ПредставлениеСпособаХРаненияФайловДляСообщения(СпособХраненияФайлов),
					ДополнениеПриПроверкеПриЗаписи);
	// АПК:1297-вкл

	Возврат Результат;

КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли