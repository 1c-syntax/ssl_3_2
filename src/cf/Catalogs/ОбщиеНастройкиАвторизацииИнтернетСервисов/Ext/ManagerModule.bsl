///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  Структура:
//   * Ссылка - СправочникСсылка.ОбщиеНастройкиАвторизацииИнтернетСервисов
//   * ИмяИнтернетСервиса - Строка
//   * ВладелецДанных - Строка
//   * АдресАвторизации - Строка
//   * АдресРегистрацииУстройства - Строка
//   * АдресПолученияКлюча - Строка
//   * АдресПеренаправления - Строка
//   * ЗапрашиваемыеРазрешения - Строка
//   * ИспользоватьКлючПроверкиПодлинностиPKCE - Булево
//   * ИдентификаторПриложения - Строка
//   * ИспользоватьПарольПриложения - Булево
//   * ПарольПриложения - Строка
//   * ДополнительныеПараметрыАвторизации - Строка
//   * ДополнительныеПараметрыПолученияТокена - Строка
//   * ПояснениеПоАдресуПеренаправления - Строка
//   * ПояснениеПоИдентификаторуПриложения - Строка
//   * ПояснениеПоПаролюПриложения - Строка
//   * ДополнительноеПояснение - Строка
//   * ПсевдонимАдресаПеренаправления - Строка
//   * ПсевдонимИдентификатораПриложения - Строка
//   * ПсевдонимПароляПриложения - Строка
//   * АдресПеренаправленияПоУмолчанию - Строка
//   * АдресПеренаправленияВебКлиент - Строка
//
Функция НастройкиАвторизацииИнтернетСервиса(ИмяИнтернетСервиса, ВладелецДанных) Экспорт
	
	НастройкиАвторизации = Новый Структура;
	НастройкиАвторизации.Вставить("Ссылка");
	НастройкиАвторизации.Вставить("ИмяИнтернетСервиса");
	НастройкиАвторизации.Вставить("ВладелецДанных");
	НастройкиАвторизации.Вставить("АдресАвторизации");
	НастройкиАвторизации.Вставить("АдресРегистрацииУстройства");
	НастройкиАвторизации.Вставить("АдресПолученияКлюча");
	НастройкиАвторизации.Вставить("АдресПеренаправления");
	НастройкиАвторизации.Вставить("АдресПеренаправленияВебКлиент");
	НастройкиАвторизации.Вставить("ЗапрашиваемыеРазрешения");
	НастройкиАвторизации.Вставить("ИспользоватьКлючПроверкиПодлинностиPKCE");
	НастройкиАвторизации.Вставить("ИдентификаторПриложения");
	НастройкиАвторизации.Вставить("ИспользоватьПарольПриложения");
	НастройкиАвторизации.Вставить("ПарольПриложения");
	НастройкиАвторизации.Вставить("ДополнительныеПараметрыАвторизации");
	НастройкиАвторизации.Вставить("ДополнительныеПараметрыПолученияТокена");
	НастройкиАвторизации.Вставить("ПояснениеПоАдресуПеренаправления", "");
	НастройкиАвторизации.Вставить("ПояснениеПоИдентификаторуПриложения", "");
	НастройкиАвторизации.Вставить("ПояснениеПоПаролюПриложения", "");
	НастройкиАвторизации.Вставить("ДополнительноеПояснение", "");
	НастройкиАвторизации.Вставить("ПсевдонимАдресаПеренаправления", "");
	НастройкиАвторизации.Вставить("ПсевдонимИдентификатораПриложения", "");
	НастройкиАвторизации.Вставить("ПсевдонимПароляПриложения", "");
	НастройкиАвторизации.Вставить("АдресПеренаправленияПоУмолчанию", "");
	НастройкиАвторизации.Вставить("ПодсказкаВводаПароля", "");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.Ссылка КАК Ссылка,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.АдресАвторизации КАК АдресАвторизации,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.АдресПолученияКлюча КАК АдресПолученияКлюча,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.АдресРегистрацииУстройства КАК АдресРегистрацииУстройства,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.АдресПеренаправления КАК АдресПеренаправления,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.АдресПеренаправленияВебКлиент КАК АдресПеренаправленияВебКлиент,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.ИспользоватьКлючПроверкиПодлинностиPKCE КАК ИспользоватьКлючПроверкиПодлинностиPKCE,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.ИдентификаторПриложения КАК ИдентификаторПриложения,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.ЗапрашиваемыеРазрешения КАК ЗапрашиваемыеРазрешения,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.ИспользоватьПарольПриложения КАК ИспользоватьПарольПриложения,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.ДополнительныеПараметрыАвторизации КАК ДополнительныеПараметрыАвторизации,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.ДополнительныеПараметрыПолученияТокена КАК ДополнительныеПараметрыПолученияТокена,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.ИмяИнтернетСервиса КАК ИмяИнтернетСервиса,
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.ВладелецДанных КАК ВладелецДанных
	|ИЗ
	|	Справочник.ОбщиеНастройкиАвторизацииИнтернетСервисов КАК ОбщиеНастройкиАвторизацииИнтернетСервисов
	|ГДЕ
	|	ОбщиеНастройкиАвторизацииИнтернетСервисов.ВладелецДанных = &ВладелецДанных
	|	И ОбщиеНастройкиАвторизацииИнтернетСервисов.ИмяИнтернетСервиса = &ИмяИнтернетСервиса
	|	И НЕ ОбщиеНастройкиАвторизацииИнтернетСервисов.ПометкаУдаления";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВладелецДанных", ВладелецДанных);
	Запрос.УстановитьПараметр("ИмяИнтернетСервиса", ИмяИнтернетСервиса);
	
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(НастройкиАвторизации, ВыборкаДетальныеЗаписи);
		
		НастройкиАвторизации.ПарольПриложения = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
			ВыборкаДетальныеЗаписи.Ссылка, "ПарольПриложения");
		
	КонецЕсли;
	
	Возврат НастройкиАвторизации;
	
КонецФункции

#КонецОбласти

#КонецЕсли