///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	ЭтоАвторизованныйПользователь(Ответственный) ИЛИ ЭтоАвторизованныйПользователь(Пользователи.Пользователь)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЭтоАвторизованныйПользователь(Ответственный)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует наименование настройки отправки в Госключ.
// 
// Параметры:
//  Объект - СправочникОбъект.НастройкиОтправкиВГосключ
//         - Структура:
//            * НаименованиеОрганизации - Строка
//            * Организация - ОпределяемыйТип.Организация
//            * ИНН - Строка
//            * Ответственный - СправочникСсылка.Пользователи
//            * СрокДействия - Дата
// 
// Возвращаемое значение:
//  Строка
//
Функция АвтоНаименование(Объект) Экспорт
	
	Наименование = Новый Массив;
	
	Если ЗначениеЗаполнено(Объект.НаименованиеОрганизации) Тогда
		Организация = СокрЛП(Объект.НаименованиеОрганизации);
	ИначеЕсли ЗначениеЗаполнено(Объект.Организация) Тогда
		Организация = СокрЛП(Объект.Организация);
	Иначе
		Организация = Объект.ИНН;
	КонецЕсли;
	
	Наименование.Добавить(Лев(Организация, 100));
	
	Если ЗначениеЗаполнено(Объект.Ответственный) Тогда
		Наименование.Добавить(Объект.Ответственный);
	КонецЕсли;
		
	Наименование.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='до %1'"),
		Формат(Объект.СрокДействия, "ДЛФ=D")));
	Наименование = Лев(СтрСоединить(Наименование, ", "), 150);
	Возврат Наименование;
	
КонецФункции

#КонецОбласти

#КонецЕсли
