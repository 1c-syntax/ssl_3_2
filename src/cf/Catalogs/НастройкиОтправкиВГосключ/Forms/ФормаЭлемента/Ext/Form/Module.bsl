///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОрганизацияИспользуется = ЭлектроннаяПодпись.ОбщиеНастройки().ОрганизацияИспользуется;
	Элементы.Организация.Видимость = ОрганизацияИспользуется;
	Элементы.ГруппаРеквизитыОрганизации.Видимость = Не ОрганизацияИспользуется;
	ИспользуетсяАвтоНаименование = ИспользуетсяАвтоНаименование(Объект);
	
	Если Параметры.Ключ.Пустая() Тогда
		Объект.Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Элементы.Ответственный.ТолькоПросмотр = Не Пользователи.ЭтоПолноправныйПользователь();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	Если Не Объект.Ссылка.Пустая() Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		КлючДоступаУстановлен = ЗначениеЗаполнено(ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Ссылка,
			"КлючДоступа"));
		ПарольСертификатаУстановлен = ЗначениеЗаполнено(ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
			Объект.Ссылка, "ПарольСертификата"));
		УстановитьПривилегированныйРежим(Ложь);

		КлючДоступа = ?(КлючДоступаУстановлен, УникальныйИдентификатор, "");
		ПарольСертификата = ?(ПарольСертификатаУстановлен, УникальныйИдентификатор, "");
		КлючДоступаИзменен = Ложь;
		ПарольСертификатаИзменен = Ложь;

		ОформитьПолеПароля(Элементы.КлючДоступа);
		ОформитьПолеПароля(Элементы.ПарольСертификата);

		Если КлючДоступаУстановлен Тогда
			УстановитьВидимостьДекорации(КлючДоступаУстановлен);
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьСписокПользователей();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если КлючДоступаИзменен Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, КлючДоступа, "КлючДоступа");
		ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(ТекущийОбъект.Ссылка, "ПараметрыАвторизацииВГосключе");
		УстановитьПривилегированныйРежим(Ложь);
		КлючДоступаИзменен = Ложь;
		
	КонецЕсли;
	
	Если ПарольСертификатаИзменен Тогда
		ЗаписатьПарольСертификата(ТекущийОбъект.Ссылка, ПарольСертификата);
		ПарольСертификатаИзменен = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ИспользуетсяАвтонаименование Или Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		УстановитьАвтоНаименование();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьВидимостьДекорации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_НастройкиОтправкиВГосключ", Новый Структура, Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	УстановитьАвтоНаименование()
КонецПроцедуры

&НаКлиенте
Процедура СрокДействияПриИзменении(Элемент)
	УстановитьАвтоНаименование()
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйПриИзменении(Элемент)
	УстановитьАвтоНаименование()
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	ИспользуетсяАвтоНаименование = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПарольСертификатаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ИзменениеТекстаРедактирования(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КлючДоступаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ИзменениеТекстаРедактирования(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольСертификатаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	НачалоВыбора(Элемент, ПарольСертификата, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КлючДоступаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	НачалоВыбора(Элемент, КлючДоступа, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьДействиеИнформационногоСообщения(ЭлементИлиКоманда,
	НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	ОбщегоНазначенияКлиент.ОбработатьДействиеИнформационногоСообщения(ЭтотОбъект,
	ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователейНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("РежимВыбора", Истина);
	ПараметрыПодбора.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыПодбора.Вставить("МножественныйВыбор", Истина);
	ПараметрыПодбора.Вставить("РасширенныйПодбор", Истина);
	ПараметрыПодбора.Вставить("СкрытьПользователейБезПользователяИБ", Истина);
	
	Выбранные = Новый Массив;
	Для Каждого Строка Из Объект.Пользователи Цикл
		Выбранные.Добавить(Строка.Пользователь);
	КонецЦикла;
	
	ПараметрыПодбора.Вставить("ВыбранныеПользователи", Выбранные);
	ПараметрыПодбора.Вставить("ЗаголовокФормыПодбора",
		НСтр("ru = 'Выберите пользователей, которые могут отправлять документы подписанту'"));
	ПараметрыПодбора.Вставить("ЗаголовокКнопкиЗавершенияПодбора", НСтр("ru = 'Выбрать'"));
	
	Обработчик = Новый ОписаниеОповещения("ДобавитьПользователей", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыПодбора,,,,, Обработчик);

КонецПроцедуры

&НаКлиенте
Процедура СписокПользователейПриИзменении(Элемент)
	Объект.Пользователи.Очистить();
	Для Каждого ЭлементСписка Из СписокПользователей Цикл
		Объект.Пользователи.Добавить().Пользователь = ЭлементСписка.Значение;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Авторизоваться(Команда)
	
	Если Модифицированность Тогда
		ЗаписатьНастройку();
	КонецЕсли;
	
	ПараметрыПодписания = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписания();
	ПараметрыПодписания.ВидМобильнойПодписи = ПредопределенноеЗначение("Перечисление.ВидыМобильныхПодписей.ГосключУКЭП");
	ПараметрыПодписания.НастройкаОтправки = Объект.Ссылка;
	НастройкаОтправки = СервисМобильнойПодписиСлужебныйВызовСервера.НастройкаОтправки(ПараметрыПодписания, Истина);
	Если ТипЗнч(НастройкаОтправки) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НастройкаОтправки);
		Возврат;
	КонецЕсли;
	Если Не НастройкаОтправки.ПараметрыАвторизацииВГосключеУстановлены 
		И (Не НастройкаОтправки.РазрешеноПодписаниеНаСервере 
			Или Не НастройкаОтправки.ПарольСертификатаУстановлен) Тогда
		
		Если Не ЗначениеЗаполнено(НастройкаОтправки.Ключ) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Не указан ключ доступа'"));
			Возврат;
		КонецЕсли;
		
		СервисМобильнойПодписиСлужебныйКлиент.ПодписатьТокенДоступа(
			Новый ОписаниеОповещения("ПослеПодписанияКлючаАвторизации", ЭтотОбъект), НастройкаОтправки);
		Возврат;
	КонецЕсли;
	
	Результат = АвторизоватьсяНаСервере();
	ОбработкаРезультатаАвторизации(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Процедура ОбновитьСписокПользователей()
	Массив = Объект.Пользователи.Выгрузить().ВыгрузитьКолонку("Пользователь");
	СписокПользователей.ЗагрузитьЗначения(Массив);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПользователей(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Пользователи.Очистить();
	Для Каждого Пользователь Из Результат Цикл
		НоваяСтрока = Объект.Пользователи.Добавить();
		НоваяСтрока.Пользователь = Пользователь;
	КонецЦикла;
	
	ОбновитьСписокПользователей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодписанияКлючаАвторизации(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Отказ = Истина Или Не Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваПодписи = Результат.СвойстваПодписи;
	Если ЭтоАдресВременногоХранилища(СвойстваПодписи) Тогда
		СвойстваПодписи = ПолучитьИзВременногоХранилища(СвойстваПодписи);
	КонецЕсли;
	
	Результат = АвторизоватьсяНаСервере(СвойстваПодписи.Подпись);
	ОбработкаРезультатаАвторизации(Результат);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРезультатаАвторизации(Результат)
	
	Если Не ЗначениеЗаполнено(Результат.Ошибка) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Успешная авторизация.'"));
	Иначе
		СервисМобильнойПодписиСлужебныйКлиент.ОткрытьФормуРасширенногоПредставленияОшибки(
			Результат.Ошибка.ЗаголовокОшибки, Результат.Ошибка, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеТекстаРедактирования(Элемент)
	
	Элементы[Элемент.Имя].КнопкаВыбора = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОформитьПолеПароля(Элемент) 
	
	Элемент.РежимПароля = Истина;
	Элемент.КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыВидны;
	Элемент.КнопкаВыбора = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбора(Элемент, Реквизит, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Реквизит = Элемент.ТекстРедактирования;
	Элемент.РежимПароля = Не Элемент.РежимПароля;
	Если Элемент.РежимПароля Тогда
		Элемент.КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыВидны;
	Иначе
		Элемент.КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыСкрыты;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуетсяАвтоНаименование(Объект)
	
	Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Объект.Наименование = Справочники.НастройкиОтправкиВГосключ.АвтоНаименование(Объект);
	
КонецФункции

&НаСервере
Процедура УстановитьАвтоНаименование()
	ПараметрыНаименования = Новый Структура("Организация, НаименованиеОрганизации, ИНН, Ответственный, СрокДействия");
	ЗаполнитьЗначенияСвойств(ПараметрыНаименования, Объект);
	Объект.Наименование = Справочники.НастройкиОтправкиВГосключ.АвтоНаименование(ПараметрыНаименования);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьПарольСертификата(Ссылка, ПарольСертификата)
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Ссылка, ПарольСертификата, "ПарольСертификата");
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

&НаСервере
Функция АвторизоватьсяНаСервере(Подпись = Неопределено)

	Результат = СервисМобильнойПодписиСлужебный.АвторизоватьсяВГосключе(Объект.Ссылка, "", Подпись);
	УстановитьВидимостьДекорации(Истина);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьДекорации(КлючДоступаУстановлен = Неопределено)
	
	ОбщегоНазначенияКлиентСервер.УдалитьСтатусИлиВажноеСообщение(ЭтотОбъект, "ИнформацияОбАвторизации");

	УстановитьПривилегированныйРежим(Истина);
	ПараметрыАвторизацииВГосключе = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Ссылка,
		"ПараметрыАвторизацииВГосключе");
	Если КлючДоступаУстановлен = Неопределено Тогда
		КлючДоступаУстановлен = ЗначениеЗаполнено(ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Ссылка,
			"КлючДоступа"));
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);

	Если ЗначениеЗаполнено(ПараметрыАвторизацииВГосключе) И ЗначениеЗаполнено(
		ПараметрыАвторизацииВГосключе.РезультатАвторизации.ДействуетДо) Тогда
		Если ПараметрыАвторизацииВГосключе.РезультатАвторизации.ДействуетДо <= ТекущаяУниверсальнаяДата() Тогда
			ЗаголовокАвторизации = НСтр("ru='Токен доступа ЕСИА просрочен.'");
		Иначе
			ЗаголовокАвторизации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Токен доступа ЕСИА действует до %1'"),
				ПараметрыАвторизацииВГосключе.РезультатАвторизации.ДействуетДо + (ТекущаяДатаСеанса()
				- ТекущаяУниверсальнаяДата()));
		КонецЕсли;

		ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
		ПараметрыСообщения.ТипСообщения = "Предупреждение";
		ПараметрыСообщения.Идентификатор = "ИнформацияОбАвторизации";
		ПараметрыСообщения.Сообщение = ЗаголовокАвторизации;
		ПараметрыСообщения.КраткоеПредставление = НСтр("ru = 'Авторизация'");

		ОбщегоНазначения.ДобавитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения);
		ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);

	КонецЕсли;

	Элементы.ФормаАвторизоваться.Видимость = КлючДоступаУстановлен;
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольСертификатаПриИзменении(Элемент)
	ПарольСертификатаИзменен = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КлючДоступаПриИзменении(Элемент)
	КлючДоступаИзменен = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройку()
	Записать();
КонецПроцедуры

#КонецОбласти