///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПочтовыйСервисПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ПочтовыйСервис) Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПочтовыйСервис;
		Возврат;
	КонецЕсли;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРегистрацияПриложения;
	
	ЗаполнитьНастройкиПочтовогоСервиса();
	ЗаполнитьПоясненияРегистрацииПриложения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Зарегистрировать(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ПроверитьЗаполнениеРеквизитовРегистрацииПриложения(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьНастройкиАвторизации();
	Закрыть();
	
	ПоказатьОповещениеПользователя(, , НСтр("ru = 'Приложение зарегистрировано.'"), БиблиотекаКартинок.ДиалогИнформация);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНастройкиПочтовогоСервиса()
	
	УстановитьПривилегированныйРежим(Истина);
	НастройкиПодключения = Справочники.УчетныеЗаписиЭлектроннойПочты.НастройкиПодключенияПоАдресуЭлектроннойПочты(ПочтовыйСервис, , Истина);
	УстановитьПривилегированныйРежим(Ложь);
	
	НастройкиПочтовогоСервиса = НастройкиПодключения.НастройкиАвторизации;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоясненияРегистрацииПриложения()
	
	Если Не ЗначениеЗаполнено(НастройкиПочтовогоСервиса) Тогда
		Возврат;
	КонецЕсли;
	
	АдресПеренаправления = АдресПеренаправленияКодаАвторизации(НастройкиПочтовогоСервиса);
	
	Если Не ЗначениеЗаполнено(НастройкиПочтовогоСервиса.ПояснениеПоАдресуПеренаправления)
		И Не ЗначениеЗаполнено(НастройкиПочтовогоСервиса.ПояснениеПоИдентификаторуПриложения)
		И Не ЗначениеЗаполнено(НастройкиПочтовогоСервиса.ПояснениеПоПаролюПриложения) Тогда
		Возврат;
	КонецЕсли;
	
	ПояснениеПоАдресуПеренаправления = НастройкиПочтовогоСервиса.ПояснениеПоАдресуПеренаправления;
	ПояснениеПоИдентификаторуПриложения = НастройкиПочтовогоСервиса.ПояснениеПоИдентификаторуПриложения;
	ПояснениеПоПаролюПриложения = НастройкиПочтовогоСервиса.ПояснениеПоПаролюПриложения;
	ДополнительноеПояснение = НастройкиПочтовогоСервиса.ДополнительноеПояснение;
	
	ПсевдонимАдресаПеренаправления = НастройкиПочтовогоСервиса.ПсевдонимАдресаПеренаправления;
	ПсевдонимИдентификатораПриложения = НастройкиПочтовогоСервиса.ПсевдонимИдентификатораПриложения;
	ПсевдонимПароляПриложения = НастройкиПочтовогоСервиса.ПсевдонимПароляПриложения;
	
	Элементы.ПояснениеПоАдресуПеренаправления.Заголовок = ПояснениеПоАдресуПеренаправления;
	Элементы.ПояснениеПоИдентификаторуПриложения.Заголовок = ПояснениеПоИдентификаторуПриложения;
	Элементы.ПояснениеПоПаролюПриложения.Заголовок = ПояснениеПоПаролюПриложения;
	Элементы.ДополнительноеПояснение.Заголовок = ДополнительноеПояснение;
	
	Элементы.АдресПеренаправления.Заголовок = ПсевдонимАдресаПеренаправления;
	Элементы.ИдентификаторПриложения.Заголовок = ПсевдонимИдентификатораПриложения;
	Элементы.ПарольПриложения.Заголовок = ПсевдонимПароляПриложения;
	
	Элементы.ПояснениеПоПаролюПриложения.Видимость = НастройкиПочтовогоСервиса.ИспользоватьПарольПриложения;
	Элементы.ПарольПриложения.Видимость = НастройкиПочтовогоСервиса.ИспользоватьПарольПриложения;
	
	Элементы.ПояснениеПоАдресуПеренаправления.Видимость = ЗначениеЗаполнено(ПояснениеПоАдресуПеренаправления);
	Элементы.ПояснениеПоИдентификаторуПриложения.Видимость = ЗначениеЗаполнено(ПояснениеПоИдентификаторуПриложения);
	Элементы.ПояснениеПоПаролюПриложения.Видимость = ЗначениеЗаполнено(ПояснениеПоПаролюПриложения);
	
	Элементы.АдресПеренаправления.Видимость = ЗначениеЗаполнено(ПсевдонимАдресаПеренаправления);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АдресПеренаправленияКодаАвторизации(Знач НастройкиПочтовогоСервиса)
	
	АдресПеренаправленияКодаАвторизации = "";
	
	Если РаботаСПочтовымиСообщениямиСлужебный.ОткрытаяАвторизацияПочтовогоСервисаОпубликована() Тогда
		АдресПеренаправленияКодаАвторизации = РаботаСПочтовымиСообщениямиСлужебный.АдресОткрытойАвторизацииПочтовогоСервиса();
	Иначе
		АдресПеренаправленияКодаАвторизации = НастройкиПочтовогоСервиса.АдресПеренаправления;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АдресПеренаправленияКодаАвторизации) Тогда
		АдресПеренаправленияКодаАвторизации = НастройкиПочтовогоСервиса.АдресПеренаправленияПоУмолчанию;
	КонецЕсли;
	
	Возврат АдресПеренаправленияКодаАвторизации;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизитовРегистрацииПриложения(Отказ)
	
	ОчиститьСообщения();
	
	ПроверитьЗаполнениеРеквизита(Элементы.АдресПеренаправления, Отказ);
	ПроверитьЗаполнениеРеквизита(Элементы.ИдентификаторПриложения, Отказ);
	ПроверитьЗаполнениеРеквизита(Элементы.ПарольПриложения, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизита(Элемент, Отказ)
	
	ИмяРеквизита = Элемент.Имя;
	
	Если Элемент.Видимость И Не ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизита]) Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Введите %1'"), Элемент.Заголовок);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , ИмяРеквизита, , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиАвторизации()
	
	НастройкиАвторизации = НастройкиПочтовогоСервиса;
	НастройкиАвторизации.ПарольПриложения = ПарольПриложения;
	НастройкиАвторизации.ИдентификаторПриложения = ИдентификаторПриложения;
	НастройкиАвторизации.АдресПеренаправления = АдресПеренаправления;
	
	УстановитьПривилегированныйРежим(Истина);
	Справочники.НастройкиАвторизацииИнтернетСервисов.ЗаписатьНастройкиАвторизации(НастройкиАвторизации);
	
КонецПроцедуры

#КонецОбласти
