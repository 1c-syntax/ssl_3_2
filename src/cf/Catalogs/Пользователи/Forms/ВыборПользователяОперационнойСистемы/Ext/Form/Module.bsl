///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		Возврат;
	КонецЕсли;
	
	// Замена командной панели формы на командную панель списка пользователей.
	СтандартныеПодсистемыСервер.ПереместитьЭлементыВГруппу(КоманднаяПанель,
		Элементы.ТаблицаПользователейДомена.КоманднаяПанель);
	
	// Корректировка порядка команд в командной панели списка пользователей.
	СтандартныеПодсистемыСервер.ПереместитьЭлементыВГруппе(
		Элементы.ТаблицаПользователейДомена.КоманднаяПанель,
		"ФормаВыбрать");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ЗаполнитьПользователейОС", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаДоменов

&НаКлиенте
Процедура ТаблицаДоменовПриАктивизацииСтроки(Элемент)
	
	ТаблицаПользователейДомена.Очистить();
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ИмяДомена = Элемент.ТекущиеДанные.ИмяДомена;
		
		Для Каждого Запись Из ТаблицаДоменовИПользователей Цикл
			Если Запись.ИмяДомена = ИмяДомена Тогда
				
				Для Каждого Пользователь Из Запись.Пользователи Цикл
					ПользовательДомена = ТаблицаПользователейДомена.Добавить();
					ПользовательДомена.ИмяПользователя = Пользователь;
				КонецЦикла;
				Прервать;
				
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаПользователейДомена.Сортировать("ИмяПользователя");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаПользователей

&НаКлиенте
Процедура ТаблицаПользователейДоменаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СкомпоноватьРезультатИЗакрытьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПользователейДоменаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СкомпоноватьРезультатИЗакрытьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьПользователейОС()
	
#Если ТолстыйКлиентОбычноеПриложение ИЛИ ТолстыйКлиентУправляемоеПриложение Тогда
	ТаблицаДоменовИПользователей = ПользователиОС();
#ИначеЕсли ТонкийКлиент Тогда
	ТаблицаДоменовИПользователей = Новый ФиксированныйМассив(ПользователиОС());
#Иначе
	ТаблицаДоменовИПользователей = Новый ФиксированныйМассив(Новый Массив());
#КонецЕсли
	
	ТаблицаДоменов.Очистить();
	
	Для Каждого Запись Из ТаблицаДоменовИПользователей Цикл
		Домен = ТаблицаДоменов.Добавить();
		Домен.ИмяДомена = Запись.ИмяДомена;
	КонецЦикла;
	
	ТаблицаДоменов.Сортировать("ИмяДомена");
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыбор;
	Элементы.ТаблицаПользователейДоменаВыбрать.Доступность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СкомпоноватьРезультатИЗакрытьФорму()
	
	ИмяДомена = Элементы.ТаблицаДоменов.ТекущиеДанные.ИмяДомена;
	ИмяПользователя = Элементы.ТаблицаПользователейДомена.ТекущиеДанные.ИмяПользователя;
	
	РезультатВыбора = "\\" + ИмяДомена + "\" + ИмяПользователя;
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти
