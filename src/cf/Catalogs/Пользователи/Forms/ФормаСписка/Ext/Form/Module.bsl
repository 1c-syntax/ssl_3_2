///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//                          ИСПОЛЬЗОВАНИЕ ФОРМЫ                               //
//
// Описание параметров формы см. ПользователиПереопределяемый.ПриОпределенииФормыВыбораПользователей
//

#Область ОписаниеПеременных

&НаКлиенте
Перем ПоследняяГруппаПриАктивизации, ПоследняяГруппаПриНаведении, ТекущийПользователь;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ХранимыеПараметры = ХранимыеПараметры();
	ПользователиСлужебный.ПриСозданииФормыСпискаНаСервере(ЭтотОбъект);
	
	НастроитьФормуПоИспользованиюГруппПользователей(Ложь, Истина);
	
	Если ХранимыеПараметры.РасширенныйПодбор Тогда
		ВыбранныеПользователи = Неопределено;
		ПользователиСлужебный.НастроитьПараметрыРасширеннойФормыПодбора(ЭтотОбъект, ВыбранныеПользователи);
		Если ВыбранныеПользователи <> Неопределено Тогда
			ОбновитьСписокВыбранных(ВыбранныеПользователи, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Обсуждения") Тогда
		 Элементы.СписокЗаблокироватьНедействительныхПользователейВСистемеВзаимодействий.Видимость = Истина;
	КонецЕсли;
	
	// Скрытие служебных пользователей.
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Служебный", Ложь, , , Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный,
			Строка(Новый УникальныйИдентификатор));
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Служебный", Ложь, , , Истина);
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		Элементы.Фотография.Высота = 4;
		Элементы.СписокТекущиеДанныеКонтактнаяИнформация.Шрифт = Элементы.Фотография.Шрифт;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85()
	   И (Не ХранимыеПараметры.РежимВыбора
	      Или ХранимыеПараметры.РасширенныйПодбор) Тогда
		
		НастроитьРаботуВТакси();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ПриОткрытииОбработчикОжидания", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ПоказыватьКонтактнуюИнформацию = Не Элементы.ГруппаКонтактнаяИнформация.Скрыта();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ВРег(ИмяСобытия) = ВРег("Запись_ГруппыПользователей")
	 Или ВРег(ИмяСобытия) = ВРег("РазмещениеПользователейВГруппах")
	 Или ВРег(ИмяСобытия) = ВРег("Запись_ГруппыДоступа") Тогда
		
		ПодключитьОбработчикОжидания("ПриИзмененииГруппыПользователейОбработчикОжидания", 0.1, Истина);
		
	ИначеЕсли ВРег(ИмяСобытия) = ВРег("Запись_НаборКонстант") Тогда
		
		Если ВРег(Источник) = ВРег("ИспользоватьГруппыПользователей") Тогда
			ПодключитьОбработчикОжидания("ПриИзмененииИспользованияГруппПользователейОбработчикОжидания", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если ТипЗнч(Настройки["ПоказыватьПользователейНижестоящихГрупп"]) = Тип("Булево") Тогда
		ПоказыватьПользователейНижестоящихГрупп = Настройки["ПоказыватьПользователейНижестоящихГрупп"];
	КонецЕсли;
	Элементы.СписокПоказатьПользователейНижестоящихГрупп.Пометка = ПоказыватьПользователейНижестоящихГрупп;
	
	Если Не ПоказыватьПользователейНижестоящихГрупп Тогда
		ОбновитьСодержимоеФормыПриИзмененииГруппы();
	КонецЕсли;
	
	Если ТипЗнч(Настройки["ПоказыватьКонтактнуюИнформацию"]) = Тип("Булево") Тогда
		ПоказыватьКонтактнуюИнформацию = Настройки["ПоказыватьКонтактнуюИнформацию"];
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		ПоказыватьКонтактнуюИнформацию = Истина;
	КонецЕсли;
	
	ПользователиСлужебный.УстановитьЭлементУпорядочения(Группы, "Предопределенный", 0, "Убыв");
	ПользователиСлужебный.УстановитьЭлементУпорядочения(Группы, "Наименование");
	ПользователиСлужебный.УстановитьЭлементУпорядочения(Список, "Наименование");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьЭлементыСписковПриИзменении(Элемент)
	
	ПоказыватьЭлементыСписковПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыГруппы

&НаКлиенте
Процедура ГруппыПриИзменении(Элемент)
	
	СписокПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		СтандартнаяОбработка = Ложь;
		
	ИначеЕсли ХранимыеПараметры.РежимВыбора Тогда
		СтандартнаяОбработка = Ложь;
		Если ХранимыеПараметры.РасширенныйПодбор Тогда
			ГруппыВключитьВВыбранные(Неопределено);
		Иначе
			ГруппыВыборЗначения(Элемент, ВыбраннаяСтрока, Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПриАктивизацииСтроки(Элемент)
	
	Если Не ХранимыеПараметры.РасширенныйПодбор
	   И ХранимыеПараметры.ВыборГрупп Тогда
		
		ПоследняяГруппаПриАктивизации = Элементы.Группы.ТекущаяСтрока;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПодключитьОбработчикОжидания("ОбновитьСодержимоеФормыПриИзмененииГруппыОбработчикОжидания", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ХранимыеПараметры.ВыборГрупп Тогда
		Возврат;
	КонецЕсли;
	
	Если ХранимыеПараметры.РасширенныйПодбор Тогда
		ОбновитьСписокВыбранных(Значение,, Истина);
	Иначе
		ОповеститьОВыборе(Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ПараметрыФормы = Новый Структура;
	
	Если ЗначениеЗаполнено(Элементы.Группы.ТекущаяСтрока) Тогда
		ПараметрыФормы.Вставить("ЗначенияЗаполнения",
			Новый Структура("Родитель", Элементы.Группы.ТекущаяСтрока));
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ГруппыПользователей.ФормаОбъекта",
		ПараметрыФормы, Элементы.Группы);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПриНаведении(Элемент, Строка, Поле)
	
	Если Не ХранимыеПараметры.РасширенныйПодбор
	   И ХранимыеПараметры.ВыборГрупп Тогда
		
		ПоследняяГруппаПриНаведении = ?(Поле = Неопределено, Неопределено, Строка);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект, Элемент, Строка);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Не ХранимыеПараметры.РасширенныйПодбор
	 Или Не ХранимыеПараметры.ВыборГрупп Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Элемент.ВыделенныеСтроки) Тогда
		ДанныеСтроки = Неопределено;
	Иначе
		ДанныеСтроки = ?(Строка = Неопределено, Элемент.ТекущиеДанные,
			Элемент.ДанныеСтроки(Строка));
	КонецЕсли;
	
	Если ДанныеСтроки = Неопределено Тогда
		Элементы.ГруппыВключитьВВыбранные.Видимость = Истина;
		Элементы.ГруппыИсключитьИзВыбранных.Видимость = Истина;
	Иначе
		Элементы.ГруппыВключитьВВыбранные.Видимость = Не ДанныеСтроки.Выбран;
		Элементы.ГруппыИсключитьИзВыбранных.Видимость = ДанныеСтроки.Выбран;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПриИзмененииСоставаВыделенныхСтрок(Элемент)
	
	Если Не ХранимыеПараметры.РасширенныйПодбор
	   И ХранимыеПараметры.ВыборГрупп
	   И ЗначениеЗаполнено(ПоследняяГруппаПриНаведении)
	   И ЗначениеЗаполнено(ПоследняяГруппаПриАктивизации)
	   И ПоследняяГруппаПриНаведении <> ПоследняяГруппаПриАктивизации Тогда
		
		ПоследняяГруппаПриАктивизации = Неопределено;
		Индекс = Элемент.ВыделенныеСтроки.Найти(ПоследняяГруппаПриНаведении);
		Если Индекс <> Неопределено Тогда
			Элемент.ВыделенныеСтроки.Удалить(Индекс);
		Иначе
			Элемент.ВыделенныеСтроки.Добавить(ПоследняяГруппаПриНаведении);
		КонецЕсли;
	КонецЕсли;
	
	ПользователиСлужебныйКлиент.ОбновитьДоступностьКнопокВыбора(ЭтотОбъект);
	
	Если ХранимыеПараметры.РасширенныйПодбор
	   И ХранимыеПараметры.ВыборГрупп
	   И ЗначениеЗаполнено(Элемент.ВыделенныеСтроки) Тогда
		
		Элементы.ГруппыВключитьВВыбранные.Видимость = Истина;
		Элементы.ГруппыИсключитьИзВыбранных.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	СтараяГруппа = Элементы.Группы.ТекущаяСтрока;
	НоваяГруппа = Строка;
	
	Если НоваяГруппа = Неопределено
	 Или СтараяГруппа = НоваяГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ВключитьВНовуюГруппу = (СтараяГруппа = ХранимыеПараметры.ГруппаВсеПользователи);
	ДанныеНовойГруппы = Элементы.Группы.ДанныеСтроки(НоваяГруппа);
	
	Контекст = Новый Структура;
	Контекст.Вставить("Пользователи", ПараметрыПеретаскивания.Значение);
	Контекст.Вставить("ИсключитьИзСтаройГруппы", ХранимыеПараметры.ГруппаВсеПользователи = НоваяГруппа);
	Контекст.Вставить("ВключитьВНовуюГруппу", ВключитьВНовуюГруппу);
	Контекст.Вставить("НоваяГруппа", НоваяГруппа);
	Контекст.Вставить("НоваяГруппаПомеченаНаУдаление", ДанныеНовойГруппы.ПометкаУдаления);
	Контекст.Вставить("СтараяГруппа", СтараяГруппа);
	Контекст.Вставить("Перемещение", ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Перемещение);
	Контекст.Вставить("ПоказыватьПользователейНижестоящихГрупп", ПоказыватьПользователейНижестоящихГрупп);
	
	Оповещение = Новый ОписаниеОповещения("ГруппыПеретаскиваниеОбработкаОтветаНаВопрос", ЭтотОбъект, Контекст);
	
	ПользователиСлужебныйКлиент.ПриПеретаскиванииПользователейВГруппу(Контекст, Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	СписокПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ХранимыеПараметры.РасширенныйПодбор Тогда
		СтандартнаяОбработка = Ложь;
		Если Не ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
			СписокВключитьВВыбранные(Неопределено);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если СтандартныеПодсистемыКлиент.ЭтоЭлементДинамическогоСписка(Элементы.Список) Тогда
		ВозможноСменитьПароль = Элементы.Список.ТекущиеДанные.ВозможноСменитьПароль;
	Иначе
		ВозможноСменитьПароль = Ложь;
	КонецЕсли;
	
	Элементы.СписокУстановитьПароль.Доступность = ВозможноСменитьПароль;
	Если Не ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		ПользователиСлужебныйКлиент.ОбновитьДоступностьКнопокВыбора(ЭтотОбъект);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"СписокКонтекстноеМенюУстановитьПароль", "Доступность", ВозможноСменитьПароль);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбновитьКонтактнуюИнформациюПользователя(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если ХранимыеПараметры.РасширенныйПодбор Тогда
		СтандартнаяОбработка = Ложь;
		ОбновитьСписокВыбранных(Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура("ГруппаНовогоПользователя",
		Элементы.Группы.ТекущаяСтрока);
	
	Если Копирование И Элемент.ТекущиеДанные <> Неопределено Тогда
		ПараметрыФормы.Вставить("ЗначениеКопирования", Элемент.ТекущаяСтрока);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Пользователи.ФормаОбъекта",
		ПараметрыФормы, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Не ЗначениеЗаполнено(Элемент.ТекущаяСтрока) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Ключ", Элемент.ТекущаяСтрока);
	ОткрытьФорму("Справочник.Пользователи.ФормаОбъекта",
		ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ПользователиСлужебный.ДинамическийСписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриНаведении(Элемент, Строка, Поле)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект, Элемент, Строка);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Не ХранимыеПараметры.РасширенныйПодбор Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Элемент.ВыделенныеСтроки) Тогда
		ДанныеСтроки = Неопределено;
	Иначе
		ДанныеСтроки = ?(Строка = Неопределено, Элемент.ТекущиеДанные,
			Элемент.ДанныеСтроки(Строка));
	КонецЕсли;
	
	Если ДанныеСтроки = Неопределено Тогда
		Элементы.СписокВключитьВВыбранные.Видимость = Истина;
		Элементы.СписокИсключитьИзВыбранных.Видимость = Истина;
	Иначе
		Элементы.СписокВключитьВВыбранные.Видимость = Не ДанныеСтроки.Выбран;
		Элементы.СписокИсключитьИзВыбранных.Видимость = ДанныеСтроки.Выбран;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзмененииСоставаВыделенныхСтрок(Элемент)
	
	ПользователиСлужебныйКлиент.ОбновитьДоступностьКнопокВыбора(ЭтотОбъект);
	
	Если ХранимыеПараметры.РасширенныйПодбор
	   И ЗначениеЗаполнено(Элемент.ВыделенныеСтроки) Тогда
		
		Элементы.СписокВключитьВВыбранные.Видимость = Истина;
		Элементы.СписокИсключитьИзВыбранных.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ФормаВыбратьВыделенные(Команда)
	
	Если ХранимыеПараметры.ВыборГрупп Тогда
		ВыбратьВыделенные(Элементы.Группы);
	КонецЕсли;
	
	ВыбратьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаВыбратьИЗакрыть(Команда)
	
	Если Не ХранимыеПараметры.РасширенныйПодбор Тогда
		ФормаВыбратьВыделенные(Неопределено);
		Закрыть();
	Иначе
		ПользователиСлужебныйКлиент.ПредупредитьОВыделенныхПриРасширенномПодборе(ЭтотОбъект,
			Новый ОписаниеОповещения("ФормаВыбратьИЗакрытьПриРасширенномПодборе", ЭтотОбъект));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаВставитьИзБуфераОбмена(Команда)
	
	ВставитьИзБуфераОбмена();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиИнформационнойБазы(Команда)
	
	ОткрытьФорму("Справочник.Пользователи.Форма.ПользователиИнформационнойБазы");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНазначитьГруппы(Команда)
	
	СтрокиДляОбработки = Элементы.Список.ПолучитьСтрокиДляОбработки();
	Если Не ЗначениеЗаполнено(СтрокиДляОбработки) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Пользователи", СтрокиДляОбработки);
	ПараметрыФормы.Вставить("ВнешниеПользователи", Ложь);
	
	ОткрытьФорму("ОбщаяФорма.ГруппыПользователей", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокУстановитьПароль(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если СтандартныеПодсистемыКлиент.ЭтоЭлементДинамическогоСписка(ТекущиеДанные) Тогда
		ПользователиСлужебныйКлиент.ОткрытьФормуСменыПароля(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбратьТакси(Команда)
	
	ВыбратьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВключитьВВыбранные(Команда)
	
	ОбновитьСписокВыбранных(Элементы.Список.Имя, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИсключитьИзВыбранных(Команда)
	
	ОбновитьСписокВыбранных(Элементы.Список.Имя, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПоказатьНедействительныхПользователей(Команда)
	
	НовоеЗначение = Не Элементы.СписокПоказатьНедействительныхПользователей.Пометка;
	Элементы.СписокПоказатьНедействительныхПользователей.Пометка = НовоеЗначение;
	
	ПоказатьНедействительныхПользователей(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаблокироватьНедействительныхПользователейВСистемеВзаимодействия(Команда)
	
	РезультатОбработки = ЗаблокироватьНедействительныхПользователейВСистемеВзаимодействийНаСервере();
	
	Если Не ПустаяСтрока(РезультатОбработки) Тогда
		ШаблонСообщения =
			НСтр("ru = 'Не удалось заблокировать пользователей по причине:
			           |
			           |%1'");
		ПоказатьПредупреждение(,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, РезультатОбработки),,
			НСтр("ru = 'Не удалось заблокировать пользователей'"));
	Иначе
		Состояние(НСтр("ru = 'Блокировка недействительных пользователей выполнена.'"),,,
			БиблиотекаКартинок.Успешно32);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТекущиеДанныеКонтактнаяИнформацияКонтекстноеМенюПоказать(Команда)
	
	СтандартныеПодсистемыКлиент.ПоказатьПодробнуюИнформацию(,
		СписокТекущиеДанныеКонтактнаяИнформация,
		НСтр("ru = 'Контактная информация пользователя'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыВыбратьТакси(Команда)
	
	ВыбратьВыделенные(Элементы.Группы);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыВключитьВВыбранные(Команда)
	
	ОбновитьСписокВыбранных(Элементы.Группы.Имя, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыИсключитьИзВыбранных(Команда)
	
	ОбновитьСписокВыбранных(Элементы.Группы.Имя, Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПоказатьПользователейНижестоящихГрупп(Команда)
	
	НовоеЗначение = Не Элементы.СписокПоказатьПользователейНижестоящихГрупп.Пометка;
	Элементы.СписокПоказатьПользователейНижестоящихГрупп.Пометка = НовоеЗначение;
	ПоказыватьПользователейНижестоящихГрупп = НовоеЗначение;
	
	ОбновитьСодержимоеФормыПриИзмененииГруппы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииОбработчикОжидания()
	
	Если ХранимыеПараметры.РежимВыбора Тогда
		ПользователиСлужебныйКлиент.НастроитьКнопкиПриВыбореИПодборе(ЭтотОбъект);
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85()
	   И ХранимыеПараметры.РежимВыбора
	   И Не ХранимыеПараметры.РасширенныйПодбор Тогда
		
		НастроитьРаботуВТакси();
	КонецЕсли;
	
	ГруппаСкрыта = Элементы.ГруппаКонтактнаяИнформация.Скрыта();
	Если ГруппаСкрыта И ПоказыватьКонтактнуюИнформацию Тогда
		Элементы.ГруппаКонтактнаяИнформация.Показать();
	ИначеЕсли Не ГруппаСкрыта И Не ПоказыватьКонтактнуюИнформацию Тогда
		Элементы.ГруппаКонтактнаяИнформация.Скрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКонтактнуюИнформациюПользователя(ЭлементСписок)
	
	ТекущиеДанные = ЭлементСписок.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
	 Или Не СтандартныеПодсистемыКлиент.ЭтоЭлементДинамическогоСписка(ЭлементСписок) Тогда
		
		НовыйТекущийПользователь = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	Иначе
		НовыйТекущийПользователь = ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	Если ТекущийПользователь = НовыйТекущийПользователь Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийПользователь = НовыйТекущийПользователь;
	ПодключитьОбработчикОжидания("ОбновитьКонтактнуюИнформациюПользователяЗавершение", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКонтактнуюИнформациюПользователяЗавершение()
	ЗаполнитьКонтактнуюИнформацию(ЭтотОбъект, ТекущийПользователь);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьКонтактнуюИнформацию(Форма, ТекущийПользователь)
	
	Если Не ЗначениеЗаполнено(ТекущийПользователь) Тогда
		Форма.СписокТекущиеДанныеКонтактнаяИнформация = "";
		Форма.СписокТекущиеДанныеФотография = Форма.ХранимыеПараметры.АдресПользовательБезФотографии;
		Возврат;
	КонецЕсли;
	
	КонтактнаяИнформацияПользователя = ?(ЗначениеЗаполнено(ТекущийПользователь),
		КонтактнаяИнформацияПользователя(ТекущийПользователь,
			Форма.ХранимыеПараметры.АдресПользовательБезФотографии,
			Форма.УникальныйИдентификатор),
		Новый Структура("Фотография", Форма.ХранимыеПараметры.АдресПользовательБезФотографии));
	
	Форма.СписокТекущиеДанныеФотография = КонтактнаяИнформацияПользователя.Фотография;
	
	Шаблон = НСтр("ru='%1
	|
	|Телефон: %2
	|Электронная почта: %3'");
	
	Подразделение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(КонтактнаяИнформацияПользователя, "Подразделение", "");
	ИмяПользователя = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(КонтактнаяИнформацияПользователя, "Наименование", "");
	ПредставлениеПользователя = ?(ЗначениеЗаполнено(ИмяПользователя), ИмяПользователя,"")
		+ ?(ЗначениеЗаполнено(Подразделение), Символы.ПС + Подразделение, "");
		
	Форма.СписокТекущиеДанныеКонтактнаяИнформация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон,
		ПредставлениеПользователя,
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(КонтактнаяИнформацияПользователя, "Телефон", ""),
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(КонтактнаяИнформацияПользователя, "АдресЭлектроннойПочты", ""));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КонтактнаяИнформацияПользователя(Знач Пользователь, Знач АдресФотографииПоУмолчанию, Знач УникальныйИдентификатор)
	КонтактнаяИнформация = ПользователиСлужебный.ОписаниеПользователя(Пользователь);
	Если КонтактнаяИнформация.Фотография <> Неопределено Тогда
		КонтактнаяИнформация.Фотография = ПоместитьВоВременноеХранилище(КонтактнаяИнформация.Фотография, УникальныйИдентификатор);
	Иначе
		КонтактнаяИнформация.Фотография = АдресФотографииПоУмолчанию;
	КонецЕсли;
	
	Возврат КонтактнаяИнформация;
КонецФункции


// Возвращаемое значение:
//  Структура:
//   * РежимВыбора - Булево
//   * ЗакрыватьПриВыборе - Булево
//   * ВыборГрупп - Булево
//   * ВыборУчастниковОбсуждения - Булево
//   * РасширенныйПодбор - Булево
//   * ИспользоватьГруппы - Произвольный
//   * ГруппаВсеПользователи - СправочникСсылка.ГруппыПользователей
//   * ЗаголовокФормыПодбора - Строка
//   * ЗаголовокКнопкиЗавершенияПодбора - Строка
//   * ПоказыватьЭлементыСписков - Строка - предыдущее значение
//   * ГруппыОтображение - ОтображениеТаблицы - отображение перед включением отбора выбранных ссылок.
//   * ГруппаТекущаяСтрока - Неопределено, СправочникСсылка.ГруппыПользователей
//   * АдресПользовательБезФотографии - Строка
//
&НаСервере
Функция ХранимыеПараметры()
	
	Результат = Новый Структура;
	Результат.Вставить("РежимВыбора",               Параметры.РежимВыбора);
	Результат.Вставить("ЗакрыватьПриВыборе",        Параметры.ЗакрыватьПриВыборе <> Ложь);
	Результат.Вставить("ВыборГрупп",                Параметры.РежимВыбора И Параметры.ВыборГруппПользователей);
	Результат.Вставить("ВыборУчастниковОбсуждения", Параметры.РежимВыбора И Параметры.ВыборУчастниковОбсуждения);
	Результат.Вставить("РасширенныйПодбор",         Параметры.РежимВыбора И Параметры.РасширенныйПодбор);
	Результат.Вставить("ИспользоватьГруппы",    ПолучитьФункциональнуюОпцию("ИспользоватьГруппыПользователей"));
	Результат.Вставить("ГруппаВсеПользователи", Пользователи.ГруппаВсеПользователи());
	Результат.Вставить("ЗаголовокФормыПодбора", "");
	Результат.Вставить("ЗаголовокКнопкиЗавершенияПодбора", "");
	Результат.Вставить("ПоказыватьЭлементыСписков", "");
	Результат.Вставить("ГруппыОтображение", Элементы.Группы.Отображение);
	Результат.Вставить("ГруппаТекущаяСтрока");
	Результат.Вставить("АдресПользовательБезФотографии",
		ПоместитьВоВременноеХранилище(БиблиотекаКартинок.ПользовательБезФотографии, УникальныйИдентификатор));
	
	СписокТекущиеДанныеФотография = Результат.АдресПользовательБезФотографии;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗаблокироватьНедействительныхПользователейВСистемеВзаимодействийНаСервере()
	
	Результат = "";
	МодульОбсужденияСлужебный = ОбщегоНазначения.ОбщийМодуль("ОбсужденияСлужебный");
	НедействительныеПользователи = МодульОбсужденияСлужебный.НедействительныеПользователи();
	
	Для Каждого Ссылка Из НедействительныеПользователи Цикл
		РезультатБлокировки = МодульОбсужденияСлужебный.ЗаблокироватьПользователяСистемыВзаимодействия(Ссылка);
		Если РезультатБлокировки <> Неопределено Тогда
			Результат = ОбработкаОшибок.КраткоеПредставлениеОшибки(РезультатБлокировки);
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Обсуждения.Блокировка недействительных пользователей'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(РезультатБлокировки));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура НастроитьФормуПоИспользованиюГруппПользователей(ИзменилосьИспользованиеГрупп = Ложь,
			УстановитьТекущуюСтроку = Ложь)
	
	Если ИзменилосьИспользованиеГрупп Тогда
		ХранимыеПараметры.ИспользоватьГруппы = ПолучитьФункциональнуюОпцию("ИспользоватьГруппыПользователей");
	КонецЕсли;
	
	Если УстановитьТекущуюСтроку Тогда
		ТекущаяСтрока = Параметры.ТекущаяСтрока;
		
		Если ТипЗнч(ТекущаяСтрока) = Тип("СправочникСсылка.ГруппыПользователей") 
		   И ХранимыеПараметры.ИспользоватьГруппы Тогда
			
			Элементы.Группы.ТекущаяСтрока = ТекущаяСтрока;
		Иначе
			ТекущийЭлемент = Элементы.Список;
			Элементы.Группы.ТекущаяСтрока = ХранимыеПараметры.ГруппаВсеПользователи;
		КонецЕсли;
		
	ИначеЕсли Не ХранимыеПараметры.ИспользоватьГруппы Тогда
		
		Если Элементы.Группы.ТекущаяСтрока <> ХранимыеПараметры.ГруппаВсеПользователи Тогда
			Элементы.Группы.ТекущаяСтрока = ХранимыеПараметры.ГруппаВсеПользователи;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПравоДоступа("Редактирование", Метаданные.Справочники.ГруппыПользователей)
	 Или ХранимыеПараметры.РасширенныйПодбор
	 Или ОбщегоНазначения.ЭтоАвтономноеРабочееМесто() Тогда
		
		Элементы.СписокНазначитьГруппы.Видимость = Ложь;
	Иначе
		Элементы.СписокНазначитьГруппы.Видимость = ХранимыеПараметры.ИспользоватьГруппы;
	КонецЕсли;
	
	ВыборГрупп = ХранимыеПараметры.ВыборГрупп
		И ХранимыеПараметры.ИспользоватьГруппы
		И ХранимыеПараметры.РежимВыбора;
	
	Если ХранимыеПараметры.РежимВыбора Тогда
		АвтоЗаголовок = Ложь;
		
		Если Не ХранимыеПараметры.ЗакрыватьПриВыборе Тогда
			// Режим подбора.
			Если ХранимыеПараметры.РасширенныйПодбор
			   И ЗначениеЗаполнено(ХранимыеПараметры.ЗаголовокФормыПодбора) Тогда
				
				Заголовок = ХранимыеПараметры.ЗаголовокФормыПодбора;
				
			ИначеЕсли ВыборГрупп Тогда
				Заголовок = НСтр("ru = 'Подбор пользователей и групп'");
			Иначе
				Заголовок = НСтр("ru = 'Подбор пользователей'");
			КонецЕсли;
			
			Если ХранимыеПараметры.РасширенныйПодбор
			   И ЗначениеЗаполнено(ХранимыеПараметры.ЗаголовокКнопкиЗавершенияПодбора) Тогда
				
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
					"ФормаВыбратьИЗакрыть", "Заголовок", ХранимыеПараметры.ЗаголовокКнопкиЗавершенияПодбора);
			КонецЕсли;
		Иначе
			// Режим выбора.
			Если ВыборГрупп Тогда
				Заголовок = НСтр("ru = 'Выбор пользователя или группы'");
			Иначе
				Заголовок = НСтр("ru = 'Выбор пользователя'");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьСодержимоеФормыПриИзмененииГруппы();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьРаботуВТакси()
	
	ПользователиСлужебный.НастроитьРаботуВТакси(ЭтотОбъект);
	
КонецПроцедуры


&НаКлиенте
Процедура ПриИзмененииГруппыПользователейОбработчикОжидания()
	
	ОбновитьСодержимоеФормыПриИзмененииГруппы();
	
	Элементы.Группы.Обновить();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииИспользованияГруппПользователейОбработчикОжидания()
	
	НастроитьФормуПоИспользованиюГруппПользователей(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСодержимоеФормыПриИзмененииГруппыОбработчикОжидания()
	
	ОбновитьСодержимоеФормыПриИзмененииГруппы();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСодержимоеФормыПриИзмененииГруппы()
	
	Если Не ХранимыеПараметры.ИспользоватьГруппы
	 Или Элементы.Группы.ТекущаяСтрока = ХранимыеПараметры.ГруппаВсеПользователи Тогда
		
		ПользователиСлужебныйКлиентСервер.ОбновитьЗначениеПараметраКомпоновкиДанных(Список,
			"ВсеПользователи", Истина);
		
		ПользователиСлужебныйКлиентСервер.ОбновитьЗначениеПараметраКомпоновкиДанных(Список,
			"ВыбиратьИерархически", Ложь);
		
		ПользователиСлужебныйКлиентСервер.ОбновитьЗначениеПараметраКомпоновкиДанных(Список,
			"ГруппаПользователей", ХранимыеПараметры.ГруппаВсеПользователи);
	Иначе
		ПользователиСлужебныйКлиентСервер.ОбновитьЗначениеПараметраКомпоновкиДанных(Список,
			"ВсеПользователи", ПоказыватьЭлементыСписков <> "");
		
		ПользователиСлужебныйКлиентСервер.ОбновитьЗначениеПараметраКомпоновкиДанных(Список,
			"ВыбиратьИерархически", ПоказыватьПользователейНижестоящихГрупп);
		
		ПользователиСлужебныйКлиентСервер.ОбновитьЗначениеПараметраКомпоновкиДанных(Список,
			"ГруппаПользователей", Элементы.Группы.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриИзмененииНаСервере()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступомСлужебный = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступомСлужебный");
		МодульУправлениеДоступомСлужебный.ЗапуститьОбновлениеДоступа();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ВставитьИзБуфераОбмена()
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("ОписаниеТипов", Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	ПараметрыПоиска.Вставить("ПараметрыВыбора", Новый СписокЗначений);
	ПараметрыПоиска.Вставить("ПредставлениеПоля", Заголовок);
	ПараметрыПоиска.Вставить("Сценарий", "ПоискСсылок");
	
	ПараметрыВыполнения = Новый Структура;
	Обработчик = Новый ОписаниеОповещения("ВставитьИзБуфераОбменаЗавершение", ЭтотОбъект, ПараметрыВыполнения);
	
	МодульЗагрузкаДанныхИзФайлаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ЗагрузкаДанныхИзФайлаКлиент");
	МодульЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗаполненияСсылок(ПараметрыПоиска, Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьИзБуфераОбменаЗавершение(НайденныеОбъекты, ПараметрыВыполнения) Экспорт
	
	Если НайденныеОбъекты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВставитьИзБуфераОбменаЗавершениеСервер(НайденныеОбъекты);
	
КонецПроцедуры

&НаСервере
Процедура ВставитьИзБуфераОбменаЗавершениеСервер(НайденныеОбъекты)
	
	ОбновитьСписокВыбранных(НайденныеОбъекты, Неопределено);
	
КонецПроцедуры


&НаСервере
Процедура ОбновитьСписокВыбранных(Знач ВыбранныеСсылки, Знач Добавить = Истина, Знач ВыборГруппы = Ложь)
	
	ПользователиСлужебный.ОбновитьСписокВыбранных(ЭтотОбъект,
		ВыбранныеСсылки, Добавить, ВыборГруппы);
	
	ПользователиСлужебный.ОбновитьКоличествоВОтбореПриПодборе(ЭтотОбъект);
	
	Если ПоказыватьЭлементыСписков <> "" Тогда
		ПоказыватьЭлементыСписковПриИзмененииНаСервере();
	КонецЕсли;
	
	ПользователиСлужебный.ОбновитьВыбранныеСсылкиДляСписка(Группы,
		Выбранные, Тип("СправочникСсылка.ГруппыПользователей"));
	
	ПользователиСлужебный.ОбновитьВыбранныеСсылкиДляСписка(Список,
		Выбранные, Тип("СправочникСсылка.Пользователи"));
	
КонецПроцедуры

&НаСервере
Процедура ПоказыватьЭлементыСписковПриИзмененииНаСервере()
	
	ПользователиСлужебный.ОбновитьОтборСсылокДляСписка(Группы,
		ПоказыватьЭлементыСписков, Выбранные, Тип("СправочникСсылка.ГруппыПользователей"));
	
	ПользователиСлужебный.ОбновитьОтборСсылокДляСписка(Список,
		ПоказыватьЭлементыСписков, Выбранные, Тип("СправочникСсылка.Пользователи"));
	
	Если ХранимыеПараметры.ПоказыватьЭлементыСписков = ПоказыватьЭлементыСписков Тогда
		Возврат;
	КонецЕсли;
	
	Если ХранимыеПараметры.ПоказыватьЭлементыСписков = "" Тогда
		ХранимыеПараметры.ГруппыОтображение   = Элементы.Группы.Отображение;
		ХранимыеПараметры.ГруппаТекущаяСтрока = Элементы.Группы.ТекущаяСтрока;
		Элементы.Группы.Отображение = ОтображениеТаблицы.Список;
		
	ИначеЕсли ПоказыватьЭлементыСписков = "" Тогда
		Элементы.Группы.Отображение = ХранимыеПараметры.ГруппыОтображение;
		Если Не ЗначениеЗаполнено(Элементы.Группы.ТекущаяСтрока) Тогда
			Элементы.Группы.ТекущаяСтрока = ХранимыеПараметры.ГруппаТекущаяСтрока;
		КонецЕсли;
	КонецЕсли;
	
	ХранимыеПараметры.ПоказыватьЭлементыСписков = ПоказыватьЭлементыСписков;
	
	ОбновитьСодержимоеФормыПриИзмененииГруппы();
	ПоказатьНедействительныхПользователей(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПоказатьНедействительныхПользователей(Форма);
	
	Скрыть = Форма.ПоказыватьЭлементыСписков = ""
		И Не Форма.Элементы.СписокПоказатьНедействительныхПользователей.Пометка;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список, "Недействителен", Ложь, , , Скрыть);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделенные(Элемент)
	
	Если Не ХранимыеПараметры.РежимВыбора
	 Или Не ХранимыеПараметры.ВыборГрупп
	   И Элемент = Элементы.Группы Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.МножественныйВыбор Тогда
		Если ЗначениеЗаполнено(Элемент.ВыделенныеСтроки) Тогда
			ОповеститьОВыборе(Элемент.ВыделенныеСтроки);
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(Элемент.ТекущаяСтрока) Тогда
		ОповеститьОВыборе(Элемент.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаВыбратьИЗакрытьПриРасширенномПодборе(Результат, Контекст) Экспорт
	
	МассивПользователей = РезультатВыбора();
	ОповеститьОВыборе(МассивПользователей);
	Закрыть(МассивПользователей);
	
КонецПроцедуры

&НаСервере
Функция РезультатВыбора()
	
	ВыбранныеПользователи = Новый Массив;
	Для Каждого Выбранный Из Выбранные Цикл
		Если Выбранный.Изменение <> "-" Тогда
			ВыбранныеПользователи.Добавить(Выбранный.Пользователь);
		КонецЕсли;
	КонецЦикла;
	
	Если ХранимыеПараметры.ВыборУчастниковОбсуждения
	   И ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Обсуждения") Тогда
		
		МодульОбсуждения = ОбщегоНазначения.ОбщийМодуль("Обсуждения");
		ИдентификаторыУчастников = МодульОбсуждения.ПользователиСистемыВзаимодействия(ВыбранныеПользователи);
		МассивПользователей = Новый Массив;
		Для Каждого ПользовательИдентификатор Из ИдентификаторыУчастников Цикл
			Если ПользовательИдентификатор.Значение <> Неопределено Тогда
				МассивПользователей.Добавить(ПользовательИдентификатор.Значение.Идентификатор);
			КонецЕсли;
		КонецЦикла;
	Иначе
		МассивПользователей = ВыбранныеПользователи;
	КонецЕсли;
	
	Возврат МассивПользователей;
	
КонецФункции

#Область ПеретаскиваниеПользователей

// Обработчик вопроса.
//
// Параметры:
//  Ответ - КодВозвратаДиалога
//  Контекст - Структура:
//    * Пользователи - Массив из СправочникСсылка.Пользователи
//    * НоваяГруппа  - СправочникСсылка.ГруппыПользователей
//    * СтараяГруппа - СправочникСсылка.ГруппыПользователей
//    * Перемещение - Булево
//
&НаКлиенте
Процедура ГруппыПеретаскиваниеОбработкаОтветаНаВопрос(Ответ, Контекст) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	СообщениеПользователю = ПеремещениеПользователяВНовуюГруппу(Контекст.Пользователи,
		Контекст.НоваяГруппа, Контекст.СтараяГруппа, Контекст.Перемещение);
	
	Если СообщениеПользователю.Сообщение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("Запись_ГруппыПользователей");
	
	Если СообщениеПользователю.ЕстьОшибки = Ложь Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Перемещение пользователей'"),,
			СообщениеПользователю.Сообщение, БиблиотекаКартинок.ДиалогИнформация);
	Иначе
		ПоказатьПредупреждение(, СообщениеПользователю.Сообщение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПеремещениеПользователяВНовуюГруппу(Знач МассивПользователей, Знач НоваяГруппа,
			Знач СтараяГруппа, Знач Перемещение)
	
	СообщениеПользователю = ПользователиСлужебный.ПеремещениеПользователяВНовуюГруппу(
		МассивПользователей, СтараяГруппа, НоваяГруппа, Перемещение);
	
	Элементы.Список.Обновить();
	Элементы.Группы.Обновить();
	
	Возврат СообщениеПользователю;
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
	МодульПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	МодульПодключаемыеКоманды = ОбщегоНазначения.ОбщийМодуль("ПодключаемыеКоманды");
	МодульПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	МодульПодключаемыеКомандыКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиентСервер");
	МодульПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти