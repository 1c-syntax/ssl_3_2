///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Срок.Подсказка
		= Метаданные.РегистрыСведений.СведенияОПользователях.Ресурсы.СрокДействия.Подсказка;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		Возврат;
	КонецЕсли;
	
	Ширина = 45;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаИзменитьФорму", "Видимость", Ложь);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаСправка", "Видимость", Ложь);
	
	КоманднаяПанель.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Низ;
	
	Элементы.Просрочка.Ширина = 10;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТипЗнч(ВладелецФормы) <> Тип("ФормаКлиентскогоПриложения") Тогда
		Возврат;
	КонецЕсли;
	
	Просрочка = ВладелецФормы.ПросрочкаРаботыВПрограммеДоЗапрещенияВхода;
	Срок      = ВладелецФормы.СрокДействия;
	
	Если ВладелецФормы.СрокДействияНеОграничен Тогда
		ВидСрока = "НеОграничен";
		
	ИначеЕсли ЗначениеЗаполнено(Срок) Тогда
		ВидСрока = "ДоДаты";
		
	ИначеЕсли ЗначениеЗаполнено(Просрочка) Тогда
		ВидСрока = "Просрочка";
	Иначе
		ВидСрока = "НеУказан";
	КонецЕсли;
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидСрокаПриИзменении(Элемент)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьСообщения();
	
	Если ВидСрока = "ДоДаты" Тогда
		Если Не ЗначениеЗаполнено(Срок) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Дата не указана.'"),, "Срок");
			Возврат;
			
		ИначеЕсли Срок <= НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса()) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Ограничение должно быть до завтра или более.'"),, "Срок");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВладелецФормы.Модифицированность = Истина;
	ВладелецФормы.ПросрочкаРаботыВПрограммеДоЗапрещенияВхода = Просрочка;
	ВладелецФормы.СрокДействия = Срок;
	ВладелецФормы.СрокДействияНеОграничен = (ВидСрока = "НеОграничен");
	
	Элементы.ФормаПродолжить.Доступность = Ложь;
	ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьВидимостьДоступность()
	
	Если ВидСрока = "ДоДаты" Тогда
		Элементы.Срок.АвтоОтметкаНезаполненного = Истина;
		Элементы.Срок.Видимость = Истина;
	Иначе
		Элементы.Срок.АвтоОтметкаНезаполненного = Ложь;
		Срок = Неопределено;
		Элементы.Срок.Видимость = Ложь;
	КонецЕсли;
	
	Если ВидСрока <> "Просрочка" Тогда
		Просрочка = 0;
	ИначеЕсли Просрочка = 0 Тогда
		Просрочка = 60;
	КонецЕсли;
	Элементы.Просрочка.Видимость = ВидСрока = "Просрочка";
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму()
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
