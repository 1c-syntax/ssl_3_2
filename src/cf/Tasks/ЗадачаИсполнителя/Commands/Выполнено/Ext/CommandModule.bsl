///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды = Неопределено Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Не выбраны задачи.'"));
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПараметрКоманды", ПараметрКоманды);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВопроса", ЭтотОбъект, ДополнительныеПараметры);
	
	Если ТипЗнч(ПараметрКоманды) = Тип("Массив") И ПараметрКоманды.Количество() > 1 Тогда
		ТекстВопроса =  НСтр("ru='Отметить задачи как выполненные?'");
	Иначе
		ТекстВопроса =  НСтр("ru='Отметить задачу как выполненную?'");
	КонецЕсли;
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопроса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрКоманды= ДополнительныеПараметры.ПараметрКоманды;
	
	ОчиститьСообщения();
	РезультатВыполненияЗадачи = ВыполнитьЗадачи(ПараметрКоманды);
	ПоказатьОповещениеПользователя(РезультатВыполненияЗадачи.ТекстСообщения, РезультатВыполненияЗадачи.НавигационнаяСсылка, РезультатВыполненияЗадачи.Пояснение);
	Оповестить("Запись_ЗадачаИсполнителя", Новый Структура("Выполнена", Истина), ПараметрКоманды);

КонецПроцедуры

&НаСервере
Функция ВыполнитьЗадачи(Знач Задачи)
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстСообщения", НСтр("ru = 'Задачи выполнены'"));
	Результат.Вставить("НавигационнаяСсылка", Неопределено);
	Результат.Вставить("Пояснение", "");
	Для Каждого Задача Из Задачи Цикл
		БизнесПроцессыИЗадачиСервер.ВыполнитьЗадачу(Задача, Истина);
		Если Результат.НавигационнаяСсылка = Неопределено Тогда
			Результат.ТекстСообщения = НСтр("ru = 'Задача выполнена'");
			Результат.НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Задача);
			Результат.Пояснение = Строка(Задача);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

#КонецОбласти