///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.
Процедура ПередДобавлениемКомандОтчетов(КомандыОтчетов, Параметры, СтандартнаяОбработка) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Отчеты.ПраваРолей)
	 Или СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации() Тогда
		Возврат;
	КонецЕсли;
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Менеджер = "Отчет.ПраваРолей";
	Команда.КлючВарианта = "ПраваРолейИПрофилейНаОбъектМетаданных";
	Команда.ИспользованиеТекущейСтроки = "НеИспользует";
	Команда.ТолькоВоВсехДействиях = Истина;
	Команда.МножественныйВыбор = Ложь;
	Команда.Важность = "СмТакже";
	
	Если Не СтрНачинаетсяС(Параметры.ИмяФормы, "Справочник.Пользователи")
	   И Не СтрНачинаетсяС(Параметры.ИмяФормы, "Справочник.ГруппыПользователей")
	   И Не СтрНачинаетсяС(Параметры.ИмяФормы, "Справочник.ВнешниеПользователи")
	   И Не СтрНачинаетсяС(Параметры.ИмяФормы, "Справочник.ГруппыВнешнихПользователей")
	   И Не СтрНачинаетсяС(Параметры.ИмяФормы, "Справочник.ПрофилиГруппДоступа") Тогда
		
		Команда.Представление = НСтр("ru = 'Права ролей и профилей'");
	Иначе
		Команда.Представление = НСтр("ru = 'Права ролей и профилей (на таблицу)'");
	КонецЕсли;
	
	Если СтрНачинаетсяС(Параметры.ИмяФормы, "Справочник.ПрофилиГруппДоступа") Тогда
		КомандаРоли = КомандыОтчетов.Добавить();
		КомандаРоли.Менеджер = "Отчет.ПраваРолей";
		КомандаРоли.КлючВарианта = "ПраваРолей";
		КомандаРоли.ИспользованиеТекущейСтроки = "НеИспользует";
		
		КомандаПрофили = КомандыОтчетов.Добавить();
		КомандаПрофили.Менеджер = "Отчет.ПраваРолей";
		КомандаПрофили.КлючВарианта = "ПраваРолиИПрофилейНаОбъектыМетаданных";
		КомандаПрофили.ИспользованиеТекущейСтроки = "НеИспользует";
		
		Если Параметры.ИмяФормы = "Справочник.ПрофилиГруппДоступа.Форма.ФормаЭлемента" Тогда
			КомандаРоли.Представление    = НСтр("ru = 'Права ролей профиля'");
			КомандаПрофили.Представление = НСтр("ru = 'Права профиля'");
		Иначе
			КомандаРоли.Представление    = НСтр("ru = 'Права ролей профилей'");
			КомандаПрофили.Представление = НСтр("ru = 'Права профилей'");
			
			Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
				КомандаРолиВСтроке = КомандыОтчетов.Добавить();
				ЗаполнитьЗначенияСвойств(КомандаРолиВСтроке, КомандаРоли);
				КомандаРолиВСтроке.ИспользованиеТекущейСтроки = "Использует";
				КомандаРолиВСтроке.МножественныйВыбор = Ложь;
				КомандаРолиВСтроке.Представление = НСтр("ru = 'Права ролей профиля'");
				
				КомандаПрофилиВСтроке = КомандыОтчетов.Добавить();
				ЗаполнитьЗначенияСвойств(КомандаПрофилиВСтроке, КомандаПрофили);
				КомандаПрофилиВСтроке.ИспользованиеТекущейСтроки = "Использует";
				КомандаПрофилиВСтроке.МножественныйВыбор = Ложь;
				КомандаПрофилиВСтроке.Представление = НСтр("ru = 'Права профиля'");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   НастройкиОтчета - см. ВариантыОтчетов.ОписаниеОтчета.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	Иначе
		Возврат;
	КонецЕсли;
	
	МодульВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Ложь);
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ПраваРолей");
	НастройкиВарианта.Описание = НСтр("ru = 'Показывает права ролей на объекты метаданных.'");
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ПраваРолиИПрофилейНаОбъектыМетаданных");
	НастройкиВарианта.Описание = НСтр("ru = 'Показывает права одной роли и профилей на разные объекты метаданных.'");
	НастройкиВарианта.Включен = Ложь;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ПраваРолейИПрофилейНаОбъектМетаданных");
	НастройкиВарианта.Описание = НСтр("ru = 'Показывает права разных ролей и профилей на один объект метаданных.'");
	НастройкиВарианта.Включен = Ложь;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ПодробныеПраваРолейНаОбъектМетаданных");
	НастройкиВарианта.Описание = НСтр("ru = 'Показывает подробные права разных ролей на один объект метаданных.'");
	НастройкиВарианта.Включен = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
