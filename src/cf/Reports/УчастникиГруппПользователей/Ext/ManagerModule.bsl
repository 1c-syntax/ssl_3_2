///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.
Процедура ПередДобавлениемКомандОтчетов(КомандыОтчетов, Параметры, СтандартнаяОбработка) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов")
	 Или Не ПравоДоступа("Просмотр", Метаданные.Отчеты.УчастникиГруппПользователей)
	 Или СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации() Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.ИмяФормы = "Справочник.Пользователи.Форма.ФормаСписка"
	 Или Параметры.ИмяФормы = "Справочник.ГруппыПользователей.Форма.ФормаСписка" Тогда
		
		ДобавитьКоманду(КомандыОтчетов, Истина, "НеИспользует",
			НСтр("ru = 'Участники групп пользователей'"),
			Новый ОписаниеТипов("СправочникСсылка.Пользователи,
			                    |СправочникСсылка.ГруппыПользователей"));
		
		ДобавитьКоманду(КомандыОтчетов, Ложь, "Использует",
			НСтр("ru = 'Участник групп пользователей'"),
			Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
		
		ДобавитьКоманду(КомандыОтчетов, Ложь, "Использует",
			НСтр("ru = 'Участники группы пользователей'"),
			Новый ОписаниеТипов("СправочникСсылка.ГруппыПользователей"));
		
	ИначеЕсли Параметры.ИмяФормы = "Справочник.Пользователи.Форма.ФормаЭлемента" Тогда
		
		ДобавитьКоманду(КомандыОтчетов, Неопределено, Неопределено,
			НСтр("ru = 'Участник групп пользователей'"),
			Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	
	ИначеЕсли Параметры.ИмяФормы = "Справочник.ГруппыПользователей.Форма.ФормаЭлемента" Тогда
		
		ДобавитьКоманду(КомандыОтчетов, Неопределено, Неопределено,
			НСтр("ru = 'Участники группы пользователей'"),
			Новый ОписаниеТипов("СправочникСсылка.ГруппыПользователей"));
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   НастройкиОтчета - см. ВариантыОтчетов.ОписаниеОтчета.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		Возврат;
	КонецЕсли;
	
	МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Основной");
	НастройкиВарианта.Описание =
		НСтр("ru = 'Выводит сведения о вхождении пользователей в группы пользователей.'");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Для процедуры ПередДобавлениемКомандОтчетов.
Процедура ДобавитьКоманду(КомандыОтчетов, МножественныйВыбор, ИспользованиеТекущейСтроки, Представление, ТипПараметра)
	
	Если ИспользованиеТекущейСтроки = "Использует"
	   И Не ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		Возврат;
	КонецЕсли;
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Представление = Представление;
	Команда.Менеджер = "Отчет.УчастникиГруппПользователей";
	Команда.КлючВарианта = "Основной";
	Команда.Важность = "Обычная";
	Команда.ТипПараметра = ТипПараметра;
	Команда.ФункциональныеОпции = "ИспользоватьГруппыПользователей";
	Команда.МножественныйВыбор = МножественныйВыбор;
	Команда.ИспользованиеТекущейСтроки = ИспользованиеТекущейСтроки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли