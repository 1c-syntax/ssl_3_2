///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(
		ЭтотОбъект,
		Параметры,
		"ДатаИзмененияВРабочемКаталоге,
		|ДатаИзмененияВХранилищеФайлов,
		|ПолноеИмяФайлаВРабочемКаталоге,
		|РазмерВРабочемКаталоге,
		|РазмерВХранилищеФайлов,
		|Сообщение,
		|Заголовок");
		
	ТестНовее = " (" + НСтр("ru = 'новее'") + ")";
	Если ДатаИзмененияВРабочемКаталоге > ДатаИзмененияВХранилищеФайлов Тогда
		ДатаИзмененияВРабочемКаталоге = Строка(ДатаИзмененияВРабочемКаталоге) + ТестНовее;
	ИначеЕсли ДатаИзмененияВРабочемКаталоге < ДатаИзмененияВХранилищеФайлов Тогда
		ДатаИзмененияВХранилищеФайлов = Строка(ДатаИзмененияВХранилищеФайлов) + ТестНовее;
	КонецЕсли;
	
	Элементы.ДекорацияСообщение.Заголовок = Сообщение;
	
	Если Параметры.ДействиеНадФайлом = "ПомещениеВХранилищеФайлов" Тогда
		
		Элементы.ФормаОткрытьСуществующий.Видимость = Ложь;
		Элементы.ФормаВзятьИзХранилища.Видимость    = Ложь;
		Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
			Элементы.ФормаПоместить.Важность = ВажностьКнопки.Главная;
		Иначе
			Элементы.ФормаПоместить.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
		
	ИначеЕсли Параметры.ДействиеНадФайлом = "ОткрытиеВРабочемКаталоге" Тогда
		
		Элементы.ФормаПоместить.Видимость  = Ложь;
		Элементы.ФормаНеПомещать.Видимость = Ложь;
		Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
			Элементы.ФормаОткрытьСуществующий.Важность = ВажностьКнопки.Главная;
		Иначе
			Элементы.ФормаОткрытьСуществующий.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестное действие над файлом'");
	КонецЕсли;
	
	ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьСуществующий(Команда)
	
	Закрыть("ОткрытьСуществующий");
	
КонецПроцедуры

&НаКлиенте
Процедура Поместить(Команда)
	
	Закрыть("ПОМЕСТИТЬ");
	
КонецПроцедуры

&НаКлиенте
Процедура ВзятьИзПрограммы(Команда)
	
	Закрыть("ВзятьИзХранилищаИОткрыть");
	
КонецПроцедуры

&НаКлиенте
Процедура НеПомещать(Команда)
	
	Закрыть("НеПомещать");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКаталог(Команда)
	
	РаботаСФайламиСлужебныйКлиент.ОткрытьПроводникСФайлом(ПолноеИмяФайлаВРабочемКаталоге);
	
КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	
	Закрыть("Отменить");
	
КонецПроцедуры

#КонецОбласти
