///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Элементы.ПутьКАрхиву.КнопкаВыбора = Ложь;
		Элементы.ПутьКАрхиву.ПодсказкаВвода = "\\servername\resource\files.zip";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутьКАрхивуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не РаботаСФайламиСлужебныйКлиент.Расширение1СПредприятияПодключено() Тогда
		РаботаСФайламиСлужебныйКлиент.ПоказатьПредупреждениеОНеобходимостиРасширения1СПредприятия(Неопределено);
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	Диалог.Заголовок                    = НСтр("ru = 'Выберите файл'");
	Диалог.ПолноеИмяФайла               = ?(ПутьКАрхиву= "", "files.zip", ПутьКАрхиву);
	Диалог.МножественныйВыбор           = Ложь;
	Диалог.ПредварительныйПросмотр      = Ложь;
	Диалог.ПроверятьСуществованиеФайла  = Истина;
	Диалог.Фильтр                       = НСтр("ru = 'Архивы zip(*.zip)|*.zip'");
	
	Если Диалог.Выбрать() Тогда
		
		ПутьКАрхиву = Диалог.ПолноеИмяФайла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Разместить(Команда)
	
	ОчиститьСообщения();
	
	Если ПустаяСтрока(ПутьКАрхиву) Тогда
		Текст = НСтр("ru = 'Укажите полное имя архива с файлами начального образа (файл *.zip)'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(Текст, , "ПутьКАрхиву");
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиент.ИнформационнаяБазаФайловая() Тогда
	
		Если Не ПустаяСтрока(ПутьКАрхиву) И (Лев(ПутьКАрхиву, 2) <> "\\" ИЛИ СтрНайти(ПутьКАрхиву, ":") <> 0) Тогда
			ТекстОшибки = НСтр("ru = 'Путь к архиву с файлами начального образа должен быть в формате UNC (\\servername\resource).'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "ПутьКАрхиву");
			Возврат;
		КонецЕсли;
	
	КонецЕсли;
	
	ДобавитьФайлыВТома();
	
	ТекстОповещения = НСтр("ru = 'Размещение файлов из архива с файлами
		|начального образа успешно завершено.'");
	ПоказатьОповещениеПользователя(НСтр("ru = 'Размещение файлов'"),, ТекстОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьФайлыВТома()
	
	РаботаСФайламиВТомахСлужебный.ДобавитьФайлыВТома(ПутьКАрхиву);
	
КонецПроцедуры

#КонецОбласти
