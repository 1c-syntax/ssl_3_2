///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		Возврат;
	КонецЕсли;
	
	КоманднаяПанель.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Низ;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаИзменитьФорму", "Видимость", Ложь);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаСправка", "Видимость", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПараметрыИнформирования = ПользователиСлужебныйКлиент.ПараметрыИнформированияОПерезапуске();
	Если Не ЗначениеЗаполнено(ПараметрыИнформирования.ДатаПерезапуска) Тогда
		ВывестиСообщение(Истина);
		Возврат;
	КонецЕсли;
	
	Элементы.ФормаНапомнитьЗавтра.Видимость = Ложь;
	ПредставлениеОсталосьМинут = ПользователиСлужебныйКлиент.ПредставлениеОсталосьМинутДоПерезапуска(
		ПользователиСлужебныйКлиент.ОсталосьМинутДоПерезапуска(ПараметрыИнформирования.ДатаПерезапуска));
	
	ВывестиСообщение(Ложь, ПредставлениеОсталосьМинут);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Перезапустить(Команда)
	
	СтандартныеПодсистемыКлиент.ПропуститьПредупреждениеПередЗавершениемРаботыСистемы();
	ЗавершитьРаботуСистемы(Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НапомнитьЗавтра(Команда)
	
	НапомнитьЗавтраНаСервере();
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


&НаСервере
Процедура ВывестиСообщение(Знач ЭтоИнформация, Знач ПредставлениеОсталосьМинут = "")
	
	ПараметрыСообщения = ПользователиСлужебный.ПараметрыСообщения();
	ПараметрыСообщения.ТипСообщения = ?(ЭтоИнформация, "Информация", "Предупреждение");
	ПараметрыСообщения.Группа = Элементы.ГруппаПредупреждение;
	ПараметрыСообщения.Заголовок = НСтр("ru = 'Администратор изменил права доступа'");
	ПараметрыСообщения.Сообщение = ?(ЭтоИнформация,
		НСтр("ru = 'Перезапустите приложение, чтобы они вступили в силу.'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Приложение будет перезапущено через %1, чтобы они вступили в силу.'"),
			ПредставлениеОсталосьМинут));
	ПользователиСлужебный.ВывестиСообщениеНаФорму(ЭтотОбъект, ПараметрыСообщения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НапомнитьЗавтраНаСервере()
	
	ОбщегоНазначения.ХранилищеСистемныхНастроекСохранить("КонтрольИзмененияРолейПользователяИБ",
		"ДатаНапомнитьЗавтра", НачалоДня(ТекущаяДатаСеанса()) + 60*60*24);
	
КонецПроцедуры

#КонецОбласти
