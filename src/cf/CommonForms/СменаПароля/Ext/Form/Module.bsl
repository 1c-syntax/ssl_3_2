///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриВходеВПрограмму        = Параметры.ПриВходеВПрограмму;
	ВернутьПарольБезУстановки = Параметры.ВернутьПарольБезУстановки;
	СтарыйПароль              = Параметры.СтарыйПароль;
	ИмяДляВхода               = Параметры.ИмяДляВхода;
	
	Если ВернутьПарольБезУстановки Или ЗначениеЗаполнено(Параметры.Пользователь) Тогда
		Пользователь = Параметры.Пользователь;
	Иначе
		Пользователь = Пользователи.АвторизованныйПользователь();
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	Если Не ВернутьПарольБезУстановки Тогда
		ДополнительныеПараметры.Вставить("ПроверятьДействительностьПользователя");
		ДополнительныеПараметры.Вставить("ПроверятьНаличиеПользователяИБ");
	КонецЕсли;
	Если Не ПользователиСлужебный.ВозможноИзменитьПароль(Пользователь, ДополнительныеПараметры) Тогда
		ТекстОшибки = ДополнительныеПараметры.ТекстОшибки;
		Возврат;
	КонецЕсли;
	ЭтоТекущийПользовательИБ = ДополнительныеПараметры.ЭтоТекущийПользовательИБ;
	ПарольУстановлен         = ДополнительныеПараметры.ПарольУстановлен;
	Если Не ЗначениеЗаполнено(ИмяДляВхода) Тогда
		ИмяДляВхода = ДополнительныеПараметры.ИмяДляВхода;
	КонецЕсли;
	
	Если Не ВернутьПарольБезУстановки Тогда
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Пользователь, , УникальныйИдентификатор);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			Если Не ПриВходеВПрограмму Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось открыть форму смены пароля по причине:
					           |
					           |%1'"),
					ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
				Возврат;
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
	Если Не ПриВходеВПрограмму Тогда
		Элементы.ПояснениеПриВходе.Видимость = Ложь;
		
	ИначеЕсли Не ПарольУстановлен Тогда
		Элементы.ПояснениеПриВходе.Заголовок =
			НСтр("ru = 'Для входа в приложение установите пароль.'")
	КонецЕсли;
	
	СтандартныеПодсистемыСервер.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
	Если Не ЭтоТекущийПользовательИБ
	 Или Не ПарольУстановлен Тогда
		
		Элементы.СтарыйПароль.Видимость = Ложь;
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru = 'Установка пароля'");
		
	ИначеЕсли Параметры.СтарыйПароль <> Неопределено Тогда
		ТекущийЭлемент = Элементы.НовыйПароль;
	
	ИначеЕсли ПриВходеВПрограмму
	        И ЭтоТекущийПользовательИБ
	        И Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		Элементы.СтарыйПароль.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.НовыйПароль.Подсказка  = ПользователиСлужебный.ПодсказкаДляНовогоПароля();
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		Возврат;
	КонецЕсли;
	
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Низ;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаИзменитьФорму", "Видимость", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Отказ = Истина;
		СтандартныеПодсистемыКлиент.УстановитьХранениеФормы(ЭтотОбъект, Истина);
		ПодключитьОбработчикОжидания("ПоказатьТекстОшибкиИСообщитьОЗакрытии", 0.1, Истина);
	Иначе
		ОбновитьОбработчикОжиданияПоказатьСтарыйПароль();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ПарольНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Элемент.РежимПароля = Не Элемент.РежимПароля;
	Элемент.КартинкаКнопкиВыбора = ?(Элемент.РежимПароля,
		БиблиотекаКартинок.ПоказатьПароль, БиблиотекаКартинок.СкрытьПароль);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьПароль(Команда)
	
	НовыйПароль = НовыйПароль(ТипЗнч(Пользователь) = Тип("СправочникСсылка.ВнешниеПользователи"));
	Подтверждение = "";
	
	Если Элементы.НовыйПароль.РежимПароля Тогда
		ПарольНачалоВыбора(Элементы.НовыйПароль, Неопределено, Ложь, Ложь);
	КонецЕсли;
	Если Не Элементы.Подтверждение.РежимПароля Тогда
		ПарольНачалоВыбора(Элементы.Подтверждение, Неопределено, Ложь, Ложь);
	КонецЕсли;
	
	ТекущийЭлемент = Элементы.Подтверждение;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПароль(Команда)
	
	ОчиститьСообщения();
	
	Если НовыйПароль <> Подтверждение Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Подтверждение пароля указано некорректно.'");
		Сообщение.ПутьКДанным = "Подтверждение";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если Не ВернутьПарольБезУстановки
	   И Не ЭтоТекущийПользовательИБ
	   И ОбщегоНазначенияКлиент.РазделениеВключено()
	   И ПарольПользователяСервиса = Неопределено Тогда
		
		ПользователиСлужебныйКлиент.ЗапроситьПарольДляАутентификацииВСервисе(
			Новый ОписаниеОповещения("УстановитьПарольЗавершение", ЭтотОбъект),
			ЭтотОбъект,
			ПарольПользователяСервиса);
	Иначе
		УстановитьПарольЗавершение(Null, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьОбработчикОжиданияПоказатьСтарыйПароль()
	
	Если ПриВходеВПрограмму
	   И ЭтоТекущийПользовательИБ
	   И ПарольУстановлен
	   И Не Элементы.СтарыйПароль.Видимость Тогда
		
		ОтключитьОбработчикОжидания("ПоказатьСтарыйПарольОбработчикОжидания");
		ПодключитьОбработчикОжидания("ПоказатьСтарыйПарольОбработчикОжидания", 60, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСтарыйПарольОбработчикОжидания()
	
	Элементы.СтарыйПароль.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТекстОшибкиИСообщитьОЗакрытии()
	
	СтандартныеПодсистемыКлиент.УстановитьХранениеФормы(ЭтотОбъект, Ложь);
	
	ПоказатьПредупреждение(Новый ОписаниеОповещения(
		"ПоказатьТекстОшибкиИСообщитьОЗакрытииЗавершение", ЭтотОбъект), ТекстОшибки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьТекстОшибкиИСообщитьОЗакрытииЗавершение(Контекст) Экспорт
	
	Если ОписаниеОповещенияОЗакрытии <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии);
		ОписаниеОповещенияОЗакрытии = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры УстановитьПароль.
&НаКлиенте
Процедура УстановитьПарольЗавершение(НовыйПарольПользователяСервиса, Контекст) Экспорт
	
	Если НовыйПарольПользователяСервиса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НовыйПарольПользователяСервиса <> Null Тогда
		ПарольПользователяСервиса = НовыйПарольПользователяСервиса;
	КонецЕсли;
	
	Результат = ПарольУстановленНаСервере();
	
	Если Результат.Установлен Тогда
		Элементы.ФормаУстановитьПароль.Доступность = Ложь;
		ПодключитьОбработчикОжидания("ВернутьРезультатИЗакрытьФорму", 0.1, Истина);
		
	ИначеЕсли ЗначениеЗаполнено(Результат.ПутьКДанным) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Результат.ТекстОшибки;
		Сообщение.ПутьКДанным = Результат.ПутьКДанным;
		Сообщение.Сообщить();
		
	ИначеЕсли ЗначениеЗаполнено(Результат.ТекстОшибки) Тогда
		ПоказатьПредупреждение(, Результат.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПарольУстановленНаСервере()
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("Пользователь",              Пользователь);
	ПараметрыВыполнения.Вставить("ИмяДляВхода",               ИмяДляВхода);
	ПараметрыВыполнения.Вставить("НовыйПароль",               НовыйПароль);
	ПараметрыВыполнения.Вставить("СтарыйПароль",              СтарыйПароль);
	ПараметрыВыполнения.Вставить("ПриВходеВПрограмму",        ПриВходеВПрограмму);
	ПараметрыВыполнения.Вставить("ПарольПользователяСервиса", ПарольПользователяСервиса);
	ПараметрыВыполнения.Вставить("ТолькоПроверить",           ВернутьПарольБезУстановки);
	
	Если Не ОбщегоНазначения.РазделениеВключено()
	   И Не Элементы.СтарыйПароль.Видимость Тогда
		ПараметрыВыполнения.СтарыйПароль = Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Установлен", Ложь);
	Результат.Вставить("ТекстОшибки", "");
	Результат.Вставить("ПутьКДанным", "");
	
	Попытка
		ТекстОшибки = ПользователиСлужебный.ОбработатьНовыйПароль(ПараметрыВыполнения);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Если ПараметрыВыполнения.Свойство("ОшибкаЗаписанаВЖурналРегистрации") Тогда
			Результат.ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
			Возврат Результат;
		Иначе
			ВызватьИсключение;
		КонецЕсли;
	КонецПопытки;
	
	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		Результат.Установлен = Истина;
		Возврат Результат;
	КонецЕсли;
	
	Результат.ТекстОшибки = ТекстОшибки;
	
	Если Не Элементы.СтарыйПароль.Видимость
	 Или ПараметрыВыполнения.СтарыйПарольСовпадает Тогда
		Результат.ПутьКДанным = "НовыйПароль";
	Иначе
		Результат.ПутьКДанным = "СтарыйПароль";
	КонецЕсли;
	
	ПарольПользователяСервиса = ПараметрыВыполнения.ПарольПользователяСервиса;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВернутьРезультатИЗакрытьФорму()
	
	Результат = Новый Структура;
	Если ВернутьПарольБезУстановки Тогда
		Результат.Вставить("НовыйПароль",  НовыйПароль);
		Результат.Вставить("СтарыйПароль", ?(Элементы.СтарыйПароль.Видимость, СтарыйПароль, Неопределено));
	Иначе
		Результат.Вставить("УстановленПустойПароль", НовыйПароль = "");
	КонецЕсли;
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НовыйПароль(ДляВнешнегоПользователя)
	
	СвойстваПароля = Пользователи.СвойстваПароля();
	СвойстваПароля.НаименьшаяДлина = 8;
	СвойстваПароля.УчестьНастройки = ?(ДляВнешнегоПользователя, "ДляВнешнихПользователей", "ДляПользователей");
	
	Возврат Пользователи.СоздатьПароль(СвойстваПароля);
	
КонецФункции

#КонецОбласти
