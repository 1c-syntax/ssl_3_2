///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ВидыКонтактнойИнформации = УправлениеКонтактнойИнформацией.ВидыКонтактнойИнформацииОбъекта(
		Справочники.Пользователи.ПустаяСсылка(), Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Если ВидыКонтактнойИнформации.Количество() > 0 Тогда
		ВидЭлектроннаяПочта = ВидыКонтактнойИнформации[0].Ссылка;
	Иначе
		ВидЭлектроннаяПочта = Неопределено;
	КонецЕсли;
	Если СписокПользователей.Параметры.Элементы.Найти("ЭлектроннаяПочта") <> Неопределено Тогда
		СписокПользователей.Параметры.УстановитьЗначениеПараметра("ЭлектроннаяПочта", ВидЭлектроннаяПочта);
	КонецЕсли;
	
	Взаимодействия.ОбработатьНеобходимостьОтображенияГруппПользователей(ЭтотОбъект);
	
	// Заполним контакты по предмету.
	Предмет = Параметры.Предмет;
	Взаимодействия.ЗаполнитьКонтактыПоПредмету(Элементы, Предмет, КонтактыПоПредмету, Истина);
	
	Взаимодействия.ДобавитьСтраницыФормыПодбораКонтактов(ЭтотОбъект);
	ЗаполнитьТаблицуПолучателей();
	УстановитьГруппуПоУмолчанию();
	
	ВариантыПоиска = "Везде";
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокПользователей, "Выбранные", СписокПолучателей);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьМенюВариантовПоиска();
	ПодключитьОбработчикОжидания("СкрытьСтандартныеКомандыСоздания", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ВзаимодействияКлиент.ОтработатьОповещение(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ПриИзмененииТолькоКонтактыСАдресами(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СвернутьГруппуНажатие(Элемент)
	ТекущийЭлемент = Элементы.ГруппаСпискаНастройки;
КонецПроцедуры

&НаКлиенте
Процедура КонтактНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ПолучателиПисьма.ТекущиеДанные;
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ТолькоEmail",                       Истина);
	ПараметрыОткрытия.Вставить("ТолькоТелефон",                     Ложь);
	ПараметрыОткрытия.Вставить("ЗаменятьПустыеАдресИПредставление", Истина);
	ПараметрыОткрытия.Вставить("ДляФормыУточненияКонтактов",        Ложь);
	ПараметрыОткрытия.Вставить("ИдентификаторФормы",                УникальныйИдентификатор);

	ВзаимодействияКлиент.ВыбратьКонтакт(Предмет, ТекущиеДанные.Адрес, ТекущиеДанные.Представление,
	                                    ТекущиеДанные.Контакт,ПараметрыОткрытия)
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПисьмаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Группа = "Кому";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПисьмаПриАктивизацииЯчейки(Элемент)
	
	Если Элемент.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент.Имя = "Адрес" Тогда
		Элементы.Адрес.СписокВыбора.Очистить();
		
		ТекущиеДанные = Элементы.ПолучателиПисьма.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ТекущиеДанные.СписокАдресов) Тогда
			Элементы.Адрес.СписокВыбора.ЗагрузитьЗначения(
				СтрРазделить(ТекущиеДанные.СписокАдресов, ";"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактыПоПредметуВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ДобавитьПолучателяИзСпискаПоПредмету();

КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаОчистка(Элемент, СтандартнаяОбработка)
	НайденныеКонтакты.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	НайтиКонтактыВыполнить();
КонецПроцедуры

// Универсальный обработчик активизации строки динамического списка, у которого есть подчиненные списки.
&НаКлиенте
Процедура Подключаемый_СписокВладелецПриАктивизацииСтроки(Элемент)
	
	ВзаимодействияКлиент.КонтактВладелецПриАктивизацииСтроки(Элемент, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПользователейПриАктивизацииСтроки(Элемент)
	
	СписокПользователей.Параметры.УстановитьЗначениеПараметра("ГруппаПользователей", Элементы.ГруппыПользователей.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьТолькоКонтактыСАдресамиПриИзменении(Элемент)
	
	ПриИзмененииТолькоКонтактыСАдресами(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСтрокиСписка(Команда)
	Если Команда.Имя = Команды.СписокПользователейВыбрать.Имя Тогда
		ПеренестиВыделенныеСтроки(Элементы.СписокПользователей.ПолучитьСтрокиДляОбработки(), Элементы.СписокПользователей.Имя);
	ИначеЕсли Команда.Имя = Команды.ПоискКонтактовВыбрать.Имя
		Или Команда.Имя = Команды.КонтактыПоПредметуВыбрать.Имя Тогда
		ЭтоПоискКонтактов = (Команда.Имя = Команды.ПоискКонтактовВыбрать.Имя);
		Если ЭтоПоискКонтактов Тогда
			ТаблицаДляОбработки = Элементы.НайденныеКонтакты;
		Иначе
			ТаблицаДляОбработки = Элементы.КонтактыПоПредмету;
		КонецЕсли;
		
		СтрокиДляОбработки = ТаблицаДляОбработки.ПолучитьСтрокиДляОбработки();
		Для Каждого ИндексСтроки Из СтрокиДляОбработки Цикл
			ТекущиеДанные = ТаблицаДляОбработки.ДанныеСтроки(ИндексСтроки);
			Результат = ВзаимодействияВызовСервера.НаименованиеИАдресаЭлектроннойПочтыКонтакта(ТекущиеДанные.Ссылка);
			Если Результат <> Неопределено И Результат.Адреса.Количество() > 0 Тогда
				СписокАдресов = СтрСоединить(Результат.Адреса.ВыгрузитьЗначения(), ";");
			Иначе
				СписокАдресов = "";
			КонецЕсли;
			
			ТекущиеДанные.Выбран = Истина;
			
			Если СписокПолучателей.НайтиПоЗначению(ТекущиеДанные.Ссылка) = Неопределено Тогда
				СписокПолучателей.Добавить(ТекущиеДанные.Ссылка);
			КонецЕсли;
			
			Если ЭтоПоискКонтактов Тогда
				ДобавитьПолучателя(ТекущиеДанные.Представление, ТекущиеДанные.НаименованиеКонтакта, ТекущиеДанные.Ссылка, СписокАдресов);
			Иначе
				ДобавитьПолучателя(ТекущиеДанные.Адрес, ТекущиеДанные.Наименование, ТекущиеДанные.Ссылка, СписокАдресов);
			КонецЕсли;
		КонецЦикла;
		
		РассчитатьКоличествоВыбранных();
	Иначе
		ИмяТаблицы = СтрЗаменить(Команда.Имя,
			ВзаимодействияКлиентСервер.ПрефиксКомандыВыбрать(),
			ВзаимодействияКлиентСервер.ПрефиксТаблица());
		Таблица = Элементы.Найти(ИмяТаблицы);
		
		Если Таблица <> Неопределено Тогда
			ПеренестиВыделенныеСтроки(Таблица.ПолучитьСтрокиДляОбработки(), Таблица.Имя);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьСтрокиСписка(Команда)
	Если Команда.Имя = Команды.СписокПользователейИсключить.Имя Тогда
		ИсключитьВыделенныеСтроки(Элементы.СписокПользователей.ПолучитьСтрокиДляОбработки(), Элементы.СписокПользователей.Имя);
	ИначеЕсли Команда.Имя = Команды.ПоискКонтактовИсключить.Имя
		Или Команда.Имя = Команды.КонтактыПоПредметуИсключить.Имя Тогда
		ЭтоПоискКонтактов = (Команда.Имя = Команды.ПоискКонтактовИсключить.Имя);
		Если ЭтоПоискКонтактов Тогда
			ТаблицаДляОбработки = Элементы.НайденныеКонтакты;
		Иначе
			ТаблицаДляОбработки = Элементы.КонтактыПоПредмету;
		КонецЕсли;
		
		СтрокиДляОбработки = ТаблицаДляОбработки.ПолучитьСтрокиДляОбработки();
		Контакты = Новый Массив;
		Для Каждого Строка Из СтрокиДляОбработки Цикл
			ДанныеСтроки = ТаблицаДляОбработки.ДанныеСтроки(Строка);
			ДанныеСтроки.Выбран = Ложь;
			Контакты.Добавить(Новый Структура("Ссылка, Адрес", ДанныеСтроки.Ссылка, ДанныеСтроки.Адрес));
		КонецЦикла;
		
		ИсключитьВыделенныеСтроки(Контакты, ТаблицаДляОбработки.Имя);
	Иначе
		ИмяТаблицы = СтрЗаменить(Команда.Имя,
			ВзаимодействияКлиентСервер.ПрефиксКомандыИсключить(),
			ВзаимодействияКлиентСервер.ПрефиксТаблица());
		Таблица = Элементы.Найти(ИмяТаблицы);
		
		Если Таблица <> Неопределено Тогда
			ИсключитьВыделенныеСтроки(Таблица.ПолучитьСтрокиДляОбработки(), Таблица.Имя);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Передает владельцу в качестве результата работы формы массив структур содержащих 
// адреса выбранных получателей и закрывает форму. 
//
&НаКлиенте
Процедура КомандаОКВыполнить()
	
	Результат = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из ПолучателиПисьма Цикл
		
		Если ПустаяСтрока(СтрокаТаблицы.Адрес) Тогда
			Продолжить;
		КонецЕсли;
		Группа = ?(ПустаяСтрока(СтрокаТаблицы.Группа), "Кому", СтрокаТаблицы.Группа);
		
		Контакт = Новый Структура;
		Контакт.Вставить("Адрес", СтрокаТаблицы.Адрес);
		Контакт.Вставить("Представление", СтрокаТаблицы.Представление);
		Контакт.Вставить("Контакт", СтрокаТаблицы.Контакт);
		Контакт.Вставить("Группа", Группа);
		Результат.Добавить(Контакт);
		
	КонецЦикла;
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

// Переносит текущий контакт из списка "Контакты по предмету" в список "Получатели письма". 
//
&НаКлиенте
Процедура ДобавитьИзСпискаПредметовВыполнить()

	ДобавитьПолучателяИзСпискаПоПредмету();

КонецПроцедуры

// Изменяет текущую группу получателей письма на группу "Кому". 
//
&НаКлиенте
Процедура ИзменитьГруппуКомуВыполнить()

	ИзменитьГруппу("Кому");

КонецПроцедуры

// Изменяет текущую группу получателей письма на группу "Копии". 
//
&НаКлиенте
Процедура ИзменитьГруппуКопииВыполнить()

	ИзменитьГруппу("Копии");

КонецПроцедуры 

// Изменяет текущую группу получателей письма на группу "Скрытые". 
//
&НаКлиенте
Процедура ИзменитьГруппуСкрытыеВыполнить()

	ИзменитьГруппу("Скрытые");

КонецПроцедуры

// Инициирует процесс поиска контактов.
//
&НаКлиенте
Процедура НайтиКонтактыВыполнить()
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не задано, что нужно найти.'"),, "СтрокаПоиска");
		Возврат;
	КонецЕсли;
	
	Результат = "";
	НайденныеКонтакты.Очистить();
	
	Если ВариантыПоиска = "Везде" Тогда
		Результат = НайтиКонтакты();
	ИначеЕсли ВариантыПоиска = "ПоEmail" Тогда
		НайтиПоEmail(Ложь);
	ИначеЕсли ВариантыПоиска = "ПоДомену" Тогда
		НайтиПоEmail(Истина);
	ИначеЕсли ВариантыПоиска = "ПоСтроке" Тогда
		Результат = КонтактыНайденныеПоСтроке();
	ИначеЕсли ВариантыПоиска = "НачинаетсяС" Тогда
		НайтиПоНачалуНаименования();
	КонецЕсли;
	
	Если Не ПустаяСтрока(Результат) Тогда
		ПоказатьПредупреждение(, Результат);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет позиционирование в соответствующем динамическом списке на текущем контакте из 
// списка "Найденные контакты".
//
&НаКлиенте
Процедура НайтиВСпискеИзСпискаНайденныхВыполнить()
	
	ТекущиеДанные = Элементы.НайденныеКонтакты.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		УстановитьТекущимКонтакт(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет позиционирование в соответствующем динамическом списке на текущем контакте
// из списка "Получатели письма".
//
&НаКлиенте
Процедура НайтиВСпискеИзСпискаПолучателейВыполнить()
	
	ТекущиеДанные = Элементы.ПолучателиПисьма.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.Контакт) Тогда
		УстановитьТекущимКонтакт(ТекущиеДанные.Контакт);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет позиционирование в соответствующем динамическом списке на текущем контакте
// из списка "Контакты по предмету".
//
&НаКлиенте
Процедура НайтиВСпискеИзСпискаПредметовВыполнить()
	
	ТекущиеДанные = Элементы.КонтактыПоПредмету.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		УстановитьТекущимКонтакт(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры 

// Инициирует поиск контактов по адресу электронной почты текущей строки списка "Получатели письма". 
//
&НаКлиенте
Процедура НайтиПоАдресуВыполнить()
	
	Элементы.СтраницыСписки.ТекущаяСтраница = Элементы.СтраницаПоискКонтактов;
	НайденныеКонтакты.Очистить();

	ТекущиеДанные = Элементы.ПолучателиПисьма.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СтрокаПоиска = ТекущиеДанные.Адрес;
	Если Не ПустаяСтрока(СтрокаПоиска) Тогда
		НайтиПоEmail(Ложь);
	КонецЕсли;

КонецПроцедуры

// Инициирует поиск контактов по представлению текущей строки списка "Получатели письма". 
//
&НаКлиенте
Процедура НайтиПоПредставлениюВыполнить()
	
	Элементы.СтраницыСписки.ТекущаяСтраница = Элементы.СтраницаПоискКонтактов;
	НайденныеКонтакты.Очистить();
	
	ТекущиеДанные = Элементы.ПолучателиПисьма.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПоиска = ТекущиеДанные.Представление;
	Если Не ПустаяСтрока(СтрокаПоиска) Тогда
		Результат = КонтактыНайденныеПоСтроке();
		Если Не ПустаяСтрока(Результат) Тогда
			ПоказатьПредупреждение(,Результат);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 

// Осуществляет поиск всех адресов электронной почты контакта из списка "Получатели письма" и
 // предлагает пользователю сделать выбор, если у контакта более одного адреса электронной почты.
//
&НаКлиенте
Процедура УстановитьАдресКонтактаВыполнить()
	
	ТекущиеДанные = Элементы.ПолучателиПисьма.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите адрес получателя в списке справа.'"));
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Контакт) Тогда
		КонтактНачалоВыбора(Элементы.ПолучателиПисьма, Неопределено, Истина);
		Возврат;
	КонецЕсли;
	
	Результат = ВзаимодействияВызовСервера.ПолучитьАдресаЭлектроннойПочтыКонтакта(ТекущиеДанные.Контакт);
	Если Результат.Количество() = 0 Тогда
		ПоказатьПредупреждение(, 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У контакта ""%1"" нет адресов электронной почты.'"),
				ТекущиеДанные.Контакт));
		Возврат;
	КонецЕсли;

	Если Результат.Количество() = 1 Тогда
		Адрес = Результат[0].АдресЭП;
		Представление = Результат[0].Представление;
		УстановитьАдресИПредставлениеВыбранногоКонтакта(ТекущиеДанные, Представление, Адрес);
	Иначе
		СписокВыбора = Новый СписокЗначений;
		Номер = 0;
		Для Каждого Элемент Из Результат Цикл
			СписокВыбора.Добавить(Номер, Элемент.ВидНаименование + ": " + Элемент.АдресЭП);
			Номер = Номер + 1;
		КонецЦикла;
		
		ПараметрыОбработкиВыбора = Новый Структура;
		ПараметрыОбработкиВыбора.Вставить("Результат", Результат);
		ПараметрыОбработкиВыбора.Вставить("ТекущиеДанные", ТекущиеДанные);

		ОбработчикОповещенияОЗакрытии = Новый ОписаниеОповещения("СписокВыбораАдресаЭППослеЗавершения", ЭтотОбъект, ПараметрыОбработкиВыбора);

		СписокВыбора.ПоказатьВыборЭлемента(ОбработчикОповещенияОЗакрытии);
	КонецЕсли;

КонецПроцедуры

// Выполняет позиционирование в соответствующем динамическом списке на текущем контакте
// из списка "Контакты по предмету".
//
&НаКлиенте
Процедура УстановитьКонтактИзСпискаПредметовВыполнить()
	
	ТекущиеДанные = Элементы.КонтактыПоПредмету.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		УстановитьКонтактВСпискеПолучателей(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура УдалитьПолучателя(Команда)
	
	ВыделенныеСтроки = Элементы.ПолучателиПисьма.ПолучитьСтрокиДляОбработки();
	Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеСтроки = ПолучателиПисьма.НайтиПоИдентификатору(ВыделеннаяСтрока);
		ПолучателиПисьма.Удалить(ДанныеСтроки);
		
		Удаляемый = СписокПолучателей.НайтиПоЗначению(ДанныеСтроки.Контакт);
		Если Удаляемый <> Неопределено Тогда
			СписокПолучателей.Удалить(Удаляемый);
		КонецЕсли;
		
		// Обработка таблицы со списком найденных.
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Ссылка", ДанныеСтроки.Контакт);
		ПараметрыОтбора.Вставить("Представление", ДанныеСтроки.Адрес);
		РезультатПоиска = НайденныеКонтакты.НайтиСтроки(ПараметрыОтбора);
		Для Каждого Строка Из РезультатПоиска Цикл
			Строка.Выбран = Ложь;
		КонецЦикла;
		
		// Обработка таблицы с контактами по предмету.
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Ссылка", ДанныеСтроки.Контакт);
		РезультатПоиска = КонтактыПоПредмету.НайтиСтроки(ПараметрыОтбора);
		Для Каждого Строка Из РезультатПоиска Цикл
			Строка.Выбран = Ложь;
		КонецЦикла;
	КонецЦикла;
	
	РассчитатьКоличествоВыбранных();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПоискаВезде(Команда)
	ВариантыПоиска = "Везде";
	ОбновитьМенюВариантовПоиска();
КонецПроцедуры

&НаКлиенте
Процедура ВариантПоискаВАдресах(Команда)
	ВариантыПоиска = "ПоEmail";
	ОбновитьМенюВариантовПоиска();
КонецПроцедуры

&НаКлиенте
Процедура ВариантПоискаВНаименованияхКонтактов(Команда)
	ВариантыПоиска = "ПоСтроке";
	ОбновитьМенюВариантовПоиска();
КонецПроцедуры

&НаКлиенте
Процедура ВариантПоискаПоДоменномуИмени(Команда)
	ВариантыПоиска = "ПоДомену";
	ОбновитьМенюВариантовПоиска();
КонецПроцедуры

&НаКлиенте
Процедура Просмотр(Команда)
	Если Элементы.СтраницыСписки.ТекущаяСтраница = Элементы.СтраницаПользователей Тогда
		ТекущиеДанные = Элементы.СписокПользователей.ТекущиеДанные;
	ИначеЕсли ТипЗнч(ТекущийЭлемент) = Тип("ТаблицаФормы") Тогда
		ТекущиеДанные = ТекущийЭлемент.ТекущиеДанные;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаКлиенте
Процедура СкрытьСтандартныеКомандыСоздания()
	
	СкрытьСтандартныеКомандыСозданияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СкрытьСтандартныеКомандыСозданияНаСервере()
	
	СкрываемыеКоманды = Новый Массив;
	СкрываемыеКоманды.Добавить("Создать");
	СкрываемыеКоманды.Добавить("СоздатьГруппу");
	СкрываемыеКоманды.Добавить("Скопировать");
	
	Для Каждого Страница Из Элементы.СтраницыСписки.ПодчиненныеЭлементы Цикл
		Если Страница.Имя = Элементы.СтраницаПоискКонтактов.Имя
			Или Страница.Имя = Элементы.СтраницаВсеКонтактыПоПредмету.Имя
			Или Страница.Имя = Элементы.СтраницаПользователей.Имя Тогда
			
			Продолжить;
		КонецЕсли;
		
		Для Каждого Элемент Из Страница.ПодчиненныеЭлементы Цикл
			Если Не СтрНачинаетсяС(Элемент.Имя, ВзаимодействияКлиентСервер.ПрефиксТаблица()) Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого ИмяКоманды Из СкрываемыеКоманды Цикл
				ПолноеИмя = Элемент.Имя + ИмяКоманды;
				ЭлементКоманда = Элементы.Найти(ПолноеИмя);
				Если ЭлементКоманда <> Неопределено Тогда
					ЭлементКоманда.Видимость = Ложь;
				КонецЕсли;
				
				ПолноеИмяКонтекст = Элемент.Имя + "ДействияСтроки" + ИмяКоманды;
				ЭлементКоманда = Элементы.Найти(ПолноеИмяКонтекст);
				Если ЭлементКоманда <> Неопределено Тогда
					ЭлементКоманда.Видимость = Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#Область ПроцедурыИФункцииОсуществленияПоиска

&НаСервере
Функция НайтиКонтакты()
	
	ТаблицаНайденных = НайденныеКонтакты.Выгрузить();
	Результат = Взаимодействия.НайтиКонтакты(СтрокаПоиска, Истина, ТаблицаНайденных);
	
	ТаблицаПолучателей = ПолучателиПисьма.Выгрузить();
	ТаблицаНайденныхИтог = РезультатПоискаСВыбранными(ТаблицаНайденных, ТаблицаПолучателей);
	НайденныеКонтакты.Загрузить(ТаблицаНайденныхИтог);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура НайтиПоEmail(ПоДомену)
	
	ТаблицаНайденных = НайденныеКонтакты.Выгрузить();
	Взаимодействия.НайтиПоEmail(СтрокаПоиска, ПоДомену, ТаблицаНайденных);
	
	ТаблицаПолучателей = ПолучателиПисьма.Выгрузить();
	ТаблицаНайденныхИтог = РезультатПоискаСВыбранными(ТаблицаНайденных, ТаблицаПолучателей);
	НайденныеКонтакты.Загрузить(ТаблицаНайденныхИтог);
	
КонецПроцедуры

&НаСервере
Функция КонтактыНайденныеПоСтроке()
	
	ТаблицаНайденных = НайденныеКонтакты.Выгрузить();
	Результат = Взаимодействия.ПолнотекстовыйПоискКонтактовПоСтроке(СтрокаПоиска, НайденныеКонтакты, Истина);
	
	ТаблицаПолучателей = ПолучателиПисьма.Выгрузить();
	ТаблицаНайденныхИтог = РезультатПоискаСВыбранными(ТаблицаНайденных, ТаблицаПолучателей);
	НайденныеКонтакты.Загрузить(ТаблицаНайденныхИтог);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура НайтиПоНачалуНаименования()
	
	ТаблицаНайденных = НайденныеКонтакты.Выгрузить();
	Взаимодействия.НайтиКонтактыСАдресамиПоНаименованию(СтрокаПоиска, НайденныеКонтакты);
	
	ТаблицаПолучателей = ПолучателиПисьма.Выгрузить();
	ТаблицаНайденныхИтог = РезультатПоискаСВыбранными(ТаблицаНайденных, ТаблицаПолучателей);
	НайденныеКонтакты.Загрузить(ТаблицаНайденныхИтог);
	
КонецПроцедуры

&НаСервере
Функция РезультатПоискаСВыбранными(ТаблицаНайденных, ТаблицаПолучателей)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НайденныеКонтакты", ТаблицаНайденных);
	Запрос.УстановитьПараметр("ПолучателиПисьма", ТаблицаПолучателей);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НайденныеКонтакты.Ссылка КАК Ссылка,
	|	НайденныеКонтакты.Представление КАК Представление,
	|	НайденныеКонтакты.ИмяСправочника КАК ИмяСправочника,
	|	НайденныеКонтакты.НаименованиеКонтакта КАК НаименованиеКонтакта,
	|	НайденныеКонтакты.ПредставлениеЗаполнено КАК ПредставлениеЗаполнено,
	|	НайденныеКонтакты.Выбран КАК Выбран
	|ПОМЕСТИТЬ НайденныеКонтакты
	|ИЗ
	|	&НайденныеКонтакты КАК НайденныеКонтакты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПолучателиПисьма.Контакт КАК Контакт,
	|	ПолучателиПисьма.Адрес КАК Адрес
	|ПОМЕСТИТЬ ПолучателиПисьма
	|ИЗ
	|	&ПолучателиПисьма КАК ПолучателиПисьма
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НайденныеКонтактыИтог.Ссылка КАК Ссылка,
	|	НайденныеКонтактыИтог.Представление КАК Представление,
	|	НайденныеКонтактыИтог.ИмяСправочника КАК ИмяСправочника,
	|	НайденныеКонтактыИтог.НаименованиеКонтакта КАК НаименованиеКонтакта,
	|	НайденныеКонтактыИтог.ПредставлениеЗаполнено КАК ПредставлениеЗаполнено,
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ПолучателиПисьма
	|		ГДЕ
	|			ПолучателиПисьма.Контакт = НайденныеКонтактыИтог.Ссылка
	|			И ПолучателиПисьма.Адрес = НайденныеКонтактыИтог.Представление) КАК Выбран
	|ИЗ
	|	НайденныеКонтакты КАК НайденныеКонтактыИтог";
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ЗаполнитьТаблицуПолучателей()
	
	табПолучатели = РеквизитФормыВЗначение("ПолучателиПисьма");
	
	Для Каждого ГруппаВыбранных Из Параметры.СписокВыбранных Цикл
		Если ГруппаВыбранных.Значение <> Неопределено Тогда
			Для Каждого Элемент Из ГруппаВыбранных.Значение Цикл
				НоваяСтрока = табПолучатели.Добавить();
				НоваяСтрока.Группа = ГруппаВыбранных.Представление;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Элемент);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	табПолучатели.Сортировать("Группа");
	
	Если табПолучатели.Количество() > 0 Тогда
		ТаблицаАдресов =
			Взаимодействия.АдресаЭлектроннойПочтыКонтактов(табПолучатели.ВыгрузитьКолонку("Контакт"));
			
			Запрос = Новый Запрос;
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	ПолучателиПисьма.Адрес,
			|	ПолучателиПисьма.Представление,
			|	ПолучателиПисьма.Контакт,
			|	ПолучателиПисьма.Группа
			|ПОМЕСТИТЬ ПолучателиПисьма
			|ИЗ
			|	&ПолучателиПисьма КАК ПолучателиПисьма
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КонтактыАдреса.Контакт,
			|	КонтактыАдреса.СписокАдресов
			|ПОМЕСТИТЬ КонтактыСписокАдресов
			|ИЗ
			|	&КонтактыАдреса КАК КонтактыАдреса
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПолучателиПисьма.Адрес,
			|	ПолучателиПисьма.Представление,
			|	ПолучателиПисьма.Контакт,
			|	ПолучателиПисьма.Группа,
			|	ЕСТЬNULL(КонтактыСписокАдресов.СписокАдресов, """") КАК СписокАдресов
			|ИЗ
			|	ПолучателиПисьма КАК ПолучателиПисьма
			|		ЛЕВОЕ СОЕДИНЕНИЕ КонтактыСписокАдресов КАК КонтактыСписокАдресов
			|		ПО КонтактыСписокАдресов.Контакт = ПолучателиПисьма.Контакт";
			
			Запрос.УстановитьПараметр("ПолучателиПисьма", табПолучатели);
			Запрос.УстановитьПараметр("КонтактыАдреса", ТаблицаАдресов);
			
			табПолучатели = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(табПолучатели, "ПолучателиПисьма");
	ЗаполнитьСписокВыбранных(табПолучатели);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПолучателя(Адрес, Наименование, Контакт, СписокАдресов = "")
	
	УдалитьПустогоПолучателя(ПолучателиПисьма);
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Адрес", Адрес);
	ПараметрыОтбора.Вставить("Контакт", Контакт);
	РезультатПоиска = ПолучателиПисьма.НайтиСтроки(ПараметрыОтбора);
	Если РезультатПоиска.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = ПолучателиПисьма.Добавить();
	НоваяСтрока.Адрес         = Адрес;
	НоваяСтрока.Представление = Наименование;
	НоваяСтрока.Контакт       = Контакт;
	НоваяСтрока.СписокАдресов = СписокАдресов;
	НоваяСтрока.Группа        = ГруппаПоУмолчанию;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УдалитьПустогоПолучателя(ПолучателиПисьма)
	
	Если ПолучателиПисьма.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
		
	ПолучательПисьма = ПолучателиПисьма[0];
	Если ПустаяСтрока(ПолучательПисьма.Адрес) И ПустаяСтрока(ПолучательПисьма.Представление) И Не ЗначениеЗаполнено(ПолучательПисьма.Контакт) Тогда
		ПолучателиПисьма.Удалить(0);
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПолучателяИзСпискаПоПредмету()
	
	ТекущиеДанные = Элементы.КонтактыПоПредмету.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ВзаимодействияВызовСервера.НаименованиеИАдресаЭлектроннойПочтыКонтакта(ТекущиеДанные.Ссылка);
	Если Результат <> Неопределено И Результат.Адреса.Количество() > 0 Тогда
		СписокАдресов = СтрСоединить(Результат.Адреса.ВыгрузитьЗначения(), ";");
	Иначе
		СписокАдресов = "";
	КонецЕсли;
	
	ДобавитьПолучателя(ТекущиеДанные.Адрес, ТекущиеДанные.Наименование, ТекущиеДанные.Ссылка, СписокАдресов);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКонтактВСпискеПолучателей(Контакт)
	
	Если ЗначениеЗаполнено(Контакт) И Элементы.ПолучателиПисьма.ТекущиеДанные <> Неопределено Тогда
		Элементы.ПолучателиПисьма.ТекущиеДанные.Контакт = Контакт;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущимКонтакт(Контакт)
	
	Взаимодействия.УстановитьТекущимКонтакт(Контакт, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьГруппу(ИмяГруппы)
	
	Для Каждого ВыделеннаяСтрока Из Элементы.ПолучателиПисьма.ПолучитьСтрокиДляОбработки() Цикл
		Элемент = ПолучателиПисьма.НайтиПоИдентификатору(ВыделеннаяСтрока);
		Элемент.Группа = ИмяГруппы;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиВыделенныеСтроки(Знач ВыделенныеСтроки, ОбрабатываемаяТаблица)

	Результат = Взаимодействия.АдресаЭлектроннойПочтыКонтактов(ВыделенныеСтроки, ГруппаПоУмолчанию);
	Если Результат <> Неопределено Тогда
		УдалитьПустогоПолучателя(ПолучателиПисьма);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Результат, ПолучателиПисьма);
	КонецЕсли;
	
	ЗаполнитьСписокВыбранных(Результат, ОбрабатываемаяТаблица);
	
КонецПроцедуры

&НаСервере
Процедура ИсключитьВыделенныеСтроки(Знач ВыделенныеСтроки, ОбрабатываемаяТаблица)
	
	Удалено = 0;
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("Структура") Тогда
			ПараметрыОтбора = Новый Структура("Контакт, Адрес", ВыделеннаяСтрока.Ссылка, ВыделеннаяСтрока.Адрес);
			Получатель = ВыделеннаяСтрока.Ссылка;
		Иначе
			ПараметрыОтбора = Новый Структура("Контакт", ВыделеннаяСтрока);
			Получатель = ВыделеннаяСтрока;
		КонецЕсли;
		УдаляемыеСтроки = ПолучателиПисьма.НайтиСтроки(ПараметрыОтбора);
		
		Если УдаляемыеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
			ПолучателиПисьма.Удалить(УдаляемаяСтрока);
		КонецЦикла;
		
		ПолучательКУдалению = СписокПолучателей.НайтиПоЗначению(Получатель);
		Если ПолучательКУдалению <> Неопределено Тогда
			Удалено = Удалено + 1;
			СписокПолучателей.Удалить(ПолучательКУдалению);
		КонецЕсли;
	КонецЦикла;
	
	Если ОбрабатываемаяТаблица = Элементы.НайденныеКонтакты.Имя Тогда
		РассчитатьКоличествоВыбранных();
		Возврат;
	КонецЕсли;
	
	ВсегоВыбрано = ПолучателиПисьма.Количество();
	
	ТаблицаФормы = Элементы[ОбрабатываемаяТаблица];
	ПутьКДаннымЗаголовка = ТаблицаФормы.Родитель.ПутьКДаннымЗаголовка;
	ЭтотОбъект[ПутьКДаннымЗаголовка] = ЭтотОбъект[ПутьКДаннымЗаголовка] - Удалено;
	
	Если ОбрабатываемаяТаблица = Элементы.КонтактыПоПредмету.Имя Тогда
		Возврат;
	КонецЕсли;
	
	ДобавленныеТаблицыКонтактов = Взаимодействия.ДобавленныеТаблицыКонтактов(ЭтотОбъект);
	ДобавленныеТаблицыКонтактов.Добавить(Элементы.СписокПользователей.Имя);
	Для Каждого ТаблицаКонтактов Из ДобавленныеТаблицыКонтактов Цикл
		ТаблицаФормы = Элементы[ТаблицаКонтактов];
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ЭтотОбъект[ТаблицаФормы.ПутьКДанным],
			"Выбранные",
			СписокПолучателей);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбранных(Таблица, ОбрабатываемаяТаблица = Неопределено)
	
	Добавлено = 0;
	Для Каждого Строка Из Таблица Цикл
		Если СписокПолучателей.НайтиПоЗначению(Строка.Контакт) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Добавлено = Добавлено + 1;
		СписокПолучателей.Добавить(Строка.Контакт);
	КонецЦикла;
	
	Если ОбрабатываемаяТаблица = Неопределено Тогда
		// Инициализация всех таблиц.
		РассчитатьКоличествоВыбранных();
	Иначе
		ТаблицаФормы = Элементы[ОбрабатываемаяТаблица];
		ПутьКДаннымЗаголовка = ТаблицаФормы.Родитель.ПутьКДаннымЗаголовка;
		ЭтотОбъект[ПутьКДаннымЗаголовка] = ЭтотОбъект[ПутьКДаннымЗаголовка] + Добавлено;
		ВсегоВыбрано = ПолучателиПисьма.Количество();
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ЭтотОбъект[ТаблицаФормы.ПутьКДанным],
			"Выбранные",
			СписокПолучателей);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьКоличествоВыбранных()
	
	ДобавленныеТаблицыКонтактов = Взаимодействия.ДобавленныеТаблицыКонтактов(ЭтотОбъект);
	ДобавленныеТаблицыКонтактов.Добавить(Элементы.СписокПользователей.Имя);
	Для Каждого ТаблицаКонтактов Из ДобавленныеТаблицыКонтактов Цикл
		ТаблицаФормы = Элементы[ТаблицаКонтактов];
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ЭтотОбъект[ТаблицаФормы.ПутьКДанным],
			"Выбранные",
			СписокПолучателей);
	КонецЦикла;
	
	КонтактыПоПредметуВыбрано = 0;
	Для Каждого КонтактПоПредмету Из КонтактыПоПредмету Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Адрес", КонтактПоПредмету.Адрес);
		ПараметрыОтбора.Вставить("Контакт", КонтактПоПредмету.Ссылка);
		Результат = ПолучателиПисьма.НайтиСтроки(ПараметрыОтбора);
		
		Если Результат.Количество() > 0 Тогда
			КонтактПоПредмету.Выбран = Истина;
			КонтактыПоПредметуВыбрано = КонтактыПоПредметуВыбрано + 1;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТаблицаКонтактов Из ДобавленныеТаблицыКонтактов Цикл
		ТаблицаФормы = Элементы[ТаблицаКонтактов];
		ПутьКДаннымЗаголовка = ТаблицаФормы.Родитель.ПутьКДаннымЗаголовка;
		ЭтотОбъект[ПутьКДаннымЗаголовка] = 0;
	КонецЦикла;
	
	// Расчет списка выбранных через запрос.
	Для Каждого ТаблицаКонтактов Из ДобавленныеТаблицыКонтактов Цикл
		ТаблицаФормы = Элементы[ТаблицаКонтактов];
		СписокКонтактов = ЭтотОбъект[ТаблицаФормы.ПутьКДанным];
		Запрос = Новый Запрос;
		Запрос.Текст = УпрощенныйТекстЗапроса(СписокКонтактов.ТекстЗапроса);
		Для Каждого ОписаниеПараметра Из СписокКонтактов.Параметры.Элементы Цикл
			Запрос.УстановитьПараметр(Строка(ОписаниеПараметра.Параметр), ОписаниеПараметра.Значение);
		КонецЦикла;
		Если ТаблицаКонтактов = Элементы.СписокПользователей.Имя Тогда
			Запрос.Параметры.ГруппаПользователей = Справочники.ГруппыПользователей.ВсеПользователи;
		КонецЕсли;
		Запрос.Параметры.Проверка = Истина;
		Результат = Запрос.Выполнить().Выгрузить(); // @skip-check query-in-loop - простые запросы, мало таблиц.
		Строка = Результат[0];
		
		ПутьКДаннымЗаголовка = ТаблицаФормы.Родитель.ПутьКДаннымЗаголовка;
		ЭтотОбъект[ПутьКДаннымЗаголовка] = ЭтотОбъект[ПутьКДаннымЗаголовка] + Строка.Количество;
	КонецЦикла;
	
	ВсегоВыбрано = ПолучателиПисьма.Количество();
	
КонецПроцедуры

&НаСервере
Функция УпрощенныйТекстЗапроса(ТекстЗапроса)
	
	ТекстЗапросаМассив = СтрРазделить(ТекстЗапроса, Символы.ПС);
	Индекс = ТекстЗапросаМассив.Найти("ИЗ"); // @query-part
	Для Итератор = 1 По Индекс - 1 Цикл
		ТекстЗапросаМассив.Удалить(1);
	КонецЦикла;
	
	ТекстЗапросаМассив.Вставить(1, "КОЛИЧЕСТВО(*) КАК Количество"); // @query-part
	
	Возврат СтрСоединить(ТекстЗапросаМассив, Символы.ПС);
	
КонецФункции

&НаСервере
Процедура УстановитьГруппуПоУмолчанию()
	
	ГруппаПоУмолчанию = Параметры.ГруппаПоУмолчанию;
	Если ПустаяСтрока(ГруппаПоУмолчанию) Тогда
		ГруппаПоУмолчанию = НСтр("ru = 'Кому'");
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура СписокВыбораАдресаЭППослеЗавершения(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт

	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Индекс = ВыбранныйЭлемент.Значение;
	Адрес = ДополнительныеПараметры.Результат[Индекс].АдресЭП;
	Представление = ДополнительныеПараметры.Результат[Индекс].Представление;
	УстановитьАдресИПредставлениеВыбранногоКонтакта(ДополнительныеПараметры.ТекущиеДанные, Представление, Адрес);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьАдресИПредставлениеВыбранногоКонтакта(ТекущиеДанные, Представление, Адрес)

	Позиция = СтрНайти(Представление, "<");
	Представление = ?(Позиция= 0, "", СокрЛП(Лев(Представление, Позиция-1)));

	ТекущиеДанные.Адрес = Адрес;
	Если Не ПустаяСтрока(Представление) Тогда
		ТекущиеДанные.Представление = Представление;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ОбщегоНазначения.УстановитьУсловноеОформлениеСпискаВыбора(ЭтотОбъект, "Группа", "ПолучателиПисьма.Группа");
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбновитьМенюВариантовПоиска()
	
	Элементы.ВариантПоискаВезде.Пометка = (ВариантыПоиска = "Везде");
	Элементы.ВариантПоискаВАдресах.Пометка = (ВариантыПоиска = "ПоEmail");
	Элементы.ВариантПоискаВНаименованияхКонтактов.Пометка = (ВариантыПоиска = "ПоСтроке");
	Элементы.ВариантПоискаПоДоменномуИмени.Пометка = (ВариантыПоиска = "ПоДомену");

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииТолькоКонтактыСАдресами(Форма)

		Для Каждого ИмяСписка Из Форма.ИменаДобавленныхТаблиц Цикл
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.ЭтотОбъект[ИмяСписка.Значение], "Адрес", , ВидСравненияКомпоновкиДанных.Заполнено,, Форма.ПоказыватьТолькоКонтактыСАдресами);
		
	КонецЦикла;
	
	Если Форма.ПоказыватьТолькоКонтактыСАдресами Тогда
		
		Форма.Элементы.КонтактыПоПредмету.ОтборСтрок = Новый ФиксированнаяСтруктура("АдресЗаполнен", Истина);
		Форма.Элементы.НайденныеКонтакты.ОтборСтрок  = Новый ФиксированнаяСтруктура("ПредставлениеЗаполнено", Истина);
		
		
	Иначе
		
		Форма.Элементы.НайденныеКонтакты.ОтборСтрок  = Неопределено;
		Форма.Элементы.КонтактыПоПредмету.ОтборСтрок = Неопределено;
		
	КонецЕсли

КонецПроцедуры

#КонецОбласти

#КонецОбласти