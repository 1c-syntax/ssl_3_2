///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОпределитьПоведениеВМобильномКлиенте();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЦентрМониторинга") Тогда 
		
		// Подсчитываем количество созданий формы, стандартный разделитель ".".
		Комментарий = Строка(ПолучитьСкоростьКлиентскогоСоединения());
		
		МодульЦентрМониторинга = ОбщегоНазначения.ОбщийМодуль("ЦентрМониторинга");
		МодульЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики("ОбщаяФорма.ФормаВариантаОтчета", 1, Комментарий);
		
	КонецЕсли;
	
	ФормаПараметры = ВариантыОтчетов.СохраняемыеПараметрыФормыОтчета(Параметры);
	
	Если ЗначениеЗаполнено(Параметры.ПредставлениеВарианта) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Изменение варианта отчета ""%1""'"), Параметры.ПредставлениеВарианта);
	КонецЕсли;
	
	Если Параметры.НастройкиОтчета <> Неопределено Тогда
		НастройкиОтчета = Параметры.НастройкиОтчета;
		Если НастройкиОтчета.СхемаМодифицирована Тогда
			Отчет.КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(НастройкиОтчета.АдресСхемы));
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.ПредставлениеВарианта) Тогда
		Параметры.ПредставлениеВарианта = Параметры.ВариантНаименование;
	КонецЕсли;
	
	НастройкиКД = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Вариант");
	Если НастройкиКД = Неопределено Тогда
		НастройкиКД = Отчет.КомпоновщикНастроек.Настройки;
	КонецЕсли;
	
	ИдентификаторЭлементаСтруктурыНастроек = Параметры.ИдентификаторЭлементаСтруктурыНастроек;
	ПутьКЭлементуСтруктурыНастроек = Параметры.ПутьКЭлементуСтруктурыНастроек;
	ЭлементСтруктуры = ОтчетыСервер.ЭлементНастроекПоПолномуПути(НастройкиКД, ПутьКЭлементуСтруктурыНастроек);
	Если ЭлементСтруктуры <> Неопределено Тогда
		ИдентификаторЭлементаСтруктурыНастроек = НастройкиКД.ПолучитьИдентификаторПоОбъекту(ЭлементСтруктуры);
	КонецЕсли;
	
	ВариантМодифицирован = Параметры.ВариантМодифицирован;
	ПользовательскиеНастройкиМодифицированы = Параметры.ПользовательскиеНастройкиМодифицированы;
	
	УстановитьТекущуюСтраницу();
	
	ВставитьВажныеСообщенияНаСтраницах();
	
	ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойВариантаНаСервере(НовыеНастройкиКД)
	Если ТипЗнч(ФормаПараметры.Отбор) = Тип("Структура") Тогда
		ОтчетыСервер.УстановитьФиксированныеОтборы(ФормаПараметры.Отбор, НовыеНастройкиКД, НастройкиОтчета);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	НовыеНастройки = Отчет.КомпоновщикНастроек.ПолучитьНастройки();
	Отчет.КомпоновщикНастроек.ЗагрузитьФиксированныеНастройки(Новый НастройкиКомпоновкиДанных);
	ОтчетыКлиентСервер.ЗагрузитьНастройки(Отчет.КомпоновщикНастроек, НовыеНастройки);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ТипЗнч(ИдентификаторЭлементаСтруктурыНастроек) = Тип("ИдентификаторКомпоновкиДанных") Тогда
		ПодключитьОбработчикОжидания("УстановитьТекущуюСтроку", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПараметрыВывода

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиПараметрыВыводаПриИзменении(Элемент)
	
	Строка = Элемент.ТекущиеДанные;
	ИдентификаторПараметра = "Заголовок";
	
	Если Строка <> Неопределено
		И Строка.Свойство("Параметр")
		И Строка.Параметр = ИдентификаторПараметра Тогда 
		
		Отчет.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить(
			"ЗаголовокУстановленИнтерактивно", ЗначениеЗаполнено(Строка.Значение));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьИСформировать(Команда)
	
	Если МодальныйРежим
	 Или РежимБлокировкиПриОткрытииОкна = РежимБлокировкиПриОткрытииОкнаФормы.БлокироватьВесьИнтерфейс
	 Или ВладелецФормы = Неопределено Тогда
		
		Закрыть(Истина);
		Возврат;
	КонецЕсли;

	РезультатВыбора = ОтчетыКлиентСервер.ПараметрыОбновленияФормыОтчета(
		ВариантыОтчетовСлужебныйКлиентСервер.ИмяСобытияФормыНастроек());
	РезультатВыбора.КомпоновщикНастроекКД = Отчет.КомпоновщикНастроек;
	РезультатВыбора.ВариантМодифицирован = ВариантМодифицирован;
	РезультатВыбора.ПользовательскиеНастройкиМодифицированы = ВариантМодифицирован Или ПользовательскиеНастройкиМодифицированы;
	РезультатВыбора.Переформировать = Истина;
	
	Если РезультатВыбора.ПользовательскиеНастройкиМодифицированы Тогда
		РезультатВыбора.СброситьПользовательскиеНастройки = Истина;
	КонецЕсли;
	
	ОповеститьОВыборе(РезультатВыбора);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПерейтиКОтчету(Команда)
	
	ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
	НастройкиЭлемента =  Отчет.КомпоновщикНастроек.Настройки.НастройкиЭлемента(ЭлементСтруктуры);
	Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока = Отчет.КомпоновщикНастроек.Настройки.ПолучитьИдентификаторПоОбъекту(НастройкиЭлемента);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОпределитьПоведениеВМобильномКлиенте()
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	Если Не ЭтоМобильныйКлиент Тогда 
		Возврат;
	КонецЕсли;
	
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Авто;
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтраницу()
	Если Не ЗначениеЗаполнено(Параметры.ИмяСтраницы) Тогда
		Возврат;
	КонецЕсли;
	
	СвязиСтраниц = Новый Соответствие;
	СвязиСтраниц.Вставить("СтраницаОтборы", "СтраницаОтбора");
	СвязиСтраниц.Вставить("СтраницаВыбранныеПоляИСортировки", "СтраницаПолейВыбора");
	СвязиСтраниц.Вставить("СтраницаОформление", "СтраницаУсловногоОформления");
	
	ИмяСтраницы = СвязиСтраниц[Параметры.ИмяСтраницы];
	НайденнаяСтраница = Элементы.Найти(ИмяСтраницы);
	
	Если НайденнаяСтраница = Неопределено Тогда
		НайденнаяСтраница = Элементы.Найти(Параметры.ИмяСтраницы);
	КонецЕсли;
	
	Если НайденнаяСтраница <> Неопределено Тогда 
		Элементы.СтраницыНастроек.ТекущаяСтраница = НайденнаяСтраница;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВставитьВажныеСообщенияНаСтраницах()
	
	// Недоступные настройки полей группировки
	ПараметрыВажногоСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыВажногоСообщения.ТипСообщения = "Предупреждение";
	ПараметрыВажногоСообщения.Идентификатор = "НедоступныеНастройкиПолейГруппировки";
	ПараметрыВажногоСообщения.Заголовок = НСтр("ru = 'Текущий элемент не может содержать поля группировки'");
	ПараметрыВажногоСообщения.Сообщение = НСтр("ru = 'Выберите в дереве структуры группировку, для которой требуется просмотреть или изменить состав полей группировки.'");
	ПараметрыВажногоСообщения.КраткоеПредставление = НСтр("ru = 'Текущий элемент не может содержать поля группировки'");
	ПараметрыВажногоСообщения.Скрываемый = Ложь;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыВажногоСообщения, Элементы.НедоступныеНастройкиПолейГруппировки);

	// Отключенные настройки выбранных полей
	ПараметрыВажногоСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыВажногоСообщения.ТипСообщения = "Информация";
	ПараметрыВажногоСообщения.Идентификатор = "ОтключенныеНастройкиВыбранныхПолей";
	ПараметрыВажногоСообщения.Заголовок = НСтр("ru = 'Выбранные поля для текущего элемента будут автоматически формироваться на основании выбранных полей всего отчета'");
	ПараметрыВажногоСообщения.Сообщение = НСтр("ru = 'Выберите в дереве структуры элемент - отчет для редактирования его выбранных полей.
		|Если необходимо, чтобы текущий элемент имел выбранные поля, отличные от выбранных полей отчета, установите флажок, расположенный над этим текстом.'");
	ПараметрыВажногоСообщения.КраткоеПредставление = НСтр("ru = 'Выбранные поля для текущего элемента будут автоматически формироваться на основании выбранных полей всего отчета'");
	ПараметрыВажногоСообщения.Скрываемый = Ложь;
	ПараметрыДействия = ОбщегоНазначения.ПараметрыДействияВажногоСообщения();
	ПараметрыДействия.ИмяКоманды = "ОтключенныеНастройкиВыбранныхПолейПереходКВыбраннымПолямОтчета";
	ПараметрыДействия.Обработчик = "Подключаемый_ПерейтиКОтчету";
	ПараметрыДействия.Заголовок = НСтр("ru = 'Перейти к выбранным полям отчета'");
	ПараметрыВажногоСообщения.ПараметрыДействия = ПараметрыДействия;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыВажногоСообщения, Элементы.ОтключенныеНастройкиВыбранныхПолей);

	// Недоступные настройки выбранных полей
	ПараметрыВажногоСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыВажногоСообщения.ТипСообщения = "Предупреждение";
	ПараметрыВажногоСообщения.Идентификатор = "НедоступныеНастройкиВыбранныхПолей";
	ПараметрыВажногоСообщения.Заголовок = НСтр("ru = 'У текущего элемента не могут быть установлены выбранные поля'");
	ПараметрыВажногоСообщения.Сообщение = НСтр("ru = 'Выберите в дереве структуры элемент - отчет для редактирования его выбранных полей.'");
	ПараметрыВажногоСообщения.КраткоеПредставление = НСтр("ru = 'У текущего элемента не могут быть установлены выбранные поля'");
	ПараметрыВажногоСообщения.Скрываемый = Ложь;
	ПараметрыДействия = ОбщегоНазначения.ПараметрыДействияВажногоСообщения();
	ПараметрыДействия.ИмяКоманды = "НедоступныеНастройкиВыбранныхПолейПереходКВыбраннымПолямОтчета";
	ПараметрыДействия.Обработчик = "Подключаемый_ПерейтиКОтчету";
	ПараметрыДействия.Заголовок = НСтр("ru = 'Перейти к выбранным полям отчета'");
	ПараметрыВажногоСообщения.ПараметрыДействия = ПараметрыДействия;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыВажногоСообщения, Элементы.НедоступныеНастройкиВыбранныхПолей);
	
	// Отключенные настройки отбора
	ПараметрыВажногоСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыВажногоСообщения.ТипСообщения = "Информация";
	ПараметрыВажногоСообщения.Идентификатор = "ОтключенныеНастройкиОтбора";
	ПараметрыВажногоСообщения.Заголовок = НСтр("ru = 'В текущем элементе фильтр не установлен'");
	ПараметрыВажногоСообщения.Сообщение = НСтр("ru = 'Выберите в дереве структуры отчет для редактирования фильтра отчета в целом.
		|Если необходимо, чтобы текущий элемент имел собственный фильтр, установите флажок, расположенный над этим текстом.'");
	ПараметрыВажногоСообщения.КраткоеПредставление = НСтр("ru = 'В текущем элементе фильтр не установлен'");
	ПараметрыВажногоСообщения.Скрываемый = Ложь;
	ПараметрыДействия = ОбщегоНазначения.ПараметрыДействияВажногоСообщения();
	ПараметрыДействия.ИмяКоманды = "ОтключенныеНастройкиОтбораПереходКОтборуОтчета";
	ПараметрыДействия.Обработчик = "Подключаемый_ПерейтиКОтчету";
	ПараметрыДействия.Заголовок = НСтр("ru = 'Перейти к фильтру отчета'");
	ПараметрыВажногоСообщения.ПараметрыДействия = ПараметрыДействия;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыВажногоСообщения, Элементы.ОтключенныеНастройкиОтбора);
	
	// Недоступные настройки отбора
	ПараметрыВажногоСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыВажногоСообщения.ТипСообщения = "Предупреждение";
	ПараметрыВажногоСообщения.Идентификатор = "НедоступныеНастройкиОтбора";
	ПараметрыВажногоСообщения.Заголовок = НСтр("ru = 'У текущего элемента не может быть установлен фильтр'");
	ПараметрыВажногоСообщения.Сообщение = НСтр("ru = 'Выберите в дереве структуры отчет для редактирования фильтра отчета в целом.'");
	ПараметрыВажногоСообщения.КраткоеПредставление = НСтр("ru = 'У текущего элемента не может быть установлен фильтр'");
	ПараметрыВажногоСообщения.Скрываемый = Ложь;
	ПараметрыДействия = ОбщегоНазначения.ПараметрыДействияВажногоСообщения();
	ПараметрыДействия.ИмяКоманды = "НедоступныеНастройкиОтбораПереходКОтборуОтчета";
	ПараметрыДействия.Обработчик = "Подключаемый_ПерейтиКОтчету";
	ПараметрыДействия.Заголовок = НСтр("ru = 'Перейти к фильтру отчета'");
	ПараметрыВажногоСообщения.ПараметрыДействия = ПараметрыДействия;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыВажногоСообщения, Элементы.НедоступныеНастройкиОтбора);
	
	// Отключенные настройки порядка
	ПараметрыВажногоСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыВажногоСообщения.ТипСообщения = "Информация";
	ПараметрыВажногоСообщения.Идентификатор = "ОтключенныеНастройкиПорядка";
	ПараметрыВажногоСообщения.Заголовок = НСтр("ru = 'В текущем элементе сортировка не установлена'");
	ПараметрыВажногоСообщения.Сообщение = НСтр("ru = 'Выберите в дереве структуры отчет для редактирования сортировки отчета в целом.
	|Если необходимо, чтобы текущий элемент имел собственную сортировку, установите флажок, расположенный над этим текстом.'");
	ПараметрыВажногоСообщения.КраткоеПредставление = НСтр("ru = 'В текущем элементе сортировка не установлена'");
	ПараметрыВажногоСообщения.Скрываемый = Ложь;
	ПараметрыДействия = ОбщегоНазначения.ПараметрыДействияВажногоСообщения();
	ПараметрыДействия.ИмяКоманды = "ОтключенныеНастройкиПорядкаПереходКПорядкуОтчета";
	ПараметрыДействия.Обработчик = "Подключаемый_ПерейтиКОтчету";
	ПараметрыДействия.Заголовок = НСтр("ru = 'Перейти к сортировке отчета'");
	ПараметрыВажногоСообщения.ПараметрыДействия = ПараметрыДействия;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыВажногоСообщения, Элементы.ОтключенныеНастройкиПорядка);

	// Недоступные настройки порядка
	ПараметрыВажногоСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыВажногоСообщения.ТипСообщения = "Предупреждение";
	ПараметрыВажногоСообщения.Идентификатор = "НедоступныеНастройкиПорядка";
	ПараметрыВажногоСообщения.Заголовок = НСтр("ru = 'У текущего элемента не может быть установлена сортировка'");
	ПараметрыВажногоСообщения.Сообщение = НСтр("ru = 'Выберите в дереве структуры отчет для редактирования сортировки отчета в целом.'");
	ПараметрыВажногоСообщения.КраткоеПредставление = НСтр("ru = 'У текущего элемента не может быть установлена сортировка'");
	ПараметрыВажногоСообщения.Скрываемый = Ложь;
	ПараметрыДействия = ОбщегоНазначения.ПараметрыДействияВажногоСообщения();
	ПараметрыДействия.ИмяКоманды = "НедоступныеНастройкиПорядкаПереходКПорядкуОтчета";
	ПараметрыДействия.Обработчик = "Подключаемый_ПерейтиКОтчету";
	ПараметрыДействия.Заголовок = НСтр("ru = 'Перейти к сортировке отчета'");
	ПараметрыВажногоСообщения.ПараметрыДействия = ПараметрыДействия;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыВажногоСообщения, Элементы.НедоступныеНастройкиПорядка);
	
	// Отключенные настройки условного оформления
	ПараметрыВажногоСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыВажногоСообщения.ТипСообщения = "Информация";
	ПараметрыВажногоСообщения.Идентификатор = "ОтключенныеНастройкиУсловногоОформления";
	ПараметрыВажногоСообщения.Заголовок = НСтр("ru = 'В текущем элементе условное оформление не установлено'");
	ПараметрыВажногоСообщения.Сообщение = НСтр("ru = 'Выберите в дереве структуры отчет для редактирования условного оформления отчета в целом.
		|Если необходимо, чтобы текущий элемент имел собственное условное оформление, установите флажок, расположенный над этим текстом.'");
	ПараметрыВажногоСообщения.КраткоеПредставление = НСтр("ru = 'В текущем элементе условное оформление не установлено'");
	ПараметрыВажногоСообщения.Скрываемый = Ложь;
	ПараметрыДействия = ОбщегоНазначения.ПараметрыДействияВажногоСообщения();
	ПараметрыДействия.ИмяКоманды = "ОтключенныеНастройкиУсловногоОформленияПереходКУсловномуОформлениюОтчета";
	ПараметрыДействия.Обработчик = "Подключаемый_ПерейтиКОтчету";
	ПараметрыДействия.Заголовок = НСтр("ru = 'Перейти к условному оформлению отчета'");
	ПараметрыВажногоСообщения.ПараметрыДействия = ПараметрыДействия;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыВажногоСообщения, Элементы.ОтключенныеНастройкиУсловногоОформления);

	// Недоступные настройки условного оформления
	ПараметрыВажногоСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыВажногоСообщения.ТипСообщения = "Предупреждение";
	ПараметрыВажногоСообщения.Идентификатор = "НедоступныеНастройкиУсловногоОформления";
	ПараметрыВажногоСообщения.Заголовок = НСтр("ru = 'У текущего элемента не может быть установлено условное оформление'");
	ПараметрыВажногоСообщения.Сообщение = НСтр("ru = 'Выберите в дереве структуры отчет для редактирования условного оформления отчета в целом.'");
	ПараметрыВажногоСообщения.КраткоеПредставление = НСтр("ru = 'У текущего элемента не может быть установлено условное оформление'");
	ПараметрыВажногоСообщения.Скрываемый = Ложь;
	ПараметрыДействия = ОбщегоНазначения.ПараметрыДействияВажногоСообщения();
	ПараметрыДействия.ИмяКоманды = "НедоступныеНастройкиУсловногоОформленияПереходКУсловномуОформлениюОтчета";
	ПараметрыДействия.Обработчик = "Подключаемый_ПерейтиКОтчету";
	ПараметрыДействия.Заголовок = НСтр("ru = 'Перейти к условному оформлению отчета'");
	ПараметрыВажногоСообщения.ПараметрыДействия = ПараметрыДействия;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыВажногоСообщения, Элементы.НедоступныеНастройкиУсловногоОформления);

	// Отключенные настройки параметров вывода
	ПараметрыВажногоСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыВажногоСообщения.ТипСообщения = "Информация";
	ПараметрыВажногоСообщения.Идентификатор = "ОтключенныеНастройкиПараметровВывода";
	ПараметрыВажногоСообщения.Заголовок = НСтр("ru = 'В текущем элементе дополнительные настройки не установлены'");
	ПараметрыВажногоСообщения.Сообщение = НСтр("ru = 'Выберите в дереве структуры отчет для редактирования дополнительных настроек отчета в целом.
		|Если необходимо, чтобы текущий элемент имел собственные дополнительные настройки, установите флажок, расположенный над этим текстом.'");
	ПараметрыВажногоСообщения.КраткоеПредставление = НСтр("ru = 'В текущем элементе дополнительные настройки не установлены'");
	ПараметрыВажногоСообщения.Скрываемый = Ложь;
	ПараметрыДействия = ОбщегоНазначения.ПараметрыДействияВажногоСообщения();
	ПараметрыДействия.ИмяКоманды = "ОтключенныеНастройкиПараметровВыводаПереходКПараметрамВыводаОтчета";
	ПараметрыДействия.Обработчик = "Подключаемый_ПерейтиКОтчету";
	ПараметрыДействия.Заголовок = НСтр("ru = 'Перейти к дополнительным настройкам отчета'");
	ПараметрыВажногоСообщения.ПараметрыДействия = ПараметрыДействия;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыВажногоСообщения, Элементы.ОтключенныеНастройкиПараметровВывода);
	
	// Недоступные настройки параметров вывода
	ПараметрыВажногоСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыВажногоСообщения.ТипСообщения = "Информация";
	ПараметрыВажногоСообщения.Идентификатор = "НедоступныеНастройкиПараметровВывода";
	ПараметрыВажногоСообщения.Заголовок = НСтр("ru = 'У текущего элемента не могут быть установлены дополнительные настройки'");
	ПараметрыВажногоСообщения.Сообщение = НСтр("ru = 'Выберите в дереве структуры отчет для редактирования дополнительных настроек отчета в целом.'");
	ПараметрыВажногоСообщения.КраткоеПредставление = НСтр("ru = 'У текущего элемента не могут быть установлены дополнительные настройки'");
	ПараметрыВажногоСообщения.Скрываемый = Ложь;
	ПараметрыДействия = ОбщегоНазначения.ПараметрыДействияВажногоСообщения();
	ПараметрыДействия.ИмяКоманды = "НедоступныеНастройкиПараметровВыводаПереходКПараметрамВыводаОтчета";
	ПараметрыДействия.Обработчик = "Подключаемый_ПерейтиКОтчету";
	ПараметрыДействия.Заголовок = НСтр("ru = 'Перейти к дополнительным настройкам отчета'");
	ПараметрыВажногоСообщения.ПараметрыДействия = ПараметрыДействия;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыВажногоСообщения, Элементы.НедоступныеНастройкиПараметровВывода);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущуюСтроку()
	Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока = ИдентификаторЭлементаСтруктурыНастроек;
КонецПроцедуры

&НаКлиенте
Процедура ПоляГруппировкиНедоступны()
	
	Элементы.СтраницыПолейГруппировки.ТекущаяСтраница = Элементы.НедоступныеНастройкиПолейГруппировки;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПоляДоступны(ЭлементСтруктуры)
	
	Если Отчет.КомпоновщикНастроек.Настройки.НаличиеВыбораУЭлемента(ЭлементСтруктуры) Тогда
		
		ЛокальныеВыбранныеПоля = Истина;
		Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.НастройкиВыбранныхПолей;
		
	Иначе
		
		ЛокальныеВыбранныеПоля = Ложь;
		Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.ОтключенныеНастройкиВыбранныхПолей;
		
	КонецЕсли;
	
	Элементы.ЛокальныеВыбранныеПоля.ТолькоПросмотр = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПоляНедоступны()
	
	ЛокальныеВыбранныеПоля = Ложь;
	Элементы.ЛокальныеВыбранныеПоля.ТолькоПросмотр = Истина;
	Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.НедоступныеНастройкиВыбранныхПолей;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборДоступен(ЭлементСтруктуры)
	
	Если Отчет.КомпоновщикНастроек.Настройки.НаличиеОтбораУЭлемента(ЭлементСтруктуры) Тогда
		
		ЛокальныйОтбор = Истина;
		Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.НастройкиОтбора;
		
	Иначе
		
		ЛокальныйОтбор = Ложь;
		Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.ОтключенныеНастройкиОтбора;
		
	КонецЕсли;
	
	Элементы.ЛокальныйОтбор.ТолькоПросмотр = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборНедоступен()
	
	ЛокальныйОтбор = Ложь;
	Элементы.ЛокальныйОтбор.ТолькоПросмотр = Истина;
	Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.НедоступныеНастройкиОтбора;
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокДоступен(ЭлементСтруктуры)
	
	Если Отчет.КомпоновщикНастроек.Настройки.НаличиеПорядкаУЭлемента(ЭлементСтруктуры) Тогда
		
		ЛокальныйПорядок = Истина;
		Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.НастройкиПорядка;
		
	Иначе
		
		ЛокальныйПорядок = Ложь;
		Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.ОтключенныеНастройкиПорядка;
		
	КонецЕсли;
	
	Элементы.ЛокальныйПорядок.ТолькоПросмотр = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокНедоступен()
	
	ЛокальныйПорядок = Ложь;
	Элементы.ЛокальныйПорядок.ТолькоПросмотр = Истина;
	Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.НедоступныеНастройкиПорядка;
	
КонецПроцедуры

&НаКлиенте
Процедура УсловноеОформлениеДоступно(ЭлементСтруктуры)
	
	Если Отчет.КомпоновщикНастроек.Настройки.НаличиеУсловногоОформленияУЭлемента(ЭлементСтруктуры) Тогда
		
		ЛокальноеУсловноеОформление = Истина;
		Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.НастройкиУсловногоОформления;
		
	Иначе
		
		ЛокальноеУсловноеОформление = Ложь;
		Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.ОтключенныеНастройкиУсловногоОформления;
		
	КонецЕсли;
	
	Элементы.ЛокальноеУсловноеОформление.ТолькоПросмотр = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УсловноеОформлениеНедоступно()
	
	ЛокальноеУсловноеОформление = Ложь;
	Элементы.ЛокальноеУсловноеОформление.ТолькоПросмотр = Истина;
	Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.НедоступныеНастройкиУсловногоОформления;
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыВыводаДоступны(ЭлементСтруктуры)
	
	Если Отчет.КомпоновщикНастроек.Настройки.НаличиеПараметровВыводаУЭлемента(ЭлементСтруктуры) Тогда
		
		ЛокальныеПараметрыВывода = Истина;
		Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.НастройкиПараметровВывода;
		
	Иначе
		
		ЛокальныеПараметрыВывода = Ложь;
		Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.ОтключенныеНастройкиПараметровВывода;
		
	КонецЕсли;
	
	Элементы.ЛокальныеПараметрыВывода.ТолькоПросмотр = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыВыводаНедоступны()
	
	ЛокальныеПараметрыВывода = Ложь;
	Элементы.ЛокальныеПараметрыВывода.ТолькоПросмотр = Истина;
	Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.НедоступныеНастройкиПараметровВывода;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиПриАктивизацииПоля(Элемент)
	
	Перем ВыбраннаяСтраница;
	
	Если Элементы.КомпоновщикНастроекНастройки.ТекущийЭлемент.Имя = "КомпоновщикНастроекНастройкиНаличиеВыбора" Тогда
		
		ВыбраннаяСтраница = Элементы.СтраницаПолейВыбора;
		
	ИначеЕсли Элементы.КомпоновщикНастроекНастройки.ТекущийЭлемент.Имя = "КомпоновщикНастроекНастройкиНаличиеОтбора" Тогда
		
		ВыбраннаяСтраница = Элементы.СтраницаОтбора;
		
	ИначеЕсли Элементы.КомпоновщикНастроекНастройки.ТекущийЭлемент.Имя = "КомпоновщикНастроекНастройкиНаличиеПорядка" Тогда
		
		ВыбраннаяСтраница = Элементы.СтраницаПорядка;
		
	ИначеЕсли Элементы.КомпоновщикНастроекНастройки.ТекущийЭлемент.Имя = "КомпоновщикНастроекНастройкиНаличиеУсловногоОформления" Тогда
		
		ВыбраннаяСтраница = Элементы.СтраницаУсловногоОформления;
		
	ИначеЕсли Элементы.КомпоновщикНастроекНастройки.ТекущийЭлемент.Имя = "КомпоновщикНастроекНастройкиНаличиеПараметровВывода" Тогда
		
		ВыбраннаяСтраница = Элементы.СтраницаПараметровВывода;
		
	КонецЕсли;
	
	Если ВыбраннаяСтраница <> Неопределено Тогда
		
		Элементы.СтраницыНастроек.ТекущаяСтраница = ВыбраннаяСтраница;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиПриАктивизацииСтроки(Элемент)
	
	ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
	ТипЭлемента = ТипЗнч(ЭлементСтруктуры); 
	
	Если ТипЭлемента = Неопределено
		Или ТипЭлемента = Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных")
		Или ТипЭлемента = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
		
		ПоляГруппировкиНедоступны();
		ВыбранныеПоляНедоступны();
		ОтборНедоступен();
		ПорядокНедоступен();
		УсловноеОформлениеНедоступно();
		ПараметрыВыводаНедоступны();
		
	ИначеЕсли ТипЭлемента = Тип("НастройкиКомпоновкиДанных")
		Или ТипЭлемента = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		
		ПоляГруппировкиНедоступны();
		
		ЛокальныеВыбранныеПоля = Истина;
		Элементы.ЛокальныеВыбранныеПоля.ТолькоПросмотр = Истина;
		Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.НастройкиВыбранныхПолей;
		
		ЛокальныйОтбор = Истина;
		Элементы.ЛокальныйОтбор.ТолькоПросмотр = Истина;
		Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.НастройкиОтбора;
		
		ЛокальныйПорядок = Истина;
		Элементы.ЛокальныйПорядок.ТолькоПросмотр = Истина;
		Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.НастройкиПорядка;
		
		ЛокальноеУсловноеОформление = Истина;
		Элементы.ЛокальноеУсловноеОформление.ТолькоПросмотр = Истина;
		Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.НастройкиУсловногоОформления;
		
		ЛокальныеПараметрыВывода = Истина;
		Элементы.ЛокальныеПараметрыВывода.ТолькоПросмотр = Истина;
		Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.НастройкиПараметровВывода;
		
	ИначеЕсли ТипЭлемента = Тип("ГруппировкаКомпоновкиДанных")
		Или ТипЭлемента = Тип("ГруппировкаТаблицыКомпоновкиДанных")
		Или ТипЭлемента = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		
		Элементы.СтраницыПолейГруппировки.ТекущаяСтраница = Элементы.НастройкиПолейГруппировки;
		
		ВыбранныеПоляДоступны(ЭлементСтруктуры);
		ОтборДоступен(ЭлементСтруктуры);
		ПорядокДоступен(ЭлементСтруктуры);
		УсловноеОформлениеДоступно(ЭлементСтруктуры);
		ПараметрыВыводаДоступны(ЭлементСтруктуры);
		
	ИначеЕсли ТипЭлемента = Тип("ТаблицаКомпоновкиДанных")
		Или ТипЭлемента = Тип("ДиаграммаКомпоновкиДанных") Тогда
		
		ПоляГруппировкиНедоступны();
		ВыбранныеПоляДоступны(ЭлементСтруктуры);
		ОтборНедоступен();
		ПорядокНедоступен();
		УсловноеОформлениеДоступно(ЭлементСтруктуры);
		ПараметрыВыводаДоступны(ЭлементСтруктуры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛокальныеВыбранныеПоляПриИзменении(Элемент)
	
	Если ЛокальныеВыбранныеПоля Тогда
		
		Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.НастройкиВыбранныхПолей;
		
	Иначе
		
		Элементы.СтраницыПолейВыбора.ТекущаяСтраница = Элементы.ОтключенныеНастройкиВыбранныхПолей;

		ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
		Отчет.КомпоновщикНастроек.Настройки.ОчиститьВыборЭлемента(ЭлементСтруктуры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛокальныйОтборПриИзменении(Элемент)
	
	Если ЛокальныйОтбор Тогда
		
		Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.НастройкиОтбора;
		
	Иначе
		
		Элементы.СтраницыОтбора.ТекущаяСтраница = Элементы.ОтключенныеНастройкиОтбора;

		ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
		Отчет.КомпоновщикНастроек.Настройки.ОчиститьОтборЭлемента(ЭлементСтруктуры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛокальныйПорядокПриИзменении(Элемент)
	
	Если ЛокальныйПорядок Тогда
		
		Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.НастройкиПорядка;
		
	Иначе
		
		Элементы.СтраницыПорядка.ТекущаяСтраница = Элементы.ОтключенныеНастройкиПорядка;
		
		ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
		Отчет.КомпоновщикНастроек.Настройки.ОчиститьПорядокЭлемента(ЭлементСтруктуры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛокальноеУсловноеОформлениеПриИзменении(Элемент)

	Если ЛокальноеУсловноеОформление Тогда
		
		Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.НастройкиУсловногоОформления;
		
	Иначе
		
		Элементы.СтраницыУсловногоОформления.ТекущаяСтраница = Элементы.ОтключенныеНастройкиУсловногоОформления;
		
		ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
		Отчет.КомпоновщикНастроек.Настройки.ОчиститьУсловноеОформлениеЭлемента(ЭлементСтруктуры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛокальныеПараметрыВыводаПриИзменении(Элемент)
	
	Если ЛокальныеПараметрыВывода Тогда
		
		Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.НастройкиПараметровВывода;
		
	Иначе
		
		Элементы.СтраницыПараметровВывода.ТекущаяСтраница = Элементы.ОтключенныеНастройкиПараметровВывода;
		
		ЭлементСтруктуры = Отчет.КомпоновщикНастроек.Настройки.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройки.ТекущаяСтрока);
		Отчет.КомпоновщикНастроек.Настройки.ОчиститьПараметрыВыводаЭлемента(ЭлементСтруктуры);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
