///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ТекстСообщения.Заголовок = Параметры.ТекстСообщения;
	Заголовок = Параметры.Заголовок;
	ДобавитьКомандыИКнопкиНаФорму(Параметры.Кнопки);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстСообщенияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, НавигационнаяСсылкаФорматированнойСтроки);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервере
Процедура ДобавитьКомандыИКнопкиНаФорму(Кнопки)
	СоответствиеКнопокЗначениям = Новый Соответствие;
	
	Индекс = 0;
	
	Для Каждого ЭлементОписаниеКнопки Из Кнопки Цикл
		Индекс = Индекс + 1;
		ИмяКоманды = "Команда" + XMLСтрока(Индекс);
		Команда = Команды.Добавить(ИмяКоманды);
		Команда.Действие  = "Подключаемый_ОбработчикКоманды";
		Команда.Заголовок = ЭлементОписаниеКнопки.Представление;
		Команда.ИзменяетСохраняемыеДанные = Ложь;
		Команда.НазначениеДействия = НазначениеДействияКомандыФормы.Завершение;
		
		Кнопка= Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), КоманднаяПанель);
		Кнопка.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
		Кнопка.ИмяКоманды = ИмяКоманды;
		Если ЭлементОписаниеКнопки.Пометка Тогда
			Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
				Кнопка.Важность = ВажностьКнопки.Главная;
			Иначе
				Кнопка.КнопкаПоУмолчанию = Истина;
			КонецЕсли;
		КонецЕсли;
		
		СоответствиеКнопокЗначениям.Вставить(ИмяКоманды, ЭлементОписаниеКнопки.Значение);
	КонецЦикла;
	
	СоответствиеКнопокИВозвращаемыхЗначений = Новый ФиксированноеСоответствие(СоответствиеКнопокЗначениям);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикКоманды(Команда)
	ВыбранноеЗначение = СоответствиеКнопокИВозвращаемыхЗначений.Получить(Команда.Имя);
	Закрыть(ВыбранноеЗначение);
КонецПроцедуры

#КонецОбласти
