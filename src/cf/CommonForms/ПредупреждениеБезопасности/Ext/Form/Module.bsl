///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидПредупреждения = Параметры.Ключ;
	Если Не ЗначениеЗаполнено(ВидПредупреждения) Тогда
		ТекстОшибки =
			НСтр("ru = 'Общая форма ""Предупреждение безопасности"" является вспомогательной и открывается из служебных механизмов приложения.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ВидыПредупреждений = ПользователиСлужебныйКлиентСервер.ВидыПредупрежденийБезопасности();
	Если Не ВидыПредупреждений.Свойство(ВидПредупреждения) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Указан некорректный вид предупреждения безопасности ""%1"".'"),
			ВидПредупреждения);
		ВызватьИсключение(ТекстОшибки, КатегорияОшибки.ОшибкаКонфигурации);
	КонецЕсли;
	
	КлючНазначенияИспользования = ВидПредупреждения;
	КлючСохраненияПоложенияОкна = ВидПредупреждения;
	
	ТекущаяГруппаКнопок = Неопределено;
	Если ВидПредупреждения = ВидыПредупреждений.ПослеОбновления Тогда
		ТекущаяГруппаКнопок = Элементы.ПослеОбновления1;
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПослеПоявленияПрава Тогда
		ТекущаяГруппаКнопок = Элементы.ПослеПоявленияПрава1;
	КонецЕсли;
	Если ТекущаяГруппаКнопок <> Неопределено Тогда
		ТекущаяГруппаКнопок.Видимость = Истина;
		Элементы.Продолжить.Видимость = Ложь;
	КонецЕсли;
	
	Если ВидПредупреждения = ВидыПредупреждений.ПередУдалениемРасширенияБезДанных
	 Или ВидПредупреждения = ВидыПредупреждений.ПередУдалениемРасширенияСДанными
	 Или ВидПредупреждения = ВидыПредупреждений.ПередОтключениемРасширенияСДанными Тогда
		Заголовок = НСтр("ru = 'Предупреждение'");
	КонецЕсли;
	
	ПараметрыСообщения = ПользователиСлужебный.ПараметрыСообщения();
	ПараметрыСообщения.ТипСообщения = "Предупреждение";
	ПараметрыСообщения.Группа = Элементы.ГруппаПредупреждение;
	
	Если ВидПредупреждения = ВидыПредупреждений.ПослеОбновления Тогда
		Элементы.ЗапретитьОткрытиеВнешнихОтчетовИОбработок.КнопкаПоУмолчанию = Истина;
		ЗаполнитьСообщениеПослеОбновления(ПараметрыСообщения);
		
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПослеПоявленияПрава Тогда
		Элементы.ЯОзнакомлен.КнопкаПоУмолчанию = Истина;
		ЗаполнитьСообщениеПослеПоявленияПрава(ПараметрыСообщения);
	
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПередДобавлениемДополнительногоОтчетаИлиОбработки Тогда
		ЗаполнитьСообщениеПередДобавлениемДополнительногоОтчетаИлиОбработки(ПараметрыСообщения);
	
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПередДобавлениемРасширений Тогда
		ЗаполнитьСообщениеПередДобавлениемРасширений(ПараметрыСообщения);
	
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПередВыборомФайлаОбновления Тогда
		ЗаполнитьСообщениеПередВыборомФайлаОбновления(ПараметрыСообщения);
	
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПередВыборомРоли Тогда
		ЗаполнитьСообщениеПередВыборомРоли(ПараметрыСообщения);
	
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПриИзмененииСпискаЗапрещенныхРасширений Тогда
		ЗаполнитьСообщениеПриИзмененииСпискаЗапрещенныхРасширений(ПараметрыСообщения);
	
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПередОткрытиемФайла Тогда
		ЗаполнитьСообщениеПередОткрытиемФайла(ПараметрыСообщения);
	
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПередДобавлениемВнешнейКомпоненты Тогда
		ЗаполнитьСообщениеПередДобавлениемВнешнейКомпоненты(ПараметрыСообщения);
	
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПередУдалениемРасширенияБезДанных Тогда
		ЗаполнитьСообщениеПередУдалениемРасширенияБезДанных(ПараметрыСообщения);
	
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПередУдалениемРасширенияСДанными Тогда
		ЗаполнитьСообщениеПередУдалениемРасширенияСДанными(ПараметрыСообщения);
	
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПередОтключениемРасширенияСДанными Тогда
		ЗаполнитьСообщениеПередОтключениемРасширенияСДанными(ПараметрыСообщения);
	
	ИначеЕсли ВидПредупреждения = ВидыПредупреждений.ПередВключениемРегламентногоЗаданияДополнительнойОбработки Тогда
		ЗаполнитьСообщениеПередВключениемРегламентногоЗаданияДополнительнойОбработки(ПараметрыСообщения);
	КонецЕсли;
	
	ПользователиСлужебный.ВывестиСообщениеНаФорму(ЭтотОбъект, ПараметрыСообщения);
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		Возврат;
	КонецЕсли;
	
	КоманднаяПанель.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Низ;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаИзменитьФорму", "Видимость", Ложь);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаСправка", "Видимость", Ложь);
	
	Элементы.ГруппаПредупреждение.Ширина = 50;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПродолжить(Команда)
	ИмяНажатойКнопки = Команда.Имя;
	ЗакрытьФормуИВернутьРезультат();
КонецПроцедуры

&НаКлиенте
Процедура ЗапретитьОткрытиеВнешнихОтчетовИОбработок(Команда)
	РазрешитьИнтерактивноеОткрытие = Ложь;
	УправлениеРольюНаКлиенте(Команда);
КонецПроцедуры

&НаКлиенте
Процедура НеЗапрещатьОткрытиеВнешнихОтчетовИОбработок(Команда)
	РазрешитьИнтерактивноеОткрытие = Истина;
	УправлениеРольюНаКлиенте(Команда);
КонецПроцедуры

&НаКлиенте
Процедура ЯОзнакомлен(Команда)
	ИмяНажатойКнопки = Команда.Имя;
	ЯОзнакомленНаСервере();
	ЗакрытьФормуИВернутьРезультат();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеРольюНаКлиенте(Команда)
	ИмяНажатойКнопки = Команда.Имя;
	УправлениеРольюНаСервере();
	ОбновитьПовторноИспользуемыеЗначения();
	ПредложитьПерезапуск();
КонецПроцедуры

&НаСервере
Процедура УправлениеРольюНаСервере()
	Если Не ПравоДоступа("Администрирование", Метаданные) Тогда
		Возврат;
	КонецЕсли;
	РольОткрытие = Метаданные.Роли.ИнтерактивноеОткрытиеВнешнихОтчетовИОбработок;
	РольАдминистратора = Метаданные.Роли.АдминистраторСистемы;
	
	ПараметрыАдминистрирования = СтандартныеПодсистемыСервер.ПараметрыАдминистрирования();
	ПараметрыАдминистрирования.Вставить("ПринятоРешениеПоОткрытиюВнешнихОтчетовИОбработок", Истина);
	СтандартныеПодсистемыСервер.УстановитьПараметрыАдминистрирования(ПараметрыАдминистрирования);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Для Каждого ПользовательИБ Из ПользователиИБ Цикл
		Если РазрешитьИнтерактивноеОткрытие Тогда
			Если ПользовательИБ.Роли.Содержит(РольАдминистратора)
				И Не ПользовательИБ.Роли.Содержит(РольОткрытие) Тогда
				ПользовательИБ.Роли.Добавить(РольОткрытие);
				ПользовательИБ.Записать();
			КонецЕсли;
		Иначе
			Если ПользовательИБ.Роли.Содержит(РольОткрытие) Тогда
				ПользовательИБ.Роли.Удалить(РольОткрытие);
				ПользовательИБ.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если РазрешитьИнтерактивноеОткрытие Тогда
		НуженПерезапуск = Не ПравоДоступа("ИнтерактивноеОткрытиеВнешнихОбработок", Метаданные);
	Иначе
		НуженПерезапуск = ПравоДоступа("ИнтерактивноеОткрытиеВнешнихОбработок", Метаданные);
	КонецЕсли;
	
	ЯОзнакомленНаСервере();
	
	// В модели сервиса право открытия внешних отчетов и обработок
	// не используется для пользователей областей данных.
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступомСлужебный = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступомСлужебный");
		МодульУправлениеДоступомСлужебный.УстановитьПравоОткрытияВнешнихОтчетовИОбработок(РазрешитьИнтерактивноеОткрытие);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЯОзнакомленНаСервере()
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПредупреждениеБезопасности", "ПользовательОзнакомлен", Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуИВернутьРезультат()
	Если Открыта() Тогда
		ОповеститьОВыборе(ИмяНажатойКнопки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредложитьПерезапуск()
	Если Не НуженПерезапуск Тогда
		ЗакрытьФормуИВернутьРезультат();
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПерезапуститьПрограмму", ЭтотОбъект);
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("Перезапустить", НСтр("ru = 'Перезапустить'"));
	Кнопки.Добавить("НеПерезапускать", НСтр("ru = 'Не перезапускать'"));
	ТекстВопроса = НСтр("ru = 'Для применения изменений требуется перезапустить приложение.'");
	ПоказатьВопрос(Обработчик, ТекстВопроса, Кнопки);
КонецПроцедуры

&НаКлиенте
Процедура ПерезапуститьПрограмму(Ответ, ПараметрыВыполнения) Экспорт
	ЗакрытьФормуИВернутьРезультат();
	Если Ответ = "Перезапустить" Тогда
		ЗавершитьРаботуСистемы(Ложь, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьДействиеСообщения(Элемент,
			НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	Если НавигационнаяСсылка = НавигационнаяСсылкаОбработкиРезервногоКопирования Тогда
		Закрыть(КодВозвратаДиалога.Отмена);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТекстСПримеромПроблемы(ИсходныйТекст)
	
	ТекстСПримером = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'См. также: <a href=""%1"">Пример проблемы с вирусом во внешней обработке</a>.'"),
		"http://1c.ru/news/info.jsp?id=21537");
	
	Возврат ИсходныйТекст + Символы.ПС + Символы.ПС + ТекстСПримером;
	
КонецФункции

&НаСервере
Функция ТекстПоясненияКДействиюСРасширением(Документация, Действия, РезервноеКопирование, ТекстВопроса)
	
	ЧастиТекста = Новый Массив;
	
	Если Документация Тогда
		ЧастиТекста.Добавить(
			НСтр("ru = 'Ознакомьтесь с документацией по расширению, которую можно получить от поставщика расширения.'"));
	КонецЕсли;
	
	Если Действия Тогда
		ЧастиТекста.Добавить(
			НСтр("ru = 'Если требуется...
			           |• обновить расширение, то вместо удаления и добавления нажмите кнопку <b>Обновить из файла...</b>;
			           |• временно отключить расширение, следует снять флажок <b>Подключать расширение при входе</b>.'"));
	КонецЕсли;
	
	Если РезервноеКопирование И Не ОбщегоНазначения.РазделениеВключено() Тогда 
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РезервноеКопированиеИБ") Тогда
			МодульРезервноеКопированиеИБСервер = ОбщегоНазначения.ОбщийМодуль("РезервноеКопированиеИБСервер");
			НавигационнаяСсылкаОбработкиРезервногоКопирования =
				МодульРезервноеКопированиеИБСервер.НавигационнаяСсылкаОбработкиРезервногоКопирования();
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Перед удалением расширения рекомендуется <a href=""%1"">выполнить резервное копирование информационной базы</a>.'"),
				НавигационнаяСсылкаОбработкиРезервногоКопирования);
		Иначе
			Текст = НСтр("ru = 'Перед удалением расширения рекомендуется выполнить резервное копирование информационной базы.'");
		КонецЕсли;
		ЧастиТекста.Добавить(Символы.ПС + Текст);
	КонецЕсли;
	
	ЧастиТекста.Добавить(Символы.ПС + ТекстВопроса);
	
	Возврат СтрСоединить(ЧастиТекста, Символы.ПС);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСообщениеПослеОбновления(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'В целях безопасности рекомендуется запретить открытие внешних отчетов и обработок из меню <b>Файл</b> - <b>Открыть</b>.'"));
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
		МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
		ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(ТекстСПримеромПроблемы(
			НСтр("ru = 'Файлы внешних отчетов и обработок, полученные из источников, с которыми нет договоренности о разработке таких отчетов и обработок, могут нанести вред компьютеру и данным в приложении.
			           |
			           |Вместо открытия внешних отчетов и обработок рекомендуется использовать <a href=""%1"">дополнительные отчеты и обработки</a> и <a href=""%2"">расширения</a>, полученные из надежных источников.'")),
			МодульДополнительныеОтчетыИОбработки.НавигационнаяСсылкаСправочника(),
			"e1cib/app/ОбщаяФорма.Расширения");
	Иначе
		ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(ТекстСПримеромПроблемы(
			НСтр("ru = 'Файлы внешних отчетов и обработок, полученные из источников, с которыми нет договоренности о разработке таких отчетов и обработок, могут нанести вред компьютеру и данным в приложении.
			           |
			           |Вместо открытия внешних отчетов и обработок рекомендуется использовать <a href=""%2"">расширения</a>, полученные из надежных источников.'")),
			"e1cib/app/ОбщаяФорма.Расширения");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПослеПоявленияПрава(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'В настройках прав доступа разрешено открывать файлы внешних отчетов и обработок из меню <b>Файл</b> - <b>Открыть</b>.'"));
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(ТекстСПримеромПроблемы(
		НСтр("ru = 'При открытии таких файлов следует обращать особое внимание на их источник и не открывать файлы, полученные из источников, с которыми нет договоренности о разработке таких отчетов и обработок.
		           |
		           |В противном случае, такие файлы могут нанести вред компьютеру и данным в приложении.'")));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПередДобавлениемДополнительногоОтчетаИлиОбработки(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок =
		НСтр("ru = 'При подключении дополнительного отчета или обработки рекомендуется обращать внимание на источник, из которого был получен файл.'");
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(ТекстСПримеромПроблемы(
		НСтр("ru = 'Если нет уверенности в содержимом этого файла или в источнике, то файл не рекомендуется подключать к приложению.'")));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПередДобавлениемРасширений(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок =
		НСтр("ru = 'При подключении расширения конфигурации рекомендуется обращать внимание на источник, из которого был получен файл расширения.'");
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(ТекстСПримеромПроблемы(
		НСтр("ru = 'Если нет уверенности в содержимом расширения или в его источнике, то расширение не рекомендуется подключать к приложению.'")));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПередВыборомФайлаОбновления(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок =
		НСтр("ru = 'При установке обновления конфигурации рекомендуется обращать внимание на источник, из которого был получен файл обновления.'");
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(ТекстСПримеромПроблемы(
		НСтр("ru = 'Если нет уверенности в содержимом обновления или в его источнике, то обновление не рекомендуется устанавливать.'")));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПередВыборомРоли(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'Выбранная роль позволяет открывать файлы внешних отчетов и обработок из меню <b>Файл</b> - <b>Открыть</b>.'"));
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(ТекстСПримеромПроблемы(
		НСтр("ru = 'При открытии таких файлов следует обращать особое внимание на их источник и не открывать файлы, полученные из источников, с которыми нет договоренности о разработке таких отчетов и обработок.
		           |
		           |В противном случае, такие файлы могут нанести вред вашему компьютеру и данным в приложении.'")));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПриИзмененииСпискаЗапрещенныхРасширений(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'Отключение данной настройки позволяет загружать потенциально опасные файлы в приложение.'"));
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(ТекстСПримеромПроблемы(
		НСтр("ru = 'При добавлении таких файлов следует обращать особое внимание на источники, из которых они были получены, и не загружать файлы, в которых нет уверенности.
		           |
		           |В противном случае, такие файлы могут нанести вред компьютерам пользователей.'")));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПередОткрытиемФайла(ПараметрыСообщения)
	
	ИмяФайла = ?(ЗначениеЗаполнено(Параметры.ДополнительныйПараметр),
		"<b>" + Параметры.ДополнительныйПараметр + "</b>" + Символы.ПС, "");
	
	ПараметрыСообщения.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		ИмяФайла + НСтр("ru = 'Файл содержит исполняемый код, который может быть потенциально опасен для компьютера.'"));
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(ТекстСПримеромПроблемы(
		НСтр("ru = 'Открывайте файлы, только если доверяете их содержимому.
		           |В противном случае, такие файлы могут нанести вред компьютеру.'")));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПередДобавлениемВнешнейКомпоненты(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок =
		НСтр("ru = 'При подключении внешней компоненты рекомендуется обращать внимание на источник, из которого был получен файл компоненты.'");
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(ТекстСПримеромПроблемы(
		НСтр("ru = 'Если нет уверенности в содержимом компоненты или в ее источнике, то компоненту не рекомендуется подключать к приложению.'")));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПередУдалениемРасширенияБезДанных(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок =
		НСтр("ru = 'Удаление расширения необратимо.
		           |Будут потеряны сделанные настройки (например, варианты отчетов).'");
	
	ТекстВопроса = ?(Параметры.ДополнительныйПараметр = Истина,
		НСтр("ru = 'Удалить выделенные расширения?'"),
		НСтр("ru = 'Удалить расширение?'"));
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(
		ТекстПоясненияКДействиюСРасширением(Ложь, Истина, Истина, ТекстВопроса));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПередУдалениемРасширенияСДанными(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок =
		НСтр("ru = 'Удаляемое расширение хранит данные в информационной базе, которые будут безвозвратно потеряны.'");
	
	ТекстВопроса = ?(Параметры.ДополнительныйПараметр = Истина,
		НСтр("ru = 'Удалить выделенные расширения?'"),
		НСтр("ru = 'Удалить расширение?'"));
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(
		ТекстПоясненияКДействиюСРасширением(Истина, Истина, Истина, ТекстВопроса));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПередОтключениемРасширенияСДанными(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок =
		НСтр("ru = 'Отключаемое расширение хранит данные в приложении, которые станут недоступными.'");
	
	ТекстВопроса = НСтр("ru = 'Отключить расширение?'");
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'Некоторые данные самого приложения могут также стать недоступными для изменения.'")
		+ Символы.ПС
		+ Символы.ПС
		+ ТекстПоясненияКДействиюСРасширением(Истина, Ложь, Ложь, ТекстВопроса));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСообщениеПередВключениемРегламентногоЗаданияДополнительнойОбработки(ПараметрыСообщения)
	
	ПараметрыСообщения.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'Команда содержит исполняемый код, который может быть потенциально опасен для компьютера и будет выполняться регламентным заданием под правами администратора.'"));
	
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(ТекстСПримеромПроблемы(
		НСтр("ru = 'Рекомендуется настраивать запуск по расписанию только если доверяете ее содержимому.
		           |
		           |В противном случае, выполнение команды может нанести вред компьютеру.'")));
	
КонецПроцедуры

#КонецОбласти
