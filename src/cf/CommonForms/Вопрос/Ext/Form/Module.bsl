///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Заголовок <> "" Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	Если Параметры.БлокироватьВесьИнтерфейс Тогда
		РежимБлокировкиПриОткрытииОкна = РежимБлокировкиПриОткрытииОкнаФормы.БлокироватьВесьИнтерфейс;
	КонецЕсли;
	
	Если Параметры.Картинка.Вид = ВидКартинки.Пустая Тогда
		Элементы.Предупреждение.Видимость = Ложь;
	Иначе
		Элементы.Предупреждение.Картинка = Параметры.Картинка;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.ТекстСообщения) = Тип("Строка") Тогда 
		ТекстСообщения = Параметры.ТекстСообщения;
		Элементы.ТекстСообщенияПолеВвода.Видимость = Истина;
		Элементы.ТекстСообщенияНадпись.Видимость = Ложь;
		
	ИначеЕсли ТипЗнч(Параметры.ТекстСообщения) = Тип("ФорматированнаяСтрока") Тогда
		Элементы.ТекстСообщенияНадпись.Заголовок = Параметры.ТекстСообщения;
		Элементы.ТекстСообщенияПолеВвода.Видимость = Ложь;
		Элементы.ТекстСообщенияНадпись.Видимость = Истина;
		
	ИначеЕсли ТипЗнч(Параметры.ТекстСообщения) = Тип("Неопределено") Тогда // для обратной совместимости
		ТекстСообщения = "";
	Иначе
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(Метаданные.ОбщиеФормы.Вопрос.ПолноеИмя(), 
			"ТекстСообщения", Параметры.ТекстСообщения, 
			Новый ОписаниеТипов("Строка, ФорматированнаяСтрока"));
	КонецЕсли;
	
	// Размещение флажка.
	Если ЗначениеЗаполнено(Параметры.ТекстФлажка) Тогда
		Элементы.БольшеНеЗадаватьЭтотВопрос.Заголовок = Параметры.ТекстФлажка;
		
	ИначеЕсли Не ПравоДоступа("СохранениеДанныхПользователя", Метаданные)
		  Или Не Параметры.ПредлагатьБольшеНеЗадаватьЭтотВопрос Тогда
		Элементы.БольшеНеЗадаватьЭтотВопрос.Видимость = Ложь;
	КонецЕсли;
	
	// Размещение кнопок.
	ДобавитьКомандыИКнопкиНаФорму(Параметры.Кнопки);
	УстановитьКнопкуПоУмолчанию(Параметры.КнопкаПоУмолчанию, Истина);
	УстановитьКнопкуОжидания(Параметры.КнопкаТаймаута);
	ОжиданиеСчетчик = Параметры.Таймаут;
	
	Если Не ПустаяСтрока(Параметры.Заголовок) Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	СтандартныеПодсистемыСервер.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Элементы.Переместить(Элементы.БольшеНеЗадаватьЭтотВопрос, ЭтотОбъект);
		ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Верх;
	Иначе
		НастроитьШиринуИВысотуФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Запуск обратного отсчета.
	Если ОжиданиеСчетчик >= 1 Тогда
		ОжиданиеСчетчик = ОжиданиеСчетчик + 1;
		ПродолжитьОбратныйОтсчет();
	КонецЕсли;
	
	ТекущийЭлемент = Элементы.ТекстСообщенияПолеВвода;
	
	Если Не СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		Элементы.ТекстСообщенияНадписьКонтекстноеМенюКопироватьВБуфер.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстСообщенияПолеВводаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

// Параметры:
//  Команда - КомандаФормы
//
&НаКлиенте
Процедура Подключаемый_ОбработчикКоманды(Команда)
	ВыбранноеЗначение = СоответствиеКнопокИВозвращаемыхЗначений.Получить(Команда.Имя);
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("БольшеНеЗадаватьЭтотВопрос", БольшеНеЗадаватьЭтотВопрос);
	РезультатВыбора.Вставить("Значение", КодВозвратаДиалогаПоЗначению(ВыбранноеЗначение));
	
	Закрыть(РезультатВыбора);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТекстСообщенияНадписьКонтекстноеМенюКопироватьВБуфер(Команда)
	
	Если Не СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементБуфера = Новый ЭлементБуфераОбмена(СтандартныйФорматДанныхБуфераОбмена.Текст,
		Строка(Элементы.ТекстСообщенияНадпись.Заголовок));
	
	СредстваБуфераОбмена.ПоместитьДанныеАсинх(ЭлементБуфера);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Клиент

&НаКлиенте
Процедура ПродолжитьОбратныйОтсчет()
	ОжиданиеСчетчик = ОжиданиеСчетчик - 1;
	Если ОжиданиеСчетчик <= 0 Тогда
		Закрыть(Новый Структура("БольшеНеЗадаватьЭтотВопрос, Значение", Ложь, КодВозвратаДиалога.Таймаут));
	Иначе
		Если ОжиданиеИмяКнопки <> "" Тогда
			НовыйЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 (осталось %2 сек.)'"),
				ОжиданиеЗаголовокКнопки,
				Строка(ОжиданиеСчетчик));
				
			ЭлементФормы = Элементы[ОжиданиеИмяКнопки]; // ВсеЭлементыФормы
			ЭлементФормы.Заголовок = НовыйЗаголовок;
		КонецЕсли;
		ПодключитьОбработчикОжидания("ПродолжитьОбратныйОтсчет", 1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция КодВозвратаДиалогаПоЗначению(Значение)
	Если ТипЗнч(Значение) <> Тип("Строка") Тогда
		Возврат Значение;
	КонецЕсли;
	
	Если Значение = "КодВозвратаДиалога.Да" Тогда
		Результат = КодВозвратаДиалога.Да;
	ИначеЕсли Значение = "КодВозвратаДиалога.Нет" Тогда
		Результат = КодВозвратаДиалога.Нет;
	ИначеЕсли Значение = "КодВозвратаДиалога.ОК" Тогда
		Результат = КодВозвратаДиалога.ОК;
	ИначеЕсли Значение = "КодВозвратаДиалога.Отмена" Тогда
		Результат = КодВозвратаДиалога.Отмена;
	ИначеЕсли Значение = "КодВозвратаДиалога.Повторить" Тогда
		Результат = КодВозвратаДиалога.Повторить;
	ИначеЕсли Значение = "КодВозвратаДиалога.Прервать" Тогда
		Результат = КодВозвратаДиалога.Прервать;
	ИначеЕсли Значение = "КодВозвратаДиалога.Пропустить" Тогда
		Результат = КодВозвратаДиалога.Пропустить;
	Иначе
		Результат = Значение;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область Сервер

&НаСервере
Процедура НастроитьШиринуИВысотуФормы()
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		ГоризонтальныйМножительЗаголовка = 0;
		ГоризонтальныйМножитель = 1;
		ВертикальныйМножитель = 1.6;
	Иначе
		ГоризонтальныйМножительЗаголовка = 1.3;
		ГоризонтальныйМножитель = 0.75;
		ВертикальныйМножитель = 1.05;
	КонецЕсли;
	
	СтрокиТекста = СтрРазделить(Параметры.ТекстСообщения, Символы.ПС);
	ШиринаВСимволах = 1;
	Для Каждого СтрокаТекста Из СтрокиТекста Цикл
		Если ШиринаВСимволах < СтрДлина(СтрокаТекста) Тогда
			ШиринаВСимволах = СтрДлина(СтрокаТекста);
		КонецЕсли;
	КонецЦикла;
	
	ШиринаВПунктах = Цел(ШиринаВСимволах * ГоризонтальныйМножитель) + 1;
	
	Если ШиринаВПунктах > 80 Тогда
		ШиринаВПунктах = 80;
		ВысотаВСтроках = ВысотаВСтрокахСУчетомПереносаСтрок(СтрокиТекста,
			ШиринаВПунктах, ГоризонтальныйМножитель);
	Иначе
		ВысотаВСтроках = СтрокиТекста.Количество();
		Если ШиринаВПунктах < 40 Тогда
			ШиринаВПунктах = 40;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Параметры.Заголовок) Тогда
		ШиринаЗаголовка = ГоризонтальныйМножительЗаголовка * СтрДлина(Заголовок);
		Если ШиринаЗаголовка > 40 И ШиринаЗаголовка < 80 Тогда
			Ширина = ШиринаЗаголовка;
		ИначеЕсли ШиринаЗаголовка >= 80 Тогда
			Ширина = 80;
		КонецЕсли;
	КонецЕсли;
	
	Если ВысотаВСтроках < 7 И ШиринаВПунктах > 40 И ШиринаВПунктах > ШиринаЗаголовка Тогда
		ШиринаВПунктах = 45;
		ВысотаВСтроках = ВысотаВСтрокахСУчетомПереносаСтрок(СтрокиТекста,
			ШиринаВПунктах, ГоризонтальныйМножитель);
	ИначеЕсли ВысотаВСтроках < 12 И ШиринаВПунктах > 60 И ШиринаВПунктах > ШиринаЗаголовка Тогда
		ШиринаВПунктах = 65;
		ВысотаВСтроках = ВысотаВСтрокахСУчетомПереносаСтрок(СтрокиТекста,
			ШиринаВПунктах, ГоризонтальныйМножитель);
	КонецЕсли;
	Если ВысотаВСтроках < 4 Тогда
		ВысотаВСтроках = 4;
	КонецЕсли;
	
	ВысотаВПунктах = ВысотаВСтроках * ВертикальныйМножитель;
	ВысотаВПунктах = Цел(ВысотаВПунктах)
		+ ?(ВысотаВПунктах - Цел(ВысотаВПунктах) > 0, 1, 0);
	Если ВысотаВПунктах > 15 Тогда
		ВысотаВПунктах = 15;
	КонецЕсли;
	
	Элементы.ТекстСообщения.Ширина = ШиринаВПунктах;
	Элементы.ТекстСообщения.Высота = ВысотаВПунктах;
	
КонецПроцедуры

&НаСервере
Функция ВысотаВСтрокахСУчетомПереносаСтрок(СтрокиТекста, ШиринаВПунктах, ГоризонтальныйМножитель)
	
	ШиринаВСимволах = Цел(ШиринаВПунктах / ГоризонтальныйМножитель);
	ВысотаВСтроках = 0;
	
	Для Каждого СтрокаТекста Из СтрокиТекста Цикл
		СтрокСПереносом = 0;
		СловаСтроки = СтрРазделить(СтрокаТекста, " ", Истина);
		ТекущаяСтрока = Неопределено;
		Для Каждого СловоСтроки Из СловаСтроки Цикл
			Пока СтрДлина(СловоСтроки) > ШиринаВСимволах Цикл
				Если ТекущаяСтрока <> Неопределено Тогда
					СтрокСПереносом = СтрокСПереносом + 1;
					ТекущаяСтрока = Неопределено;
				КонецЕсли;
				СтрокСПереносом = СтрокСПереносом + 1;
				СловоСтроки = Сред(СловоСтроки, СтрДлина(СловоСтроки) + 1);
			КонецЦикла;
			НоваяСтрока = ?(ПустаяСтрока(ТекущаяСтрока), "", ТекущаяСтрока + " ") + СловоСтроки;
			Если СтрДлина(НоваяСтрока) > ШиринаВСимволах Тогда
				СтрокСПереносом = СтрокСПереносом + 1;
				ТекущаяСтрока = СловоСтроки;
			Иначе
				ТекущаяСтрока = НоваяСтрока;
			КонецЕсли;
		КонецЦикла;
		Если ТекущаяСтрока <> Неопределено Тогда
			СтрокСПереносом = СтрокСПереносом + 1;
		КонецЕсли;
		ВысотаВСтроках = ВысотаВСтроках
			+ Цел(СтрокСПереносом)
			+ ?(СтрокСПереносом - Цел(СтрокСПереносом) > 0, 1, 0);
	КонецЦикла;
	
	Возврат ВысотаВСтроках;
	
КонецФункции

&НаСервере
Процедура ДобавитьКомандыИКнопкиНаФорму(Кнопки)
	// Добавляет команды и соответствующие им кнопки на форму.
	//
	// Параметры:
	//  Кнопки - Строка / СписокЗначений - набор кнопок
	//		   если строка - строковый идентификатор в формате "РежимДиалогаВопрос.<одно из значений РежимДиалогаВопрос>",
	//		   например "РежимДиалогаВопрос.ДаНет"
	//		   если СписокЗначений - для каждой записи,
	//		   Значение - значение возвращаемое формой при нажатии кнопки.
	//		   Представление - заголовок кнопки.
	
	Если ТипЗнч(Кнопки) = Тип("Строка") Тогда
		КнопкиСписокЗначений = СтандартныйНабор(Кнопки);
	Иначе
		КнопкиСписокЗначений = Кнопки;
	КонецЕсли;
	
	СоответствиеКнопокЗначениям = Новый Соответствие;
	
	Индекс = 0;
	
	Для Каждого ЭлементОписаниеКнопки Из КнопкиСписокЗначений Цикл
		Индекс = Индекс + 1;
		ИмяКоманды = "Команда" + XMLСтрока(Индекс);
		Команда = Команды.Добавить(ИмяКоманды);
		Команда.Действие  = "Подключаемый_ОбработчикКоманды";
		Команда.Заголовок = ЭлементОписаниеКнопки.Представление;
		Команда.ИзменяетСохраняемыеДанные = Ложь;
		Команда.НазначениеДействия = НазначениеДействияКомандыФормы.Завершение;
		
		Кнопка= Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), КоманднаяПанель);
		Кнопка.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
		Кнопка.ИмяКоманды = ИмяКоманды;
		
		СоответствиеКнопокЗначениям.Вставить(ИмяКоманды, ЭлементОписаниеКнопки.Значение);
	КонецЦикла;
	
	СоответствиеКнопокИВозвращаемыхЗначений = Новый ФиксированноеСоответствие(СоответствиеКнопокЗначениям);
КонецПроцедуры

&НаСервере
Процедура УстановитьКнопкуПоУмолчанию(КнопкаПоУмолчанию, ВыделятьКнопкуПоУмолчанию)
	Если СоответствиеКнопокИВозвращаемыхЗначений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Кнопка = Неопределено;
	Для Каждого Элемент Из СоответствиеКнопокИВозвращаемыхЗначений Цикл
		Если Элемент.Значение = КнопкаПоУмолчанию Тогда
			Кнопка = Элементы[Элемент.Ключ];
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Кнопка = Неопределено Тогда
		Кнопка = КоманднаяПанель.ПодчиненныеЭлементы[0];
	КонецЕсли;
	
	Если ВыделятьКнопкуПоУмолчанию Тогда
		Кнопка.Важность = ВажностьКнопки.Главная;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКнопкуОжидания(ЗначениеКнопкиОжидания)
	Если СоответствиеКнопокИВозвращаемыхЗначений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из СоответствиеКнопокИВозвращаемыхЗначений Цикл
		Если Элемент.Значение = ЗначениеКнопкиОжидания Тогда
			ОжиданиеИмяКнопки = Элемент.Ключ;
			КомандаФормы = Команды[ОжиданиеИмяКнопки]; // КомандаФормы
			ОжиданиеЗаголовокКнопки = КомандаФормы.Заголовок;
			Возврат;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтандартныйНабор(Кнопки)
	Результат = Новый СписокЗначений;
	
	Если Кнопки = "РежимДиалогаВопрос.ДаНет" Тогда
		Результат.Добавить("КодВозвратаДиалога.Да",  НСтр("ru = 'Да'"));
		Результат.Добавить("КодВозвратаДиалога.Нет", НСтр("ru = 'Нет'"));
	ИначеЕсли Кнопки = "РежимДиалогаВопрос.ДаНетОтмена" Тогда
		Результат.Добавить("КодВозвратаДиалога.Да",     НСтр("ru = 'Да'"));
		Результат.Добавить("КодВозвратаДиалога.Нет",    НСтр("ru = 'Нет'"));
		Результат.Добавить("КодВозвратаДиалога.Отмена", НСтр("ru = 'Отмена'"));
	ИначеЕсли Кнопки = "РежимДиалогаВопрос.ОК" Тогда
		Результат.Добавить("КодВозвратаДиалога.ОК", НСтр("ru = 'ОК'"));
	ИначеЕсли Кнопки = "РежимДиалогаВопрос.ОКОтмена" Тогда
		Результат.Добавить("КодВозвратаДиалога.ОК",     НСтр("ru = 'ОК'"));
		Результат.Добавить("КодВозвратаДиалога.Отмена", НСтр("ru = 'Отмена'"));
	ИначеЕсли Кнопки = "РежимДиалогаВопрос.ПовторитьОтмена" Тогда
		Результат.Добавить("КодВозвратаДиалога.Повторить", НСтр("ru = 'Повторить'"));
		Результат.Добавить("КодВозвратаДиалога.Отмена",    НСтр("ru = 'Отмена'"));
	ИначеЕсли Кнопки = "РежимДиалогаВопрос.ПрерватьПовторитьПропустить" Тогда
		Результат.Добавить("КодВозвратаДиалога.Прервать",   НСтр("ru = 'Прервать'"));
		Результат.Добавить("КодВозвратаДиалога.Повторить",  НСтр("ru = 'Повторить'"));
		Результат.Добавить("КодВозвратаДиалога.Пропустить", НСтр("ru = 'Пропустить'"));
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецОбласти