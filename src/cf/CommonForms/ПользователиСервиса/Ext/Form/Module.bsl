///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	// Форма недоступна пока подготовка не закончена.
	Доступность = Ложь;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		Возврат;
	КонецЕсли;
	
	СтандартныеПодсистемыСервер.ПереместитьЭлементыВГруппу(КоманднаяПанель,
		Элементы.ПользователиСервиса.КоманднаяПанель);
	
	// Корректировка порядка команд в командной панели.
	СтандартныеПодсистемыСервер.ПереместитьЭлементыВГруппе(Элементы.ПользователиСервиса.КоманднаяПанель,
		"ФормаВключитьДоступПомеченным
		|ПользователиСервисаУстановитьФлажки
		|ПользователиСервисаСнятьФлажки");
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаСправка", "Видимость", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПарольПользователяСервиса = Неопределено Тогда
		Отказ = Истина;
		СтандартныеПодсистемыКлиент.УстановитьХранениеФормы(ЭтотОбъект, Истина);
		ПодключитьОбработчикОжидания("ЗапроситьПарольДляАутентификацииВСервисе", 0.1, Истина);
	Иначе
		ПодготовитьФорму();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПользователиСервисаУстановитьФлажки(Команда)
	
	ПользователиСервисаИзменитьФлажки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиСервисаСнятьФлажки(Команда)
	
	ПользователиСервисаИзменитьФлажки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаВключитьДоступПомеченным(Команда)
	
	Количество = КоличествоПользователейСВключеннымДоступомНаСервере();
	Если Количество = 0 Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Пометьте пользователей, которым нужно включить доступ.'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПользователиСервисаДобавить.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаПользователиСервиса.Доступ");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьПарольДляАутентификацииВСервисе()
	
	СтандартныеПодсистемыКлиент.УстановитьХранениеФормы(ЭтотОбъект, Ложь);
	
	ПользователиСлужебныйКлиент.ЗапроситьПарольДляАутентификацииВСервисе(
		Новый ОписаниеОповещения("ПриОткрытииПродолжение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииПродолжение(НовыйПарольПользователяСервиса, Контекст) Экспорт
	
	Если НовыйПарольПользователяСервиса <> Неопределено Тогда
		ПарольПользователяСервиса = НовыйПарольПользователяСервиса;
		Открыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФорму()
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		Доступность = Истина;
		Возврат;
	КонецЕсли;
	
	ПользователиСлужебныйВМоделиСервиса.ПолучитьДействияСПользователемСервиса(
		Справочники.Пользователи.ПустаяСсылка());
	
	ТаблицаПользователей = ПользователиСлужебныйВМоделиСервиса.ПолучитьПользователейСервиса(
		ПарольПользователяСервиса);
	
	Для Каждого ИнформацияОПользователе Из ТаблицаПользователей Цикл
		НоваяСтрока = ТаблицаПользователиСервиса.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ИнформацияОПользователе);
		НоваяСтрока.Добавить = НоваяСтрока.Доступ;
	КонецЦикла;
	
	Доступность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиСервисаИзменитьФлажки(Включить)
	
	Для Каждого СтрокаТаблицы Из ТаблицаПользователиСервиса Цикл
		Если СтрокаТаблицы.Доступ Тогда
			Продолжить;
		КонецЕсли;
		СтрокаТаблицы.Добавить = Включить;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция КоличествоПользователейСВключеннымДоступомНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	Количество = 0;
	
	Для Каждого СтрокаТаблицы Из ТаблицаПользователиСервиса Цикл
		Если СтрокаТаблицы.Доступ Или Не СтрокаТаблицы.Добавить Тогда
			Продолжить;
		КонецЕсли;
		
		ПользователиСлужебныйВМоделиСервиса.ПредоставитьДоступПользователюСервиса(
			СтрокаТаблицы.Идентификатор, ПарольПользователяСервиса);
		
		СтрокаТаблицы.Доступ = Истина;
		СтрокаТаблицы.Добавить = Истина;
		Количество = Количество + 1;
	КонецЦикла;
	
	Возврат Количество;
	
КонецФункции

#КонецОбласти
