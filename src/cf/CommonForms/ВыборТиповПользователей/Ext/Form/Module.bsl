///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Заголовок;
	
	Если Параметры.ВыбиратьПользователей Тогда
		ДобавитьСтрокуТипа(Справочники.Пользователи.ПустаяСсылка(),
			Тип("СправочникСсылка.Пользователи"));
	КонецЕсли;
	
	Если ВнешниеПользователи.ИспользоватьВнешнихПользователей() Тогда
		ПустыеСсылки = ПользователиСлужебныйПовтИсп.ПустыеСсылкиТиповОбъектовАвторизации();
		Для Каждого ПустаяСсылка Из ПустыеСсылки Цикл
			ДобавитьСтрокуТипа(ПустаяСсылка, ТипЗнч(ПустаяСсылка));
		КонецЦикла;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		Возврат;
	КонецЕсли;
	
	СтандартныеПодсистемыСервер.ПереместитьЭлементыВГруппу(КоманднаяПанель,
		Элементы.Назначение.КоманднаяПанель);
	
	// Корректировка порядка команд в командной панели.
	СтандартныеПодсистемыСервер.ПереместитьЭлементыВГруппе(Элементы.Назначение.КоманднаяПанель,
		"ФормаВыбрать
		|НазначениеУстановитьФлажки
		|НазначениеСнятьФлажки");
	
	Элементы.Назначение.Ширина = 50;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть(Назначение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьСтрокуТипа(Значение, Тип)
	
	Если Не ЗначениеЗаполнено(Параметры.Назначение) Тогда
		Пометка = Ложь;
	ИначеЕсли Параметры.ЭтоОтбор Тогда
		Пометка = Параметры.Назначение.НайтиПоЗначению(Значение) <> Неопределено;
	Иначе
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ТипПользователей", Значение);
		НайденныеСтроки = Параметры.Назначение.НайтиСтроки(ПараметрыОтбора);
		Пометка = НайденныеСтроки.Количество() > 0;
	КонецЕсли;
	
	Назначение.Добавить(Значение, Метаданные.НайтиПоТипу(Тип).Синоним, Пометка);
	
КонецПроцедуры

#КонецОбласти