///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

Функция ОтправитьКодАвторизации(Запрос)
	
	Попытка
		
		ПараметрыЗапроса = ПараметрыЗапроса(Запрос);
		ОтправитьСерверноеОповещение(ПараметрыЗапроса);
		
		КодОтвета = КодыОтветов().Успех;
		ТекстОтвета = ТекстУспешногоОтвета();
		
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		Если ИнформацияОбОшибке.Код = "НеизвестныйЗапрос" Тогда
			КодОтвета = КодыОтветов().НеизвестныйЗапрос;
		Иначе
			КодОтвета = КодыОтветов().ВнутренняяОшибка;
		КонецЕсли;
		
		ТекстОтвета = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		
		ИмяСобытия = ИмяСобытия() + "." + НСтр("ru = 'Исключение при отправке кода авторизации'", КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ТекстОтвета);
		
	КонецПопытки;
	
	Ответ = Новый HTTPСервисОтвет(КодОтвета);
	Ответ.Заголовки.Вставить("Content-Type", "text/html; charset=utf-8");
	Ответ.УстановитьТелоИзСтроки(ТекстОтвета);
	
	Возврат Ответ;
	
КонецФункции

Функция ПроверитьДоступностьСервиса(Запрос)
	
	КодОтвета = КодыОтветов().Успех;
	
	Ответ = Новый HTTPСервисОтвет(КодОтвета);
	Ответ.Заголовки.Вставить("Content-Type", "text/html; charset=utf-8");
	Ответ.УстановитьТелоИзСтроки("soccess");
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыЗапроса(Запрос)
	
	ВходящиеПараметрыЗапроса = Запрос.ПараметрыЗапроса;
	
	ОбработанныеПараметрыЗапроса = Новый Структура;
	ОбработанныеПараметрыЗапроса.Вставить("КодАвторизации");
	ОбработанныеПараметрыЗапроса.Вставить("НомерСеанса");
	ОбработанныеПараметрыЗапроса.Вставить("НомерОбласти");
	ОбработанныеПараметрыЗапроса.Вставить("ИдентификаторЗапроса");
	ОбработанныеПараметрыЗапроса.Вставить("ИдентификаторПользователя");
	
	ЗаполнитьКодАвторизации(ВходящиеПараметрыЗапроса, ОбработанныеПараметрыЗапроса);
	ЗаполнитьНомерСеанса(ВходящиеПараметрыЗапроса, ОбработанныеПараметрыЗапроса);
	ЗаполнитьНомерОбласти(ВходящиеПараметрыЗапроса, ОбработанныеПараметрыЗапроса);
	ЗаполнитьИдентификаторЗапроса(ВходящиеПараметрыЗапроса, ОбработанныеПараметрыЗапроса);
	ЗаполнитьИдентификаторПользователя(ВходящиеПараметрыЗапроса, ОбработанныеПараметрыЗапроса);
	
	Возврат ОбработанныеПараметрыЗапроса;
	
КонецФункции

Процедура ЗаполнитьКодАвторизации(Источник, Приемник)
	
	КодАвторизации = Источник.Получить("code");
	
	Если КодАвторизации = Неопределено Тогда
		ТекстОтвета = НСтр("ru = 'Не заполнен обязательный параметр запроса ""code""'", КодОсновногоЯзыка());
		ВызватьИсключение(ТекстОтвета, , "НеизвестныйЗапрос");
	КонецЕсли;
	
	Приемник.КодАвторизации = КодАвторизации;
	
КонецПроцедуры

Процедура ЗаполнитьНомерСеанса(Источник, Приемник)
	
	НомерСеанса = Источник.Получить("session");
	
	Если НомерСеанса = Неопределено Тогда
		ТекстОтвета = НСтр("ru = 'Не заполнен обязательный параметр запроса ""session""'", КодОсновногоЯзыка());
		ВызватьИсключение(ТекстОтвета, , "НеизвестныйЗапрос");
	КонецЕсли;
	
	Попытка
		НомерСеанса = Число(НомерСеанса);
	Исключение
		ТекстОтвета = НСтр("ru = 'Параметра запроса ""session"" не является числом'", КодОсновногоЯзыка());
		ВызватьИсключение(ТекстОтвета, , "НеизвестныйЗапрос");
	КонецПопытки;
	
	Приемник.НомерСеанса = НомерСеанса;
	
КонецПроцедуры

Процедура ЗаполнитьНомерОбласти(Источник, Приемник)
	
	НомерОбласти = Источник.Получить("zone");
	
	Если НомерОбласти = Неопределено Тогда
		ТекстОтвета = НСтр("ru = 'Не заполнен обязательный параметр запроса ""zone""'", КодОсновногоЯзыка());
		ВызватьИсключение(ТекстОтвета, , "НеизвестныйЗапрос");
	КонецЕсли;
	
	Попытка
		НомерОбласти = Число(НомерОбласти);
	Исключение
		ТекстОтвета = НСтр("ru = 'Параметра запроса ""zone"" не является числом'", КодОсновногоЯзыка());
		ВызватьИсключение(ТекстОтвета, , "НеизвестныйЗапрос");
	КонецПопытки;
	
	Приемник.НомерОбласти = НомерОбласти;
	
КонецПроцедуры

Процедура ЗаполнитьИдентификаторЗапроса(Источник, Приемник)
	
	ИдентификаторЗапроса = Источник.Получить("request_id");
	
	Если ИдентификаторЗапроса = Неопределено Тогда
		ТекстОтвета = НСтр("ru = 'Не заполнен обязательный параметр запроса ""request_id""'", КодОсновногоЯзыка());
		ВызватьИсключение(ТекстОтвета, , "НеизвестныйЗапрос");
	КонецЕсли;
	
	Попытка
		ИдентификаторЗапроса = Новый УникальныйИдентификатор(ИдентификаторЗапроса);
	Исключение
		ТекстОтвета = НСтр("ru = 'Параметра запроса ""request_id"" не является уникальным идентификатором'", КодОсновногоЯзыка());
		ВызватьИсключение(ТекстОтвета, , "НеизвестныйЗапрос");
	КонецПопытки;
	
	Приемник.ИдентификаторЗапроса = ИдентификаторЗапроса;
	
КонецПроцедуры

Процедура ЗаполнитьИдентификаторПользователя(Источник, Приемник)
	
	ИдентификаторПользователя = Источник.Получить("user_id");
	
	Если ИдентификаторПользователя = Неопределено Тогда
		ТекстОтвета = НСтр("ru = 'Не заполнен обязательный параметр запроса ""user_id""'", КодОсновногоЯзыка());
		ВызватьИсключение(ТекстОтвета, , "НеизвестныйЗапрос");
	КонецЕсли;
	
	Попытка
		ИдентификаторПользователя = Новый УникальныйИдентификатор(ИдентификаторПользователя);
	Исключение
		ТекстОтвета = НСтр("ru = 'Параметра запроса ""user_id"" не является уникальным идентификатором'", КодОсновногоЯзыка());
		ВызватьИсключение(ТекстОтвета, , "НеизвестныйЗапрос");
	КонецПопытки;
	
	Приемник.ИдентификаторПользователя = ИдентификаторПользователя;
	
КонецПроцедуры

Процедура ОтправитьСерверноеОповещение(ПараметрыЗапроса)
	
	Контекст = Новый Структура;
	Контекст.Вставить("КодАвторизации", ПараметрыЗапроса.КодАвторизации);
	Контекст.Вставить("ИдентификаторЗапроса", ПараметрыЗапроса.ИдентификаторЗапроса);
	
	ПараметрыСерверногоОповещения = Новый Структура;
	ПараметрыСерверногоОповещения.Вставить("ИмяСобытия", "ОткрытаяАвторизацияПочтовогоСервиса");
	ПараметрыСерверногоОповещения.Вставить("Контекст", Контекст);
	
	ИмяСерверногоОповещения = РаботаСПочтовымиСообщениямиСлужебныйКлиентСервер.ИмяСерверногоОповещения();
	АдресатыСерверногоОповещения = АдресатыСерверногоОповещения(ПараметрыЗапроса);
	
	СерверныеОповещения.ОтправитьСерверноеОповещение(
		ИмяСерверногоОповещения,
		ПараметрыСерверногоОповещения,
		АдресатыСерверногоОповещения,
		Истина);
	
КонецПроцедуры

Функция АдресатыСерверногоОповещения(ПараметрыЗапроса)
	
	АдресатыСерверногоОповещения = Новый Соответствие;
	
	АктивныеСеансы = ПолучитьСеансыИнформационнойБазы();
	НеобходимыйСеанс = Неопределено;
	
	Для Каждого АктивныйСеанс Из АктивныеСеансы Цикл
		Если АктивныйСеанс.НомерСеанса = ПараметрыЗапроса.НомерСеанса Тогда
			НеобходимыйСеанс = АктивныйСеанс;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	КлючСеанса = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СерверныеОповещения.КлючСеанса(НеобходимыйСеанс));
	АдресатыСерверногоОповещения.Вставить(ПараметрыЗапроса.ИдентификаторПользователя, КлючСеанса);
	
	Возврат АдресатыСерверногоОповещения;
	
КонецФункции

Функция ТекстУспешногоОтвета()
	
	ТекстУспешногоОтвета = "<!DOCTYPE html>
		|<html>
		|<head>
		|    <meta charset='utf-8'>
		|    <title>Авторизация завершена</title>
		|    <style>
		|        * { margin: 0; padding: 0; box-sizing: border-box; }
		|        body {
		|            font-family: -apple-system, BlinkMacSystemFont, 'Segoe UI', Arial, sans-serif;
		|            text-align: center;
		|            padding: 60px 20px;
		|            background: #F7FAFC url(" + КотDataURL() + ") no-repeat center bottom;
		|            background-size: contain;
		|            color: #2D3748;
		|            min-height: 100vh;
		|            display: flex;
		|            align-items: center;
		|            justify-content: center;
		|        }
		|        .container {
		|            max-width: 420px;
		|            background: rgba(255,255,255,0.9);
		|            padding: 40px 30px;
		|            border-radius: 16px;
		|            box-shadow: 0 10px 30px rgba(0,0,0,0.08);
		|        }
		|        .message {
		|            font-size: 24px;
		|            margin-bottom: 12px;
		|            font-weight: 600;
		|        }
		|        p {
		|            font-size: 18px;
		|            color: #4A5568;
		|            line-height: 1.6;
		|        }
		|    </style>
		|</head>
		|<body>
		|    <div class='container'>
		|        <div class='message'>Авторизация завершена</div>
		|        <p>Эту вкладку можно закрыть</p>
		|    </div>
		|    <script>
		|        if (window.history && window.history.replaceState) {
		|            window.history.replaceState({}, document.title, window.location.pathname);
		|        }
		|        window.close();
		|    </script>
		|</body>
		|</html>";
		
	Возврат ТекстУспешногоОтвета;
	
КонецФункции

Функция КотDataURL()
	
	Картинка = БиблиотекаКартинок.КотОткрытаяАвторизацияПочтовогоСервиса;
	ДвоичныеДанные = Картинка.ПолучитьДвоичныеДанные();
	
	СтрокаBase64 = Base64Строка(ДвоичныеДанные);
	СтрокаBase64 = СтрЗаменить(СтрокаBase64, Символы.ВК, "");
	СтрокаBase64 = СтрЗаменить(СтрокаBase64, Символы.ПС, "");
	
	КотDataURL = "data:image/svg+xml;base64," + СтрокаBase64;
	
	Возврат КотDataURL;
	
КонецФункции

Функция КодыОтветов()
	
	КодыОтветов = Новый Структура;
	КодыОтветов.Вставить("Успех", 200);
	КодыОтветов.Вставить("НеизвестныйЗапрос", 400);
	КодыОтветов.Вставить("ВнутренняяОшибка", 500);
	
	Возврат КодыОтветов;
	
КонецФункции

Функция КодОсновногоЯзыка()
	
	Возврат ОбщегоНазначения.КодОсновногоЯзыка();
	
КонецФункции

Функция ИмяСобытия()
	
	Возврат НСтр("ru = 'Открытая авторизация почтового сервиса'", КодОсновногоЯзыка());
	
КонецФункции

#КонецОбласти
