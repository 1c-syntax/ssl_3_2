///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДоступныеВерсииФормата = Новый Соответствие;
	КоллекцияРасширений = Новый Соответствие;
	
	Попытка
		
		ОбменДаннымиПереопределяемый.ПриПолученииДоступныхВерсийФормата(ДоступныеВерсииФормата);
		ОбменДаннымиПереопределяемый.ПриПолученииДоступныхРасширенийФормата(КоллекцияРасширений);
		
	Исключение
		
		// Не удалось получить доступные версии формата.
		ВызватьИсключение НСтр("ru = 'В информационной базе не поддерживается синхронизация данных через универсальный формат'");
		
	КонецПопытки;
	
	Для Каждого Расширение Из КоллекцияРасширений Цикл
		
		СтрокаРасширения = РасширенияФормата.Добавить();
		СтрокаРасширения.ПространствоИмен = Расширение.Ключ;
		СтрокаРасширения.БазоваяВерсия    = Расширение.Значение;
		
	КонецЦикла;
	
	ОпределитьРежимОткрытияФормы();
	ОпределитьИмяОбработки();
	
	Объект.ИсточникВыгрузки = "Отбор";
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	
	ЗаполнитьВерсиюФорматаПоУмолчанию(ДоступныеВерсииФормата);
	ТипизироватьУзелОбмена();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВариантОткрытияФормы = "ТолькоЗагрузка" Тогда
		
		ВидОперации = "Загрузка";
		РежимРазработчика = Ложь;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидОперации) Тогда
		
		ВидОперации = "Загрузка";
		
	КонецЕсли;
	
	Если РежимРазработчика = Ложь Тогда
		
		ИсточникЗагрузки = 0;
		
	КонецЕсли;
	
	// сохраненное по умолчанию значение появляется только при открытии формы.
	Если ЗначениеЗаполнено(Объект.ПутьКМенеджеруОбменаВыгрузки) Тогда
		
		ОбновитьПравилаВыгрузкиНаСервере();
		
	КонецЕсли;
	
	ОбновитьСвойстваЭлементовФормы();
	ОбновитьПометкуСпискаВерсийФормата();
	ОбновитьЗаголовкиДекорацийЗагрузки();
	ОбновитьЗаголовкиДекорацийВыгрузки();
	
#Если ВебКлиент Тогда
	НачатьУстановкуРасширенияРаботыСФайлами();
#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Объект.ВерсияФормата) Тогда
		
		Если СписокВерсийФормата.НайтиПоЗначению(Объект.ВерсияФормата) = Неопределено Тогда
			
			ДоступныеВерсииФормата = Новый Соответствие;
			ОбменДаннымиПереопределяемый.ПриПолученииДоступныхВерсийФормата(ДоступныеВерсииФормата);
			ЗаполнитьВерсиюФорматаПоУмолчанию(ДоступныеВерсииФормата);
			
		Иначе
			
			ОбновитьПравилаВыгрузкиНаСервере();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Объект.РезультатВыгрузкиXDTO) Тогда
		
		ЗаголовокДекорации = СтрШаблон(НСтр("ru ='Показать результат прошлого сеанса'"));
		Элементы.ДекорацияРезультатВыгрузкиВФайл.Заголовок = ЗаголовокДекорации;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыгрузкаЗагрузкаПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ВидОперации = ТекущаяСтраница.Имя;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникЗагрузкиПриИзменении(Элемент)
	
	// Вместо УстановитьВидимость
	ОбновитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникВыгрузкиПриИзменении(Элемент)
	
	// Вместо УстановитьВидимость
	ОбновитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура МестоВыгрузкиПриИзменении(Элемент)
	
	// Вместо УстановитьВидимость
	ОбновитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораФайлаВыгрузки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораФайлаЗагрузки();
	
КонецПроцедуры

#Область ОбработчикиДекораций

// Загрузка

&НаКлиенте
Процедура ДекорацияВерсииФорматаЗагрузкаНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://its.1c.ru/db/metod8dev#content:5934:hdoc");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВерсииФорматаЗагрузкаЕщеНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://its.1c.ru/db/metod8dev#content:5934:hdoc");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияМенеджерЗагрузкиНажатие(Элемент)
	
	НачалоВыбораМодуляМенеджера("ПутьКМенеджеруОбменаЗагрузки");
	
КонецПроцедуры

// Выгрузка

&НаКлиенте
Процедура ДекорацияВерсияФорматаВыгрузкаНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораВерсииФорматаВыгрузки", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОписаниеОповещения, СписокВерсийФормата, Объект.ВерсияФормата);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВерсияРасширенияФорматаВыгрузкаНажатие(Элемент)
	
	СписокРасширенийФормата = Новый СписокЗначений;
	СписокРасширенийФормата.Добавить("", НСтр("ru = '<Без расширения>'"));
	Для Каждого Расширение Из РасширенияФормата.НайтиСтроки(Новый Структура("БазоваяВерсия", Объект.ВерсияФормата)) Цикл
		
		СписокРасширенийФормата.Добавить(Расширение.ПространствоИмен);
		
	КонецЦикла;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораВерсииРасширенияФорматаВыгрузки", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОписаниеОповещения, СписокРасширенийФормата, Объект.РасширениеФормата);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияМенеджерВыгрузкиНажатие(Элемент)
	
	НачалоВыбораМодуляМенеджера("ПутьКМенеджеруОбменаВыгрузки");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияМенеджерВыгрузкиОчисткаНажатие(Элемент)
	
	Объект.ПутьКМенеджеруОбменаВыгрузки = "";
	ОбновитьЗаголовкиДекорацийВыгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРезультатВыгрузкиВФайлНажатие(Элемент)
	
	ПоказатьВыгрузкуКакТекст();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаПравилВыгрузки

&НаКлиенте
Процедура ТаблицаПравилВыгрузкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ТаблицаПравилВыгрузки.ТекущиеДанные;
	Если ТекущиеДанные.ПолноеИмяМетаданных = "" Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПолноеИмяМетаданных = ТекущиеДанные.ПолноеИмяМетаданных;
	ПредставлениеОтбора = ТекущиеДанные.Представление;
	
	УстановитьИзменитьОтборСтроки(ПолноеИмяМетаданных, ПредставлениеОтбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравилВыгрузкиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПолноеИмяМД = Элементы.ТаблицаПравилВыгрузки.ТекущиеДанные.ПолноеИмяМетаданных;
	ИдентификаторТекущейСтроки = Элементы.ТаблицаПравилВыгрузки.ТекущиеДанные.ПолучитьИдентификатор();
	РедактированиеОтбораСтрокиДополнительныйСоставСервер(ВыбранноеЗначение, ПолноеИмяМД, ИдентификаторТекущейСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравилВыгрузкиВключитьПриИзменении(Элемент)
	
	ДанныеТекущейСтроки = Элементы.ТаблицаПравилВыгрузки.ТекущиеДанные;
	Если ДанныеТекущейСтроки.ПолучитьЭлементы().Количество() > 0 Тогда
		
		Для Каждого Стр Из ДанныеТекущейСтроки.ПолучитьЭлементы() Цикл
			
			Стр.Включить = ДанныеТекущейСтроки.Включить;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВсеФлажкиВключить(Команда)
	
	УстановитьСнятьФлажкиОтборов(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеФлажкиВыключить(Команда)
	
	УстановитьСнятьФлажкиОтборов(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОперацию(Команда)
	
	ИдентификаторЗадания = Неопределено;
	
	Если ВидОперации = "Загрузка" Тогда
		
		Если ИсточникЗагрузки = 1 Тогда // текстовое поле
			
			Если Не ЗначениеЗаполнено(ДанныеДляЗагрузкиXDTO) Тогда
				
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Текстовое поле с данными для загрузки не заполнено.'"));
				Возврат;
				
			КонецЕсли;
			
		Иначе
			
			Если Не ЗначениеЗаполнено(ПутьКФайлуЗагрузки) Тогда
				
				НачалоВыбораФайлаЗагрузки(Истина);
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ПодключитьОбработчикОжидания("ЗагрузитьСообщение", 0.1, Истина);
		
	Иначе
		
		ПодключитьОбработчикОжидания("ВыгрузитьДанные", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрированныеКОтправкеДанные(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.УзелОбмена) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОбменДаннымиКлиент.ОткрытьСоставОтправляемыхДанных(Объект.УзелОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрерватьВыгрузкуЗагрузку(Команда)
	
	Если ИдентификаторЗадания = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПрерватьВыгрузкуЗагрузкуСервер();
	
	ИдентификаторЗадания = Неопределено;
	Элементы.ВыгрузкаЗагрузка.ТекущаяСтраница = ?(ВидОперации = "Загрузка", Элементы.Загрузка, Элементы.Выгрузка);
	УстановитьВидимостьДоступностьКнопок(Истина);
	
	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = НСтр("ru = 'Выполнение операции прервано.'");
	Сообщение.Сообщить();

КонецПроцедуры

&НаКлиенте
Процедура СохранитьXML(Команда)
	
	АдресФайлаВХранилище = СохранитьXMLНаСервере();
	Если ЗначениеЗаполнено(ПутьКФайлуВыгрузки) Тогда
		
		ЗаписатьРезультатВыгрузкиВФайл(АдресФайлаВХранилище);
		
	Иначе
		
		НачалоВыбораФайлаВыгрузки(Истина, АдресФайлаВХранилище);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьXML(Команда)
	
	ПутьКФайлуЗагрузки = ""; // Очистим значение, т.к. будет выбрано новое
	
	ДополнительныеПараметры = Новый Структура("ВидФайла", "ФайлДанных");
	Оповещение = Новый ОписаниеОповещения("ПоместитьФайлВХранилищеЗавершить", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыЗагрузки.Диалог.Фильтр = "Файлы XML (*.xml)|*.xml";
	
	ФайловаяСистемаКлиент.ЗагрузитьФайл(Оповещение, ПараметрыЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	АдресФайлаВХранилище = СохранитьНастройкиВыгрузкиНаСервере();
	
	ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
	ПараметрыСохранения.Диалог.Фильтр = "Файлы XML (*.xml)|*.xml";
	
	ИмяФайлаПоУмолчанию = НСтр("ru = 'Файл настроек выгрузки.xml'");
	ФайловаяСистемаКлиент.СохранитьФайл(Неопределено, АдресФайлаВХранилище, ИмяФайлаПоУмолчанию, ПараметрыСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьНастройки(Команда)
	
	ДополнительныеПараметры = Новый Структура("ВидФайла", "ФайлНастроек");
	Оповещение = Новый ОписаниеОповещения("ПоместитьФайлВХранилищеЗавершить", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыЗагрузки.Диалог.Фильтр = "Файлы XML (*.xml)|*.xml";
	
	ФайловаяСистемаКлиент.ЗагрузитьФайл(Оповещение, ПараметрыЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьРежимРазработчика(Команда)
	
	РежимРазработчика = Не РежимРазработчика;
	ИспользоватьКаталогСообщенийОбменаДанными = ?(РежимРазработчика, ИспользоватьКаталогСообщенийОбменаДанными, Ложь);
	ИсточникЗагрузки = ?(РежимРазработчика, ИсточникЗагрузки, 0);
	МестоВыгрузки = ?(РежимРазработчика, МестоВыгрузки, 0);
	Объект.ИсточникВыгрузки = ?(РежимРазработчика, Объект.ИсточникВыгрузки, "Отбор");
	
	ОбновитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьВнутреннийКаталогСообщений(Команда)
	
	Элементы.ФормаИспользоватьВнутреннийКаталогСообщений.Пометка = НЕ Элементы.ФормаИспользоватьВнутреннийКаталогСообщений.Пометка;
	ИспользоватьКаталогСообщенийОбменаДанными = Элементы.ФормаИспользоватьВнутреннийКаталогСообщений.Пометка;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛогироватьВыгрузкуОбъектов(Команда)
	
	Элементы.ФормаЛогироватьВыгрузкуОбъектов.Пометка = НЕ Элементы.ФормаЛогироватьВыгрузкуОбъектов.Пометка;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИзменитьОтбор(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаПравилВыгрузки.ТекущиеДанные;
	Если ТекущиеДанные.ПолноеИмяМетаданных = "" Тогда
		
		ТекстПредупреждения = НСтр("ru ='Отборы не применимы к группам. Используйте строки с данными (конкретный справочник, документ).'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения, 10);
		Возврат;
		
	КонецЕсли;
	
	ПолноеИмяМетаданных = ТекущиеДанные.ПолноеИмяМетаданных;
	ПредставлениеОтбора = ТекущиеДанные.Представление;
	
	УстановитьИзменитьОтборСтроки(ПолноеИмяМетаданных, ПредставлениеОтбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// ::: Методы на клиенте :::

&НаКлиенте
Процедура ВывестиСообщенияФоновогоЗадания()
	
	Если НЕ ЗначениеЗаполнено(МассивСообщений) Тогда
		
		МассивСообщений = ПрочитатьСообщенияФоновогоЗадания(ИдентификаторЗадания);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивСообщений) Тогда
		
		Для Каждого ТекСообщение Из МассивСообщений Цикл
			
			Если СтрНачинаетсяС(ТекСообщение.Текст,"{") Тогда
				Продолжить;
			КонецЕсли;
			
			ТекСообщение.Сообщить();
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанные()
	
	Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = НСтр("ru = 'Выполняется выгрузка данных...'");
	УстановитьВидимостьДоступностьКнопок(Ложь);
	
	МассивСообщений = Неопределено;
	ДлительнаяОперация = ВыгрузитьДанныеНаСервере();
	
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		АдресХраненияРезультата = ДлительнаяОперация.АдресРезультата;
		ДлительнаяОперация.Свойство("Сообщения", МассивСообщений);
		ОбработатьРезультатВыгрузки();
		
	Иначе
		
		МодульДлительныеОперацииКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ДлительныеОперацииКлиент");
		ПараметрыОжидания = МодульДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииОперацииВыгрузки", ЭтотОбъект);
		МодульДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыгрузкуДанныхВТекст()
	
	Объект.РезультатВыгрузкиXDTO = ПолучитьИзВременногоХранилища(АдресХраненияРезультата);
	
	ЗаголовокДекорации = СтрШаблон(НСтр("ru ='Показать результат от %1'"), Формат(ОбщегоНазначенияКлиент.ДатаСеанса(), "ДФ=HH:mm:ss"));
	Элементы.ДекорацияРезультатВыгрузкиВФайл.Заголовок = ЗаголовокДекорации;
	
	СообщитьОбОкончанииОперации(Ложь);
	
	ПоказатьВыгрузкуКакТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСообщение()
	
	Элементы.ДекорацияПоясняющийТекстДлительнойОперации.Заголовок = НСтр("ru = 'Выполняется загрузка данных...'");
	
	УстановитьВидимостьДоступностьКнопок(Ложь);
	
	Если ИсточникЗагрузки = 1 Тогда
		
		ЗапуститьЗагрузкуДанных();
		
	Иначе
		
		ЗагрузитьИзФайлаНаКлиенте();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаНаКлиенте()
	
	ДополнительныеПараметры = Новый Структура("ВидФайла", "ФайлДанныхДляЗагрузки");
	Оповещение = Новый ОписаниеОповещения("ПоместитьФайлВХранилищеЗавершить", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	
	Если ЗначениеЗаполнено(ПутьКФайлуЗагрузки) Тогда
		ПараметрыЗагрузки.Интерактивно = Ложь;
		ФайловаяСистемаКлиент.ЗагрузитьФайл(Оповещение, ПараметрыЗагрузки, ПутьКФайлуЗагрузки);
	Иначе
		ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
		ПараметрыЗагрузки.Диалог.Фильтр = "Файлы XML (*.xml)|*.xml";
		
		ФайловаяСистемаКлиент.ЗагрузитьФайл(Оповещение, ПараметрыЗагрузки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьРезультатВыгрузкиВФайл(АдресХраненияРезультата)
	
	ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
	ПараметрыСохранения.Интерактивно = Ложь;
	
	ФайловаяСистемаКлиент.СохранитьФайл(
		Новый ОписаниеОповещения("ЗаписатьРезультатВыгрузкиВФайлЗавершение", ЭтотОбъект),
		АдресХраненияРезультата,
		ПутьКФайлуВыгрузки,
		ПараметрыСохранения);
	
КонецПроцедуры

// Параметры:
//   ПолученныеФайлы - Массив из ОписаниеПереданногоФайла
//                   - Неопределено - результат получения файлов.
//   ДополнительныеПараметры - Произвольный - произвольные дополнительные параметры.
// 
&НаКлиенте
Процедура ЗаписатьРезультатВыгрузкиВФайлЗавершение(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ПолученныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПутьКФайлуВыгрузки) Тогда
		ПутьКФайлуВыгрузки = ПолученныеФайлы.Получить(0).Имя;
	КонецЕсли;
	СообщитьОбОкончанииОперации(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьЗагрузкуДанных()
	
	МассивСообщений = Неопределено;
	ДлительнаяОперация = ЗагрузитьДанныеНаСервере();
	
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ДлительнаяОперация.Свойство("Сообщения", МассивСообщений);
		СообщитьОбОкончанииОперации(Истина);
		
	Иначе
		
		МодульДлительныеОперацииКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ДлительныеОперацииКлиент");
		ПараметрыОжидания = МодульДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииОперацииЗагрузки", ЭтотОбъект);
		МодульДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораФайлаВыгрузки(ВыгружатьПослеВыбора = Ложь, АдресХраненияРезультата = "")
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогОткрытияФайла.Фильтр = НСтр("ru = 'Данные выгрузки (*.xml)|*.xml'");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДляЗагрузки",             Ложь);
	ДополнительныеПараметры.Вставить("ВыгружатьПослеВыбора",    ВыгружатьПослеВыбора);
	ДополнительныеПараметры.Вставить("АдресХраненияРезультата", АдресХраненияРезультата);
	
	ОповещениеВыбора = Новый ОписаниеОповещения("ОбработкаВыбораФайлаДанныхВыгрузки", ЭтотОбъект, ДополнительныеПараметры);
	
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(ОповещениеВыбора, ДиалогОткрытияФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВыгрузкуКакТекст()
	
	Если ПустаяСтрока(Объект.РезультатВыгрузкиXDTO) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗаголовокОкна = Элементы.ДекорацияРезультатВыгрузкиВФайл.Заголовок;
	ИмяФайла = СтрЗаменить(Элементы.ДекорацияРезультатВыгрузкиВФайл.Заголовок, ":", "_") + ".xml";
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(Объект.РезультатВыгрузкиXDTO);
	ТекстовыйДокумент.Показать(ЗаголовокОкна, ИмяФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовкиДекорацийЗагрузки()
	
	Элементы.ДекорацияМенеджерЗагрузки.Заголовок = Объект.ПутьКМенеджеруОбменаЗагрузки;
	Если ПустаяСтрока(Элементы.ДекорацияМенеджерЗагрузки.Заголовок) Тогда
		
		Элементы.ДекорацияМенеджерЗагрузки.Заголовок = НСтр("ru ='<Используется встроенный>'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовкиДекорацийВыгрузки()
	
	Элементы.ДекорацияВерсияФорматаВыгрузка.Заголовок = Объект.ВерсияФормата;
	Если ПустаяСтрока(Элементы.ДекорацияВерсияФорматаВыгрузка.Заголовок) Тогда
		
		Элементы.ДекорацияВерсияФорматаВыгрузка.Заголовок = НСтр("ru ='<Не указано>'");
		
	КонецЕсли;
	
	Элементы.ДекорацияВерсияРасширенияФорматаВыгрузка.Заголовок = Объект.РасширениеФормата;
	Если ПустаяСтрока(Элементы.ДекорацияВерсияРасширенияФорматаВыгрузка.Заголовок) Тогда
		
		Элементы.ДекорацияВерсияРасширенияФорматаВыгрузка.Заголовок = НСтр("ru ='<Без расширения>'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПометкуСпискаВерсийФормата()
	
	СписокВерсийФормата.ЗаполнитьПометки(Ложь);
	
	ТекущийЭлементСписка = СписокВерсийФормата.НайтиПоЗначению(Объект.ВерсияФормата);
	Если ТекущийЭлементСписка <> Неопределено Тогда
		
		ТекущийЭлементСписка.Пометка = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСвойстваЭлементовФормы()
	
	Элементы.ФормаПрервать.Видимость = Ложь;
	
	// Вид операции.
	Элементы.ВыгрузкаЗагрузка.ТекущаяСтраница = ?(ВидОперации = "Загрузка", Элементы.Загрузка, Элементы.Выгрузка);
	
	// см. модуль этой обработки, метод КомпонентыОбмена()
	Элементы.ГруппаМенеджерЗагрузки.Видимость = Ложь;
	Элементы.ГруппаМенеджерВыгрузки.Видимость = Ложь;
	
	// Ограниченный вариант использования обработки.
	Если ВариантОткрытияФормы = "ТолькоЗагрузка" Тогда
		
		Элементы.ВыгрузкаЗагрузка.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.ФормаВключитьРасширенныеВозможности.Видимость = Ложь;
		Элементы.ГруппаДанныеДляЗагрузки.Видимость = Ложь;
		Элементы.ИсточникЗагрузки.Видимость = Ложь;
		
	Иначе
		
		// Загрузка
		Элементы.ФормаИспользоватьВнутреннийКаталогСообщений.Видимость = РежимРазработчика;
		Элементы.ИсточникЗагрузки.Видимость = РежимРазработчика;
		Элементы.ГруппаДанныеДляЗагрузки.Видимость = (ИсточникЗагрузки = 1);
		Элементы.ПутьКФайлуЗагрузки.Видимость = (ИсточникЗагрузки <> 1);
		
		// Выгрузка
		Элементы.ИсточникВыгрузки.Видимость = РежимРазработчика;
		Элементы.МестоВыгрузки.Видимость = РежимРазработчика;
		Элементы.РезультатВыгрузки.Видимость = РежимРазработчика;
		Элементы.ПутьКФайлуВыгрузки.Видимость = МестоВыгрузки <> 1;
		Элементы.ДекорацияРезультатВыгрузкиВФайл.Видимость = МестоВыгрузки = 1;
		Элементы.ГруппаПодсказкаНастройкиРезультатаВыгрузки.Видимость = Не РежимРазработчика;
		
		Элементы.СтраницыОтбораДанных.ТекущаяСтраница = ?(Объект.ИсточникВыгрузки = "Отбор",
			Элементы.ВыгружаемыеДанные, Элементы.ГруппаУзелОбмена);
		
		Элементы.ФормаВключитьРасширенныеВозможности.Пометка = РежимРазработчика;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораФайлаДанныхВыгрузки(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПутьКФайлуВыгрузки = ВыбранныеФайлы[0];
	Если ДополнительныеПараметры.ВыгружатьПослеВыбора Тогда
		
		ЗаписатьРезультатВыгрузкиВФайл(ДополнительныеПараметры.АдресХраненияРезультата);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатВыгрузки()
	
	Если МестоВыгрузки = 1 Тогда
		
		ЗавершитьВыгрузкуДанныхВТекст();
		
	Иначе
		
		Если НЕ ЗначениеЗаполнено(ПутьКФайлуВыгрузки) Тогда
			
			// После выбора файла результат выгрузки будет туда записан.
			НачалоВыбораФайлаВыгрузки(Истина, АдресХраненияРезультата);
			
		Иначе
			
			ЗаписатьРезультатВыгрузкиВФайл(АдресХраненияРезультата);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииОперацииЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		
		УстановитьВидимостьДоступностьКнопок(Истина);
		ВызватьИсключение НСтр("ru = 'Загрузка данных завершена неудачно.'");
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		УстановитьВидимостьДоступностьКнопок(Истина);
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
		
	КонецЕсли;
	
	СообщитьОбОкончанииОперации(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииОперацииВыгрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		
		ВызватьИсключение НСтр("ru = 'Выгрузка данных завершена неудачно. Отсутствует результат выгрузки.'");
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
		
	КонецЕсли;
	
	АдресХраненияРезультата = Результат.АдресРезультата;
	ОбработатьРезультатВыгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораВерсииФорматаВыгрузки(Результат, ДополнительныеСвойства) Экспорт
	
	Если Результат = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Объект.ВерсияФормата = Результат.Значение;
	Объект.РасширениеФормата = "";
	
	ОбновитьПометкуСпискаВерсийФормата();
	ОбновитьЗаголовкиДекорацийВыгрузки();
	ОбновитьПравилаВыгрузкиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораВерсииРасширенияФорматаВыгрузки(Результат, ДополнительныеСвойства) Экспорт
	
	Если Результат = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Объект.РасширениеФормата = Результат.Значение;
	
	ОбновитьЗаголовкиДекорацийВыгрузки();
	ОбновитьПравилаВыгрузкиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОбОкончанииОперации(Загрузка = Ложь)
	
	ВывестиСообщенияФоновогоЗадания();
	
	ИдентификаторЗадания = Неопределено;
	Элементы.ВыгрузкаЗагрузка.ТекущаяСтраница = ?(Загрузка, Элементы.Загрузка, Элементы.Выгрузка);
	
	УстановитьВидимостьДоступностьКнопок(Истина);
	
	Если Загрузка Тогда
		
		ТекстСообщения = НСтр("ru = 'Загрузка данных завершена.'");
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Выгрузка данных завершена.'");
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьЗаписьНаСервереБезКонтекста(ЭтоОкончаниеОперации)
	
	ЗаписьЖурналаРегистрации(НСтр("ru = 'Доступность.ФормаВыполнитьОперацию'",  ОбщегоНазначения.КодОсновногоЯзыка()), 
		УровеньЖурналаРегистрации.Примечание,, 
		Строка(ЭтоОкончаниеОперации));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьКнопок(ЭтоОкончаниеОперации)
	
	Элементы.ГруппаОсновнойФункционал.ТекущаяСтраница = 
		?(ЭтоОкончаниеОперации = Истина, Элементы.СтраницаФункциональность, Элементы.СтраницаОжидание);
	
	Элементы.ФормаВыполнитьОперацию.Доступность = ЭтоОкончаниеОперации;
	ДобавитьЗаписьНаСервереБезКонтекста(ЭтоОкончаниеОперации);
	
	Элементы.ФормаВключитьРасширенныеВозможности.Доступность = ЭтоОкончаниеОперации;
	
	Элементы.ФормаПрервать.Видимость = НЕ ЭтоОкончаниеОперации;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИзменитьОтборСтроки(ПолноеИмяМетаданных, ПредставлениеОтбора)
	
	СтруктураОтбор = Новый Структура("ПолноеИмяМетаданных", ПолноеИмяМетаданных);
	СтрокиДопРегистрации = Объект.ДополнительнаяРегистрация.НайтиСтроки(СтруктураОтбор);
	ТекВыборПериода = Неопределено;
	ТекПериодДанных = Неопределено;
	ТекОтбор = Неопределено;
	
	Если СтрокиДопРегистрации.Количество() > 0 Тогда
		
		ТекВыборПериода = СтрокиДопРегистрации[0].ВыборПериода;
		ТекПериодДанных = СтрокиДопРегистрации[0].Период;
		ТекОтбор = СтрокиДопРегистрации[0].Отбор;
		
	КонецЕсли;
	
	ИмяОткрываемойФормы = БазовоеИмяДляФормы + ".Форма.РедактированиеПериодаИОтбора";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок",           ПредставлениеОтбора);
	ПараметрыФормы.Вставить("ВыборПериода",        ТекВыборПериода);
	
	КомпоновщикНастроек = КомпоновщикНастроекПоИмениТаблицы(ПолноеИмяМетаданных, ПредставлениеОтбора, ТекОтбор);
	
	ПараметрыФормы.Вставить("КомпоновщикНастроек", КомпоновщикНастроек);
	ПараметрыФормы.Вставить("ПериодДанных",        ТекПериодДанных);
	ПараметрыФормы.Вставить("АдресХранилищаФормы", УникальныйИдентификатор);
	
	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыФормы, Элементы.ТаблицаПравилВыгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСнятьФлажкиОтборов(ЗначениеФлага)
	
	СтрокиДерева = Объект.ТаблицаПравилВыгрузки.ПолучитьЭлементы();
	Для каждого СтрокаОтбора Из СтрокиДерева Цикл
		
		СтрокаОтбора.Включить = ЗначениеФлага;
		
		ВложенныеСтроки = СтрокаОтбора.ПолучитьЭлементы();
		Для каждого ВложеннаяСтрокаОтбора Из ВложенныеСтроки Цикл
			
			ВложеннаяСтрокаОтбора.Включить = ЗначениеФлага;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// ::: Методы на сервере :::

&НаСервереБезКонтекста
Функция ПрочитатьСообщенияФоновогоЗадания(Идентификатор)
	
	Если НЕ ЗначениеЗаполнено(Идентификатор) Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(Идентификатор);
	Если Задание = Неопределено Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат Задание.ПолучитьСообщенияПользователю(Истина);
	
КонецФункции

&НаСервере
Функция ВыгрузитьДанныеНаСервере()
	ДополнитьДанныеОтбораПриНеобходимости();
	АдресХраненияРезультата = "";
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("МестоВыгрузки", МестоВыгрузки);
	СтруктураПараметров.Вставить("ЭтоФоновоеЗадание", Истина);
	СтруктураПараметров.Вставить("ПутьКМенеджеруОбменаВыгрузки", ОбработкаОбъект.ПутьКМенеджеруОбменаВыгрузки);
	СтруктураПараметров.Вставить("ВерсияФормата", ОбработкаОбъект.ВерсияФормата);
	СтруктураПараметров.Вставить("РасширениеФормата", ОбработкаОбъект.РасширениеФормата);
	СтруктураПараметров.Вставить("УзелОбмена", ОбработкаОбъект.УзелОбмена);
	СтруктураПараметров.Вставить("ПериодОтбораВсехДокументов", ОбработкаОбъект.ПериодОтбораВсехДокументов);
	СтруктураПараметров.Вставить("ДополнительнаяРегистрация", ОбработкаОбъект.ДополнительнаяРегистрация);
	СтруктураПараметров.Вставить("ЛогироватьВыгрузкуОбъектов", Элементы.ФормаЛогироватьВыгрузкуОбъектов.Пометка);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ИмяОбработки", ИмяОбработки);
	ПараметрыЗадания.Вставить("ИмяМетода", "РезультатВыгрузкиВXML");
	ПараметрыЗадания.Вставить("ПараметрыВыполнения", СтруктураПараметров);
	ПараметрыЗадания.Вставить("ЭтоВнешняяОбработка", Ложь);
	
	ВыполняемыйМетод = "ДлительныеОперации.ВыполнитьПроцедуруМодуляОбъектаОбработки";
	
	МодульДлительныеОперации = ОбщегоНазначения.ОбщийМодуль("ДлительныеОперации");
	ПараметрыВыполнения = МодульДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Выгрузка данных EnterpriseData'");
	РезультатФоновогоЗадания = МодульДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыЗадания, ПараметрыВыполнения);
	Возврат РезультатФоновогоЗадания;
КонецФункции

&НаСервере
Функция ЗагрузитьДанныеНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	СтруктураПараметров = Новый Структура;
	Если ИсточникЗагрузки = 1 Тогда
		
		СтруктураПараметров.Вставить("ТекстXML", ДанныеДляЗагрузкиXDTO);
		
	Иначе
		
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаЗагрузки); // ДвоичныеДанные
		Если ИспользоватьКаталогСообщенийОбменаДанными = Истина Тогда
			
			КаталогВременногоХранилищаФайлов = ОбменДаннымиПовтИсп.КаталогВременногоХранилищаФайлов();
			АдресНаСервере = КаталогВременногоХранилищаФайлов + "\" + Строка(Новый УникальныйИдентификатор) + ".xml";
			
		Иначе
			
			АдресНаСервере = ПолучитьИмяВременногоФайла("xml");
			// Удаление временного файла происходит не через УдалитьФайлы(АдресНаСервере) в этой функции,
			// а через УдалитьФайлы(АдресНаСервере) в процедуре ЗагрузкаСообщения модуля обработки.
			
		КонецЕсли;
		
		ДвоичныеДанные.Записать(АдресНаСервере);
		УдалитьИзВременногоХранилища(АдресФайлаЗагрузки);
		СтруктураПараметров.Вставить("АдресНаСервере", АдресНаСервере);
		
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ПутьКМенеджеруОбменаЗагрузки", ОбработкаОбъект.ПутьКМенеджеруОбменаЗагрузки);
	СтруктураПараметров.Вставить("ВерсияФормата", ОбработкаОбъект.ВерсияФормата);
	СтруктураПараметров.Вставить("ЭтоФоновоеЗадание", Истина);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ИмяОбработки", ИмяОбработки);
	ПараметрыЗадания.Вставить("ИмяМетода", "ЗагрузкаСообщения");
	ПараметрыЗадания.Вставить("ПараметрыВыполнения", СтруктураПараметров);
	ПараметрыЗадания.Вставить("ЭтоВнешняяОбработка", Ложь);
	
	ВыполняемыйМетод = "ДлительныеОперации.ВыполнитьПроцедуруМодуляОбъектаОбработки";
	
	МодульДлительныеОперации = ОбщегоНазначения.ОбщийМодуль("ДлительныеОперации");
	ПараметрыВыполнения = МодульДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка данных EnterpriseData'");
	РезультатФоновогоЗадания = МодульДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыЗадания, ПараметрыВыполнения);
	Возврат РезультатФоновогоЗадания;

КонецФункции

&НаСервере
Функция КомпоновщикНастроекПоИмениТаблицы(ИмяТаблицы, Представление, Отбор)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ОбработкаОбъект.КомпоновщикНастроекПоИмениТаблицы(ИмяТаблицы, Представление, Отбор, УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Функция ПредставлениеОтбора(Период, Отбор)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ОбработкаОбъект.ПредставлениеОтбора(Период, Отбор);
	
КонецФункции

&НаСервере
Функция СохранитьXMLНаСервере()
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(Объект.РезультатВыгрузкиXDTO);
	АдресНаСервере = ПолучитьИмяВременногоФайла("xml");
	ТекстовыйДокумент.Записать(АдресНаСервере);
	АдресВХранилище = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(АдресНаСервере));
	
	УдалитьФайлы(АдресНаСервере);
	
	Возврат АдресВХранилище;
	
КонецФункции

&НаСервере
Процедура ДополнитьДанныеОтбораПриНеобходимости()
	
	Если Объект.ИсточникВыгрузки = "Узел" Тогда
		
		Объект.ДополнительнаяРегистрация.Очистить();
		Возврат;
		
	Иначе
		
		Объект.УзелОбмена = Неопределено;
		
	КонецЕсли;
	
	Для Каждого СтрГруппыМД Из Объект.ТаблицаПравилВыгрузки.ПолучитьЭлементы() Цикл
		
		Для Каждого СтрМД Из СтрГруппыМД.ПолучитьЭлементы() Цикл
			ПолноеИмяМД = СтрМД.ПолноеИмяМетаданных;
			СтрокиДополнения = Объект.ДополнительнаяРегистрация.НайтиСтроки(Новый Структура("ПолноеИмяМетаданных", ПолноеИмяМД));
			
			// Если отбор выкл, но раннее использовался...
			Если СтрМД.Включить = Ложь Тогда
				Если СтрокиДополнения.Количество() > 0 Тогда
					СтрМД.ПредставлениеОтбора = "";
					ВсегоСтрок = СтрокиДополнения.Количество();
					Для Счетчик = 1 По ВсегоСтрок Цикл
						Объект.ДополнительнаяРегистрация.Удалить(СтрокиДополнения[ВсегоСтрок-Счетчик]);
					КонецЦикла;
				КонецЕсли;
			
			// Если отбор включили первый раз...
			ИначеЕсли СтрокиДополнения.Количество() = 0 Тогда
				НовСтрока = Объект.ДополнительнаяРегистрация.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтрока, СтрМД, "ПолноеИмяМетаданных, Представление");
				НовСтрока.ОтборСтрокой = НСтр("ru = 'Все объекты'");
				НовСтрока.ВыборПериода = СтрМД.ОтборПоПериоду;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВерсиюФорматаПоУмолчанию(ДоступныеВерсииФормата)
	
	Если ДоступныеВерсииФормата.Количество() = 0 Тогда
		
		Элементы.ДекорацияВерсииФорматаЗагрузка.Заголовок = НСтр("ru ='Не обнаружены'", ОбщегоНазначения.КодОсновногоЯзыка());
		Элементы.ФормаВыполнитьОперацию.Доступность = Ложь;
		Элементы.ВыгрузкаЗагрузка.Доступность = Ложь;
		
	Иначе
		
		ЗаполнитьВерсияФорматаИзДоступныхВерсий(ДоступныеВерсииФормата);
		
		ПредставлениеОсновныхВерсий = "";
		ПредставлениеПрошлыхВерсий = "";
		
		КоличествоОбработанных = 0;
		Для каждого ЭлементСписка Из СписокВерсийФормата Цикл
			
			КоличествоОбработанных = КоличествоОбработанных + 1;
			Если КоличествоОбработанных = 1 Тогда
				
				ПредставлениеОсновныхВерсий = ЭлементСписка.Значение;
				
			ИначеЕсли КоличествоОбработанных < 3 Тогда
				
				ПредставлениеОсновныхВерсий = ПредставлениеОсновныхВерсий + ", ";
				ПредставлениеОсновныхВерсий = ПредставлениеОсновныхВерсий + ЭлементСписка.Значение;
				
			Иначе
				
				Если НЕ ПустаяСтрока(ПредставлениеПрошлыхВерсий) Тогда
					
					ПредставлениеПрошлыхВерсий = ПредставлениеПрошлыхВерсий + Символы.ПС;
					
				КонецЕсли;
				
				ПредставлениеПрошлыхВерсий = ПредставлениеПрошлыхВерсий + ЭлементСписка.Значение
				
			КонецЕсли;
			
		КонецЦикла;
		
		Элементы.ДекорацияВерсииФорматаЗагрузка.Заголовок = СтрШаблон(НСтр("ru = '%1'"), ПредставлениеОсновныхВерсий);
		Если СписокВерсийФормата.Количество() > 2 Тогда
			
			КоличествоОсталось = СписокВерсийФормата.Количество() - 2;
			Элементы.ГруппаОстальныеВерсииФорматаЗагрузка.Заголовок = СтрШаблон(НСтр("ru = '(и еще %1)'"), КоличествоОсталось);
			Элементы.ДекорацияВерсииФорматаЗагрузкаЕще.Заголовок = ПредставлениеПрошлыхВерсий;
			
		Иначе
			
			Элементы.ГруппаОстальныеВерсииФорматаЗагрузка.Видимость = Ложь;
			
		КонецЕсли;
		
		ОбновитьПравилаВыгрузкиНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВерсияФорматаИзДоступныхВерсий(ДоступныеВерсииФормата)
	
	ВерсииФорматаДетально = Новый ТаблицаЗначений;
	ВерсииФорматаДетально.Колонки.Добавить("ВерсияФормата", Новый ОписаниеТипов("Строка"));
	ВерсииФорматаДетально.Колонки.Добавить("ПервыйРазряд", Новый ОписаниеТипов("Число"));
	ВерсииФорматаДетально.Колонки.Добавить("ВторойРазряд", Новый ОписаниеТипов("Число"));
	
	Для Каждого ЭлементМассива Из ДоступныеВерсииФормата Цикл
		
		ВерсияРаздельно = СтрРазделить(ЭлементМассива.Ключ, ".");
		Если ВерсияРаздельно.Количество() <> 2 Тогда
			
			Продолжить; // Версия ED строго в два разряда
			
		КонецЕсли;
		
		НоваяВерсия = ВерсииФорматаДетально.Добавить();
		НоваяВерсия.ВерсияФормата = ЭлементМассива.Ключ;
		НоваяВерсия.ПервыйРазряд = ВерсияРаздельно[0];
		НоваяВерсия.ВторойРазряд = ВерсияРаздельно[1];
		
	КонецЦикла;
	
	ВерсииФорматаДетально.Сортировать("ПервыйРазряд Убыв, ВторойРазряд Убыв");
	
	СписокВерсийФормата.Очистить();
	Для Каждого СтрокаТаблицы Из ВерсииФорматаДетально Цикл
		
		СписокВерсийФормата.Добавить(СтрокаТаблицы.ВерсияФормата,);
		
	КонецЦикла;
	
	Объект.ВерсияФормата = СтрокаТаблицы.ВерсияФормата; // Всегда максимальная
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПравилаВыгрузкиНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнениеПравилВыгрузки();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьИмяОбработки()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	МетаОбработкаИмя = ОбработкаОбъект.Метаданные().Имя;
	ЧастиИмени = СтрРазделить(ИмяФормы, ".");
	
	БазовоеИмяДляФормы = "Обработка." + МетаОбработкаИмя;
	ИмяОбработки = ЧастиИмени[1];
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьРежимОткрытияФормы()
	
	ВариантОткрытияФормы = ?(Параметры.Свойство("ТолькоЗагрузка"), "ТолькоЗагрузка", "");
	Если ВариантОткрытияФормы = "ТолькоЗагрузка" Тогда
		
		Заголовок = НСтр("ru = 'Загрузка данных EnterpriseData'");
		Элементы.НадписьЗагрузкаВстроеннойОбработкой.Видимость = Истина;
		
	Иначе
		
		Заголовок = НСтр("ru = 'Выгрузка и загрузка данных EnterpriseData'");
		Элементы.НадписьЗагрузкаВстроеннойОбработкой.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрерватьВыгрузкуЗагрузкуСервер()
	
	МодульДлительныеОперации = ОбщегоНазначения.ОбщийМодуль("ДлительныеОперации");
	МодульДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаСервере
Процедура РедактированиеОтбораСтрокиДополнительныйСоставСервер(СтруктураВыбора, ПолноеИмяМД, ИДТекСтроки)
	
	ДанныеДопРегистрации = Объект.ДополнительнаяРегистрация.НайтиСтроки(
		Новый Структура("ПолноеИмяМетаданных", ПолноеИмяМД));
	ТекущиеДанные = Объект.ТаблицаПравилВыгрузки.НайтиПоИдентификатору(ИДТекСтроки);
	Если ДанныеДопРегистрации.Количество() = 0 Тогда
		Строка = Объект.ДополнительнаяРегистрация.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, ТекущиеДанные,"ПолноеИмяМетаданных, Представление");
		СтрокаЗаполнения = Строка;
	Иначе
		СтрокаЗаполнения = ДанныеДопРегистрации[0];
	КонецЕсли;
	
	СтрокаЗаполнения.Период       = СтруктураВыбора.ПериодДанных;
	СтрокаЗаполнения.Отбор        = СтруктураВыбора.КомпоновщикНастроек.Настройки.Отбор;
	СтрокаЗаполнения.ОтборСтрокой = ПредставлениеОтбора(СтрокаЗаполнения.Период, СтрокаЗаполнения.Отбор);
	
	ТекущиеДанные.ПредставлениеОтбора = СтрокаЗаполнения.ОтборСтрокой;
	ТекущиеДанные.Включить = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ТипизироватьУзелОбмена()
	
	МассивДопустимыхТипов = Новый Массив;
	
	ПланыОбменаБСП = ОбменДаннымиПовтИсп.ПланыОбменаБСП();
	Для каждого ИмяПланаОбмена Из ПланыОбменаБСП Цикл
		
		Если НЕ ОбменДаннымиСервер.ЭтоПланОбменаXDTO(ИмяПланаОбмена) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ТипПланОбмена = ТипЗнч(ПланыОбмена[ИмяПланаОбмена].ПустаяСсылка());
		МассивДопустимыхТипов.Добавить(ТипПланОбмена);
		
	КонецЦикла;
	
	Элементы.УзелОбмена.ОграничениеТипа = Новый ОписаниеТипов(МассивДопустимыхТипов);
	
КонецПроцедуры

// Файлы загрузки, выгрузки, модулей менеджеров
#Область ВыборФайлов 

&НаКлиенте
Процедура НачалоВыбораФайлаЗагрузки(ЗагружатьПослеВыбора = Ложь)
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.Фильтр = НСтр("ru = 'Данные загрузки (*.xml)|*.xml'");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДляЗагрузки",          Истина);
	ДополнительныеПараметры.Вставить("ЗагружатьПослеВыбора", ЗагружатьПослеВыбора);
	
	ОповещениеВыбора = Новый ОписаниеОповещения("ОбработкаВыбораФайлаДанныхЗагрузки", ЭтотОбъект, ДополнительныеПараметры);
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(ОповещениеВыбора, ДиалогОткрытияФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораФайлаДанныхЗагрузки(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПутьКФайлуЗагрузки = ВыбранныеФайлы[0];
	Если ДополнительныеПараметры.ЗагружатьПослеВыбора Тогда
		
		ПодключитьОбработчикОжидания("ЗагрузитьСообщение", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораМодуляМенеджера(ОбновляемыйРеквизит)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МодульМенеджера",     Истина);
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.Фильтр = НСтр("ru = 'Внешняя обработка (*.epf)|*.epf'");
	
	Если ОбновляемыйРеквизит = "ПутьКМенеджеруОбменаВыгрузки" Тогда
		
		ОповещениеВыбора = Новый ОписаниеОповещения("ВыбранФайлМенеджераВыгрузки", ЭтотОбъект, ДополнительныеПараметры);
		
	Иначе
		
		ОповещениеВыбора = Новый ОписаниеОповещения("ВыбранФайлМенеджераЗагрузки", ЭтотОбъект, ДополнительныеПараметры);
		
	КонецЕсли;
	
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(ОповещениеВыбора, ДиалогОткрытияФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранФайлМенеджераЗагрузки(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Объект.ПутьКМенеджеруОбменаЗагрузки = ВыбранныеФайлы[0];
	ОбновитьЗаголовкиДекорацийЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранФайлМенеджераВыгрузки(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Объект.ПутьКМенеджеруОбменаВыгрузки = ВыбранныеФайлы[0];
		
	// Вместо ПутьКМенеджеруОбменаВыгрузкиПриИзмененииНаСервере вызываем обновление:
	ОбновитьПравилаВыгрузкиНаСервере();
	
	ОбновитьЗаголовкиДекорацийВыгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияМенеджерЗагрузкиОчисткаНажатие(Элемент)
	
	Объект.ПутьКМенеджеруОбменаЗагрузки = "";
	ОбновитьЗаголовкиДекорацийЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоместитьФайлВХранилищеЗавершить(ПомещенныйФайл, ДополнительныеПараметры) Экспорт
	
	АдресФайлаЗагрузки = "";
	Если ПомещенныйФайл = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	АдресФайлаЗагрузки = ПомещенныйФайл.Хранение;
	Если ДополнительныеПараметры.ВидФайла = "ФайлДанных" Тогда
		
		ОткрытьXMLНаСервере();
		
	ИначеЕсли ДополнительныеПараметры.ВидФайла = "ФайлДанныхДляЗагрузки" Тогда
		
		ЗапуститьЗагрузкуДанных();
		
	ИначеЕсли ДополнительныеПараметры.ВидФайла = "ФайлНастроек" Тогда
		
		ЗагрузитьНастройкиВыгрузкиНаСервере();
		Элементы.ТаблицаПравилВыгрузки.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВерхнийУровень;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОткрытьXMLНаСервере()
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаЗагрузки); // ДвоичныеДанные
	
	АдресНаСервере = ПолучитьИмяВременногоФайла("xml");
	ДвоичныеДанные.Записать(АдресНаСервере);
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(АдресНаСервере);
	ДанныеДляЗагрузкиXDTO = ТекстовыйДокумент.ПолучитьТекст();
	
	УдалитьФайлы(АдресНаСервере);
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаНастроекВыгрузки

&НаСервере
Процедура ЗагрузитьНастройкиВыгрузкиНаСервере()
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаЗагрузки); // ДвоичныеДанные
	ИмяФайлаНаСервере = ПолучитьИмяВременногоФайла("xml");
	ДвоичныеДанные.Записать(ИмяФайлаНаСервере);

	Объект.ДополнительнаяРегистрация.Очистить();
	Объект.ПериодОтбораВсехДокументов = Новый СтандартныйПериод;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяФайлаНаСервере);
	
	НоваяСтрокаОтбораСКД = Неопределено;
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если ЧтениеXML.Имя = "Period" Тогда
			
			ПрочитатьЗначениеПериода(ЧтениеXML)
			
		ИначеЕсли ЧтениеXML.Имя = "Object" Тогда
			
			ПрочитатьЗначениеОбъектногоТипа(ЧтениеXML, НоваяСтрокаОтбораСКД);
		
		ИначеЕсли ЧтениеXML.Имя = "Filter" Тогда
			
			ЗаписьXML = Новый ЗаписьXML;
			ЗаписьXML.УстановитьСтроку("UTF-8");
			ПрочитатьУзел(ЧтениеXML, ЗаписьXML);
			Текст = ЗаписьXML.Закрыть();
			
			НоваяСтрокаОтбораСКД.Отбор = ОбщегоНазначения.ЗначениеИзСтрокиXML(Текст);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	УдалитьФайлы(ИмяФайлаНаСервере);
	
	ОбновитьПравилаВыгрузкиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЗначениеПериода(ЧтениеXML)
	
	Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
		
		Если ЧтениеXML.Имя = "Beg" Тогда
			
			Объект.ПериодОтбораВсехДокументов.ДатаНачала = XMLЗначение(Тип("Дата"), ЧтениеXML.Значение);
			
		ИначеЕсли ЧтениеXML.Имя = "End" Тогда
			
			Объект.ПериодОтбораВсехДокументов.ДатаОкончания = XMLЗначение(Тип("Дата"), ЧтениеXML.Значение);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЗначениеОбъектногоТипа(ЧтениеXML, НоваяСтрокаОтбораСКД)
	
	НоваяСтрокаОтбораСКД = Объект.ДополнительнаяРегистрация.Добавить();
	
	Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
		
		Если ЧтениеXML.Имя = "Type" Тогда
			
			НоваяСтрокаОтбораСКД.ПолноеИмяМетаданных = XMLЗначение(Тип("Строка"), ЧтениеXML.Значение);
			
		ИначеЕсли ЧтениеXML.Имя = "Sel_Period" Тогда
			
			НоваяСтрокаОтбораСКД.ВыборПериода = XMLЗначение(Тип("Булево"), ЧтениеXML.Значение);
			
		ИначеЕсли ЧтениеXML.Имя = "Beg_Period" Тогда
			
			НоваяСтрокаОтбораСКД.Период.ДатаНачала = XMLЗначение(Тип("Дата"), ЧтениеXML.Значение);
			 
		ИначеЕсли ЧтениеXML.Имя = "End_Period" Тогда
			
			НоваяСтрокаОтбораСКД.Период.ДатаОкончания = XMLЗначение(Тип("Дата"), ЧтениеXML.Значение);
			
		ИначеЕсли ЧтениеXML.Имя = "F_String" Тогда
			
			НоваяСтрокаОтбораСКД.ОтборСтрокой = XMLЗначение(Тип("Строка"), ЧтениеXML.Значение);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьУзел(ЧтениеXML, ЗаписьXML)
	
	ЗаписьXML.ЗаписатьНачалоЭлемента(ЧтениеXML.Имя);
	Для Сч = 0 По ЧтениеXML.КоличествоАтрибутов() - 1 Цикл
		ИмяАтрибута = ЧтениеXML.ИмяАтрибута(Сч);
		ЗначениеАтрибута = ЧтениеXML.ЗначениеАтрибута(Сч);
		ЗаписьXML.ЗаписатьАтрибут(ИмяАтрибута, ЗначениеАтрибута);
	КонецЦикла;
	
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ПрочитатьУзел(ЧтениеXML, ЗаписьXML)
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			ЗаписьXML.ЗаписатьТекст(ЧтениеXML.Значение);
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			ЗаписьXML.ЗаписатьКонецЭлемента();
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНастройкиВыгрузкиНаСервере()
	ДополнитьДанныеОтбораПриНеобходимости();
	ИмяВремФайла = ПолучитьИмяВременногоФайла("xml");
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяВремФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("Objects");
	Если ЗначениеЗаполнено(Объект.ПериодОтбораВсехДокументов) Тогда
		ЗаписьXML.ЗаписатьНачалоЭлемента("Period");
		ЗаписьXML.ЗаписатьАтрибут("Beg", XMLСтрока(Объект.ПериодОтбораВсехДокументов.ДатаНачала));
		ЗаписьXML.ЗаписатьАтрибут("End", XMLСтрока(Объект.ПериодОтбораВсехДокументов.ДатаОкончания));
		ЗаписьXML.ЗаписатьКонецЭлемента(); //Period
	КонецЕсли;
	Для Каждого Стр Из Объект.ДополнительнаяРегистрация Цикл
		ЗаписьXML.ЗаписатьНачалоЭлемента("Object");
		ЗаписьXML.ЗаписатьАтрибут("Type", XMLСтрока(Стр.ПолноеИмяМетаданных));
		Если Стр.ВыборПериода Тогда
			ЗаписьXML.ЗаписатьАтрибут("Sel_Period", XMLСтрока(Стр.ВыборПериода));
			Если ЗначениеЗаполнено(Стр.Период) Тогда
				ЗаписьXML.ЗаписатьАтрибут("Beg_Period", XMLСтрока(Стр.Период.ДатаНачала));
				ЗаписьXML.ЗаписатьАтрибут("End_Period", XMLСтрока(Стр.Период.ДатаОкончания));
			КонецЕсли;
		КонецЕсли;
		ЗаписьXML.ЗаписатьАтрибут("F_String", XMLСтрока(Стр.ОтборСтрокой));
		
		Текст = ОбщегоНазначения.ЗначениеВСтрокуXML(Стр.Отбор);
		ЗаписьXML.ЗаписатьБезОбработки(Текст);
		
		ЗаписьXML.ЗаписатьКонецЭлемента(); //Object
	КонецЦикла;
	ЗаписьXML.ЗаписатьКонецЭлемента(); //Objects
	ЗаписьXML.Закрыть();
	АдресВХранилище = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяВремФайла));
	УдалитьФайлы(ИмяВремФайла);
	Возврат АдресВХранилище;
КонецФункции

#КонецОбласти

#КонецОбласти