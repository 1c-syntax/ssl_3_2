///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресныйОбъект = Параметры.Идентификатор;
	
	ЗаполнитьСписок(?(Параметры.Отбор, Параметры.ТипПомещения + " " + Параметры.НомерПомещения, Неопределено));
	
	Если СписокАдресныхОбъектов.Количество() = 0 Тогда
		
		Если ЗначениеЗаполнено(Параметры.НаименованиеАдресногоОбъекта) Тогда
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Отсутствует информация о помещениях для %1.'"), Параметры.НаименованиеАдресногоОбъекта);
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Для просмотра списка помещений заполните поля адреса.'");
		КонецЕсли;
		
	Иначе
		
		// Позиционирование
		Отбор = Новый Структура("НомерПомещения, ТипПомещения", Параметры.НомерПомещения, Параметры.ТипПомещения);
		Строки = СписокАдресныхОбъектов.НайтиСтроки(Отбор);
		Если Строки.Количество() > 0 Тогда
			Элементы.СписокАдресныхОбъектов.ТекущаяСтрока = Строки[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаВыбрать", "Видимость", Ложь);
	КонецЕсли;
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
	Если Не ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		СтандартныеПодсистемыСервер.ПереместитьЭлементыВГруппу(КоманднаяПанель, Элементы.СписокАдресныхОбъектов.КоманднаяПанель);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		Результат = Новый Структура("КраткоеПредставлениеОшибки", ТекстПредупреждения);
		ОповеститьОВыборе(Результат);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокАдресныхОбъектов

&НаКлиенте
Процедура СписокАдресныхОбъектовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Закрыть(Элементы.СписокАдресныхОбъектов.ТекущиеДанные.Значение);
КонецПроцедуры

&НаКлиенте
Процедура СписокАдресныхОбъектовПриАктивизацииСтроки(Элемент)
	УстановитьВидимостьДоступность(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	Если Элементы.СписокАдресныхОбъектов.ТекущиеДанные <> Неопределено Тогда
		Закрыть(Элементы.СписокАдресныхОбъектов.ТекущиеДанные.Значение);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписок(Текст)
	
	СписокАдресныхОбъектов.Очистить();
	СтрокаПоиска = ?(ЗначениеЗаполнено(Текст), Текст, "");
	ПорцияПриПоиске = 2000;
	ПолныйСписокПомещений = Обработки.РасширенныйВводКонтактнойИнформации.СписокПомещений(АдресныйОбъект, СтрокаПоиска, ПорцияПриПоиске);
	Если ПолныйСписокПомещений = Неопределено ИЛИ ПолныйСписокПомещений.Количество() = 0 Тогда
		Элементы.ФормаВыбрать.Доступность = Ложь;
	Иначе
		СписокАдресныхОбъектов.Загрузить(ПолныйСписокПомещений);
		Элементы.ФормаВыбрать.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоступность(Форма)
	
	Форма.Элементы.ФормаВыбрать.Доступность = Форма.Элементы.СписокАдресныхОбъектов.ТекущаяСтрока <> Неопределено;
	
КонецПроцедуры

#КонецОбласти