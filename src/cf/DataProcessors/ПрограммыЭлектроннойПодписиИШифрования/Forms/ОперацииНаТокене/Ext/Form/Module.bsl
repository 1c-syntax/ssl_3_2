///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем ОбъектКомпоненты, ОжидаемыеОперации, ПараметрыОперации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Событие = "EDMP_OP_FINISH" Тогда
	
		Если ОжидаемыеОперации = Неопределено Тогда
			ОжидаемыеОперации = Новый Соответствие;
		КонецЕсли;

		ОписаниеОшибки = Неопределено;
		Попытка
			НовыйРезультат = ПрочитатьЗначениеJSON(Данные);
		Исключение
			ОписаниеОшибки = ИнформацияОбОшибке();
		КонецПопытки;

		Если ОписаниеОшибки <> Неопределено Тогда
			МассивСообщений = Новый Массив;
			МассивСообщений.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось прочитать результат выполнения операции: %1'"), Данные));
			Для Каждого КлючИЗначение Из ОжидаемыеОперации Цикл
				МассивСообщений.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Операция %1: %2'"), КлючИЗначение.Ключ, 
						ЗаписатьЗначениеJSON(КлючИЗначение.Значение)));
			КонецЦикла;
			ЗаписатьВЖурналРегистрации(ИнформацияОбОшибке(), СтрСоединить(МассивСообщений, Символы.ПС));
			ЗавершитьОперацию(НСтр("ru = 'Не удалось выполнить операцию. Подробности см. в журнале регистрации.'"));
			Возврат;
		КонецЕсли;
		
		Ключ = КлючОперации(НовыйРезультат.operation, НовыйРезультат.operationid);
		СвойстваОперации = ОжидаемыеОперации.Получить(Ключ);
		Если СвойстваОперации = Неопределено Тогда
			ВнешниеСобытия = ОжидаемыеОперации.Получить("ВнешниеСобытия");
			Если ВнешниеСобытия = Неопределено Тогда
				ВнешниеСобытия = Новый Соответствие;
			КонецЕсли;
			ВнешниеСобытия.Вставить(Ключ, Новый Структура("ОжидатьДо, Результат", ТекущаяДата() + 300, НовыйРезультат)); // АПК:143 Должна использоваться ТекущаяДата()
			ОжидаемыеОперации.Вставить("ВнешниеСобытия", ВнешниеСобытия);
			ПодключитьОбработчикОжидания("ОбновитьВнешниеСобытия", 1, Истина);
			Возврат;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НовыйРезультат.Message) Тогда
			НовыйРезультат.Вставить("Ошибка", НовыйРезультат.Message);
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(СвойстваОперации.Оповещение, НовыйРезультат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "БСП_ОперацииНаТокене" Тогда
		
#Если МобильныйКлиент Тогда
			УстановитьЗапретЗасыпанияКомпьютера(Истина);
#КонецЕсли
		
		ПараметрыОперации = Параметр;
		ВыполнитьТекущуюОперацию();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Повторить(Команда)
	ВыполнитьТекущуюОперацию();
КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	ЗавершитьОперацию(НСтр("ru = 'Операция отменена. Подробности см. в журнале регистрации.'"));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьТекущуюОперацию()
	
	Элементы.ГруппаПовторить.Видимость = Ложь;
	
	Если ТипЗнч(ПараметрыОперации) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыОперации.Операция = ЭлектроннаяПодписьКлиентЛокализация.ПолучениеСертификатов() Тогда
		Элементы.ДекорацияОперация.Заголовок = НСтр("ru = 'Поиск сертификатов на токене...'");
		ПолучениеСертификатовНаТокене();
		Возврат;
	ИначеЕсли ПараметрыОперации.Операция = ЭлектроннаяПодписьКлиентЛокализация.Подписание() Тогда
		Элементы.ДекорацияОперация.Заголовок = НСтр("ru = 'Подписание на токене...'");
		ПодписаниеНаТокене();
		Возврат;
	ИначеЕсли ПараметрыОперации.Операция = ЭлектроннаяПодписьКлиентЛокализация.Шифрование() Тогда
		Элементы.ДекорацияОперация.Заголовок = НСтр("ru = 'Шифрование на токене...'");
		ШифрованиеНаТокене();
		Возврат;
	ИначеЕсли ПараметрыОперации.Операция = ЭлектроннаяПодписьКлиентЛокализация.Расшифровка() Тогда
		Элементы.ДекорацияОперация.Заголовок = НСтр("ru = 'Расшифровка на токене...'");
		РасшифровкаНаТокене();
		Возврат;
	ИначеЕсли ПараметрыОперации.Операция = ЭлектроннаяПодписьКлиентЛокализация.ПроверкаПодписи() Тогда
		Элементы.ДекорацияОперация.Заголовок = НСтр("ru = 'Проверка подписи на токене...'");
		ПроверкаПодписиНаТокене();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьВЖурналРегистрации(ТекстСообщения, Подробности = Неопределено, ЭтоОшибка = Истина)
	
	Массив = Новый Массив;
	Если ТипЗнч(ТекстСообщения) = Тип("ИнформацияОбОшибке") Тогда
		Массив.Добавить(ОбработкаОшибок.ПодробноеПредставлениеОшибки(ТекстСообщения));
	Иначе
		Массив.Добавить(ТекстСообщения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Подробности) Тогда
		Массив.Добавить(Подробности);
	КонецЕсли;
	
	ТекстОшибки = СтрСоединить(Массив, Символы.ПС);
	
	ЗаписьЖурналаРегистрации(НСтр("ru = 'Электронная подпись.Операции на токене в мобильном клиенте'", ОбщегоНазначения.КодОсновногоЯзыка()),
		?(ЭтоОшибка, УровеньЖурналаРегистрации.Ошибка, УровеньЖурналаРегистрации.Информация), , ,ТекстОшибки);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПолучениеСертификатовНаТокене()
	
	Если ОбъектКомпоненты = Неопределено Тогда
		Попытка
			ОбъектКомпоненты = Ждать ОбъектВнешнейКомпоненты();
		Исключение
			ОписаниеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписатьВЖурналРегистрации(ИнформацияОбОшибке());
		КонецПопытки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ЗавершитьОперацию(ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	ВидыТокенов = Ждать ОбъектКомпоненты.ПоддерживаемыеБэкендыАсинх();
	ВидыТокенов = ПрочитатьЗначениеJSON(ВидыТокенов.Значение);
	
	Контекст = Новый Структура;
	Контекст.Вставить("Индекс", -1);
	Контекст.Вставить("Элементы", ВидыТокенов);
	Контекст.Вставить("Операция", "НайтиТокены");
	Контекст.Вставить("СледующееОповещение", Новый Структура("ПроцедураМодуля, Модуль", "ПослеНайтиТокены", ЭтотОбъект));
	Контекст.Вставить("ВидыТокенов", ВидыТокенов);
	Контекст.Вставить("Слоты", Новый Массив);
	Контекст.Вставить("Токены", Новый Массив);
	
	ПродолжитьЦиклВыполнения(Контекст);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПодписаниеНаТокене()
	
	ПараметрыПодписи = ПараметрыОперации.ПараметрыОперации;
	IDТокена = ПараметрыПодписи.Токен.Слот;
	ВидТокена = ПараметрыПодписи.Токен.Токен;
	
	ПараметрыJSON = Новый Структура;
	ПараметрыJSON.Вставить("slot", IDТокена);
	ПараметрыJSON.Вставить("sign_type", "cadesbes");
	Сертификаты = Новый Массив;
	Сертификат = Новый Структура("type, value_type, value", "token", "fingerprint", ПараметрыПодписи.Отпечаток);
	Сертификаты.Добавить(Сертификат);
	ПараметрыJSON.Вставить("certificates", Сертификаты);
	ПараметрыJSONСтрока = ЗаписатьЗначениеJSON(ПараметрыJSON);
	
	Результат = Ждать ОбъектКомпоненты.ПодписатьАсинх(ВидТокена, ПараметрыJSONСтрока, ПараметрыОперации.Данные, ПараметрыПодписи.Токен.Пароль);
	Оповещение = Новый ОписаниеОповещения("ПродолжитьОперацию", ЭтотОбъект, Новый Структура("Операция", ПараметрыОперации.Операция));
	ДобавитьОжидаемуюОперацию(Результат.Значение, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура РасшифровкаНаТокене()
	
	ПараметрыРасшифровки = ПараметрыОперации.ПараметрыОперации;
	IDТокена = ПараметрыРасшифровки.Токен.Слот;
	ВидТокена = ПараметрыРасшифровки.Токен.Токен;
	
	ПараметрыJSON = Новый Структура;
	ПараметрыJSON.Вставить("slot", IDТокена);
	ПараметрыJSON.Вставить("backend", ВидТокена);
	ПараметрыJSON.Вставить("encrypt_type", "");
	
	Сертификаты = Новый Массив; 
	Сертификат = Новый Структура("type, value_type, value", "token", "fingerprint", ПараметрыРасшифровки.Отпечаток);
	Сертификаты.Добавить(Сертификат);
	
	ПараметрыJSON.Вставить("certificates", Сертификаты);
	ПараметрыJSONСтрока = ЗаписатьЗначениеJSON(ПараметрыJSON);
	
	Данные = ПараметрыОперации.Данные;
	Если ЭтоАдресВременногоХранилища(Данные) Тогда
		Данные = ПолучитьИзВременногоХранилища(Данные);
	КонецЕсли;
	
	Результат = Ждать ОбъектКомпоненты.DecryptАсинх(ВидТокена, ПараметрыJSONСтрока, ПараметрыРасшифровки.Токен.Пароль, Base64Строка(Данные));
	Оповещение = Новый ОписаниеОповещения("ПродолжитьОперацию", ЭтотОбъект,  Новый Структура("Операция", ПараметрыОперации.Операция));
	ДобавитьОжидаемуюОперацию(Результат.Значение, Оповещение);

КонецПроцедуры

&НаКлиенте
Асинх Процедура ШифрованиеНаТокене()
	
	ПараметрыШифрования = ПараметрыОперации.ПараметрыОперации;
	IDТокена = ПараметрыШифрования.Токен.Слот;
	ВидТокена = ПараметрыШифрования.Токен.Токен;
	
	ПараметрыJSON = Новый Структура;
	ПараметрыJSON.Вставить("slot", IDТокена);
	ПараметрыJSON.Вставить("backend", ВидТокена);
	ПараметрыJSON.Вставить("encrypt_type", "cms");
	ПараметрыJSON.Вставить("cypher", ПараметрыШифрования.АлгоритмШифрования);
	
	Сертификаты = Новый Массив; 
	
	Для Каждого Сертификат Из ПараметрыШифрования.Сертификаты Цикл
		СертификатШифрования = Новый Структура("type, value_type, value", "data", "", Сертификат);
		Сертификаты.Добавить(СертификатШифрования);
	КонецЦикла;
	
	ПараметрыJSON.Вставить("certificates", Сертификаты);
	ПараметрыJSONСтрока = ЗаписатьЗначениеJSON(ПараметрыJSON);
	
	Данные = ПараметрыОперации.Данные;
	Если ЭтоАдресВременногоХранилища(Данные) Тогда
		Данные = ПолучитьИзВременногоХранилища(Данные);
	КонецЕсли;
	
	Результат = Ждать ОбъектКомпоненты.EncryptАсинх(ВидТокена, ПараметрыJSONСтрока, Данные);
	Оповещение = Новый ОписаниеОповещения("ПродолжитьОперацию", ЭтотОбъект,  Новый Структура("Операция", ПараметрыОперации.Операция));
	ДобавитьОжидаемуюОперацию(Результат.Значение, Оповещение);

КонецПроцедуры

&НаКлиенте
Асинх Процедура ПроверкаПодписиНаТокене()
	
	ПараметрыПроверкиПодписи = ПараметрыОперации.ПараметрыОперации;
	IDТокена = ПараметрыПроверкиПодписи.Токен.Слот;
	ВидТокена = ПараметрыПроверкиПодписи.Токен.Токен;
	ОткрепленнаяПодпись = ПараметрыПроверкиПодписи.ОткрепленнаяПодпись;
	Подпись = ПараметрыПроверкиПодписи.Подпись;
	Если ЭтоАдресВременногоХранилища(Подпись) Тогда
		Подпись = ПолучитьИзВременногоХранилища(Подпись);
	КонецЕсли;
	
	Данные = ПараметрыОперации.Данные;
	Если ЭтоАдресВременногоХранилища(Данные) Тогда
		Данные = ПолучитьИзВременногоХранилища(Данные);
	КонецЕсли;
	
	Данные = Base64Строка(Данные);
	Подпись = Base64Строка(Подпись);
	
	ПараметрыJSON = Новый Структура;
	ПараметрыJSON.Вставить("slot", IDТокена);
	ПараметрыJSON.Вставить("backend", ВидТокена);
	ПараметрыJSON.Вставить("sign_type", "cadesbes");
	
	ПараметрыJSONСтрока = ЗаписатьЗначениеJSON(ПараметрыJSON);
	
	Если ОткрепленнаяПодпись Тогда
		Результат = Ждать ОбъектКомпоненты.VerifySignАсинх(ВидТокена, ПараметрыJSONСтрока, Подпись, Данные);
	Иначе
		Результат = Ждать ОбъектКомпоненты.VerifySignАсинх(ВидТокена, ПараметрыJSONСтрока, Подпись);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПродолжитьОперацию", ЭтотОбъект, Новый Структура("Операция", ПараметрыОперации.Операция));
	ДобавитьОжидаемуюОперацию(Результат.Значение, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Асинх Функция ОбъектВнешнейКомпоненты()

#Если МобильноеПриложениеКлиент ИЛИ МобильныйКлиент Тогда

	ПараметрыПодключения = ОбщегоНазначенияКлиент.ПараметрыПодключенияКомпоненты();
	ПараметрыПодключения.ТекстПояснения = НСтр("ru = 'Требуется установка компоненты для работы с токенами.'");
	ПараметрыПодключения.ПредложитьУстановить = Истина;
	ПараметрыПодключения.ПредложитьЗагрузить = Ложь;
	ПараметрыПодключения.Кэшировать = Истина;
	ИмяОбъекта = "EDOMP";
	
	Результат = Ждать ОбщегоНазначенияКлиент.ПодключитьКомпонентуИзМакетаАсинх(
		ИмяОбъекта,
		"Обработка.ПрограммыЭлектроннойПодписиИШифрования.Макет.КомпонентаМобильнойПодписи",
		ПараметрыПодключения);
	
	Если Результат.Подключено Тогда
		ОбъектКомпоненты = Результат.ПодключаемыйМодуль;
	ИначеЕсли ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда
		ВызватьИсключение Результат.ОписаниеОшибки;
	Иначе
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось подключить внешнюю компоненту %1.'"), ИмяОбъекта);
			ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Версия = Ждать ОбъектКомпоненты.ВерсияАсинх();
	Элементы.Версия.Заголовок = "v. " + Версия.Значение;
	Элементы.Версия.Видимость = Истина;
		
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ПутьКФайлуДиагностики = ПолучитьНастройку(СистемнаяИнформация.ИдентификаторКлиента);
	Если ЗначениеЗаполнено(ПутьКФайлуДиагностики) Тогда
		Файл = Новый Файл(ПутьКФайлуДиагностики);
		ФайлСуществует = Ждать Файл.СуществуетАсинх();
		Если ФайлСуществует Тогда
			Если Ждать Файл.ЭтоКаталогАсинх() Тогда
				ПутьКФайлуДиагностики = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Файл.ПолноеИмя) + "EDOMP.log";
			КонецЕсли;
			ОбъектКомпоненты.УстановитьЛог(ПутьКФайлуДиагностики);
		КонецЕсли;
		ЗаписатьВЖурналРегистрации(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Подключена внешняя компонента %1, версия %2.'"), ИмяОбъекта, Версия.Значение),,Ложь);
	КонецЕсли;
	
	Возврат ОбъектКомпоненты;
#Иначе
	ВызватьИсключение 
				НСтр("ru = 'Действие доступно только на мобильных устройствах.'");
#КонецЕсли

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьНастройку(ИдентификаторКлиента)
	ПерсональныеНастройки = ЭлектроннаяПодпись.ПерсональныеНастройки();
	Возврат ПерсональныеНастройки.ПутьККаталогуДиагностики.Получить(ИдентификаторКлиента);
КонецФункции

&НаКлиенте
Функция КлючОперации(Операция, Идентификатор)
	Возврат Операция + "_" + Идентификатор;
КонецФункции

&НаКлиенте
Процедура ДобавитьОжидаемуюОперацию(Результат, Оповещение, Таймаут = Неопределено)
	
	Если ОжидаемыеОперации = Неопределено Тогда
		ОжидаемыеОперации = Новый Соответствие;
	КонецЕсли;
	
	Если Таймаут = Неопределено Тогда
		Таймаут = 20;
	КонецЕсли;
	
	НовыйРезультат = ПрочитатьЗначениеJSON(Результат);
	Если НовыйРезультат.State = "0" Тогда
		Ключ = КлючОперации(НовыйРезультат.operation, НовыйРезультат.opid);
		ОжидаемыеОперации.Вставить(Ключ, Новый Структура("ОжидатьДо, Оповещение", ТекущаяДата() + Таймаут, Оповещение)); // АПК:143 Должна использоваться ТекущаяДата()
	Иначе
		Ошибка = ОписаниеОшибкиОперацииНаТокене(НовыйРезультат.message);
		ВыполнитьОбработкуОповещения(Оповещение, Новый Структура("Ошибка", Ошибка));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеОшибкиОперацииНаТокене(ТекстОшибки)
	ТекстОшибкиВНижнемРегистре = НРег(ТекстОшибки); 
	Если ТекстОшибкиВНижнемРегистре = НРег("Pkcs11 error: CKR_PIN_INCORRECT") Тогда
		Возврат НСтр("ru = 'Укажите корректный пин-код токена.'");
	ИначеЕсли ТекстОшибкиВНижнемРегистре = НРег("Pkcs11 error: CKR_DEVICE_ERROR") 
		Или ТекстОшибкиВНижнемРегистре = НРег("Pkcs11 error: Unknown pkcs11 error") Тогда
		Возврат ТекстОшибкиОшибкаУстройства();
	ИначеЕсли ТекстОшибкиВНижнемРегистре = НРег("Pkcs11 error: CKR_TOKEN_NOT_PRESENT")
		Или ТекстОшибкиВНижнемРегистре = НРег("Rutoken not ready") Тогда
		Возврат ТекстОшибкиОтсутствуетТокен();
	ИначеЕсли ТекстОшибкиВНижнемРегистре = НРег("Pkcs11 error: CKR_ARGUMENTS_BAD") Тогда
		Возврат НСтр("ru = 'Недопустимые аргументы.'");
	ИначеЕсли ТекстОшибкиВНижнемРегистре = "user cancel operation" Тогда
		Возврат НСтр("ru = 'Операция отменена.'");
	КонецЕсли;
	Возврат ТекстОшибки;
КонецФункции

&НаКлиенте
Функция ТекстОшибкиОшибкаУстройства()
	Возврат НСтр("ru = 'Не удалось выполнить операцию на токене.'")
КонецФункции

&НаКлиенте
Функция ТекстОшибкиОтсутствуетТокен()
	Возврат НСтр("ru = 'Потеряна связь с токеном.'")
КонецФункции

&НаКлиенте
Функция ЭтоОшибкаУстройства(ТекстОшибки)
	ТекстОшибкиВНижнемРегистре = НРег(ТекстОшибки);
	
	Если ТекстОшибкиВНижнемРегистре = НРег("Pkcs11 error: CKR_DEVICE_ERROR")
		Или ТекстОшибкиВНижнемРегистре = НРег("Pkcs11 error: CKR_TOKEN_NOT_PRESENT")
		Или ТекстОшибкиВНижнемРегистре = НРег("Rutoken not ready")
		Или ТекстОшибкиВНижнемРегистре = НРег("Pkcs11 error: Unknown pkcs11 error")
		Или ТекстОшибкиВНижнемРегистре = НРег("Sign: no certs specified")
		Или СтрНайти(ТекстОшибкиВНижнемРегистре, НРег("0xffffffff80100001")) Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ПослеНайтиТокены(Результат, Контекст) Экспорт // АПК:78 - обработчик оповещения
	ЗавершитьОперацию(Результат.Токены);
КонецПроцедуры

&НаКлиенте
Процедура ПослеНайтиСлоты(Результат, Контекст) Экспорт // АПК:78 - обработчик оповещения
	ПродолжитьЦиклВыполнения(Результат.ПредыдущийКонтекст)
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПродолжитьЦиклВыполнения(Контекст, Ошибка = Неопределено)
	
	Если Ошибка <> Неопределено Тогда
		ЗавершитьОперацию(Ошибка);
		Возврат;
	КонецЕсли;
	
	Если Контекст.Индекс + 1 >= Контекст.Элементы.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения(Контекст.СледующееОповещение.ПроцедураМодуля, Контекст.СледующееОповещение.Модуль);
		ВыполнитьОбработкуОповещения(Оповещение, Контекст);
		Возврат;
	КонецЕсли;
	
	Контекст.Индекс = Контекст.Индекс + 1; 
	
	Если Контекст.Операция = "НайтиТокены" Тогда
		
		ВидТокена = Контекст.Элементы[Контекст.Индекс];
		
		Если ВидТокена = "rutoken_mock" Тогда
			ПродолжитьЦиклВыполнения(Контекст);
			Возврат;
		КонецЕсли;
		
		НастройкиВидаТокена = Ждать ОбъектКомпоненты.НастройкиБэкендаПоУмолчаниюАсинх(ВидТокена);
		Результат = Ждать ОбъектКомпоненты.ИнициализироватьАсинх(ВидТокена, НастройкиВидаТокена.Значение);
		Оповещение = Новый ОписаниеОповещения("ПродолжитьНастройкуВидаТокеновПослеИнициализации", ЭтотОбъект, Контекст);
		ДобавитьОжидаемуюОперацию(Результат.Значение, Оповещение);
		
	ИначеЕсли Контекст.Операция = "НайтиСлоты" Тогда

		ПараметрыJSON = Новый Структура;
		ПараметрыJSON.Вставить("backend", Контекст.ВидТокена);
		ПараметрыJSON.Вставить("slot", Контекст.Элементы[Контекст.Индекс]);

		Поля = Новый Массив;
		Поля.Добавить("slot");
		Поля.Добавить("token");
		Поля.Добавить("mechs");
		Поля.Добавить("env");
		
		ПараметрыJSON.Вставить("topics", Поля);
		ПараметрыJSONСтрока = ЗаписатьЗначениеJSON(ПараметрыJSON);
		
		ИнформацияОСлоте = Ждать ОбъектКомпоненты.ИнформацияАсинх(Контекст.ВидТокена, ПараметрыJSONСтрока);
		Оповещение = Новый ОписаниеОповещения("ПродолжитьПолучениеИнформацииОСлоте", ЭтотОбъект, Контекст);
		ДобавитьОжидаемуюОперацию(ИнформацияОСлоте.Значение, Оповещение);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Асинх Процедура ПродолжитьНастройкуВидаТокеновПослеИнициализации(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("Ошибка") Тогда
		ЗаписатьВЖурналРегистрации(НСтр("ru = 'Инициализация токена:'"), Результат.Ошибка);
		ПродолжитьЦиклВыполнения(Контекст, Результат.Ошибка);
		Возврат;
	КонецЕсли;
	
	ВидТокена = Контекст.Элементы[Контекст.Индекс];
	
	ПараметрыJSON = Новый Структура;
	ПараметрыJSON.Вставить("token_preset", Истина);
	ПараметрыJSONСтрока = ЗаписатьЗначениеJSON(ПараметрыJSON);
	
	СписокСлотов = Ждать ОбъектКомпоненты.СписокСлотовАсинх(ВидТокена, ПараметрыJSONСтрока);
	
	Оповещение = Новый ОписаниеОповещения("ПродолжитьНастройкуВидаТокеновПослеПолученияСпискаСлотов", ЭтотОбъект, Контекст);
	ДобавитьОжидаемуюОперацию(СписокСлотов.Значение, Оповещение);

КонецПроцедуры 

&НаКлиенте
Процедура ПродолжитьНастройкуВидаТокеновПослеПолученияСпискаСлотов(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("Ошибка") Тогда
		ЗаписатьВЖурналРегистрации(НСтр("ru = 'Получение списка слотов:'"), Результат.Ошибка);
		ЗавершитьОперацию(Результат.Ошибка);
		Возврат;
	КонецЕсли;

	КонтекстДляСлотов = Новый Структура;
	КонтекстДляСлотов.Вставить("Индекс", -1);
	КонтекстДляСлотов.Вставить("Элементы", Результат.slots);
	КонтекстДляСлотов.Вставить("Операция", "НайтиСлоты");
	КонтекстДляСлотов.Вставить("СледующееОповещение", Новый Структура("ПроцедураМодуля, Модуль", "ПослеНайтиСлоты", ЭтотОбъект));
	КонтекстДляСлотов.Вставить("ПредыдущийКонтекст", Контекст);
	КонтекстДляСлотов.Вставить("Результат", Новый Массив);
	КонтекстДляСлотов.Вставить("ВидТокена", Контекст.Элементы[Контекст.Индекс]);

	ПродолжитьЦиклВыполнения(КонтекстДляСлотов);

КонецПроцедуры

&НаКлиенте
Асинх Процедура ПродолжитьПолучениеИнформацииОСлоте(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("Ошибка") Тогда
		ЗаписатьВЖурналРегистрации(НСтр("ru = 'Получение информации о слоте:'"), Результат.Ошибка);
		ЗавершитьОперацию(Результат.Ошибка);
		Возврат;
	КонецЕсли;
	
	СвойстваТокена = ЭлектроннаяПодписьСлужебныйКлиент.НовыеСвойстваТокена();
	СвойстваТокена.Токен = Контекст.ВидТокена;
	СвойстваТокена.Слот = Контекст.Элементы[Контекст.Индекс];
	СвойстваТокена.Механизмы = ЗаписатьЗначениеJSON(Результат.information.mechanisms.mechanisms);
	СвойстваТокена.Модель = ЭлектроннаяПодписьКлиентЛокализация.МодельТокена(Результат.information.token.token.model, Контекст.ВидТокена);
	СвойстваТокена.СерийныйНомер = СокрЛП(Результат.information.token.token.serialNumber);
	СвойстваТокена.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 (серийный номер %2)'"), СвойстваТокена.Модель, СвойстваТокена.СерийныйНомер);
	
	Если Результат.information.env.env.Свойство("ciphers") Тогда
		СвойстваТокена.Вставить("Шифры", ЗаписатьЗначениеJSON(Результат.information.env.env.ciphers));
	Иначе
		СвойстваТокена.Вставить("Шифры", Неопределено);
	КонецЕсли;
	СвойстваТокена.Библиотеки.Вставить("APKLibraryPath", Результат.information.env.env.APKLibraryPath);
	СвойстваТокена.Библиотеки.Вставить("NativeLibraryDirectories", Результат.information.env.env.NativeLibraryDirectories);

	ПараметрыJSON = Новый Структура;
	ПараметрыJSON.Вставить("slot", Контекст.Элементы[Контекст.Индекс]);
	ПараметрыJSONСтрока = ЗаписатьЗначениеJSON(ПараметрыJSON);
		
	Результат = Ждать ОбъектКомпоненты.СписокСертификатовАсинх(Контекст.ВидТокена, ПараметрыJSONСтрока);
	
	ПараметрыОперацииНаТокене = Новый Структура("Операция, СвойстваТокена, Контекст", ПараметрыОперации.Операция, СвойстваТокена, Контекст);
	
	Оповещение = Новый ОписаниеОповещения("ПродолжитьОперацию", ЭтотОбъект, ПараметрыОперацииНаТокене);
	ДобавитьОжидаемуюОперацию(Результат.Значение, Оповещение);
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПродолжитьОперацию(Результат, ПараметрыОперацииНаТокене) Экспорт
	
	Операция = ПараметрыОперацииНаТокене.Операция;
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("Ошибка") Тогда
		Если Операция <> ЭлектроннаяПодписьКлиентЛокализация.ПолучениеСертификатов() Тогда
			ЗаписатьВЖурналРегистрации(Операция + ":", Результат.Ошибка);
			ЗавершитьОперацию(Результат.Ошибка);
		Иначе
			ЗаписатьВЖурналРегистрации(НСтр("ru = 'Получение списка сертификатов:'"), Результат.Ошибка);
			ПродолжитьЦиклВыполнения(ПараметрыОперацииНаТокене.Контекст, Результат.Ошибка);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если Операция = ЭлектроннаяПодписьКлиентЛокализация.ПолучениеСертификатов() Тогда
		
		СвойстваТокена = ПараметрыОперацииНаТокене.СвойстваТокена;
		Сертификаты = Новый Соответствие;
	
		Для Каждого Сертификат Из Результат.certificates Цикл
			Строка = СертификатСтрокой(Сертификат);
			СертификатКриптографии = Новый СертификатКриптографии;
			Ждать СертификатКриптографии.ИнициализироватьАсинх(Base64Значение(Строка));
			Сертификаты.Вставить(Base64Строка(СертификатКриптографии.Отпечаток), Ждать СертификатКриптографии.ВыгрузитьАсинх());
		КонецЦикла;
		
		СвойстваТокена.Сертификаты = Сертификаты;
		ПараметрыОперацииНаТокене.Контекст.ПредыдущийКонтекст.Токены.Добавить(СвойстваТокена);

		ПродолжитьЦиклВыполнения(ПараметрыОперацииНаТокене.Контекст);
		
	ИначеЕсли Операция = ЭлектроннаяПодписьКлиентЛокализация.Подписание() Тогда

		ПодписьCMS = Base64Значение(Результат.sign.value);
		ЗавершитьОперацию(ПодписьCMS);
				
	ИначеЕсли Операция = ЭлектроннаяПодписьКлиентЛокализация.Шифрование() Тогда

		ЗашифрованныеДанные = Base64Значение(Результат.encrypted.data);
		ЗавершитьОперацию(ЗашифрованныеДанные);

	ИначеЕсли Операция = ЭлектроннаяПодписьКлиентЛокализация.Расшифровка() Тогда
		
		РасшифрованныеДанные = Base64Значение(Результат.decrypted.data);
		ЗавершитьОперацию(РасшифрованныеДанные);
				
	ИначеЕсли Операция = ЭлектроннаяПодписьКлиентЛокализация.ПроверкаПодписи() Тогда
		
		РезультатПроверки = Новый Структура("Статус, Сертификаты", "", Новый Массив);
		РезультатПроверки.Статус = Результат.verification.status;
		
		Для Каждого Сертификат Из Результат.verification.certificates Цикл
			Строка = СертификатСтрокой(Сертификат);
			РезультатПроверки.Сертификаты.Добавить(Base64Значение(Строка));
		КонецЦикла;
		
		ЗавершитьОперацию(РезультатПроверки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СертификатСтрокой(Сертификат)
	
	Строка = СтрЗаменить(Сертификат.value, "-----BEGIN CERTIFICATE-----", "");
	Строка = СтрЗаменить(Строка, "-----END CERTIFICATE-----", "");
	Строка = СтрЗаменить(Строка, Символы.ПС, "");
	Возврат Строка;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьОперацию(Знач Результат)
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		Если ЭтоОшибкаУстройства(Результат) Тогда
			Элементы.ДекорацияПовторить.Заголовок = ОписаниеОшибкиОперацииНаТокене(Результат);
			Элементы.ГруппаПовторить.Видимость = Истина;
			Если Не Открыта() Тогда
				Открыть();
			КонецЕсли;
			Возврат;
		ИначеЕсли ПараметрыОперации.Операция = ЭлектроннаяПодписьКлиентЛокализация.ПолучениеСертификатов() Тогда
			Результат = Неопределено;
		Иначе
			Результат = ОписаниеОшибкиОперацииНаТокене(Результат);
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ПараметрыОперации.Оповещение, Результат);
	
#Если МобильныйКлиент Тогда
		УстановитьЗапретЗасыпанияКомпьютера(Ложь);
#КонецЕсли
	
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВнешниеСобытия()
	ВнешниеСобытия = ОжидаемыеОперации.Получить("ВнешниеСобытия");
	Если ВнешниеСобытия <> Неопределено Тогда
		КлючиДляУдаления = Новый Массив;
		Для Каждого КлючИЗначение Из ВнешниеСобытия Цикл
			СвойстваОперации = ОжидаемыеОперации.Получить(КлючИЗначение.Ключ);
			Если СвойстваОперации = Неопределено Тогда
				Если КлючИЗначение.Значение.ОжидатьДо < ТекущаяДата() Тогда // АПК:143 Должна использоваться ТекущаяДата()
					КлючиДляУдаления.Добавить(КлючИЗначение.Ключ);
				Иначе
					ПодключитьОбработчикОжидания("ОбновитьВнешниеСобытия", 3, Истина);
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			НовыйРезультат = КлючИЗначение.Значение.Результат;
			КлючиДляУдаления.Добавить(КлючИЗначение.Ключ);
			ВыполнитьОбработкуОповещения(СвойстваОперации.Оповещение, НовыйРезультат);
		КонецЦикла;
		ВнешниеСобытия = ОжидаемыеОперации.Получить("ВнешниеСобытия");
		Для Каждого Ключ Из КлючиДляУдаления Цикл
			ВнешниеСобытия.Удалить(Ключ);
			ОжидаемыеОперации.Вставить("ВнешниеСобытия", ВнешниеСобытия);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
