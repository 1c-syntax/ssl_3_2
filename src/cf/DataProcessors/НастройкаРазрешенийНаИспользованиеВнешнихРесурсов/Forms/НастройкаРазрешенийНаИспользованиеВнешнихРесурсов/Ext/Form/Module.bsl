///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем АдресХранилища, ПоследняяОшибка;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресХранилищаНаСервере = Параметры.АдресХранилища;
	РезультатОбработкиЗапросов = ПолучитьИзВременногоХранилища(АдресХранилищаНаСервере);
	
	ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ЭтоМобильныйКлиент = Истина; // Завершение в обработчике ПриОткрытии.
		Возврат;
		
	ИначеЕсли ОбщегоНазначения.ЭтоВебКлиент() Тогда
		ЭтоВебКлиент = Истина; // Завершение в обработчике ПриОткрытии.
		Элементы.ФормаНазад.Видимость = Ложь;
		Элементы.ФормаДалее.Видимость = Ложь;
		
		ПараметрыСообщения.ТипСообщения = "Ошибка";
		ПараметрыСообщения.Заголовок = НСтр("ru = 'Веб-клиент не поддерживается'");
		ПараметрыСообщения.Сообщение =
			НСтр("ru = 'Требуется настроить разрешения, как указано ниже (в профилях безопасности кластера серверов 1С:Предприятия).
			           |Однако это невозможно сделать из веб-клиента. Используйте тонкий клиент 1С:Предприятия.'");
		
	ИначеЕсли Константы.ИспользуютсяПрофилиБезопасности.Получить()
	        И Константы.АвтоматическиНастраиватьРазрешенияВПрофиляхБезопасности.Получить() Тогда
		
		Если Параметры.РежимПроверки Тогда
			ПараметрыСообщения.ТипСообщения = "Предупреждение";
			ПараметрыСообщения.Заголовок = НСтр("ru = 'Требуется обновить разрешения'");
			ПараметрыСообщения.Сообщение =
				НСтр("ru = 'Изменился состав разрешений, требуемых для работы этой информационной базы.
				           |Будут обновлены разрешения, как указано ниже (в профилях безопасности с теми же именами, какие были сгенерированы при их создании для этой информационной базы в кластере серверов 1С:Предприятия).'");
			
		ИначеЕсли Параметры.РежимВосстановления Тогда
			ПараметрыСообщения.ТипСообщения = "Информация";
			ПараметрыСообщения.Заголовок = НСтр("ru = 'Восстановление разрешений'");
			ПараметрыСообщения.Сообщение =
				НСтр("ru = 'Для восстановления будут установлены разрешения, как указано ниже (в профилях безопасности с теми же именами, какие были сгенерированы при их создании для этой информационной базы в кластере серверов 1С:Предприятия).'");
		Иначе
			ПараметрыСообщения.ТипСообщения = "Предупреждение";
			ПараметрыСообщения.Заголовок = НСтр("ru = 'Изменение разрешений'");
			ПараметрыСообщения.Сообщение =
				НСтр("ru = 'Для продолжения выполняемого действия будут изменены разрешения, как указано ниже (в профилях безопасности с теми же именами, какие были сгенерированы при их создании для этой информационной базы в кластере серверов 1С:Предприятия).'");
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Константы.ПрофильБезопасностиИнформационнойБазы.Получить()) Тогда
		ПараметрыСообщения.ТипСообщения = "Информация";
		ПараметрыСообщения.Заголовок = НСтр("ru = 'Повторная установка разрешений'");
		ПараметрыСообщения.Сообщение =
			НСтр("ru = 'Будут установлены разрешения, как указано ниже (в профилях безопасности с теми же именами, какие были сгенерированы при их создании для этой информационной базы в кластере серверов 1С:Предприятия).'");
	Иначе
		ПараметрыСообщения.ТипСообщения = "Информация";
		ПараметрыСообщения.Заголовок = НСтр("ru = 'Установка разрешений'");
		ПараметрыСообщения.Сообщение =
			НСтр("ru = 'Для этой информационной базы в кластере серверов 1С:Предприятия будут созданы новые профили безопасности с разрешениями, как указанно ниже.'");
	КонецЕсли;
	
	ПараметрыСообщения.КраткоеПредставление = ПараметрыСообщения.Заголовок;
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения, Элементы.ГруппаЗаголовок);
	ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);
	
	СценарийПримененияЗапросов = РезультатОбработкиЗапросов.Сценарий;
	
	Если СценарийПримененияЗапросов.Количество() = 0 Тогда
		ТребуетсяВнесениеИзмененийВПрофиляхБезопасности = Ложь;
		Возврат;
	КонецЕсли;
	
	ПредставлениеРазрешений = РезультатОбработкиЗапросов.Представление;
	
	ТребуетсяВнесениеИзмененийВПрофиляхБезопасности = Истина;
	ТребуютсяПараметрыАдминистрированияИнформационнойБазы = Ложь;
	Для Каждого ШагСценария Из СценарийПримененияЗапросов Цикл
		Если ШагСценария.Операция = Перечисления.ОперацииАдминистрированияПрофилейБезопасности.Назначение
		 Или ШагСценария.Операция = Перечисления.ОперацииАдминистрированияПрофилейБезопасности.УдалениеНазначения Тогда
			ТребуютсяПараметрыАдминистрированияИнформационнойБазы = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыАдминистрирования = СтандартныеПодсистемыСервер.ПараметрыАдминистрирования();
	
	Если ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(
			ПараметрыАдминистрирования.ИмяАдминистратораИнформационнойБазы);
		
		Если ПользовательИБ <> Неопределено Тогда
			ИдентификаторАдминистратораИБ = ПользовательИБ.УникальныйИдентификатор;
		КонецЕсли;
	КонецЕсли;
	
	ТипПодключения = ПараметрыАдминистрирования.ТипПодключения;
	ПортКластераСерверов = ПараметрыАдминистрирования.ПортКластера;
	
	АдресАгентаСервера = ПараметрыАдминистрирования.АдресАгентаСервера;
	ПортАгентаСервера  = ПараметрыАдминистрирования.ПортАгентаСервера;
	
	АдресСервераАдминистрирования = ПараметрыАдминистрирования.АдресСервераАдминистрирования;
	ПортСервераАдминистрирования  = ПараметрыАдминистрирования.ПортСервераАдминистрирования;
	
	ИмяВКластере = ПараметрыАдминистрирования.ИмяВКластере;
	ИмяАдминистратораКластера = ПараметрыАдминистрирования.ИмяАдминистратораКластера;
	
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(
		ПараметрыАдминистрирования.ИмяАдминистратораИнформационнойБазы);
	Если ПользовательИБ <> Неопределено Тогда
		ИдентификаторАдминистратораИБ = ПользовательИБ.УникальныйИдентификатор;
	КонецЕсли;
	
	Пользователи.НайтиНеоднозначныхПользователейИБ(Неопределено, ИдентификаторАдминистратораИБ);
	АдминистраторИБ = Справочники.Пользователи.НайтиПоРеквизиту("ИдентификаторПользователяИБ", ИдентификаторАдминистратораИБ);
	
	Элементы.ГруппаАдминистрирование.Видимость = ТребуютсяПараметрыАдминистрированияИнформационнойБазы;
	Элементы.ПредупреждениеОНеобходимостиПерезапуска.Видимость = ТребуютсяПараметрыАдминистрированияИнформационнойБазы;
	
	НастроитьКнопкуФормаДалее(ЭтотОбъект);
	Элементы.ФормаНазад.Видимость = Ложь;
	
	УправлениеВидимостью();
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		Возврат;
	КонецЕсли;
	
	КоманднаяПанель.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Низ;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаИзменитьФорму", "Видимость", Ложь);
	
	Ширина = 110;
	Элементы.ПредставлениеРазрешений.Высота = 18;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтоМобильныйКлиент Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Настройка недоступна в мобильном клиенте.
			           |
			           |Запустите тонкий клиент для настройки (или веб-клиент только для просмотра требуемой настройки).'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ЭтоВебКлиент Тогда
		Возврат;
	КонецЕсли;
	
	Если ТребуетсяВнесениеИзмененийВПрофиляхБезопасности Тогда
		АдресХранилища = АдресХранилищаНаСервере;
	Иначе
		Закрыть(КодВозвратаДиалога.Пропустить);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ТребуютсяПараметрыАдминистрированияИнформационнойБазы Тогда
		Если Не ЗначениеЗаполнено(АдминистраторИБ) Тогда
			Возврат;
		КонецЕсли;
		
		ИмяПоля = "АдминистраторИБ";
		ПользовательИБ = ПолучитьАдминистратораИБ();
		Если ПользовательИБ = Неопределено Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Указанный пользователь не имеет доступа к информационной базе.'"),,
				ИмяПоля,,Отказ);
			Возврат;
		КонецЕсли;
		
		Если Не Пользователи.ЭтоПолноправныйПользователь(ПользовательИБ, Истина) Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'У пользователя нет административных прав.'"),,
				ИмяПоля,,Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипПодключенияПриИзменении(Элемент)
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВажноеСообщениеОбработкаНавигационнойСсылки(Элемент,
			НавигационнаяСсылка, СтандартнаяОбработка)
	
	НавигационнаяСсылкаОбработана = Истина;
	
	Если НавигационнаяСсылка = "ПоказатьТекстОшибки" Тогда
		СтандартныеПодсистемыКлиент.ПоказатьПодробнуюИнформацию(,
			ПоследняяОшибка, НСтр("ru = 'Текст ошибки'"));
		
	ИначеЕсли НавигационнаяСсылка = "ЗарегистрироватьCOMСоединитель" Тогда
		ОбщегоНазначенияКлиент.ЗарегистрироватьCOMСоединитель();
	Иначе
		НавигационнаяСсылкаОбработана = Ложь;
	КонецЕсли;
	
	Если НавигационнаяСсылкаОбработана Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьДействиеИнформационногоСообщения(ЭлементИлиКоманда,
			НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	ОбщегоНазначенияКлиент.ОбработатьДействиеИнформационногоСообщения(ЭтотОбъект,
		ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРазрешения Тогда
		ПоследняяОшибка = Неопределено;
		Элементы.ГруппаОшибка.Видимость = Ложь;
		НастроитьКнопкуФормаДалее(ЭтотОбъект, Ложь);
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПодключение;
		Элементы.ФормаНазад.Видимость = Истина;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПодключение Тогда
		ПоследняяОшибка = Неопределено;
		Попытка
			ПрименитьРазрешения();
			ЗавершитьПрименениеЗапросов(АдресХранилища);
			ОжидатьПримененияНастроекВКластере();
		Исключение
			ПоследняяОшибка = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ВывестиОшибку(ПоследняяОшибка);
			Элементы.ГруппаОшибка.Видимость = Истина;
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаНазад(Команда)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПодключение Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРазрешения;
		Элементы.ФормаНазад.Видимость = Ложь;
		НастроитьКнопкуФормаДалее(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВывестиОшибку(Знач ПоследняяОшибка)
	
	СтрокиОшибки = СтрРазделить(ПоследняяОшибка, Символы.ПС + Символы.ВК, Ложь);
	СокращенныйТекстОшибки = ?(СтрокиОшибки.Количество() <= 2,
		СтрСоединить(СтрокиОшибки, Символы.ПС),
		СтрокиОшибки[0] + Символы.ПС + СтрокиОшибки[1]);
	
	СообщениеСФорматированием = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 <a href = %2>Подробнее</a>'"),
		СокращенныйТекстОшибки,
		"ПоказатьТекстОшибки");
	
	Если Элементы.ГруппаПараметрыПодключенияККластеруCOM.Видимость Тогда
		СообщениеСФорматированием = СообщениеСФорматированием + Символы.ПС + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При ошибке версии компоненты <b>comcntr</b>, <a href = %1>выполните ее регистрацию<a> и перезапустите сеанс.'"),
			"ЗарегистрироватьCOMСоединитель");
	КонецЕсли;
	
	ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыСообщения.КраткоеПредставление = НСтр("ru = 'Возникла ошибка'");
	ПараметрыСообщения.ТипСообщения = "Ошибка";
	ПараметрыСообщения.Сообщение = СтроковыеФункции.ФорматированнаяСтрока(СообщениеСФорматированием);
	ПараметрыСообщения.Обработчик = "Подключаемый_ВажноеСообщениеОбработкаНавигационнойСсылки";
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения, Элементы.ГруппаОшибка);
	ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьКнопкуФормаДалее(Форма, Далее = Истина)
	
	Элементы = Форма.Элементы;
	
	Если Далее Тогда
		Элементы.ФормаДалее.Заголовок = НСтр("ru = 'Далее'");
		Элементы.ФормаДалее.РасширеннаяПодсказка.Заголовок = "";
		Элементы.ФормаДалее.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Иначе
		Элементы.ФормаДалее.Заголовок = НСтр("ru = 'Настроить разрешения в кластере серверов'");
		Элементы.ФормаДалее.РасширеннаяПодсказка.Заголовок =
			НСтр("ru = 'Разрешить или запретить доступ к внешним ресурсам'");
		Элементы.ФормаДалее.Отображение = ОтображениеКнопки.Текст;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостью()
	
	ТипПодключенияCOM = ТипПодключения = "COM";
	
	Элементы.ГруппаПараметрыПодключенияККластеруCOM.Видимость =    ТипПодключенияCOM;
	Элементы.ГруппаПараметрыПодключенияККластеруRAS.Видимость = Не ТипПодключенияCOM;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдминистратораИБ()
	
	Если Не ЗначениеЗаполнено(АдминистраторИБ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(АдминистраторИБ, "ИдентификаторПользователяИБ"));
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяПользователяИнформационнойБазы(Знач Пользователь)
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат "";
	КонецЕсли;
	
	ИдентификаторПользователяИБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "ИдентификаторПользователяИБ");
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ИдентификаторПользователяИБ);
	
	Возврат ПользовательИБ.Имя;
	
КонецФункции

&НаКлиенте
Процедура ПрименитьРазрешения()
	
	ПрименитьРазрешенияНаСервере(АдресХранилища);
	
КонецПроцедуры

&НаСервере
Функция НачатьПрименениеЗапросов(Знач АдресХранилища)
	
	Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
	СценарийПримененияЗапросов = Результат.Сценарий;
	
	ВидыОпераций = Новый Структура();
	Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.ОперацииАдминистрированияПрофилейБезопасности.ЗначенияПеречисления Цикл
		ВидыОпераций.Вставить(ЗначениеПеречисления.Имя, Перечисления.ОперацииАдминистрированияПрофилейБезопасности[ЗначениеПеречисления.Имя]);
	КонецЦикла;
	
	Возврат Новый Структура("ВидыОпераций, СценарийПримененияЗапросов, ТребуютсяПараметрыАдминистрированияИнформационнойБазы",
		ВидыОпераций, СценарийПримененияЗапросов, ТребуютсяПараметрыАдминистрированияИнформационнойБазы);
	
КонецФункции

&НаСервере
Процедура ЗавершитьПрименениеЗапросов(Знач АдресХранилища)
	
	Обработки.НастройкаРазрешенийНаИспользованиеВнешнихРесурсов.ЗафиксироватьПрименениеЗапросов(ПолучитьИзВременногоХранилища(АдресХранилища).Состояние);
	СохранитьПараметрыАдминистрирования();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьПараметрыАдминистрирования()
	
	СохраняемыеПараметрыАдминистрирования = Новый Структура();
	
	// Параметры администрирования кластера.
	СохраняемыеПараметрыАдминистрирования.Вставить("ТипПодключения", ТипПодключения);
	СохраняемыеПараметрыАдминистрирования.Вставить("АдресАгентаСервера", АдресАгентаСервера);
	СохраняемыеПараметрыАдминистрирования.Вставить("ПортАгентаСервера", ПортАгентаСервера);
	СохраняемыеПараметрыАдминистрирования.Вставить("АдресСервераАдминистрирования", АдресСервераАдминистрирования);
	СохраняемыеПараметрыАдминистрирования.Вставить("ПортСервераАдминистрирования", ПортСервераАдминистрирования);
	СохраняемыеПараметрыАдминистрирования.Вставить("ПортКластера", ПортКластераСерверов);
	СохраняемыеПараметрыАдминистрирования.Вставить("ИмяАдминистратораКластера", ИмяАдминистратораКластера);
	СохраняемыеПараметрыАдминистрирования.Вставить("ПарольАдминистратораКластера", "");
	
	// Параметры администрирования информационной базы.
	СохраняемыеПараметрыАдминистрирования.Вставить("ИмяВКластере", ИмяВКластере);
	СохраняемыеПараметрыАдминистрирования.Вставить("ИмяАдминистратораИнформационнойБазы", ИмяПользователяИнформационнойБазы(АдминистраторИБ));
	СохраняемыеПараметрыАдминистрирования.Вставить("ПарольАдминистратораИнформационнойБазы", "");
	
	СтандартныеПодсистемыСервер.УстановитьПараметрыАдминистрирования(СохраняемыеПараметрыАдминистрирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьПримененияНастроекВКластере()
	
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьРазрешенияНаСервере(АдресХранилища)
	
	ПараметрыПрименения = НачатьПрименениеЗапросов(АдресХранилища);
	
	ВидыОпераций = ПараметрыПрименения.ВидыОпераций;
	Сценарий = ПараметрыПрименения.СценарийПримененияЗапросов;
	ТребуютсяПараметрыАдминистрированияИБ = ПараметрыПрименения.ТребуютсяПараметрыАдминистрированияИнформационнойБазы;
	
	ПараметрыАдминистрированияКластера = АдминистрированиеКластера.ПараметрыАдминистрированияКластера();
	ПараметрыАдминистрированияКластера.ТипПодключения = ТипПодключения;
	ПараметрыАдминистрированияКластера.АдресАгентаСервера = АдресАгентаСервера;
	ПараметрыАдминистрированияКластера.ПортАгентаСервера = ПортАгентаСервера;
	ПараметрыАдминистрированияКластера.АдресСервераАдминистрирования = АдресСервераАдминистрирования;
	ПараметрыАдминистрированияКластера.ПортСервераАдминистрирования = ПортСервераАдминистрирования;
	ПараметрыАдминистрированияКластера.ПортКластера = ПортКластераСерверов;
	ПараметрыАдминистрированияКластера.ИмяАдминистратораКластера = ИмяАдминистратораКластера;
	ПараметрыАдминистрированияКластера.ПарольАдминистратораКластера = ПарольАдминистратораКластера;
	
	Если ТребуютсяПараметрыАдминистрированияИБ Тогда
		ПараметрыАдминистрированияИБ = АдминистрированиеКластера.ПараметрыАдминистрированияИнформационнойБазыКластера();
		ПараметрыАдминистрированияИБ.ИмяВКластере = ИмяВКластере;
		ПараметрыАдминистрированияИБ.ИмяАдминистратораИнформационнойБазы = ИмяПользователяИнформационнойБазы(АдминистраторИБ);
		ПараметрыАдминистрированияИБ.ПарольАдминистратораИнформационнойБазы = ПарольАдминистратораИБ;
	Иначе
		ПараметрыАдминистрированияИБ = Неопределено;
	КонецЕсли;
	
	ПрименитьИзмененияРазрешенийВПрофиляхБезопасностиВКластереСерверов(
		ВидыОпераций, Сценарий, ПараметрыАдминистрированияКластера, ПараметрыАдминистрированияИБ);
	
КонецПроцедуры

#Область ПрименениеЗапросовРазрешенийНаИспользованиеВнешнихРесурсов

// Применяет изменения разрешений в профилях безопасности в кластере серверов по сценарию.
//
// Параметры:
//  ВидыОпераций - Структура - описывающая значения перечисления ОперацииАдминистрированияПрофилейБезопасности:
//                   * Ключ - Строка - имя значения перечисления,
//                   * Значение - ПеречислениеСсылка.ОперацииАдминистрированияПрофилейБезопасности,
//  СценарийПримененияРазрешений - Массив из Структура - сценарий применения изменений разрешений на
//    использование профилей безопасности в кластере серверов. Значениями массива являются структуры
//    со следующими полями:
//                   * Операция - ПеречислениеСсылка.ОперацииАдминистрированияПрофилейБезопасности - операция, которую
//                      требуется выполнить,
//                   * Профиль - Строка - имя профиля безопасности,
//                   * Разрешения - см. АдминистрированиеКластера.СвойстваПрофиляБезопасности
//  ПараметрыАдминистрированияКластера - см. АдминистрированиеКластера.ПараметрыАдминистрированияКластера
//  ПараметрыАдминистрированияИнформационнойБазы - см. АдминистрированиеКластера.ПараметрыАдминистрированияИнформационнойБазыКластера
//
&НаСервереБезКонтекста
Процедура ПрименитьИзмененияРазрешенийВПрофиляхБезопасностиВКластереСерверов(Знач ВидыОпераций, Знач СценарийПримененияРазрешений, Знач ПараметрыАдминистрированияКластера, Знач ПараметрыАдминистрированияИнформационнойБазы = Неопределено)
	
	ТребуютсяПараметрыАдминистрированияИБ = (ПараметрыАдминистрированияИнформационнойБазы <> Неопределено);
	
	АдминистрированиеКластера.ПроверитьПараметрыАдминистрирования(ПараметрыАдминистрированияКластера,
		ПараметрыАдминистрированияИнформационнойБазы,
		Истина,
		ТребуютсяПараметрыАдминистрированияИБ);
	
	Для Каждого ЭлементСценария Из СценарийПримененияРазрешений Цикл
		Если ЭлементСценария.Операция = ВидыОпераций.Создание Тогда
			Если АдминистрированиеКластера.ПрофильБезопасностиСуществует(
					ПараметрыАдминистрированияКластера, ЭлементСценария.Профиль) Тогда
				
				ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Профиль безопасности %1 уже присутствует в кластере серверов. Настройки в профиле безопасности будут замещены...'"),
					ЭлементСценария.Профиль));
				
				АдминистрированиеКластера.УстановитьСвойстваПрофиляБезопасности(ПараметрыАдминистрированияКластера,
					ЭлементСценария.Разрешения);
			Иначе
				АдминистрированиеКластера.СоздатьПрофильБезопасности(ПараметрыАдминистрированияКластера,
					ЭлементСценария.Разрешения);
			КонецЕсли;
			
		ИначеЕсли ЭлементСценария.Операция = ВидыОпераций.Назначение Тогда
			АдминистрированиеКластера.УстановитьПрофильБезопасностиИнформационнойБазы(ПараметрыАдминистрированияКластера,
				ПараметрыАдминистрированияИнформационнойБазы, ЭлементСценария.Профиль);
			
		ИначеЕсли ЭлементСценария.Операция = ВидыОпераций.Обновление Тогда
			АдминистрированиеКластера.УстановитьСвойстваПрофиляБезопасности(ПараметрыАдминистрированияКластера,
				ЭлементСценария.Разрешения);
			
		ИначеЕсли ЭлементСценария.Операция = ВидыОпераций.Удаление Тогда
			Если АдминистрированиеКластера.ПрофильБезопасностиСуществует(
					ПараметрыАдминистрированияКластера, ЭлементСценария.Профиль) Тогда
				
				АдминистрированиеКластера.УдалитьПрофильБезопасности(ПараметрыАдминистрированияКластера,
					ЭлементСценария.Профиль);
			Иначе
				ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Профиль безопасности %1 отсутствует в кластере серверов. Возможно, профиль безопасности был удален ранее...'"),
					ЭлементСценария.Профиль));
			КонецЕсли;
			
		ИначеЕсли ЭлементСценария.Операция = ВидыОпераций.УдалениеНазначения Тогда
			АдминистрированиеКластера.УстановитьПрофильБезопасностиИнформационнойБазы(ПараметрыАдминистрированияКластера,
				ПараметрыАдминистрированияИнформационнойБазы, "");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти