///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПравоДоступа("АдминистрированиеДанных", Метаданные) Тогда
		ВызватьИсключение(НСтр("ru = 'Недостаточно прав для настройки автоматического REST-сервиса.'"),
			КатегорияОшибки.НарушениеПравДоступа);
	КонецЕсли;
	
	НастройкиАвторизации = Обработки.НастройкаСтандартногоИнтерфейсаOData.НастройкиАвторизацииДляСтандартногоИнтерфейсаOData();
	СоздатьПользователяСтандартногоИнтерфейсаOData = НастройкиАвторизации.Используется;
	
	Если ЗначениеЗаполнено(НастройкиАвторизации.Логин) Тогда
		
		ИмяПользователя = НастройкиАвторизации.Логин;
		Если СоздатьПользователяСтандартногоИнтерфейсаOData Тогда
			
			ПроверкаИзмененияПароля = Строка(Новый УникальныйИдентификатор());
			Пароль = ПроверкаИзмененияПароля;
			ПодтверждениеПароля = ПроверкаИзмененияПароля;
			
		КонецЕсли;
		
	Иначе
		ИмяПользователя = "odata.user";
	КонецЕсли;
		
	УстановитьВидимостьИДоступность();
	УстановитьУсловноеОформление();
	ИнициализироватьВажныеСообщенияНаФорме();
	
	ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не СоздатьПользователяСтандартногоИнтерфейсаOData Тогда
		Возврат;
	КонецЕсли;
		
	ПроверяемыеРеквизиты.Добавить("ИмяПользователя");
	ПроверяемыеРеквизиты.Добавить("Пароль");
	ПроверяемыеРеквизиты.Добавить("ПодтверждениеПароля");
	
	Если Пароль <> ПодтверждениеПароля Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Подтверждение пароля не совпадает с паролем'"), , "ПодтверждениеПароля");
		Отказ = Истина;
	КонецЕсли;
	
	Если ОбъектыМетаданных.ПолучитьЭлементы().Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Не указаны объекты, доступ к которым может быть предоставлен через автоматический REST-сервис.'"),, 
			"ОбъектыМетаданных");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПродолжитьЗакрытиеПослеВопроса", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбъектыМетаданныхИспользованиеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОбъектыМетаданных.ТекущиеДанные;
	
	Если ТекущиеДанные.Использование Тогда
		Добавление = Истина;
		Зависимости = ЗависимостиДляДобавленияОбъекта(ТекущиеДанные.ПолучитьИдентификатор());
	Иначе
		Добавление = Ложь;
		Зависимости = ЗависимостиДляУдаленияОбъекта(ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
	Если Зависимости.Количество() > 0 Тогда
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ПолноеИмяОбъекта", ТекущиеДанные.ПолноеИмя);
		ПараметрыФормы.Вставить("ЗависимостиОбъекта", Зависимости);
		ПараметрыФормы.Вставить("Добавление", Добавление);
		
		Контекст = Новый Структура();
		Контекст.Вставить("Зависимости", Зависимости);
		Контекст.Вставить("Добавление", Добавление);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбъектыМетаданныхИспользованиеПриИзмененииПродолжение", ЭтотОбъект, Контекст);
		
		ОткрытьФорму("Обработка.НастройкаСтандартногоИнтерфейсаOData.Форма.ЗависимостиОбъектаМетаданных",
			ПараметрыФормы,,,,, ОписаниеОповещения, РежимБлокировкиПриОткрытииОкнаФормы.БлокироватьВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПользователяСтандартногоИнтерфейсаODataПриИзменении(Элемент)
	
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьДействиеИнформационногоСообщения(ЭлементИлиКоманда,
	НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	ОбщегоНазначенияКлиент.ОбработатьДействиеИнформационногоСообщения(ЭтотОбъект,
	ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Функция Сохранить(Команда = Неопределено)
	
	ОчиститьСообщения();
	
	Если Не Модифицированность Тогда
		Возврат Истина;
	КонецЕсли;
		
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СохранитьНаСервере();
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда = Неопределено)
	
	Если Сохранить() Тогда
		Закрыть();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьМетаданные(Команда)
	
	Если Модифицированность
		И ОбъектыМетаданных.ПолучитьЭлементы().Количество() > 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("ЗагрузитьМетаданныеПродолжение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Загрузить метаданные заново? Внесенные изменения будут потеряны.'"), РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ЗагрузитьМетаданныеПродолжение(КодВозвратаДиалога.Да, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнструкцию(Команда)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://its.1c.eu/db/v8323doc#bookmark:dev:ti000001358");
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьМетаданныеПродолжение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ЗапуститьПодготовкуПараметровНастройки();
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ЗавершениеПолученияПараметровНастройки", ЭтотОбъект);
	Если Результат.Статус = "Выполнено" Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Результат);
	Иначе
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение,
			ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗакрытиеПослеВопроса(Результат, Контекст) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьИЗакрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//   Результат - КодВозвратаДиалога
//             - Неопределено
//   Контекст  - Структура
//
&НаКлиенте
Процедура ОбъектыМетаданныхИспользованиеПриИзмененииПродолжение(Результат, Контекст) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УстановитьИспользованиеЗависимостей(Контекст.Зависимости, Контекст.Добавление);
	Иначе
		Элементы.ОбъектыМетаданных.ТекущиеДанные.Использование = Не Элементы.ОбъектыМетаданных.ТекущиеДанные.Использование;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ЗавершениеПолученияПараметровНастройки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Результат.Статус <> "Выполнено" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(
			Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	ЗагрузитьПараметрыНастройки(Результат.АдресРезультата);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступность()
	
	Элементы.ИмяПользователяИПароль.Видимость = СоздатьПользователяСтандартногоИнтерфейсаOData;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Использование = Истина;
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ОбъектыМетаданныхИспользование");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбъектыМетаданных.Корневой");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
КонецПроцедуры

&НаСервере
Функция ЗависимостиДляДобавленияОбъекта(Знач ИдентификаторСтроки)
	
	ТаблицаЗависимостей = РеквизитФормыВЗначение("ЗависимостиДляДобавления");
	Возврат ЗависимостиОбъекта(ИдентификаторСтроки, ТаблицаЗависимостей, Истина);
	
КонецФункции

&НаСервере
Функция ЗависимостиДляУдаленияОбъекта(Знач ИдентификаторСтроки)
	
	ТаблицаЗависимостей = РеквизитФормыВЗначение("ЗависимостиДляУдаления");
	Возврат ЗависимостиОбъекта(ИдентификаторСтроки, ТаблицаЗависимостей, Ложь);
	
КонецФункции

&НаСервере
Функция ЗависимостиОбъекта(Знач ИдентификаторСтроки, ТаблицаЗависимостей, ЗначениеИспользования)
	
	ИмяТекущегоОбъекта = ОбъектыМетаданных.НайтиПоИдентификатору(ИдентификаторСтроки).ПолноеИмя;
	
	ДеревоОбъектов = РеквизитФормыВЗначение("ОбъектыМетаданных");
	
	Результат = ЗависимостиОбъектаПоСтроке(
		ДеревоОбъектов, ТаблицаЗависимостей, ИмяТекущегоОбъекта, ЗначениеИспользования);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗависимостиОбъектаПоСтроке(ДеревоОбъектов, ТаблицаЗависимостей, ИмяНачальногоОбъекта, ЗначениеИспользования)
	
	Результат = Новый Массив;
	
	ОчередьОбработки = Новый Массив;
	ОчередьОбработки.Добавить(ИмяНачальногоОбъекта);
	
	Индекс = 0;
	Пока Индекс < ОчередьОбработки.Количество() Цикл
		
		ТекущееИмя = ОчередьОбработки[Индекс];
		Индекс = Индекс + 1;
		
		// Ищем строки, где текущий объект является родителем.
		ПараметрыОтбора = Новый Структура("ИмяОбъекта", ТекущееИмя);
		СтрокиЗависимостей = ТаблицаЗависимостей.НайтиСтроки(ПараметрыОтбора);
		
		Для Каждого Строка Из СтрокиЗависимостей Цикл
			
			ИмяЗависимого = Строка.ИмяЗависимогоОбъекта;
			
			Если Результат.Найти(ИмяЗависимого) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЗависимыйВДереве = ДеревоОбъектов.Строки.Найти(ИмяЗависимого, "ПолноеИмя", Истина);
			
			Если ЗависимыйВДереве <> Неопределено
				И ЗависимыйВДереве.Использование <> ЗначениеИспользования Тогда
				
				Результат.Добавить(ИмяЗависимого);
				
				ОчередьОбработки.Добавить(ИмяЗависимого);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьИспользованиеЗависимостей(Знач Зависимости, Знач Использование)
	
	КорневыеЭлементы = ОбъектыМетаданных.ПолучитьЭлементы();
	Для Каждого КорневойЭлемент Из КорневыеЭлементы Цикл
		
		ЭлементыДерева = КорневойЭлемент.ПолучитьЭлементы();
		Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
			
			Если Зависимости.Найти(ЭлементДерева.ПолноеИмя) <> Неопределено Тогда
				ЭлементДерева.Использование = Использование;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	
	НачатьТранзакцию();
	
	Попытка
		
		Настройки = Новый Структура();
		
		Настройки.Вставить("Используется", СоздатьПользователяСтандартногоИнтерфейсаOData);
		Настройки.Вставить("Логин", ИмяПользователя);
		Если ПроверкаИзмененияПароля <> Пароль Тогда
			Настройки.Вставить("Пароль", Пароль);
		КонецЕсли;
		
		Обработки.НастройкаСтандартногоИнтерфейсаOData.ЗаписатьНастройкиАвторизацииДляСтандартногоИнтерфейсаOData(Настройки);
		
		Состав = Новый Массив();
		Дерево = РеквизитФормыВЗначение("ОбъектыМетаданных");
		Строки = Дерево.Строки.НайтиСтроки(Новый Структура("Использование", Истина), Истина);
		Для Каждого Строка Из Строки Цикл
			Состав.Добавить(Строка.ПолноеИмя);
		КонецЦикла;
		
		УстановитьСоставСтандартногоИнтерфейсаOData(Состав);
		
		Модифицированность = Ложь;
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПараметрыНастройки(Знач АдресХранилища)
	
	Данные = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(Данные) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(Данные.ДеревоОбъектов, "ОбъектыМетаданных");
	ЗначениеВРеквизитФормы(Данные.ЗависимостиДобавления, "ЗависимостиДляДобавления");
	ЗначениеВРеквизитФормы(Данные.ЗависимостиУдаления, "ЗависимостиДляУдаления");
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьПодготовкуПараметровНастройки()
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.НаименованиеФоновогоЗадания = "ПодготовитьПараметрыНастройкиСоставаСтандартногоИнтерфейсаOData";
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.НастройкаСтандартногоИнтерфейсаOData.ПодготовитьПараметрыНастройкиСоставаСтандартногоИнтерфейсаOData",
		Новый Структура,
		ПараметрыВыполненияВФоне);
	
КонецФункции

&НаСервере
Процедура ИнициализироватьВажныеСообщенияНаФорме()
	
	ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыСообщения.ТипСообщения = "Предупреждение";
	ПараметрыСообщения.Идентификатор = "ИмяПользователяИПарольВажноеСообщение";
	ПараметрыСообщения.Сообщение = НСтр("ru = 'В целях безопасности при использовании автоматического REST-сервиса, не передавайте имя и пароль этого пользователя третьим лицам без необходимости, так как использование REST-сервиса позволяет выполнять чтение и изменение данных приложения. Сообщайте их только надежным лицам и используйте в сторонних приложениях, которым доверяете.'");
	ПараметрыСообщения.КраткоеПредставление = НСтр("ru = 'Не передавайте сведения третьим лицам'");
	ПараметрыСообщения.Скрываемый = Ложь;
	
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения, Элементы.ИмяПользователяИПароль);
	
КонецПроцедуры

#КонецОбласти


