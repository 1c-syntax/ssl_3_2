///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПроверитьДоступностьОбработки();
	
	ОбновитьПредставление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ХранилищаДвоичныхДанныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	РедактироватьХранилище(Элемент);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьХранилище(Команда)

	Оповещение = Новый ОписаниеОповещения("ПриДобавленииХранилища", ЭтотОбъект);

	ПараметрыОткрытияФормы = Новый Структура("ОткрытаПоСценарию", Истина);

	ОткрытьФорму(ИмяФормыДляОткрытия("ДобавитьХранилище", ИмяФормы), ПараметрыОткрытияФормы, , , , , Оповещение, РежимБлокировкиПриОткрытииОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура РедактироватьХранилище(Команда)

	ТекущиеДанныеСтроки = Элементы.ХранилищаДвоичныхДанных.ТекущиеДанные;
	Если ТекущиеДанныеСтроки <> Неопределено Тогда
		Оповещение = Новый ОписаниеОповещения("ПриРедактированииХранилища", ЭтотОбъект);
		
		ПараметрыОткрытияФормы = Новый Структура("ОткрытаПоСценарию", Истина);
		
		Если ТекущиеДанныеСтроки.АдресХранилища = "" Тогда
			ОткрытьФорму(ИмяФормыДляОткрытия("ВстроенноеХранилище", ИмяФормы), ПараметрыОткрытияФормы, , , , , Оповещение, РежимБлокировкиПриОткрытииОкнаФормы.БлокироватьВесьИнтерфейс);
		Иначе
			ПараметрыОткрытияФормы.Вставить("ИмяХранилища", ТекущиеДанныеСтроки.ИмяХранилища);

			ОткрытьФорму(ИмяФормыДляОткрытия("ВнешнееХранилище", ИмяФормы), ПараметрыОткрытияФормы, , , , , Оповещение, РежимБлокировкиПриОткрытииОкнаФормы.БлокироватьВесьИнтерфейс);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УдалитьХранилище(Команда)

	ТекущиеДанныеСтроки = Элементы.ХранилищаДвоичныхДанных.ТекущиеДанные;
	Если ТекущиеДанныеСтроки <> Неопределено Тогда
		
		Оповещение = Новый ОписаниеОповещения("УдалитьХранилищеДвоичныхДанных", ЭтотОбъект);
		
		ТекстВопроса = НСтр("ru = 'Удалить хранилище двоичных данных?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ИмяФормыДляОткрытия(ФормаИмя, ПолноеИмяФормы)

	Путь = Лев(ПолноеИмяФормы,СтрНайти(ПолноеИмяФормы, ".", НаправлениеПоиска.СКонца));
	Возврат Путь + "." + ФормаИмя;

КонецФункции

&НаКлиенте
Процедура ПриДобавленииХранилища(Результат, ДополнительныеПараметры) Экспорт
	ОбновитьПредставление();
КонецПроцедуры

&НаКлиенте
Процедура ПриРедактированииХранилища(Результат, ДополнительныеПараметры) Экспорт
	ОбновитьПредставление();
КонецПроцедуры

&НаСервере
Процедура ДобавитьХранилищеВТаблицу(ИмяХранилища, АдресХранилища, РежимЧтенияЗаписиХДД, ХранилищеПоУмолчанию, МинимальныйРазмерДанных, ДатаОчисткиХранилища)

	СтрокаХДД = ХранилищаДвоичныхДанных.Добавить();
	СтрокаХДД.ИмяХранилища = ИмяХранилища;	
	СтрокаХДД.АдресХранилища = АдресХранилища;

	Если РежимЧтенияЗаписиХДД = РежимЧтенияЗаписиХранилищаДвоичныхДанных.ТолькоЧтение Тогда
 		СтрокаХДД.РежимИспользования = НСтр("ru = 'Только чтение'", ОбщегоНазначения.КодОсновногоЯзыка());
		СтрокаХДД.МинимальныйРазмерДанных = "";
	Иначе 
		СтрокаХДД.РежимИспользования = НСтр("ru = 'Чтение и запись'", ОбщегоНазначения.КодОсновногоЯзыка());
		СтрокаХДД.МинимальныйРазмерДанных = МинимальныйРазмерДанных;
	КонецЕсли;

	СтрокаХДД.ХранилищеПоУмолчанию = ?(ХранилищеПоУмолчанию, 0, 1);	
	
	Если СтрокаХДД.ДатаОчисткиХранилища <> Неопределено Тогда
		СтрокаХДД.ДатаОчисткиХранилища = ДатаОчисткиХранилища;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставление()

	ХранилищаДвоичныхДанных.Очистить();
	
	// встроенное хранилище
	РежимИспользованияХДД = ХранилищеДвоичныхДанных.ПолучитьРежимИспользованияХранилищаДвоичныхДанных();
	Если РежимИспользованияХДД = РежимИспользованияХранилищаДвоичныхДанных.Разрешить Тогда
		ИмяХранилища = НСтр("ru = 'Встроенное хранилище'", ОбщегоНазначения.КодОсновногоЯзыка());
		АдресХранилища = "";
		РежимЧтенияЗаписиХДД = Ложь;
		МинимальныйРазмерДанных = 2048;
		ХранилищеПоУмолчанию = Ложь;
		ДатаОчисткиХранилища = Неопределено;

		Попытка
			РежимЧтенияЗаписиХДД = ХранилищеДвоичныхДанных.ПолучитьРежимЧтенияЗаписиХранилищаДвоичныхДанных();
			МинимальныйРазмерДанных = ХранилищеДвоичныхДанных.ПолучитьМинимальныйРазмерЗаписываемыхДанных();

			ХранилищеПоУмолчанию = (РежимЧтенияЗаписиХДД <> РежимЧтенияЗаписиХранилищаДвоичныхДанных.ТолькоЧтение);
			Если ХранилищеПоУмолчанию Тогда
				
				ХранилищеПоУмолчанию = ХранилищеДвоичныхДанных.ПолучитьИспользованиеКакХранилищаПоУмолчанию();

			КонецЕсли; 
			
			ИнформацияОХранилище = ХранилищеДвоичныхДанных.ПолучитьИнформацию();
			Если ИнформацияОХранилище.ДатаПоследнейОчистки > Дата(1, 1, 1) Тогда
				ДатаОчисткиХранилища = ИнформацияОХранилище.ДатаПоследнейОчистки;
			КонецЕсли;
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Управление хранилищем двоичных данных'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			НСтр("ru = 'Ошибка получения параметров встроенного хранилища'", ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецПопытки;
		
    	ДобавитьХранилищеВТаблицу(ИмяХранилища, АдресХранилища, РежимЧтенияЗаписиХДД, ХранилищеПоУмолчанию, МинимальныйРазмерДанных, ДатаОчисткиХранилища);

	КонецЕсли; 
	
	// внешние хранилища
	Для Каждого МенеджерХранилища Из ВнешниеХранилищаДвоичныхДанных Цикл

		ИмяХранилища			= "";
		АдресХранилища			= "";
		РежимЧтенияЗаписиХДД	= Ложь;
		МинимальныйРазмерДанных = 2048;
		ХранилищеПоУмолчанию	= Ложь;
		ДатаОчисткиХранилища	= Неопределено;

		Попытка
			ИмяХранилища			= МенеджерХранилища.Имя;
			РежимЧтенияЗаписиХДД	= МенеджерХранилища.РежимЧтенияЗаписи;
			МинимальныйРазмерДанных = МенеджерХранилища.МинимальныйРазмерЗаписываемыхДанных ;
			ХранилищеПоУмолчанию	= (РежимЧтенияЗаписиХДД <> РежимЧтенияЗаписиХранилищаДвоичныхДанных.ТолькоЧтение) И МенеджерХранилища.ХранилищеПоУмолчанию;

			Если МенеджерХранилища.ДатаПоследнейОчистки > Дата(1,1,1) Тогда
				ДатаОчисткиХранилища = МенеджерХранилища.ДатаПоследнейОчистки;
			КонецЕсли;			

			АдресХранилища	= МенеджерХранилища.ПараметрыПодключения.URL;

		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Управление хранилищем двоичных данных'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			НСтр("ru = 'Ошибка получения параметров внешнего хранилища'", ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецПопытки;
			
    	ДобавитьХранилищеВТаблицу(ИмяХранилища, АдресХранилища, РежимЧтенияЗаписиХДД, ХранилищеПоУмолчанию, МинимальныйРазмерДанных, ДатаОчисткиХранилища);

	КонецЦикла;	                 

	ДоступностьКомандРедактирования = ХранилищаДвоичныхДанных.Количество() > 0;

	Элементы.РедактироватьХранилище.Доступность = ДоступностьКомандРедактирования;
	Элементы.УдалитьХранилище.Доступность		= ДоступностьКомандРедактирования;

КонецПроцедуры

&НаКлиенте
Процедура УдалитьХранилищеДвоичныхДанных(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;	
	
	ТекущиеДанныеСтроки = Элементы.ХранилищаДвоичныхДанных.ТекущиеДанные;
	
	Если ТекущиеДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьХранилищеНаСервере(ТекущиеДанныеСтроки.АдресХранилища, ТекущиеДанныеСтроки.ИмяХранилища);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьХранилищеНаСервере(Знач АдресХранилища, Знач ИмяХранилища)

	Если АдресХранилища = "" Тогда 
		РежимИспользованияHDD = РежимИспользованияХранилищаДвоичныхДанных.Запретить;
		ХранилищеДвоичныхДанных.УстановитьРежимИспользованияХранилищаДвоичныхДанных(РежимИспользованияHDD);	
	Иначе    
		МенеджерХранилища = ВнешниеХранилищаДвоичныхДанных[ИмяХранилища];
		ВнешниеХранилищаДвоичныхДанных.Удалить(МенеджерХранилища);
	КонецЕсли;

	ОбновитьПредставление();
КонецПроцедуры

&НаСервере
Процедура ПроверитьДоступностьОбработки()

	ТекстИсключения = "";

	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ТекстИсключения = НСтр("ru = 'Обработка не предназначена для использования в файловой информационной базе.'");
	ИначеЕсли Не РаботаСФайловымАрхивомСервер.ДоступныИзмененияВСпособахХраненияФайлов() Тогда
		ТекстИсключения = НСтр("ru = 'Обработка не предназначена для использования на версиях платформы 1С:Предприятие ниже 8.3.26.'");
	ИначеЕсли ОбщегоНазначения.РазделениеВключено() Тогда
		ПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
		Если ПользовательИБ.РазделениеДанных.Количество() <> 0 Тогда
			ТекстИсключения = НСтр("ru = 'Обработка не предназначена для использования в разделенном сеансе.'");
		КонецЕсли;
	КонецЕсли;

	Если ТекстИсключения <> "" Тогда
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти