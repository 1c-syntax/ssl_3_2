///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не Параметры.ОткрытаПоСценарию Тогда
		ВызватьИсключение НСтр("ru = 'Форма не предназначена для непосредственного использования.'");
	КонецЕсли;
	
	ОбновитьНастройки();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)

	Если ТекущаяСтраница.Имя = "СтраницаНастройки" Тогда
		ОбновитьНастройки();
	ИначеЕсли ТекущаяСтраница.Имя = "СтраницаИнформацияОХранилище" Тогда
		ОбновитьИнформациюНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РазрешитьЗаписьПриИзменении(Элемент)

	Элементы.МинимальныйРазмерЗаписываемыхДанных.Доступность = РазрешитьЗапись;	

	Если РазрешитьЗапись Тогда

		МинимальныйРазмерЗаписываемыхДанных = МинимальныйРазмерЗаписываемыхДанныхНаСервере();

		ДоступностьКнопкиОК();
	Иначе

		МинимальныйРазмерЗаписываемыхДанных = 0;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура МинимальныйРазмерЗаписываемыхДанныхПриИзменении(Элемент)

	ДоступностьКнопкиОК();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьИнформацию(Команда)

    ОбновитьИнформациюНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьХранилище(Команда)

	Если ДатаОчистки = Дата(1, 1, 1) Тогда
		ВызватьИсключение НСтр("ru = 'Для запуска очистки хранилища двоичных данных нужно задать непустую дату очистки'");	
	КонецЕсли;

	Состояние(НСтр("ru = 'Подождите'"), , СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Идет очистка на дату %1 ...'"), ДатаОчистки));

	ОчиститьХранилищеПоДатеОчистки(ДатаОчистки); 

	Состояние("", , НСтр("ru = 'Очистка завершена'"));

КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)

	Попытка

		ИзменитьНастройкиХранилищаНаСервере();

	Исключение
		ОбновитьНастройки();
		ВызватьИсключение;
	КонецПопытки;

	Закрыть(Истина);

КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)

	Закрыть(Ложь);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДоступностьКнопкиОК()

	ДоступнаКнопкаОК = МинимальныйРазмерЗаписываемыхДанных >= 2048;

	Элементы.КнопкаОК.Доступность = ДоступнаКнопкаОК;

КонецПроцедуры

&НаСервере                 
Процедура ОбновитьНастройки()

	МинимальныйРазмерЗаписываемыхДанных	= ХранилищеДвоичныхДанных.ПолучитьМинимальныйРазмерЗаписываемыхДанных();
	
	РежимЧтенияЗаписи = ХранилищеДвоичныхДанных.ПолучитьРежимЧтенияЗаписиХранилищаДвоичныхДанных();
	ДоступныЧтениеИЗапись = РежимЧтенияЗаписи = РежимЧтенияЗаписиХранилищаДвоичныхДанных.ЧтениеИЗапись;

	Если ДоступныЧтениеИЗапись Тогда 

		РазрешитьЗапись = Истина;

	Иначе
		РазрешитьЗапись			= Ложь;		
	КонецЕсли;

	Элементы.МинимальныйРазмерЗаписываемыхДанных.Доступность = ДоступныЧтениеИЗапись;	

КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюНаСервере()

	ИнформацияОХранилище = ХранилищеДвоичныхДанных.ПолучитьИнформацию();

	ОбъемХранимыхДанных						= ИнформацияОХранилище.ОбъемХранимыхДанных;
	ОбъемУдаленныхДанных					= ИнформацияОХранилище.ОбъемУдаленныхДанных;
    КоличествоХранимыхЭлементов				= ИнформацияОХранилище.КоличествоХранимыхЭлементов;
	КоличествоУдаленныхЭлементов			= ИнформацияОХранилище.КоличествоУдаленныхЭлементов;
	ОбъемХранимыхДанныхНаДиске				= ИнформацияОХранилище.ОбъемХранимыхДанныхНаДиске;
	КоличествоДедуплицированныхЭлементов	= ИнформацияОХранилище.КоличествоДедуплицированныхЭлементов;
	ДатаПоследнейОчистки					= МестноеВремя(ИнформацияОХранилище.ДатаПоследнейОчистки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОчиститьХранилищеПоДатеОчистки(Знач ДатаОчистки)

	ХранилищеДвоичныхДанных.ОчиститьНеиспользуемоеМестоПоУниверсальнойДате(УниверсальноеВремя(ДатаОчистки));

КонецПроцедуры

&НаСервереБезКонтекста
Функция МинимальныйРазмерЗаписываемыхДанныхНаСервере()

	Возврат ХранилищеДвоичныхДанных.ПолучитьМинимальныйРазмерЗаписываемыхДанных();

КонецФункции

&НаСервере
Процедура ИзменитьНастройкиХранилищаНаСервере()

	Если РазрешитьЗапись Тогда
		ХранилищеДвоичныхДанных.УстановитьРежимЧтенияЗаписиХранилищаДвоичныхДанных(РежимЧтенияЗаписиХранилищаДвоичныхДанных.ЧтениеИЗапись);
	Иначе 
		ХранилищеДвоичныхДанных.УстановитьРежимЧтенияЗаписиХранилищаДвоичныхДанных(РежимЧтенияЗаписиХранилищаДвоичныхДанных.ТолькоЧтение);
	КонецЕсли;

	ХранилищеДвоичныхДанных.УстановитьМинимальныйРазмерЗаписываемыхДанных(МинимальныйРазмерЗаписываемыхДанных);

КонецПроцедуры

#КонецОбласти