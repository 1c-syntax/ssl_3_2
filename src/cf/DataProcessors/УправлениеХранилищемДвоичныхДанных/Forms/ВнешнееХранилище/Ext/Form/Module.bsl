///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.ОткрытаПоСценарию Тогда
		ВызватьИсключение НСтр("ru = 'Форма не предназначена для непосредственного использования.'");
	КонецЕсли;

	ИмяХранилища = Параметры.ИмяХранилища;

	ВнешнееХДД = ВнешниеХранилищаДвоичныхДанных.Найти(ИмяХранилища);
	Если ВнешнееХДД = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	ЗаполнитьРеквизитыФормы(ВнешнееХДД);

	Элементы.МинимальныйРазмерЗаписываемыхДанных.Доступность = РазрешитьЗапись;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РазрешитьЗаписьПриИзменении(Элемент)
	
	Элементы.МинимальныйРазмерЗаписываемыхДанных.Доступность = РазрешитьЗапись;	

	Если РазрешитьЗапись Тогда     
		МинимальныйРазмерЗаписываемыхДанных = ЗаписываемыхДанныхМинимальныйРазмер(ИмяХранилища);
	Иначе                    
		МинимальныйРазмерЗаписываемыхДанных = 0;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Очистить(Команда)

	ОчиститьНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)

	ЗаписатьИзмененияВХранилище();
	Закрыть(Истина);

КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)

	Закрыть(Ложь);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЗаписываемыхДанныхМинимальныйРазмер(Знач ИмяХранилища)

	ВнешнееХДД = ВнешниеХранилищаДвоичныхДанных[ИмяХранилища];
	Возврат ВнешнееХДД.МинимальныйРазмерЗаписываемыхДанных;

КонецФункции

&НаСервере
Процедура ЗаполнитьРеквизитыФормы(ВнешнееХДД)

	Имя						= ВнешнееХДД.Имя;
	ИдентификаторДоступа	= ВнешнееХДД.ПараметрыДоступа.ИдентификаторДоступа;
	СекретныйКлюч			= ВнешнееХДД.ПараметрыДоступа.СекретныйКлюч; 
	Регион					= ВнешнееХДД.ПараметрыДоступа.Регион;

	РазрешитьЗапись = ВнешнееХДД.РежимЧтенияЗаписи = РежимЧтенияЗаписиХранилищаДвоичныхДанных.ЧтениеИЗапись;

	Если РазрешитьЗапись Тогда

		МинимальныйРазмерЗаписываемыхДанных = ВнешнееХДД.МинимальныйРазмерЗаписываемыхДанных ;

	Иначе

		МинимальныйРазмерЗаписываемыхДанных = 0;

	КонецЕсли;

	Если ВнешнееХДД.ДатаПоследнейОчистки > Дата(1, 1, 1) Тогда
		ДатаПоследнейОчистки = ВнешнееХДД.ДатаПоследнейОчистки;
	Иначе
		ДатаПоследнейОчистки = Неопределено;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНаСервере()

	ВнешнееХДД = ВнешниеХранилищаДвоичныхДанных[ИмяХранилища];
	ВнешнееХДД.ОчиститьНеиспользуемоеМестоПоУниверсальнойДате(УниверсальноеВремя(ДатаОчистки));
	Если ВнешнееХДД.ДатаПоследнейОчистки > Дата(1, 1, 1) Тогда
		ДатаПоследнейОчистки = ВнешнееХДД.ДатаПоследнейОчистки;
	Иначе
		ДатаПоследнейОчистки = Неопределено;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияВХранилище()

	ПараметрыДоступаВХДД = Новый ПараметрыДоступаВнешнегоХранилищаДвоичныхДанных();

	ПараметрыДоступаВХДД.ИдентификаторДоступа	= ИдентификаторДоступа;
	ПараметрыДоступаВХДД.СекретныйКлюч			= СекретныйКлюч;
	ПараметрыДоступаВХДД.Регион					= Регион;
	
	ВнешнееХДД = ВнешниеХранилищаДвоичныхДанных[ИмяХранилища];	
	ВнешнееХДД.ПараметрыДоступа		= ПараметрыДоступаВХДД;
	ВнешнееХДД.Имя					= Имя;

	Если РазрешитьЗапись Тогда
		ВнешнееХДД.РежимЧтенияЗаписи = РежимЧтенияЗаписиХранилищаДвоичныхДанных.ЧтениеИЗапись;
		ВнешнееХДД.МинимальныйРазмерЗаписываемыхДанных = МинимальныйРазмерЗаписываемыхДанных;
	Иначе
		ВнешнееХДД.РежимЧтенияЗаписи = РежимЧтенияЗаписиХранилищаДвоичныхДанных.ТолькоЧтение;
	КонецЕсли;
	
	ВнешнееХДД.Записать();

КонецПроцедуры

#КонецОбласти