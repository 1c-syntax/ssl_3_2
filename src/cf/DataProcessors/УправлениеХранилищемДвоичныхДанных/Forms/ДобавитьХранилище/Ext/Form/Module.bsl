///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не Параметры.ОткрытаПоСценарию Тогда
		ВызватьИсключение НСтр("ru = 'Форма не предназначена для непосредственного использования.'");
	КонецЕсли;	
	
	Если Не ДоступностьВстроенногоХранилищаДвоичныхДанных() Тогда
		Элементы.ТипХранилища.СписокВыбора.Добавить(0, НСтр("ru = 'Встроенное хранилище'", ОбщегоНазначения.КодОсновногоЯзыка()));
		ТипХранилища = 0;
	Иначе
		ТипХранилища = 1;
	КонецЕсли;
	Элементы.ТипХранилища.СписокВыбора.Добавить(1, НСтр("ru = 'Внешнее хранилище по протоколу S3'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Элементы.ТипХранилища.СписокВыбора.Добавить(2, НСтр("ru = 'Внешнее хранилище по протоколу S3 (виртуальный хост)'", ОбщегоНазначения.КодОсновногоЯзыка()));

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновитьФорму();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипХранилищаПриИзменении(Элемент)
    ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ИмяХранилищаПриИзменении(Элемент)
	ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура АдресПодключенияПриИзменении(Элемент)
	ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторДоступаПриИзменении(Элемент)
	ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура СекретныйКлючПриИзменении(Элемент)
	ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура РегионПриИзменении(Элемент)
	ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьЗаписьПриИзменении(Элемент)

	Элементы.МинимальныйРазмерЗаписываемыхДанных.Доступность = РазрешитьЗапись;	

	Если РазрешитьЗапись Тогда
		МинимальныйРазмерЗаписываемыхДанных = 2048;
	Иначе                    
		МинимальныйРазмерЗаписываемыхДанных = 0;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура МинимальныйРазмерЗаписываемыхДанныхПриИзменении(Элемент)
	ОбновитьФорму();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)

    ДобавитьХранилище();
	Закрыть(Истина);

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)

	Закрыть(Ложь);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьХранилище()

	// Встроенное хранилище
	Если ТипХранилища = 0 Тогда

		ХранилищеДвоичныхДанных.УстановитьРежимИспользованияХранилищаДвоичныхДанных(РежимИспользованияХранилищаДвоичныхДанных.Разрешить);

		Если РазрешитьЗапись Тогда
			ХранилищеДвоичныхДанных.УстановитьРежимЧтенияЗаписиХранилищаДвоичныхДанных(РежимЧтенияЗаписиХранилищаДвоичныхДанных.ЧтениеИЗапись);
		Иначе 
			ХранилищеДвоичныхДанных.УстановитьРежимЧтенияЗаписиХранилищаДвоичныхДанных(РежимЧтенияЗаписиХранилищаДвоичныхДанных.ТолькоЧтение);
		КонецЕсли;		

		Возврат;
	КонецЕсли;

	// Внешнее хранилище
	// Параметры подключения
	ПараметрыПодключения = Новый ПараметрыПодключенияВнешнегоХранилищаДвоичныхДанных();
	ПараметрыПодключения.URL = АдресПодключения;
	
	// АПК:216-выкл Значение системного перечисления содержат кириллицу и латиницу.
	Если ТипХранилища = 1 Тогда                                      
		ПараметрыПодключения.ТипURL = ТипURLВнешнегоХранилищаДвоичныхДанных.ХостS3СБакетомВСоставеПути ;
	ИначеЕсли ТипХранилища = 2 Тогда
		ПараметрыПодключения.ТипURL = ТипURLВнешнегоХранилищаДвоичныхДанных.ВиртуальныйХостS3;
	КонецЕсли;
	// АПК:216-вкл

	// Параметры доступа
	ПараметрыДоступаВХДД = Новый ПараметрыДоступаВнешнегоХранилищаДвоичныхДанных();

	ПараметрыДоступаВХДД.ИдентификаторДоступа	= ИдентификаторДоступа;
	ПараметрыДоступаВХДД.СекретныйКлюч			= СекретныйКлюч;
	ПараметрыДоступаВХДД.Регион					= Регион;

	ВнешнееХДД = ВнешниеХранилищаДвоичныхДанных.Добавить(ИмяХранилища, ПараметрыПодключения, ПараметрыДоступаВХДД);
	
	Если РазрешитьЗапись Тогда
		ВнешнееХДД.РежимЧтенияЗаписи = РежимЧтенияЗаписиХранилищаДвоичныхДанных.ЧтениеИЗапись;
		ВнешнееХДД.МинимальныйРазмерЗаписываемыхДанных = МинимальныйРазмерЗаписываемыхДанных;
		
		ВнешнееХДД.Записать();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ДоступностьВстроенногоХранилищаДвоичныхДанных()

	РежимИспользованияХДД = ХранилищеДвоичныхДанных.ПолучитьРежимИспользованияХранилищаДвоичныхДанных();
	Результат = РежимИспользованияХДД = РежимИспользованияХранилищаДвоичныхДанных.Разрешить;

	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ОбновитьФорму()

	ЭтоВнешнееХДД = ТипХранилища <> 0;

	Элементы.ИмяХранилища			.Доступность = ЭтоВнешнееХДД;
	Элементы.АдресПодключения		.Доступность = ЭтоВнешнееХДД;
	Элементы.ИдентификаторДоступа	.Доступность = ЭтоВнешнееХДД;
	Элементы.СекретныйКлюч			.Доступность = ЭтоВнешнееХДД;
	Элементы.Регион					.Доступность = ЭтоВнешнееХДД;	

	КнопкаОКДоступность = Истина;

	Если ЭтоВнешнееХДД Тогда
		Если ИмяХранилища = "" Или АдресПодключения = "" Или ИдентификаторДоступа = "" Или СекретныйКлюч = "" Или Регион = "" Тогда
			КнопкаОКДоступность = Ложь;
		КонецЕсли;	
	КонецЕсли;

	Если РазрешитьЗапись И КнопкаОКДоступность Тогда
		КнопкаОКДоступность = МинимальныйРазмерЗаписываемыхДанных >= 2048;
	КонецЕсли;	
	
	Элементы.КнопкаОК.Доступность = КнопкаОКДоступность;

КонецПроцедуры

#КонецОбласти