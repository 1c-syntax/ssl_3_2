///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПравоДоступа("Администрирование", Метаданные) Тогда
		
		ВызватьИсключение НСтр("ru = 'Использование обработки в интерактивном режиме доступно только администратору.'");
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры); // АрхивироватьФайл, ПарольДляСжатияФайлаОбмена
	
	ИмяФайлаПравил = Параметры.ИмяФайлаПравил;
	ИмяФайлаДанных = Параметры.ИмяФайлаДанных;
	ЭтоКлиент = Параметры.ЭтоКлиент;
	
	ВыгрузкаДанныхВФайл = Параметры.ВыгрузкаДанныхВФайл;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтоКлиент Тогда
		
		Элементы.ИмяФайлаДанных.Видимость = ЛОЖЬ;
		Элементы.ДекорацияПредупреждениеНаКлиенте.Видимость = ИСТИНА;
		
	Иначе
		
		Элементы.ИмяФайлаДанных.Видимость = ИСТИНА;
		Элементы.ДекорацияПредупреждениеНаКлиенте.Видимость = ЛОЖЬ;
		
		Элементы.ИмяФайлаДанных.СписокВыбора.ЗагрузитьЗначения(ВыгрузкаДанныхВФайл.ВыгрузитьЗначения());
		
	КонецЕсли;
	
	АрхивироватьФайлПриИзменении(Элементы.АрхивироватьФайл);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АрхивироватьФайлПриИзменении(Элемент)
	
	АрхивироватьФайлПриИзмененииЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаДанныхПриИзменении(Элемент)
	
	Если ПустоеЗначениеРеквизита(ИмяФайлаДанных, "ИмяФайлаДанных", Элементы.ИмяФайлаДанных.Заголовок)
		Или ИменаФайловПравилИОбменаСовпадают() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Объект.ИмяФайлаОбмена = ИмяФайлаДанных;
	
	Файл = Новый Файл(Объект.ИмяФайлаОбмена);
	Объект.АрхивироватьФайл = (ВРЕГ(Файл.Расширение) = ВРЕГ(".zip"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаДанныхНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборФайла(Элемент, Ложь, "xml");
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаДанныхОткрытие(Элемент, СтандартнаяОбработка)
	
	ОткрытьВПриложении(Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольДляСжатияФайлаОбменаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Элемент.РежимПароля = Не Элемент.РежимПароля;
	
	Если Элемент.РежимПароля Тогда
		Элемент.КартинкаКнопкиВыбора = БиблиотекаКартинок.ПоказатьПароль;
	Иначе
		Элемент.КартинкаКнопкиВыбора = БиблиотекаКартинок.СкрытьПароль;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если НЕ ПустоеЗначениеРеквизита(ИмяФайлаДанных, "ИмяФайлаДанных", Элементы.ИмяФайлаДанных.Заголовок) Тогда
		
		ДобавитьСтрокуКСпискуВыбора(Элементы.ИмяФайлаДанных.СписокВыбора, ИмяФайлаДанных, ВыгрузкаДанныхВФайл);
		
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ИмяФайлаПравил", ИмяФайлаПравил);
	ПараметрыЗакрытия.Вставить("ИмяФайлаДанных", ИмяФайлаДанных);
	ПараметрыЗакрытия.Вставить("АрхивироватьФайл", Объект.АрхивироватьФайл);
	ПараметрыЗакрытия.Вставить("ПарольДляСжатияФайлаОбмена", Объект.ПарольДляСжатияФайлаОбмена);
	ПараметрыЗакрытия.Вставить("ВыгрузкаДанныхВФайл", ВыгрузкаДанныхВФайл);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ИменаФайловПравилИОбменаСовпадают()
	
	Если ВРег(СокрЛП(ИмяФайлаПравил)) = ВРег(СокрЛП(ИмяФайлаДанных)) Тогда
		
		СообщитьПользователю(НСтр("ru = 'Файл правил обмена не может совпадать с файлом данных.
		|Выберите другой файл для выгрузки данных.'"));
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ПустоеЗначениеРеквизита(Реквизит, ПутьКДанным, Заголовок)
	
	Если ПустаяСтрока(Реквизит) Тогда
		
		ТекстСообщения = НСтр("ru = 'Поле ""%1"" не заполнено'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", Заголовок);
		
		СообщитьПользователю(ТекстСообщения, ПутьКДанным);
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура АрхивироватьФайлПриИзмененииЗначения()
	
	Если Объект.АрхивироватьФайл Тогда
		
		ИмяФайлаДанных = СтрЗаменить(ИмяФайлаДанных, ".xml", ".zip");
		
	Иначе
		
		ИмяФайлаДанных = СтрЗаменить(ИмяФайлаДанных, ".zip", ".xml");
		
	КонецЕсли;
	
	Элементы.ПарольДляСжатияФайлаОбмена.Доступность = Объект.АрхивироватьФайл;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайла(Элемент, ПроверятьСуществование, РасширениеПоУмолчанию)
	
	ИмяСвойства = "ИмяФайлаДанных";
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	Если Объект.АрхивироватьФайл Тогда	
		ФильтрДиалогаВыбора = "Архивный файл данных (*.zip)|*.zip";
		РасширениеДиалогаВыбора = "zip";	
	Иначе	
		ФильтрДиалогаВыбора = "Файл данных (*.xml)|*.xml";
		РасширениеДиалогаВыбора = "xml";	
	КонецЕсли;
	
	ДиалогВыбораФайла.Фильтр = ФильтрДиалогаВыбора;
	ДиалогВыбораФайла.Расширение = РасширениеДиалогаВыбора;
	ДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выберите файл'");
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла = Элемент.ТекстРедактирования;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = ПроверятьСуществование;
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяСвойства", ИмяСвойства);
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	
	Оповещение = Новый ОписаниеОповещения("ДиалогВыбораФайлаОбработкаВыбора", ЭтотОбъект, ДополнительныеПараметры);
	ДиалогВыбораФайла.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокуКСпискуВыбора(СписокСохраняемыхЗначений, ЗначениеСохранения, ИмяПараметраДляСохранения)
	
	Если ПустаяСтрока(ЗначениеСохранения) Тогда
		Возврат;
	КонецЕсли;
	
	НайденныйЭлемент = СписокСохраняемыхЗначений.НайтиПоЗначению(ЗначениеСохранения);
	Если НайденныйЭлемент <> Неопределено Тогда
		СписокСохраняемыхЗначений.Удалить(НайденныйЭлемент);
	КонецЕсли;
	
	СписокСохраняемыхЗначений.Вставить(0, ЗначениеСохранения);
	
	Пока СписокСохраняемыхЗначений.Количество() > 10 Цикл
		СписокСохраняемыхЗначений.Удалить(СписокСохраняемыхЗначений.Количество() - 1);
	КонецЦикла;
	
	ИмяПараметраДляСохранения = СписокСохраняемыхЗначений;
	
КонецПроцедуры

// Параметры:
//   ВыбранныйФайл - Структура - результат выбора файла.
//   ДополнительныеПараметры - Структура - произвольные дополнительные параметры:
//     * ОбъектХранения - Структура
//                      - ФормаКлиентскогоПриложения - приемник для хранения свойства.
//     * ИмяСвойства - Строка - имя свойства объекта хранения.
//     * Элемент - ПолеФормы - источник события выбора файла.
//
&НаКлиенте
Процедура ДиалогВыбораФайлаОбработкаВыбора(ВыбранныйФайл, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйФайл = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайлаДанных = ВыбранныйФайл[0];
	ИмяФайлаДанныхПриИзменении(Элементы.ИмяФайлаДанных);
	
КонецПроцедуры

// Открывает файл обмена во внешнем приложении.
//
// Параметры:
// ИмяФайла - Строка - имя открываемого файла.
// СтандартнаяОбработка - Булево - признак необходимости использовать стандартный механизм.
//
&НаКлиенте
Процедура ОткрытьВПриложении(ИмяФайла, СтандартнаяОбработка = Ложь)
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ИмяФайла", ИмяФайла);
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", Новый ОписаниеОповещения);
	
	Файл = Новый Файл(ИмяФайла);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОпределенияСуществованияФайла", ЭтотОбъект, ДополнительныеПараметры);
	Файл.НачатьПроверкуСуществования(ОписаниеОповещения);
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
//
// Параметры:
//   Существует - Булево
//   ДополнительныеПараметры - Структура:
//      * ИмяФайла - Строка
//      * ОписаниеОповещения - ОписаниеОповещения
//
&НаКлиенте
Процедура ПослеОпределенияСуществованияФайла(Существует, ДополнительныеПараметры) Экспорт
	
	Если Существует Тогда
		НачатьЗапускПриложения(ДополнительныеПараметры.ОписаниеОповещения, ДополнительныеПараметры.ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СообщитьПользователю(Текст, ПутьКДанным = "")
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.ПутьКДанным = ПутьКДанным;
	Сообщение.Сообщить();
	
КонецПроцедуры

#КонецОбласти