///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	МножественныйВыбор = Ложь;
	
	ПрочитатьДеревоУзловОбмена();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ТекПараметры = УстановитьПараметрыФормы();
	РазвернутьУзлы(ТекПараметры.Отмеченные);
	Элементы.ДеревоУзловОбмена.ТекущаяСтрока = ТекПараметры.ТекущаяСтрока;
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()
	ТекПараметры = УстановитьПараметрыФормы();
	РазвернутьУзлы(ТекПараметры.Отмеченные);
	Элементы.ДеревоУзловОбмена.ТекущаяСтрока = ТекПараметры.ТекущаяСтрока;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоУзловОбмена
//

&НаКлиенте
Процедура ДеревоУзловОбменаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПроизвестиВыборУзлов(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоУзловОбменаПометкаПриИзменении(Элемент)
	ИзменениеПометки(Элементы.ДеревоУзловОбмена.ТекущаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Выбирает узел и передает выбранные значения в вызывающую форму.
&НаКлиенте
Процедура ВыбратьУзел(Команда)
	ПроизвестиВыборУзлов(МножественныйВыбор);
КонецПроцедуры

// Открывает форму узла, заданную в конфигурации.
&НаКлиенте
Процедура ИзменитьУзел(Команда)
	КлючСсылка = Элементы.ДеревоУзловОбмена.ТекущиеДанные.Ссылка;
	Если КлючСсылка <> Неопределено Тогда
		ОткрытьФорму(ПолучитьИмяФормы(КлючСсылка) + "ФормаОбъекта", Новый Структура("Ключ", КлючСсылка));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПредопределенные(Команда)
	
	ПоказыватьПредопределенные = НЕ ПоказыватьПредопределенные;
	Элементы.ФормаПоказыватьПредопределенные.Пометка = ПоказыватьПредопределенные;
	
	ПрочитатьДеревоУзловОбмена();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоУзловОбменаКод.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоУзловОбмена.Ссылка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);

КонецПроцедуры

&НаКлиенте
Процедура РазвернутьУзлы(Отмеченные) 
	Если Отмеченные <> Неопределено Тогда
		Для Каждого ТекИд Из Отмеченные Цикл
			ТекСтрока = ДеревоУзловОбмена.НайтиПоИдентификатору(ТекИд);
			ТекРодитель = ТекСтрока.ПолучитьРодителя();
			Если ТекРодитель <> Неопределено Тогда
				Элементы.ДеревоУзловОбмена.Развернуть(ТекРодитель.ПолучитьИдентификатор());
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры	

&НаКлиенте
Процедура ПроизвестиВыборУзлов(ЭтоМножественныйВыбор)
	
	Если ЭтоМножественныйВыбор Тогда
		Данные = ВыбранныеУзлы();
		Если Данные.Количество() > 0 Тогда
			ОповеститьОВыборе(Данные);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Данные = Элементы.ДеревоУзловОбмена.ТекущиеДанные;
	Если Данные <> Неопределено И Данные.Ссылка <> Неопределено Тогда
		ОповеститьОВыборе(Данные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыбранныеУзлы(Знач НовыеДанные = Неопределено)
	
	Если НовыеДанные <> Неопределено Тогда
		// Установка
		Отмеченные = Новый Массив;
		ВнутрУстановитьВыбранныеУзлы(ЭтотОбъект(), ДеревоУзловОбмена, НовыеДанные, Отмеченные);
		Возврат Отмеченные;
	КонецЕсли;
	
	// Получение
	Результат = Новый Массив;
	Для Каждого ТекПлан Из ДеревоУзловОбмена.ПолучитьЭлементы() Цикл
		Для Каждого ТекСтрока Из ТекПлан.ПолучитьЭлементы() Цикл
			Если ТекСтрока.Пометка И ТекСтрока.Ссылка <> Неопределено Тогда
				Результат.Добавить(ТекСтрока.Ссылка);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаСервере
Процедура ВнутрУстановитьВыбранныеУзлы(ТекущийОбъект, Данные, НовыеДанные, Отмеченные)
	Для Каждого ТекСтрока Из Данные.ПолучитьЭлементы() Цикл
		Если НовыеДанные.Найти(ТекСтрока.Ссылка) <> Неопределено Тогда
			ТекСтрока.Пометка = Истина;
			ТекущийОбъект.ПроставитьПометкиВверх(ТекСтрока);
			Отмеченные.Добавить(ТекСтрока.ПолучитьИдентификатор());
		КонецЕсли;
		ВнутрУстановитьВыбранныеУзлы(ТекущийОбъект, ТекСтрока, НовыеДанные, Отмеченные);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ЭтотОбъект(ТекущийОбъект = Неопределено) 
	Если ТекущийОбъект = Неопределено Тогда
		Возврат РеквизитФормыВЗначение("Объект");
	КонецЕсли;
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	Возврат Неопределено;
КонецФункции

&НаСервере
Функция ПолучитьИмяФормы(Знач ТекущийОбъект = Неопределено)
	Возврат ЭтотОбъект().ПолучитьИмяФормы(ТекущийОбъект);
КонецФункции	

&НаСервере
Процедура ПрочитатьДеревоУзловОбмена()
	
	Дерево = СформироватьДеревоУзлов();
	ЗначениеВРеквизитФормы(Дерево, "ДеревоУзловОбмена");
	
КонецПроцедуры

&НаСервере
Процедура ИзменениеПометки(Знач СтрокаДанных)
	ЭлементДанных = ДеревоУзловОбмена.НайтиПоИдентификатору(СтрокаДанных);
	ЭтотОбъект().ИзменениеПометки(ЭлементДанных);
КонецПроцедуры

&НаСервере
Функция УстановитьПараметрыФормы()
	
	Результат = Новый Структура("ТекущаяСтрока, Отмеченные");
	
	// Множественный выбор
	Элементы.ДеревоУзловОбменаПометка.Видимость = Параметры.МножественныйВыбор;
	// Сбрасываем пометки только если выбор изменился.
	Если Параметры.МножественныйВыбор <> МножественныйВыбор Тогда
		ТекущийОбъект = ЭтотОбъект();
		Для Каждого ТекСтрока Из ДеревоУзловОбмена.ПолучитьЭлементы() Цикл
			ТекСтрока.Пометка = Ложь;
			ТекущийОбъект.ПроставитьПометкиВниз(ТекСтрока);
		КонецЦикла;
	КонецЕсли;
	МножественныйВыбор = Параметры.МножественныйВыбор;
	
	// Позиционирование
	Если МножественныйВыбор И ТипЗнч(Параметры.НачальноеЗначениеВыбора) = Тип("Массив") Тогда 
		Отмеченные = ВыбранныеУзлы(Параметры.НачальноеЗначениеВыбора);
		Результат.Отмеченные = Отмеченные;
		Если Отмеченные.Количество() > 0 Тогда
			Результат.ТекущаяСтрока = Отмеченные[0];
		КонецЕсли;
			
	ИначеЕсли Параметры.НачальноеЗначениеВыбора <> Неопределено Тогда
		// Одиночный вариант
		Результат.ТекущаяСтрока = ИдентификаторСтрокиПоУзлу(ДеревоУзловОбмена, Параметры.НачальноеЗначениеВыбора);
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция ИдентификаторСтрокиПоУзлу(Данные, Ссылка)
	Для Каждого ТекСтрока Из Данные.ПолучитьЭлементы() Цикл
		Если ТекСтрока.Ссылка = Ссылка Тогда
			Возврат ТекСтрока.ПолучитьИдентификатор();
		КонецЕсли;
		Результат = ИдентификаторСтрокиПоУзлу(ТекСтрока, Ссылка);
		Если Результат <> Неопределено Тогда 
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

#Область ЗаполнениеДерева

// Возвращает дерево значений, заполненное данными для выбора узла обмена. В дереве 2 уровня: 
// план обмена -> узлы обмена. Служебные узлы выброшены. 
//
// Параметры:
//    ОбъектДанных - ЛюбаяСсылка
//                 - Структура - ссылка или структура со значениями измерений набор записей. Для этих
//                   данных  которых  анализируются узлы обмена. Если не указано, то для всех.
//    ИмяТаблицы   - Строка - если ОбъектДанных - структура, то имя таблицы для набора записей.
//
// Возвращаемое значение:
//    ДеревоЗначений:
//        * Наименование                  - Строка - представление плана обмена или узла обмена.
//        * ИндексКартинки                - Число  - 1 = план обмена, 2 = узел , 3 = помеченный для удаления узел.
//        * ИндексКартинкиАвторегистрация - Число  - если не указан параметр ОбъектДанных, то Неопределено.
//                                                   Иначе: 0 = нет, 1 = запрещена, 2 = разрешена, Неопределено для
//                                                   плана обмена.
//        * ПланОбменаИмя                 - Строка - имя плана обмена узла.
//        * Ссылка                        - ПланОбменаСсылка - ссылка узла, Неопределено для плана обмена.
//        * Код                           - Число
//                                        - Строка - код узла, Неопределено для плана обмена.
//        * НомерОтправленного            - Число - данные узла.
//        * НомерПринятого                - Число - данные узла.
//        * НомерСообщения                - Число
//                                        - Null - если указан объект, то номер сообщения для него, иначе NULL.
//        * НеВыгружалось                 - Булево
//                                        - Null - если указан объект, то флаг выгрузки, иначе NULL.
//        * Пометка                       - Булево       - если указан объект, то 0 = нет регистрации, 1 - есть, иначе
//                                                         всегда 0.
//        * ИсходнаяПометка               - Булево       - аналогично колонке "Пометка".
//        * ИдентификаторСтроки           - Число        - индекс добавленной строки (обход дерева сверху вниз слева
//                                                         направо).
//
&НаСервере
Функция СформироватьДеревоУзлов(ОбъектДанных = Неопределено, ИмяТаблицы = Неопределено)
	
	Дерево = Новый ДеревоЗначений; // см. СформироватьДеревоУзлов
	Колонки = Дерево.Колонки;
	Строки  = Дерево.Строки;
	
	Колонки.Добавить("Наименование");
	Колонки.Добавить("ИндексКартинки");
	Колонки.Добавить("ИндексКартинкиАвторегистрация");
	Колонки.Добавить("ПланОбменаИмя");
	Колонки.Добавить("Ссылка");
	Колонки.Добавить("Код");
	Колонки.Добавить("НомерОтправленного");
	Колонки.Добавить("НомерПринятого");
	Колонки.Добавить("НомерСообщения");
	Колонки.Добавить("НеВыгружалось");
	Колонки.Добавить("Пометка");
	Колонки.Добавить("ИсходнаяПометка");
	Колонки.Добавить("ИдентификаторСтроки");
	
	ТекстПодзапроса = "";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПоказыватьПредопределенные", ПоказыватьПредопределенные);
	
	Если ОбъектДанных = Неопределено Тогда
		
		МетаОбъект = Неопределено;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПланОбмена.ЭтотУзел КАК ЭтотУзел,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПланОбмена.Ссылка) КАК Наименование,
		|	ВЫБОР
		|		КОГДА ПланОбмена.ПометкаУдаления
		|			ТОГДА 2
		|		КОГДА ПланОбмена.ЭтотУзел
		|			ТОГДА 3
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ИндексКартинки,
		|	""&ТекстовоеПредставлениеПланаОбмена"" КАК ПланОбменаИмя,
		|	ПланОбмена.Код КАК Код,
		|	ПланОбмена.Ссылка КАК Ссылка,
		|	ПланОбмена.НомерОтправленного КАК НомерОтправленного,
		|	ПланОбмена.НомерПринятого КАК НомерПринятого,
		|	NULL КАК НомерСообщения,
		|	NULL КАК НеВыгружалось,
		|	0 КАК КоличествоИзмененийНаУзле
		|ИЗ
		|	&ИмяТаблицыПланаОбмена КАК ПланОбмена
		|ГДЕ
		|	(НЕ ПланОбмена.ЭтотУзел
		|			ИЛИ &ПоказыватьПредопределенные)";
		
	Иначе
		
		Если ТипЗнч(ОбъектДанных) = Тип("Структура") Тогда
			
			Для Каждого КлючЗначение Из ОбъектДанных Цикл
				
				ТекИмя = КлючЗначение.Ключ;
				ТекстПодзапроса = ТекстПодзапроса + "
					|И ТаблицаИзменений." + ТекИмя + " = &" + ТекИмя;
				Запрос.УстановитьПараметр(ТекИмя, ОбъектДанных[ТекИмя]);
				
			КонецЦикла;
			
			ТекИмяТаблицы = ИмяТаблицы;
			МетаОбъект    = МетаданныеПоПолномуИмени(ИмяТаблицы);
			
		ИначеЕсли ТипЗнч(ОбъектДанных) = Тип("Строка") Тогда
			
			ТекИмяТаблицы = ОбъектДанных;
			МетаОбъект    = МетаданныеПоПолномуИмени(ОбъектДанных);
			
		Иначе
			
			ТекстПодзапроса = "
				|И ТаблицаИзменений.Ссылка = &ОбъектРегистрации";
			Запрос.УстановитьПараметр("ОбъектРегистрации", ОбъектДанных);
			
			МетаОбъект    = ОбъектДанных.Метаданные();
			ТекИмяТаблицы = МетаОбъект.ПолноеИмя();
			
		КонецЕсли;
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПланОбмена.ЭтотУзел КАК ЭтотУзел,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПланОбмена.Ссылка) КАК Наименование,
		|	ВЫБОР
		|		КОГДА ПланОбмена.ПометкаУдаления
		|			ТОГДА 2
		|		КОГДА ПланОбмена.ЭтотУзел
		|			ТОГДА 3
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ИндексКартинки,
		|	""&ТекстовоеПредставлениеПланаОбмена"" КАК ПланОбменаИмя,
		|	ПланОбмена.Код КАК Код,
		|	ПланОбмена.Ссылка КАК Ссылка,
		|	ПланОбмена.НомерОтправленного КАК НомерОтправленного,
		|	ПланОбмена.НомерПринятого КАК НомерПринятого,
		|	ТаблицаИзменений.НомерСообщения КАК НомерСообщения,
		|	ВЫБОР
		|		КОГДА ТаблицаИзменений.НомерСообщения ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НеВыгружалось,
		|	КОЛИЧЕСТВО(ТаблицаИзменений.Узел) КАК КоличествоИзмененийНаУзле
		|ИЗ
		|	&ИмяТаблицыПланаОбмена КАК ПланОбмена
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СценарииОбменовДанными.Изменения КАК ТаблицаИзменений
		|		ПО ПланОбмена.Ссылка = ТаблицаИзменений.Узел
		|			И &УсловияОбъединенияТаблиц
		|ГДЕ
		|	(НЕ ПланОбмена.ЭтотУзел ИЛИ &ПоказыватьПредопределенные)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПланОбмена.Ссылка,
		|	ТаблицаИзменений.НомерСообщения,
		|	ВЫБОР
		|		КОГДА ПланОбмена.ПометкаУдаления
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ,
		|	ПланОбмена.Код,
		|	ПланОбмена.НомерОтправленного,
		|	ПланОбмена.НомерПринятого,
		|	ВЫБОР
		|		КОГДА ТаблицаИзменений.НомерСообщения ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ";
		
	КонецЕсли;
	
	ТекНомерСтроки = 0;
	Для Каждого Мета Из Метаданные.ПланыОбмена Цикл
		
		Если Не ПравоДоступа("Чтение", Мета) Тогда
			Продолжить;
		КонецЕсли;
	
		ИмяПлана = Мета.Имя;
		Авторегистрация = Неопределено;
		Если МетаОбъект <> Неопределено Тогда
			ЭлементСостава = Мета.Состав.Найти(МетаОбъект);
			Если ЭлементСостава = Неопределено Тогда
				// Не входит в текущий план обмена.
				Продолжить;
			КонецЕсли;
			Авторегистрация = ?(ЭлементСостава.Авторегистрация = АвтоРегистрацияИзменений.Запретить, 1, 2);
		КонецЕсли;
		
		ИмяПлана = Мета.Имя;
		ИмяТаблицыПланаОбмена = СтрШаблон("ПланОбмена.%1", ИмяПлана);
		
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&ТекстовоеПредставлениеПланаОбмена", ИмяПлана);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяТаблицыПланаОбмена", ИмяТаблицыПланаОбмена);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.СценарииОбменовДанными", ТекИмяТаблицы);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &УсловияОбъединенияТаблиц", ТекстПодзапроса);
		
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			
			СтрокаПлана = Строки.Добавить();
			СтрокаПлана.Наименование   = Мета.Представление();
			СтрокаПлана.ИндексКартинки = 0;
			СтрокаПлана.ПланОбменаИмя  = ИмяПлана;
			
			СтрокаПлана.ИдентификаторСтроки = ТекНомерСтроки;
			ТекНомерСтроки = ТекНомерСтроки + 1;
			
			// Сортировка по представлению, в запросе нельзя.
			ВременнаяТаблица = Результат.Выгрузить();
			ВременнаяТаблица.Сортировать("Наименование");
			Для Каждого СтрокаУзла Из ВременнаяТаблица Цикл;
				
				НоваяСтрока = СтрокаПлана.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаУзла);
				
				НоваяСтрока.ИсходнаяПометка = ?(СтрокаУзла.КоличествоИзмененийНаУзле > 0, 1, 0);
				НоваяСтрока.Пометка         = НоваяСтрока.ИсходнаяПометка;
				
				НоваяСтрока.ИндексКартинкиАвторегистрация = Авторегистрация;
				
				НоваяСтрока.ИдентификаторСтроки = ТекНомерСтроки;
				ТекНомерСтроки = ТекНомерСтроки + 1;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Дерево;
	
КонецФункции

// Возвращает объект метаданных по его полному имени. Пустая строка обозначает конфигурацию.
//
// Параметры:
//    ИмяМетаданных - Строка - имя объекта метаданных, например "Справочник.Валюты" или "Константы".
//
// Возвращаемое значение:
//    ОбъектМетаданных - результат поиска.
//
&НаСервере
Функция МетаданныеПоПолномуИмени(ИмяМетаданных)
	
	Если ПустаяСтрока(ИмяМетаданных) Тогда
		// Вся конфигурация
		Возврат Метаданные;
	КонецЕсли;
		
	Значение = Метаданные.НайтиПоПолномуИмени(ИмяМетаданных);
	Если Значение = Неопределено Тогда
		Значение = Метаданные[ИмяМетаданных];
	КонецЕсли;
	
	Возврат Значение;
КонецФункции

#КонецОбласти

#КонецОбласти
