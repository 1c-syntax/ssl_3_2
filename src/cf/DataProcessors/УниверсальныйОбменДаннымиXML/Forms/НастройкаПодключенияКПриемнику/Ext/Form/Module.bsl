///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Проверка прав доступа должна располагаться самой первой.
	Если Не ПравоДоступа("Администрирование", Метаданные) Тогда
		ВызватьИсключение НСтр("ru = 'Использование обработки в интерактивном режиме доступно только администратору.'");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТипИнформационнойБазыДляПодключенияПриИзмененииЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АутентификацияWindowsИнформационнойБазыДляПодключенияПриИзменении(Элемент)
	
	Элементы.ПользовательИнформационнойБазыДляПодключения.Доступность = НЕ Объект.АутентификацияWindowsИнформационнойБазыДляПодключения;
	Элементы.ПарольИнформационнойБазыДляПодключения.Доступность = НЕ Объект.АутентификацияWindowsИнформационнойБазыДляПодключения;
	
КонецПроцедуры

&НаКлиенте
Процедура ВерсияПлатформыИнформационнойБазыДляПодключенияПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.ВерсияПлатформыИнформационнойБазыДляПодключения) Тогда
		
		Объект.ВерсияПлатформыИнформационнойБазыДляПодключения = "V8";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогИнформационнойБазыДляПодключенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("КаталогИнформационнойБазыДляПодключенияОбработкаВыбора", ЭтотОбъект);
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);	
	ДиалогВыбораФайла.Заголовок = НСтр("ru = 'Выберите каталог информационной базы'");
		
	ДиалогВыбораФайла.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогИнформационнойБазыДляПодключенияОткрытие(Элемент, СтандартнаяОбработка)
	
	ОткрытьВПриложении(Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипИнформационнойБазыДляПодключенияПриИзменении(Элемент)
	
	ТипИнформационнойБазыДляПодключенияПриИзмененииЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ТипИнформационнойБазыДляПодключения", Объект.ТипИнформационнойБазыДляПодключения);
	ПараметрыЗакрытия.Вставить("ВерсияПлатформыИнформационнойБазыДляПодключения", Объект.ВерсияПлатформыИнформационнойБазыДляПодключения);
	ПараметрыЗакрытия.Вставить("КаталогИнформационнойБазыДляПодключения", Объект.КаталогИнформационнойБазыДляПодключения);
	ПараметрыЗакрытия.Вставить("ИмяСервераИнформационнойБазыДляПодключения", Объект.ИмяСервераИнформационнойБазыДляПодключения);
	ПараметрыЗакрытия.Вставить("ИмяИнформационнойБазыНаСервереДляПодключения", Объект.ИмяИнформационнойБазыНаСервереДляПодключения);
	ПараметрыЗакрытия.Вставить("АутентификацияWindowsИнформационнойБазыДляПодключения", Объект.АутентификацияWindowsИнформационнойБазыДляПодключения);
	ПараметрыЗакрытия.Вставить("ПользовательИнформационнойБазыДляПодключения", Объект.ПользовательИнформационнойБазыДляПодключения);
	ПараметрыЗакрытия.Вставить("ПарольИнформационнойБазыДляПодключения", Объект.ПарольИнформационнойБазыДляПодключения);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ТестПодключения(Команда)
	
	Если ВыполнитьПодключениеКИБПриемникуНаСервере() Тогда
		
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Подключение успешно установлено.'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура КаталогИнформационнойБазыДляПодключенияОбработкаВыбора(ВыбранныйКаталог, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйКаталог = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.КаталогИнформационнойБазыДляПодключения = ВыбранныйКаталог[0];
	
КонецПроцедуры

// Открывает файл обмена во внешнем приложении
//
// Параметры:
// ИмяФайла - Строка - имя открываемого файла
// СтандартнаяОбработка - Булево
//
&НаКлиенте
Процедура ОткрытьВПриложении(ИмяФайла, СтандартнаяОбработка = Ложь)
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ИмяФайла", ИмяФайла);
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", Новый ОписаниеОповещения);
	
	Файл = Новый Файл(ИмяФайла);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОпределенияСуществованияФайла", ЭтотОбъект, ДополнительныеПараметры);
	Файл.НачатьПроверкуСуществования(ОписаниеОповещения);
	
КонецПроцедуры

// Продолжение процедуры ОткрытьВПриложении()
//
&НаКлиенте
Процедура ПослеОпределенияСуществованияФайла(Существует, ДополнительныеПараметры) Экспорт
	
	Если Существует Тогда
		НачатьЗапускПриложения(ДополнительныеПараметры.ОписаниеОповещения, ДополнительныеПараметры.ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипИнформационнойБазыДляПодключенияПриИзмененииЗначения()
	
	Элементы.ТипБазы.ТекущаяСтраница =
		?(Объект.ТипИнформационнойБазыДляПодключения, Элементы.ФайловаяБаза, Элементы.БазаНаСервере);
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПодключениеКИБПриемникуНаСервере()
	
	ОбъектДляСервера = РеквизитФормыВЗначение("Объект");
	ЗаполнитьЗначенияСвойств(ОбъектДляСервера, Объект);
	
	РезультатПодключения = ОбъектДляСервера.ВыполнитьПодключениеКИБПриемнику();
	
	Возврат (РезультатПодключения <> Неопределено);
	
КонецФункции

#КонецОбласти