///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

// Если подсистема "Работа с файлами" не внедрена в конфигурацию,
// то эту форму также необходимо удалить из конфигурации.

#Область ОписаниеПеременных

&НаКлиенте
Перем ОбновитьИнтерфейс;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДоступныХранилищаДвоичныхДанных = РаботаСФайловымАрхивомСервер.ДоступныХранилищаДвоичныхДанных();

	СпособХраненияФайловНастроитьСписокВыбора();

	ИспользоватьФайловыйАрхив = НаборКонстант.ИспользоватьФайловыйАрхив;
	
	МаксимальныйРазмерФайла = РаботаСФайлами.МаксимальныйРазмерФайлаОбщий() / (1024*1024);
	МаксимальныйРазмерФайлаОбластиДанных = РаботаСФайлами.МаксимальныйРазмерФайла() / (1024*1024);
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	Если РазделениеВключено Тогда
		Элементы.МаксимальныйРазмерФайла.МаксимальноеЗначение = МаксимальныйРазмерФайла;
	КонецЕсли;
	
	ЗапрещатьЗагрузкуФайловПоРасширению = НаборКонстант.ЗапрещатьЗагрузкуФайловПоРасширению;
	
	ПараметрыХраненияФайловВИБ = РаботаСФайламиВТомахСлужебный.ПараметрыХраненияФайловВИнформационнойБазе();
	Если ПараметрыХраненияФайловВИБ <> Неопределено Тогда
		РасширенияФайловВИБ = ПараметрыХраненияФайловВИБ.РасширенияФайлов;
		МаксимальныйРазмерФайлаВИБ = ПараметрыХраненияФайловВИБ.МаксимальныйРазмер / (1024*1024);
	КонецЕсли;
	
	РаботаСФайламиСлужебный.ЗаполнитьСписокТипамиФайлов(Элементы.РасширенияФайловВИБ.СписокВыбора);
	
	ЭтоАдминистраторСистемы = Пользователи.ЭтоПолноправныйПользователь(, Истина);
	Элементы.УправлениеХранениемФайлов.Видимость = ЭтоАдминистраторСистемы;
	Элементы.ГруппаУправлениеТомамиФайлов.Видимость = ЭтоАдминистраторСистемы;
	Элементы.ГруппаРазмерФайловВИБ.Видимость = ЭтоАдминистраторСистемы;
	Элементы.ОбщиеПараметрыДляВсехОбластейДанных.Видимость = ЭтоАдминистраторСистемы И РазделениеВключено;
	Элементы.ГруппаСписокРасширенийТекстовыхФайлов.Видимость = Не РазделениеВключено;
	Элементы.ГруппаУправлениеРасширениямиФайловВИБ.Видимость = ЭтоАдминистраторСистемы;
	
	Если ЭтоАдминистраторСистемы Тогда
		Элементы.ВыполнитьДедупликацию.Видимость = НаборКонстант.СпособХраненияФайлов <> "ВТомахНаДиске" И Не ДедупликацияВыполнена();
		ЗначениеСпособаХраненияФайлов = НаборКонстант.СпособХраненияФайлов;
		УстановитьДоступностьНастроекХраненияВТомах();
	Иначе
		Элементы.ВыполнитьДедупликацию.Видимость = Ложь;
	КонецЕсли;
	
	ФорматЧисла = НСтр("ru = '%Число% Мбайт'");
	ФорматЧисла = СтрЗаменить(ФорматЧисла, "%Число%", "Ч");
	Элементы.МаксимальныйРазмерФайлаВИБ.ФорматРедактирования =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ЧФ='%1'", ФорматЧисла);
	Элементы.МаксимальныйРазмерФайла.ФорматРедактирования =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ЧФ='%1'", ФорматЧисла);

	ДоступноИспользованиеФайловогоАрхива = РаботаСФайловымАрхивомСервер.ДоступенФайловыйАрхив();
	Элементы.ГруппаФайловыйАрхив.Видимость = ДоступноИспользованиеФайловогоАрхива;
	Если ДоступноИспользованиеФайловогоАрхива Тогда
		УстановитьДоступностьНастроекУправленияФайловымАрхивом();
		Элементы.ИспользоватьФайловыйАрхив.Доступность = Не РазделениеВключено;
	КонецЕсли;
	
	УстановитьДоступность();
	
	Если Не ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		СтандартныеПодсистемыСервер.ПереместитьЭлементыВГруппу(КоманднаяПанель, Элементы.ГруппаСправкаТакси);
	КонецЕсли;
	
	НастройкиПрограммыПереопределяемый.НастройкиРаботыСФайламиПриСозданииНаСервере(ЭтотОбъект);
	
	ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_НаборКонстант" И Источник = "РазрешитьДоступКИнтернетСервисам" Тогда
		УстановитьДоступность("РазрешитьДоступКИнтернетСервисам");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособХраненияФайловПриИзменении(Элемент)

	Если НаборКонстант.СпособХраненияФайлов = ЗначениеСпособаХраненияФайлов Тогда
		Возврат;
	КонецЕсли;
	
	НаборКонстант.ХранитьФайлыВТомахНаДиске = ЭтоСпособХранениеФайловНаДиске(НаборКонстант.СпособХраненияФайлов);

	ОбработкаОповещения = Новый ОписаниеОповещения(
		"СпособХраненияФайловПриИзмененииЗавершение", ЭтотОбъект, Элемент);

	Если ЭтоСпособХранениеФайловНаДиске(ЗначениеСпособаХраненияФайлов)
		И НаборКонстант.ХранитьФайлыВТомахНаДиске Тогда
		
		ВыполнитьОбработкуОповещения(ОбработкаОповещения, КодВозвратаДиалога.ОК);
		Возврат;
	КонецЕсли;

	Попытка

		ЗапросыНаИспользованиеВнешнихРесурсов = ЗапросыНаИспользованиеВнешнихРесурсовТомовХраненияФайлов(
			НаборКонстант.ХранитьФайлыВТомахНаДиске);

		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПрофилиБезопасности") Тогда
			МодульРаботаВБезопасномРежимеКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаВБезопасномРежимеКлиент");
			МодульРаботаВБезопасномРежимеКлиент.ПрименитьЗапросыНаИспользованиеВнешнихРесурсов(
				ЗапросыНаИспользованиеВнешнихРесурсов, ЭтотОбъект, ОбработкаОповещения);
		Иначе
			ВыполнитьОбработкуОповещения(ОбработкаОповещения, КодВозвратаДиалога.ОК);
		КонецЕсли;

	Исключение

		НаборКонстант.СпособХраненияФайлов = ЗначениеСпособаХраненияФайлов;
		НаборКонстант.ХранитьФайлыВТомахНаДиске = ЭтоСпособХранениеФайловНаДиске(ЗначениеСпособаХраненияФайлов);
		ВызватьИсключение;

	КонецПопытки;		
	
КонецПроцедуры

&НаКлиенте
Процедура СоздаватьПодкаталогиСИменамиВладельцевПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасширенияФайловВИБПриИзменении(Элемент)
	
	ПриИзмененииНастроекХраненияФайловВИБ();
	
КонецПроцедуры

&НаКлиенте
Процедура РасширенияФайловВИБОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасширенияФайловВИБ = РаботаСФайламиСлужебныйКлиент.РасширенияПоТипуФайла(ВыбранноеЗначение);
	ПриИзмененииНастроекХраненияФайловВИБ();
	
КонецПроцедуры

&НаКлиенте
Процедура МаксимальныйРазмерФайлаВИБПриИзменении(Элемент)
	
	ПриИзмененииНастроекХраненияФайловВИБ();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапрещатьЗагрузкуФайловПоРасширениюПриИзменении(Элемент)
	
	Если Не ЗапрещатьЗагрузкуФайловПоРасширению Тогда
		
		Оповещение = Новый ОписаниеОповещения(
			"ЗапрещатьЗагрузкуФайловПоРасширениюПослеПодтверждения", ЭтотОбъект, Новый Структура("Элемент", Элемент));
		ПользователиСлужебныйКлиент.ПоказатьПредупреждениеБезопасности(Оповещение,
			ПользователиСлужебныйКлиентСервер.ВидыПредупрежденийБезопасности().ПриИзмененииСпискаЗапрещенныхРасширений);
		Возврат;
		
	КонецЕсли;
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СинхронизироватьФайлыПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗапрещенныхРасширенийОбластиДанныхПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура МаксимальныйРазмерФайлаОбластиДанныхПриИзменении(Элемент)
	
	Если МаксимальныйРазмерФайлаОбластиДанных = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Поле ""Максимальный размер файла"" не заполнено.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,"МаксимальныйРазмерФайлаОбластиДанных");
		Возврат;
		
	КонецЕсли;
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРасширенийФайловOpenDocumentОбластиДанныхПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРасширенийТекстовыхФайловПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьДействиеИнформационногоСообщения(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	ОбщегоНазначенияКлиент.ОбработатьДействиеИнформационногоСообщения(ЭтотОбъект, ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка);
КонецПроцедуры

#Область ОбщиеПараметрыДляВсехОбластейДанных

&НаКлиенте
Процедура МаксимальныйРазмерФайлаПриИзменении(Элемент)
	
	Если МаксимальныйРазмерФайла = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Поле ""Максимальный размер файла"" не заполнено.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,"МаксимальныйРазмерФайла");
		Возврат;
		
	КонецЕсли;
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗапрещенныхРасширенийПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРасширенийФайловOpenDocumentПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыРаботыСФайловымАрхивом

&НаКлиенте
Процедура ИспользоватьФайловыйАрхивПриИзменении(Элемент)

	Подключаемый_ПриИзмененииРеквизита(Элемент);

КонецПроцедуры

&НаКлиенте
Процедура ТекстИнформированияПользователяОНедоступностиФайлаВАрхивеПриИзменении(Элемент)

	Подключаемый_ПриИзмененииРеквизита(Элемент);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СправочникФайлы(Команда)
	
	ОткрытьФорму("Справочник.Файлы.ФормаСписка", , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СправочникТомаХраненияФайлов(Команда)

	ВидТомаХраненияФайлов = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ВидыХраненияФайлов.ОперационноеХранение");
	ОткрытьФормуСпискаСправочникаТомаХраненияФайловСОтборомПоВидуТомаХраненияФайлов(ЭтотОбъект, ВидТомаХраненияФайлов);

КонецПроцедуры

&НаКлиенте
Процедура НастройкаСинхронизацииФайлов(Команда)
	
	ОткрытьФорму("РегистрСведений.НастройкиСинхронизацииФайлов.ФормаСписка", , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереносФайлов(Команда)
	
	РаботаСФайламиСлужебныйКлиент.ПеренестиФайлы();
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыполнитьДедупликацию(Команда)
	
	ЗаголовокВопроса = НСтр("ru = 'Дедупликация файлов'");
	ШаблонВопроса = НСтр("ru = 'Дедупликация файлов позволяет экономить до 30%% места в информационной базе за счет устранения дублей файлов, хранящихся в приложении (вариант хранения ""В информационной базе""). Дедупликация имеющихся файлов занимает от нескольких минут до нескольких часов в зависимости от объема файлов в приложении. В любой момент ее можно будет прервать и возобновить позднее в более подходящий момент времени. При этом все вновь добавляемые файлы уже автоматически сохраняются в приложении только в одном экземпляре.
	 |
	 |Во время дедупликации файлов размер информационной базы может существенно вырасти. Поэтому перед запуском рекомендуется:
	 |• убедиться, что имеется достаточно свободного места на устройстве, где размещается информационная база (требуется не менее %1 Мб);
	 |• сделать резервную копию информационной базы.
	 |
	 |После завершения выполнить сжатие информационной базы, чтобы дедупликация файлов вступила в силу.
	 |
	 |Запустить дедупликацию файлов?'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонВопроса, РазмерФайловВБАзе());
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, ЗаголовокВопроса);
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ЗапуститьДедупликациюНаСервере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершениеДедупликации", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Заголовок = НСтр("ru = 'Выполняется дедупликация файлов'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ЗаголовокКнопкиОтмена = НСтр("ru = 'Прервать'");
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
КонецПроцедуры

&НаКлиенте
Процедура СправочникТомаХраненияФайловАрхивные(Команда)

	ВидТомаХраненияФайлов = ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ВидыХраненияФайлов.АрхивноеХранение");
	ОткрытьФормуСпискаСправочникаТомаХраненияФайловСОтборомПоВидуТомаХраненияФайлов(ЭтотОбъект, ВидТомаХраненияФайлов);

КонецПроцедуры

&НаКлиенте
Процедура НастройкаРаботыСФайловымАрхивом(Команда)

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РазрешеноОткрытиеФормы", Истина);
	
	ОткрытьФорму("РегистрСведений.НастройкиРаботыСФайловымАрхивом.ФормаСписка", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СпособХраненияФайловПриИзмененииЗавершение(Ответ, Элемент) Экспорт

	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		НаборКонстант.СпособХраненияФайлов = ЗначениеСпособаХраненияФайлов;
		НаборКонстант.ХранитьФайлыВТомахНаДиске = ЭтоСпособХранениеФайловНаДиске(ЗначениеСпособаХраненияФайлов);
		Возврат;
	КонецЕсли;

	ИзменятьСоставХранимыхДанных = Ложь;
	
	Если Не ДоступныХранилищаДвоичныхДанных Тогда
		Если ЗначениеСпособаХраненияФайлов = "ВИнформационнойБазе"
			И НаборКонстант.ХранитьФайлыВТомахНаДиске
			И Не ЕстьТомаХраненияФайлов("ВТомахНаДиске") Тогда
			
			ПоказатьПредупреждение(, НСтр("ru = 'Включено хранение файлов в томах на файловом сервере, но тома еще не настроены.
				|Добавляемые файлы будут сохраняться в информационной базе до тех пор, пока не будет настроен хотя бы один том хранения файлов.'"));
		КонецЕсли;
	Иначе

		ИзменятьСоставХранимыхДанных = Истина;

		Если РаботаСФайловымАрхивомКлиентСервер.СпособХраненияИспользуетТома(НаборКонстант.СпособХраненияФайлов) Тогда
			Если Не ЕстьТомаХраненияФайлов(НаборКонстант.СпособХраненияФайлов) Тогда
				ТекстПредупреждения = ПолучитьТекстПредупрежденияПоСпособуХраненияФайлов(НаборКонстант.СпособХраненияФайлов);
				ПоказатьПредупреждение(, НСтр("ru = '" + ТекстПредупреждения + "'"));
			КонецЕсли;
		КонецЕсли;				
	КонецЕсли;
		
	ПриИзмененииСпособаХраненияФайловНаСервере(ИзменятьСоставХранимыхДанных);
	ОбновитьПовторноИспользуемыеЗначения();
	ПослеИзмененияРеквизита("СпособХраненияФайлов", Ложь);
	ПослеИзмененияРеквизита("ХранитьФайлыВТомахНаДиске");
	
КонецПроцедуры

// Параметры:
//  Результат - Неопределено
//            - Строка
//  ДополнительныеПараметры - Структура:
//    * Элемент - ПолеФормы
//              - РасширениеПоляФормыДляПоляФлажка
//
&НаКлиенте
Процедура ЗапрещатьЗагрузкуФайловПоРасширениюПослеПодтверждения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено
		И Результат = "Продолжить" Тогда
		
		Подключаемый_ПриИзмененииРеквизита(ДополнительныеПараметры.Элемент);
	Иначе
		ЗапрещатьЗагрузкуФайловПоРасширению = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииНастроекХраненияФайловВИБ()
	
	УстановитьПараметрыХраненияФайловВИБ(
		Новый Структура("РасширенияФайлов, МаксимальныйРазмер",
		РасширенияФайловВИБ, МаксимальныйРазмерФайлаВИБ*1024*1024));
	
	ОбновитьПовторноИспользуемыеЗначения();
	ПослеИзмененияРеквизита("ПараметрыХраненияФайловВИБ", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРеквизита(Элемент, ОбновлятьИнтерфейс = Истина)
	
	КонстантаИмя = ПриИзмененииРеквизитаСервер(Элемент.Имя);
	ОбновитьПовторноИспользуемыеЗначения();
	ПослеИзмененияРеквизита(КонстантаИмя, ОбновлятьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияРеквизита(КонстантаИмя, ОбновлятьИнтерфейс = Истина)
	
	Если ОбновлятьИнтерфейс Тогда
		ОбновитьИнтерфейс = Истина;
		ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 2, Истина);
	КонецЕсли;
	
	Если КонстантаИмя <> "" Тогда
		Оповестить("Запись_НаборКонстант", Новый Структура, КонстантаИмя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	КонстантаИмя = СохранитьЗначениеРеквизита(РеквизитПутьКДанным);
	
	УстановитьДоступность(РеквизитПутьКДанным);
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат КонстантаИмя;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииСпособаХраненияФайловНаСервере(Знач ИзменятьСоставХранимыхДанных = Ложь)

	НачатьТранзакцию();
	Попытка
		Константы.СпособХраненияФайлов.Установить(НаборКонстант.СпособХраненияФайлов);
		Константы.ХранитьФайлыВТомахНаДиске.Установить(НаборКонстант.ХранитьФайлыВТомахНаДиске);
		
		Если ИзменятьСоставХранимыхДанных Тогда
			ИзменитьСоставХранимыхДанных(НаборКонстант.СпособХраненияФайлов);
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		НаборКонстант.СпособХраненияФайлов		= ЗначениеСпособаХраненияФайлов;
		НаборКонстант.ХранитьФайлыВТомахНаДиске = РаботаСФайламиКлиентСервер.ХранитьФайлыВТомах(ЗначениеСпособаХраненияФайлов);

		ВызватьИсключение;		
	КонецПопытки;
	
	ЗначениеСпособаХраненияФайлов = НаборКонстант.СпособХраненияФайлов;
	УстановитьДоступность("НаборКонстант.СпособХраненияФайлов");
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступность(РеквизитПутьКДанным = "")
	
	Если РеквизитПутьКДанным = "НаборКонстант.СпособХраненияФайлов" Тогда
		УстановитьДоступностьНастроекХраненияВТомах();
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "ЗапрещатьЗагрузкуФайловПоРасширению"
		Или РеквизитПутьКДанным = "" Тогда
		
		Элементы.СписокЗапрещенныхРасширенийОбластиДанных.Доступность = ЗапрещатьЗагрузкуФайловПоРасширению;
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.СинхронизироватьФайлы"
		Или РеквизитПутьКДанным = "РазрешитьДоступКИнтернетСервисам" 
		Или РеквизитПутьКДанным = ""  Тогда
		
		РазрешитьДоступКИнтернетСервисам = ОбщегоНазначения.РазрешенДоступКИнтернетСервисам();
		Элементы.НастройкиСинхронизацииФайлов.Доступность = НаборКонстант.СинхронизироватьФайлы
			И РазрешитьДоступКИнтернетСервисам;
		Элементы.СинхронизироватьФайлы1.Видимость = Не РазрешитьДоступКИнтернетСервисам;
		Элементы.СинхронизироватьФайлы.Видимость = РазрешитьДоступКИнтернетСервисам;
		Элементы.ГруппаКомментарийРазрешитьЗагрузкуИзИнтернетаВСинхронизации.Видимость = Не РазрешитьДоступКИнтернетСервисам;
		
		Если Не РазрешитьДоступКИнтернетСервисам Тогда
			ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
			ПараметрыСообщения.Идентификатор = "ЗаблокированДоступКИнтернетСервисам";
			ПараметрыСообщения.Сообщение = НСтр("ru = 'Для разблокировки настройки необходимо включить доступ к Интернет-сервисам в настройке ""Разрешить доступ к Интернет-сервисам"".'");
			ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения, Элементы.ГруппаКомментарийРазрешитьЗагрузкуИзИнтернетаВСинхронизации);
			ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);
		Иначе
			ОбщегоНазначенияКлиентСервер.УдалитьСтатусИлиВажноеСообщение(ЭтотОбъект, "ЗаблокированДоступКИнтернетСервисам");
		КонецЕсли;
	
	КонецЕсли;

	Если РеквизитПутьКДанным = "ИспользоватьФайловыйАрхив" Тогда
		УстановитьДоступностьНастроекУправленияФайловымАрхивом();
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьНастроекХраненияВТомах()
	
	СпособХраненияИспользуетТома = РаботаСФайловымАрхивомКлиентСервер.СпособХраненияИспользуетТома(НаборКонстант.СпособХраненияФайлов);

	Элементы.ГруппаУправлениеТомамиФайлов.Доступность = СпособХраненияИспользуетТома;
	Элементы.СправочникТомаХраненияФайлов.Доступность = СпособХраненияИспользуетТома;
	Элементы.СоздаватьПодкаталогиСИменамиВладельцев.Доступность = НаборКонстант.ХранитьФайлыВТомахНаДиске;
	Элементы.ГруппаРазмерФайловВИБ.Доступность =
		НаборКонстант.СпособХраненияФайлов = "ВИнформационнойБазеИТомахНаДиске";
	Элементы.ГруппаУправлениеРасширениямиФайловВИБ.Доступность =
		НаборКонстант.СпособХраненияФайлов = "ВИнформационнойБазеИТомахНаДиске";
	Элементы.ВыполнитьДедупликацию.Видимость = НаборКонстант.СпособХраненияФайлов <> "ВТомахНаДиске" И Не ДедупликацияВыполнена();
	
КонецПроцедуры

&НаСервере
Функция СохранитьЗначениеРеквизита(РеквизитПутьКДанным)
	
	ЧастиИмени = СтрРазделить(РеквизитПутьКДанным, ".");
	Если ЧастиИмени.Количество() <> 2 Тогда
		
		Если РеквизитПутьКДанным = "МаксимальныйРазмерФайла" Тогда
			НаборКонстант.МаксимальныйРазмерФайла = МаксимальныйРазмерФайла * (1024*1024);
			КонстантаИмя = "МаксимальныйРазмерФайла";
		ИначеЕсли РеквизитПутьКДанным = "МаксимальныйРазмерФайлаОбластиДанных" Тогда
			
			Если Не ОбщегоНазначения.РазделениеВключено() Тогда
				НаборКонстант.МаксимальныйРазмерФайла = МаксимальныйРазмерФайлаОбластиДанных * (1024*1024);
				КонстантаИмя = "МаксимальныйРазмерФайла";
			Иначе
				НаборКонстант.МаксимальныйРазмерФайлаОбластиДанных = МаксимальныйРазмерФайлаОбластиДанных * (1024*1024);
				КонстантаИмя = "МаксимальныйРазмерФайлаОбластиДанных";
			КонецЕсли;
			
		ИначеЕсли РеквизитПутьКДанным = "ЗапрещатьЗагрузкуФайловПоРасширению" Тогда
			НаборКонстант.ЗапрещатьЗагрузкуФайловПоРасширению = ЗапрещатьЗагрузкуФайловПоРасширению;
			КонстантаИмя = "ЗапрещатьЗагрузкуФайловПоРасширению";
		ИначеЕсли РеквизитПутьКДанным = "ИспользоватьФайловыйАрхив" Тогда
			НаборКонстант.ИспользоватьФайловыйАрхив = ИспользоватьФайловыйАрхив;
			КонстантаИмя = "ИспользоватьФайловыйАрхив";

			ПриИзмененииИспользованияФайловогоАрхива(ИспользоватьФайловыйАрхив);
		КонецЕсли;
		
	Иначе
		КонстантаИмя = ЧастиИмени[1];
	КонецЕсли;
	
	Если ПустаяСтрока(КонстантаИмя) Тогда
		Возврат "";
	КонецЕсли;
	
	КонстантаМенеджер = Константы[КонстантаИмя];
	КонстантаЗначение = НаборКонстант[КонстантаИмя];
	
	Если КонстантаМенеджер.Получить() <> КонстантаЗначение Тогда
		КонстантаМенеджер.Установить(КонстантаЗначение);
	КонецЕсли;
	
	Возврат КонстантаИмя;
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьПараметрыХраненияФайловВИБ(ПараметрыХранения)
	
	РаботаСФайламиВТомахСлужебный.УстановитьПараметрыХраненияФайловВИнформационнойБазе(ПараметрыХранения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапросыНаИспользованиеВнешнихРесурсовТомовХраненияФайлов(Включение)
	
	ЗапросыНаИспользование = Новый Массив;
	ИмяСправочника = "ТомаХраненияФайлов";
	
	Если Включение Тогда
		Справочники[ИмяСправочника].ДобавитьЗапросыНаИспользованиеВнешнихРесурсовВсехТомов(ЗапросыНаИспользование);
	Иначе
		Справочники[ИмяСправочника].ДобавитьЗапросыНаОтменуИспользованияВнешнихРесурсовВсехТомов(ЗапросыНаИспользование);
	КонецЕсли;
	
	Возврат ЗапросыНаИспользование;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьТомаХраненияФайлов(Знач ОбрабатываемыйСпособХраненияФайлов = Неопределено)

	Если ОбрабатываемыйСпособХраненияФайлов = Неопределено Тогда
		
		ВидТомаХраненияФайлов = Неопределено;
		СпособХраненияФайлов = Неопределено;
	Иначе

		ВидТомаХраненияФайлов = Перечисления.ВидыХраненияФайлов.ОперационноеХранение;

		СпособХраненияФайлов = РаботаСФайловымАрхивомСервер.СпособХраненияДляПодбораТомовХранения(ОбрабатываемыйСпособХраненияФайлов);

	КонецЕсли;

	Возврат РаботаСФайламиВТомахСлужебный.ЕстьТомаХраненияФайлов(ВидТомаХраненияФайлов, СпособХраненияФайлов);

КонецФункции

&НаСервере
Функция ЗапуститьДедупликациюНаСервере()
	
	АдресРезультатаДедупликации = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(, "РегистрыСведений.ХранилищеФайлов.ПеренестиДанные", Истина, АдресРезультатаДедупликации);
	
КонецФункции

// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ЗавершениеДедупликации(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Дедупликация прервана, при необходимости, можно запустить позднее'"));
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(
			Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	ОшибкиДедупликации = ПолучитьИзВременногоХранилища(АдресРезультатаДедупликации);
	Если ДедупликацияВыполнена() И ОшибкиДедупликации = Неопределено Тогда
		Элементы.ВыполнитьДедупликацию.Видимость = Ложь;
		ПоказатьПредупреждение(, НСтр("ru = 'Дедупликация файлов завершена.'"));
	ИначеЕсли ОшибкиДедупликации = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не все файлы были обработаны, требуется повторный запуск.'"));
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Дедупликация", Истина);
		ПараметрыФормы.Вставить("Пояснение", НСтр("ru = 'Не все файлы были обработаны, для продолжения требуется устранить выявленные проблемы:'"));
		ПараметрыФормы.Вставить("ФайлыСОшибками", ОшибкиДедупликации);
		ОткрытьФорму("Обработка.ПереносФайлов.Форма.ФормаОтчета", ПараметрыФормы);
	КонецЕсли;
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ДедупликацияВыполнена()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Проверка
	|ИЗ
	|	РегистрСведений.УдалитьДвоичныеДанныеФайлов КАК УдалитьДвоичныеДанныеФайлов";
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

&НаСервереБезКонтекста
Функция РазмерФайловВБАзе()
	
	ВключитьОбъекты = Новый Массив();
	ВключитьОбъекты.Добавить(Метаданные.РегистрыСведений.УдалитьДвоичныеДанныеФайлов);
	РазмерФайловМб = ПолучитьРазмерДанныхБазыДанных(, ВключитьОбъекты) / 1024 / 1024;
	Если РазмерФайловМб > 100 Тогда
		РазмерФайловМб = Окр(РазмерФайловМб, 0);
	Иначе
		РазмерФайловМб = Окр(РазмерФайловМб, 2);
	КонецЕсли;
	
	Возврат РазмерФайловМб;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуСпискаСправочникаТомаХраненияФайловСОтборомПоВидуТомаХраненияФайлов(ФормаВладелец, ЗначениеДляОтбора)

	ПараметрыФормы = Новый Структура("Отбор, ЗначениеОтбораПоВидуТомаХраненияФайлов", Новый Структура("ВидТомаХраненияФайлов", ЗначениеДляОтбора), ЗначениеДляОтбора);
	ОткрытьФорму("Справочник.ТомаХраненияФайлов.ФормаСписка", ПараметрыФормы, ФормаВладелец, Истина);

КонецПроцедуры

&НаСервере
Процедура СпособХраненияФайловНастроитьСписокВыбора()

	Если Не РаботаСФайловымАрхивомСервер.ДоступныИзмененияВСпособахХраненияФайлов() Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Найти("СпособХраненияФайлов") = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИзменяемыеСпособыХранения = Новый Структура;
	ИзменяемыеСпособыХранения.Вставить("ВТомахНаДиске"						, НСтр("ru = 'в томах (в сетевых каталогах)'"));
	ИзменяемыеСпособыХранения.Вставить("ВИнформационнойБазе"				, НСтр("ru = 'в базе данных'"));
	ИзменяемыеСпособыХранения.Вставить("ВИнформационнойБазеИТомахНаДиске"	, НСтр("ru = 'в базе данных или в томах'"));
	
	СписокВыбораСпособХраненияФайлов = Элементы.СпособХраненияФайлов.СписокВыбора;
	
	Для Каждого ИзменяемыйСпособХранения Из ИзменяемыеСпособыХранения Цикл
		РезультатПоиска = СписокВыбораСпособХраненияФайлов.НайтиПоЗначению(ИзменяемыйСпособХранения.Ключ);
		Если РезультатПоиска <> Неопределено Тогда
			РезультатПоиска.Представление = ИзменяемыйСпособХранения.Значение;
		КонецЕсли;		
	КонецЦикла;
	
	Если Не ДоступныХранилищаДвоичныхДанных Тогда
		Возврат;
	КонецЕсли;

	СписокВыбораСпособХраненияФайлов.Добавить("ВоВстроенномХранилищеДвоичныхДанных"	, НСтр("ru = 'во встроенном хранилище двоичных данных (КОРП)'"));
	СписокВыбораСпособХраненияФайлов.Добавить("ВоВнешнемХранилищеДвоичныхДанных"	, НСтр("ru = 'во внешнем хранилище двоичных данных (КОРП)'"));

КонецПроцедуры

&НаКлиенте
Функция ЭтоСпособХранениеФайловНаДиске(ПроверяемыйСпособХраненияФайлов)

	Возврат РаботаСФайламиКлиентСервер.ХранитьФайлыВТомах(ПроверяемыйСпособХраненияФайлов);

КонецФункции

&НаКлиенте
Функция ПолучитьТекстПредупрежденияПоСпособуХраненияФайлов(ОбрабатываемыйСпособХраненияФайлов)

	Результат = "";

	Если ОбрабатываемыйСпособХраненияФайлов = "ВТомахНаДиске" Тогда

		Результат = НСтр("ru = 'Включено хранение файлов в томах на файловом сервере, но тома еще не настроены.'");

	ИначеЕсли ОбрабатываемыйСпособХраненияФайлов = "ВИнформационнойБазеИТомахНаДиске" Тогда

		Результат = НСтр("ru = 'Включено хранение файлов в базе данных и внешних местах хранения, но тома хранения файлов еще не настроены.'");

	ИначеЕсли ОбрабатываемыйСпособХраненияФайлов = "ВоВстроенномХранилищеДвоичныхДанных" Тогда

		Результат = НСтр("ru = 'Включено хранение файлов во встроенном хранилище двоичных данных, но том хранения файлов еще не настроен.'");

	ИначеЕсли ОбрабатываемыйСпособХраненияФайлов = "ВоВнешнемХранилищеДвоичныхДанных" Тогда
		
		Результат = НСтр("ru = 'Включено хранение файлов во внешнем хранилище двоичных данных, но том хранения файлов еще не настроен.'");

	КонецЕсли;

	Если Не ПустаяСтрока(Результат) Тогда

		ШаблонСообщения = НСтр("ru = '%1
		|Добавляемые файлы будут сохраняться в информационной базе до тех пор, пока не будет настроен хотя бы один том хранения файлов.'");

		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Результат);

	КонецЕсли;

	Возврат Результат;

КонецФункции

&НаСервереБезКонтекста
Процедура ИзменитьСоставХранимыхДанных(СпособХраненияФайлов)

	АтрибутыДляНастройкиСоставаХранимыхДанных = РаботаСФайловымАрхивомСервер.ИменаРеквизитовХраненияФайловПоСпособуХранения(СпособХраненияФайлов);

	Если НужноИзменитьСоставХранимыхДанных(СпособХраненияФайлов, АтрибутыДляНастройкиСоставаХранимыхДанных) Тогда
		РаботаСХранилищамиДвоичныхДанныхСервер.ВыполнитьНастройкуСоставаХранимыхДанныхДляРеквизитов(АтрибутыДляНастройкиСоставаХранимыхДанных);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция НужноИзменитьСоставХранимыхДанных(СпособХраненияФайлов, РеквизитыДляХраненияФайлов)

	Если РаботаСХранилищамиДвоичныхДанныхСервер.ХранилищаДвоичныхДанныхОтсутствуют() Тогда
		Возврат Ложь;
	КонецЕсли;

	Для Каждого ИмяРеквизитаХраненияФайлов Из РеквизитыДляХраненияФайлов Цикл

		Если РаботаСХранилищамиДвоичныхДанныхСервер.НужноВыполнитьНастройкуСоставаХранимыхДанныхДляРеквизита(ИмяРеквизитаХраненияФайлов, СпособХраненияФайлов) Тогда
			Возврат Истина;
		КонецЕсли;

	КонецЦикла;

	Возврат Ложь;

КонецФункции

&НаСервере
Процедура УстановитьДоступностьНастроекУправленияФайловымАрхивом()

	Элементы.НастройкаРаботыСФайловымАрхивом							.Доступность = ИспользоватьФайловыйАрхив;
	Элементы.СправочникТомаХраненияФайловАрхивные						.Доступность = ИспользоватьФайловыйАрхив;
	Элементы.ТекстИнформированияПользователяОНедоступностиФайлаВАрхиве	.Доступность = ИспользоватьФайловыйАрхив;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриИзмененииИспользованияФайловогоАрхива(ИспользоватьФайловыйАрхив)

	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПереносФайловМеждуОперационнымХранилищемИФайловымАрхивом);
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		ПараметрыЗадания.Вставить("ИмяМетода", Метаданные.РегламентныеЗадания.ПереносФайловМеждуОперационнымХранилищемИФайловымАрхивом.ИмяМетода);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокЗаданий = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыЗадания);
	ИмяПараметра = "Использование";
	Если СписокЗаданий.Количество() = 0 Тогда
		ПараметрыЗадания.Вставить(ИмяПараметра, ИспользоватьФайловыйАрхив);
		РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
	Иначе
		ПараметрыЗадания = Новый Структура(ИмяПараметра, ИспользоватьФайловыйАрхив);
		Для Каждого Задание Из СписокЗаданий Цикл
			РегламентныеЗаданияСервер.ИзменитьЗадание(Задание, ПараметрыЗадания);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти