///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем ОбновитьИнтерфейс;	// Булево

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоАдминистраторСистемы   = Пользователи.ЭтоПолноправныйПользователь(, Истина);
	РазделениеВключено        = ОбщегоНазначения.РазделениеВключено();
	ЭтоАвтономноеРабочееМесто = ОбщегоНазначения.ЭтоАвтономноеРабочееМесто();
	
	НастройкиПрограммы.ИнтернетПоддержкаИСервисыПриСозданииНаСервере(
		ЭтотОбъект,
		Отказ,
		СтандартнаяОбработка);
	
	// Локализация
	Элементы.ИспользоватьСервисСклоненияMorpher.РасширеннаяПодсказка.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'Разрешить использование внешнего сервиса <a href=""http://www.morpher.ru"">Морфер</a> для получения альтернативного варианта склонения представлений объектов в падежах (требуется подключение к Интернет).
		|Если внешний сервис не используется, склонение представлений выполняется с применением встроенных возможностей приложения.'"));
	// Конец Локализация
	
	Если Не ОбщегоНазначенияКлиентСервер.ЭтоВариантИнтерфейса85() Тогда
		СтандартныеПодсистемыСервер.ПереместитьЭлементыВГруппу(КоманднаяПанель, Элементы.ГруппаСправкаТакси);
	КонецЕсли;
	
	ВывестиВажныеСообщения();
	
КонецПроцедуры

&НаСервере
Процедура ВывестиВажныеСообщения()
	
	ЗапрещенДоступКИнтернетСервисам = Не ОбщегоНазначения.РазрешенДоступКИнтернетСервисам();
	
	ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
	ПараметрыСообщения.ТипСообщения = "Информация";
	ПараметрыСообщения.Идентификатор = "ИнформацияОтчетОбИспользованииПриложения";
	ПараметрыСообщения.Сообщение = СтрЗаменить(НСтр("ru = 'Отчеты об использовании приложения помогают нам определить, над чем следует поработать в первую очередь.
		|Собранные сведения являются обезличенными и содержат только статистические показатели. Подготовка
		|и отправка отчетов не замедлит работу приложения.'"), Символы.ПС, " ");
	ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения, Элементы.ГруппаЦентрМониторинга, Элементы.ГруппаРазрешитьОтправлятьДанные);
	
	Если ЗапрещенДоступКИнтернетСервисам Тогда
		ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
		ПараметрыСообщения.Идентификатор = "ЗаблокированДоступКИнтернетСервисамАдресныйКлассификатор";
		ПараметрыСообщения.Сообщение = НСтр("ru = 'Для разблокировки настройки необходимо включить доступ к Интернет-сервисам в настройке ""Разрешить доступ к Интернет-сервисам"".'");
		ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения, Элементы.ГруппаКомментарийРазрешенДоступКИнтернетСервисамАдресногоКлассификатора);
		
		ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
		ПараметрыСообщения.Идентификатор = "ЗаблокированДоступКИнтернетСервисамСклонения";
		ПараметрыСообщения.Сообщение = НСтр("ru = 'Для разблокировки настройки необходимо включить доступ к Интернет-сервисам в настройке ""Разрешить доступ к Интернет-сервисам"".'");
		ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения, Элементы.ГруппаКомментарийРазрешенДоступКИнтернетСервисамСклонения);
		
		ПараметрыСообщения = ОбщегоНазначения.ПараметрыВажногоСообщения();
		ПараметрыСообщения.Идентификатор = "ЗаблокированДоступКИнтернетСервисамЦентрМониторинга";
		ПараметрыСообщения.Сообщение = НСтр("ru = 'Для разблокировки настройки необходимо включить доступ к Интернет-сервисам в настройке ""Разрешить доступ к Интернет-сервисам"".'");
		ОбщегоНазначения.ВставитьВажноеСообщение(ЭтотОбъект, ПараметрыСообщения, Элементы.ГруппаКомментарийРазрешенДоступКИнтернетСервисамЦентраМониторинга);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьСтатусИлиВажноеСообщение(ЭтотОбъект, "ЗаблокированДоступКИнтернетСервисамЦентрМониторинга");
		ОбщегоНазначенияКлиентСервер.УдалитьСтатусИлиВажноеСообщение(ЭтотОбъект, "ЗаблокированДоступКИнтернетСервисамАдресныйКлассификатор");
		ОбщегоНазначенияКлиентСервер.УдалитьСтатусИлиВажноеСообщение(ЭтотОбъект, "ЗаблокированДоступКИнтернетСервисамСклонения");
	КонецЕсли;
	
	ОбщегоНазначения.ВывестиСтатусыИВажныеСообщенияНаФорму(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыПриОткрытии(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыОбработкаОповещения(
		ЭтотОбъект,
		ИмяСобытия,
		Параметр,
		Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	ОбновитьИнтерфейсПрограммы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьВебСервисАдресовПриИзменении(Элемент)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.НастройкиПрограммы") Тогда
		ПриИзмененииИспользованияВебСервисАдресов(ИспользоватьВебСервисАдресов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьОтправлятьДанныеПриИзменении(Элемент)
	
	ПриИзмененииРежимаОтправкиДанныхВЦентрМониторинга(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьОтправлятьДанныеСтороннемуПриИзменении(Элемент)
	
	ПриИзмененииРежимаОтправкиДанныхВЦентрМониторинга(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапретитьОтправлятьДанныеПриИзменении(Элемент)
	
	ПриИзмененииРежимаОтправкиДанныхВЦентрМониторинга(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦентрМониторингаАдресСервисаПриИзменении(Элемент)
	
	ЦентрМониторингаАдресСервисаПриИзмененииНаСервере(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСервисСклоненияMorpherПриИзменении(Элемент)
	
	ПриИзмененииКонстантыНаСервере("ИспользоватьСервисСклоненияMorpher");
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыПриИзмененииКонстанты(
		ЭтотОбъект,
		Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьДоступКИнтернетСервисамПриИзменении(Элемент)
	
	ПриИзмененииКонстантыНаСервере("РазрешитьДоступКИнтернетСервисам");
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыПриИзмененииКонстанты(
		ЭтотОбъект,
		Элемент);
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ЦентрМониторинга") Тогда
		ПриИзмененииРежимаОтправкиДанныхВЦентрМониторинга(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьДействиеИнформационногоСообщения(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	ОбщегоНазначенияКлиент.ОбработатьДействиеИнформационногоСообщения(ЭтотОбъект, ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузкаАдресногоКлассификатора(Команда)
	
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыЗагрузкаАдресногоКлассификатора(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчисткаАдресныхСведений(Команда)
	
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыОчисткаАдресныхСведений(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаКурсовВалют(Команда)
	
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыЗагрузкаКурсовВалют(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОтключитьОбсуждения(Команда)
	
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыПодключитьОтключитьОбсуждения(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбсужденияНастроитьИнтеграциюСВнешнимиСистемами(Команда)
	
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыПоказатьНастройкуИнтеграцииСВнешнимиСистемами(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаДоступаКСервисуMorpher(Команда)
	
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыНастройкаДоступаКСервисуMorpher(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦентрМониторингаНастройки(Команда)
	
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыЦентрМониторингаНастройки(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦентрМониторингаОтправитьКонтактнуюИнформацию(Команда)
	
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыЦентрМониторингаОтправитьКонтактнуюИнформацию(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВнешниеКомпоненты(Команда)
	
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыОткрытьВнешниеКомпоненты(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВызовСервера

&НаСервере
Процедура ПриИзмененииКонстантыНаСервере(ИмяКонстанты)
	
	НастройкиПрограммы.ИнтернетПоддержкаИСервисыПриИзмененииКонстанты(
		ЭтотОбъект,
		ИмяКонстанты,
		ЭтотОбъект[ИмяКонстанты]);
	
	Если ИмяКонстанты = "РазрешитьДоступКИнтернетСервисам" Тогда
		ВывестиВажныеСообщения();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РазрешитьОтправлятьДанныеПриИзмененииНаСервере(ИмяЭлемента, ПараметрыОперации)
	
	НастройкиПрограммы.ИнтернетПоддержкаИСервисыРазрешитьОтправлятьДанныеПриИзменении(
		ЭтотОбъект,
		Элементы[ИмяЭлемента],
		ПараметрыОперации);
	
КонецПроцедуры

&НаСервере
Процедура ЦентрМониторингаАдресСервисаПриИзмененииНаСервере(ИмяЭлемента)
	
	НастройкиПрограммы.ИнтернетПоддержкаИСервисыЦентрМониторингаАдресСервисаПриИзменении(
		ЭтотОбъект,
		Элементы[ИмяЭлемента]);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриИзмененииИспользованияВебСервисАдресов(ИспользоватьВебСервисАдресов)
	
	НастройкиПрограммы.ИнтернетПоддержкаИСервисыУстановитьИспользованиеВебСервиса(ИспользоватьВебСервисАдресов);
	
КонецПроцедуры

#КонецОбласти

#Область Клиент

&НаКлиенте
Процедура ПриИзмененииРежимаОтправкиДанныхВЦентрМониторинга(Элемент)
	
	ПараметрыЦентраМониторинга = Новый Структура();
	РазрешитьОтправлятьДанныеПриИзмененииНаСервере(Элемент.Имя, ПараметрыЦентраМониторинга);
	НастройкиПрограммыКлиент.ИнтернетПоддержкаИСервисыРазрешитьОтправлятьДанныеПриИзменении(
		ЭтотОбъект,
		Элемент,
		ПараметрыЦентраМониторинга);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти